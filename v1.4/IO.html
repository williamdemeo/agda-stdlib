<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>IO</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- IO</a>
<a id="112" class="Comment">------------------------------------------------------------------------</a>

<a id="186" class="Symbol">{-#</a> <a id="190" class="Keyword">OPTIONS</a> <a id="198" class="Pragma">--without-K</a> <a id="210" class="Pragma">--guardedness</a> <a id="224" class="Symbol">#-}</a>

<a id="229" class="Keyword">module</a> <a id="236" href="IO.html" class="Module">IO</a> <a id="239" class="Keyword">where</a>

<a id="246" class="Keyword">open</a> <a id="251" class="Keyword">import</a> <a id="258" href="Codata.Musical.Notation.html" class="Module">Codata.Musical.Notation</a>
<a id="282" class="Keyword">open</a> <a id="287" class="Keyword">import</a> <a id="294" href="Codata.Musical.Colist.html" class="Module">Codata.Musical.Colist</a>
<a id="316" class="Keyword">open</a> <a id="321" class="Keyword">import</a> <a id="328" href="Codata.Musical.Costring.html" class="Module">Codata.Musical.Costring</a>
<a id="352" class="Keyword">open</a> <a id="357" class="Keyword">import</a> <a id="364" href="Data.Unit.Polymorphic.html" class="Module">Data.Unit.Polymorphic</a>
<a id="386" class="Keyword">open</a> <a id="391" class="Keyword">import</a> <a id="398" href="Data.String.html" class="Module">Data.String</a>
<a id="410" class="Keyword">open</a> <a id="415" class="Keyword">import</a> <a id="422" href="Function.html" class="Module">Function</a>
<a id="431" class="Keyword">import</a> <a id="438" href="IO.Primitive.html" class="Module">IO.Primitive</a> <a id="451" class="Symbol">as</a> <a id="454" class="Module">Prim</a>
<a id="459" class="Keyword">open</a> <a id="464" class="Keyword">import</a> <a id="471" href="Level.html" class="Module">Level</a>

<a id="478" class="Comment">------------------------------------------------------------------------</a>
<a id="551" class="Comment">-- The IO monad</a>

<a id="568" class="Comment">-- One cannot write &quot;infinitely large&quot; computations with the</a>
<a id="629" class="Comment">-- postulated IO monad in IO.Primitive without turning off the</a>
<a id="692" class="Comment">-- termination checker (or going via the FFI, or perhaps abusing</a>
<a id="757" class="Comment">-- something else). The following coinductive deep embedding is</a>
<a id="821" class="Comment">-- introduced to avoid this problem. Possible non-termination is</a>
<a id="886" class="Comment">-- isolated to the run function below.</a>

<a id="926" class="Keyword">infixl</a> <a id="933" class="Number">1</a> <a id="935" href="IO.html#1053" class="InductiveConstructor Operator">_&gt;&gt;=_</a> <a id="941" href="IO.html#1123" class="InductiveConstructor Operator">_&gt;&gt;_</a>

<a id="947" class="Keyword">data</a> <a id="IO"></a><a id="952" href="IO.html#952" class="Datatype">IO</a> <a id="955" class="Symbol">{</a><a id="956" href="IO.html#956" class="Bound">a</a><a id="957" class="Symbol">}</a> <a id="959" class="Symbol">(</a><a id="960" href="IO.html#960" class="Bound">A</a> <a id="962" class="Symbol">:</a> <a id="964" class="PrimitiveType">Set</a> <a id="968" href="IO.html#956" class="Bound">a</a><a id="969" class="Symbol">)</a> <a id="971" class="Symbol">:</a> <a id="973" class="PrimitiveType">Set</a> <a id="977" class="Symbol">(</a><a id="978" href="Agda.Primitive.html#606" class="Primitive">suc</a> <a id="982" href="IO.html#956" class="Bound">a</a><a id="983" class="Symbol">)</a> <a id="985" class="Keyword">where</a>
  <a id="IO.lift"></a><a id="993" href="IO.html#993" class="InductiveConstructor">lift</a>   <a id="1000" class="Symbol">:</a> <a id="1002" class="Symbol">(</a><a id="1003" href="IO.html#1003" class="Bound">m</a> <a id="1005" class="Symbol">:</a> <a id="1007" href="Agda.Builtin.IO.html#138" class="Postulate">Prim.IO</a> <a id="1015" href="IO.html#960" class="Bound">A</a><a id="1016" class="Symbol">)</a> <a id="1018" class="Symbol">→</a> <a id="1020" href="IO.html#952" class="Datatype">IO</a> <a id="1023" href="IO.html#960" class="Bound">A</a>
  <a id="IO.return"></a><a id="1027" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="1034" class="Symbol">:</a> <a id="1036" class="Symbol">(</a><a id="1037" href="IO.html#1037" class="Bound">x</a> <a id="1039" class="Symbol">:</a> <a id="1041" href="IO.html#960" class="Bound">A</a><a id="1042" class="Symbol">)</a> <a id="1044" class="Symbol">→</a> <a id="1046" href="IO.html#952" class="Datatype">IO</a> <a id="1049" href="IO.html#960" class="Bound">A</a>
  <a id="IO._&gt;&gt;=_"></a><a id="1053" href="IO.html#1053" class="InductiveConstructor Operator">_&gt;&gt;=_</a>  <a id="1060" class="Symbol">:</a> <a id="1062" class="Symbol">{</a><a id="1063" href="IO.html#1063" class="Bound">B</a> <a id="1065" class="Symbol">:</a> <a id="1067" class="PrimitiveType">Set</a> <a id="1071" href="IO.html#956" class="Bound">a</a><a id="1072" class="Symbol">}</a> <a id="1074" class="Symbol">(</a><a id="1075" href="IO.html#1075" class="Bound">m</a> <a id="1077" class="Symbol">:</a> <a id="1079" href="Agda.Builtin.Coinduction.html#188" class="Record">∞</a> <a id="1081" class="Symbol">(</a><a id="1082" href="IO.html#952" class="Datatype">IO</a> <a id="1085" href="IO.html#1063" class="Bound">B</a><a id="1086" class="Symbol">))</a> <a id="1089" class="Symbol">(</a><a id="1090" href="IO.html#1090" class="Bound">f</a> <a id="1092" class="Symbol">:</a> <a id="1094" class="Symbol">(</a><a id="1095" href="IO.html#1095" class="Bound">x</a> <a id="1097" class="Symbol">:</a> <a id="1099" href="IO.html#1063" class="Bound">B</a><a id="1100" class="Symbol">)</a> <a id="1102" class="Symbol">→</a> <a id="1104" href="Agda.Builtin.Coinduction.html#188" class="Record">∞</a> <a id="1106" class="Symbol">(</a><a id="1107" href="IO.html#952" class="Datatype">IO</a> <a id="1110" href="IO.html#960" class="Bound">A</a><a id="1111" class="Symbol">))</a> <a id="1114" class="Symbol">→</a> <a id="1116" href="IO.html#952" class="Datatype">IO</a> <a id="1119" href="IO.html#960" class="Bound">A</a>
  <a id="IO._&gt;&gt;_"></a><a id="1123" href="IO.html#1123" class="InductiveConstructor Operator">_&gt;&gt;_</a>   <a id="1130" class="Symbol">:</a> <a id="1132" class="Symbol">{</a><a id="1133" href="IO.html#1133" class="Bound">B</a> <a id="1135" class="Symbol">:</a> <a id="1137" class="PrimitiveType">Set</a> <a id="1141" href="IO.html#956" class="Bound">a</a><a id="1142" class="Symbol">}</a> <a id="1144" class="Symbol">(</a><a id="1145" href="IO.html#1145" class="Bound">m₁</a> <a id="1148" class="Symbol">:</a> <a id="1150" href="Agda.Builtin.Coinduction.html#188" class="Record">∞</a> <a id="1152" class="Symbol">(</a><a id="1153" href="IO.html#952" class="Datatype">IO</a> <a id="1156" href="IO.html#1133" class="Bound">B</a><a id="1157" class="Symbol">))</a> <a id="1160" class="Symbol">(</a><a id="1161" href="IO.html#1161" class="Bound">m₂</a> <a id="1164" class="Symbol">:</a> <a id="1166" href="Agda.Builtin.Coinduction.html#188" class="Record">∞</a> <a id="1168" class="Symbol">(</a><a id="1169" href="IO.html#952" class="Datatype">IO</a> <a id="1172" href="IO.html#960" class="Bound">A</a><a id="1173" class="Symbol">))</a> <a id="1176" class="Symbol">→</a> <a id="1178" href="IO.html#952" class="Datatype">IO</a> <a id="1181" href="IO.html#960" class="Bound">A</a>

<a id="1184" class="Symbol">{-#</a> <a id="1188" class="Keyword">NON_TERMINATING</a> <a id="1204" class="Symbol">#-}</a>

<a id="run"></a><a id="1209" href="IO.html#1209" class="Function">run</a> <a id="1213" class="Symbol">:</a> <a id="1215" class="Symbol">∀</a> <a id="1217" class="Symbol">{</a><a id="1218" href="IO.html#1218" class="Bound">a</a><a id="1219" class="Symbol">}</a> <a id="1221" class="Symbol">{</a><a id="1222" href="IO.html#1222" class="Bound">A</a> <a id="1224" class="Symbol">:</a> <a id="1226" class="PrimitiveType">Set</a> <a id="1230" href="IO.html#1218" class="Bound">a</a><a id="1231" class="Symbol">}</a> <a id="1233" class="Symbol">→</a> <a id="1235" href="IO.html#952" class="Datatype">IO</a> <a id="1238" href="IO.html#1222" class="Bound">A</a> <a id="1240" class="Symbol">→</a> <a id="1242" href="Agda.Builtin.IO.html#138" class="Postulate">Prim.IO</a> <a id="1250" href="IO.html#1222" class="Bound">A</a>
<a id="1252" href="IO.html#1209" class="Function">run</a> <a id="1256" class="Symbol">(</a><a id="1257" href="IO.html#993" class="InductiveConstructor">lift</a> <a id="1262" href="IO.html#1262" class="Bound">m</a><a id="1263" class="Symbol">)</a>   <a id="1267" class="Symbol">=</a> <a id="1269" href="IO.html#1262" class="Bound">m</a>
<a id="1271" href="IO.html#1209" class="Function">run</a> <a id="1275" class="Symbol">(</a><a id="1276" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="1283" href="IO.html#1283" class="Bound">x</a><a id="1284" class="Symbol">)</a> <a id="1286" class="Symbol">=</a> <a id="1288" href="IO.Primitive.html#610" class="Postulate">Prim.return</a> <a id="1300" href="IO.html#1283" class="Bound">x</a>
<a id="1302" href="IO.html#1209" class="Function">run</a> <a id="1306" class="Symbol">(</a><a id="1307" href="IO.html#1307" class="Bound">m</a>  <a id="1310" href="IO.html#1053" class="InductiveConstructor Operator">&gt;&gt;=</a> <a id="1314" href="IO.html#1314" class="Bound">f</a><a id="1315" class="Symbol">)</a> <a id="1317" class="Symbol">=</a> <a id="1319" href="IO.Primitive.html#650" class="Postulate Operator">Prim._&gt;&gt;=_</a> <a id="1330" class="Symbol">(</a><a id="1331" href="IO.html#1209" class="Function">run</a> <a id="1335" class="Symbol">(</a><a id="1336" href="Agda.Builtin.Coinduction.html#256" class="Field">♭</a> <a id="1338" href="IO.html#1307" class="Bound">m</a> <a id="1340" class="Symbol">))</a> <a id="1343" class="Symbol">λ</a> <a id="1345" href="IO.html#1345" class="Bound">x</a> <a id="1347" class="Symbol">→</a> <a id="1349" href="IO.html#1209" class="Function">run</a> <a id="1353" class="Symbol">(</a><a id="1354" href="Agda.Builtin.Coinduction.html#256" class="Field">♭</a> <a id="1356" class="Symbol">(</a><a id="1357" href="IO.html#1314" class="Bound">f</a> <a id="1359" href="IO.html#1345" class="Bound">x</a><a id="1360" class="Symbol">))</a>
<a id="1363" href="IO.html#1209" class="Function">run</a> <a id="1367" class="Symbol">(</a><a id="1368" href="IO.html#1368" class="Bound">m₁</a> <a id="1371" href="IO.html#1123" class="InductiveConstructor Operator">&gt;&gt;</a> <a id="1374" href="IO.html#1374" class="Bound">m₂</a><a id="1376" class="Symbol">)</a> <a id="1378" class="Symbol">=</a> <a id="1380" href="IO.Primitive.html#650" class="Postulate Operator">Prim._&gt;&gt;=_</a> <a id="1391" class="Symbol">(</a><a id="1392" href="IO.html#1209" class="Function">run</a> <a id="1396" class="Symbol">(</a><a id="1397" href="Agda.Builtin.Coinduction.html#256" class="Field">♭</a> <a id="1399" href="IO.html#1368" class="Bound">m₁</a><a id="1401" class="Symbol">))</a> <a id="1404" class="Symbol">λ</a> <a id="1406" href="IO.html#1406" class="Bound">_</a> <a id="1408" class="Symbol">→</a> <a id="1410" href="IO.html#1209" class="Function">run</a> <a id="1414" class="Symbol">(</a><a id="1415" href="Agda.Builtin.Coinduction.html#256" class="Field">♭</a> <a id="1417" href="IO.html#1374" class="Bound">m₂</a><a id="1419" class="Symbol">)</a>

<a id="1422" class="Comment">------------------------------------------------------------------------</a>
<a id="1495" class="Comment">-- Utilities</a>

<a id="sequence"></a><a id="1509" href="IO.html#1509" class="Function">sequence</a> <a id="1518" class="Symbol">:</a> <a id="1520" class="Symbol">∀</a> <a id="1522" class="Symbol">{</a><a id="1523" href="IO.html#1523" class="Bound">a</a><a id="1524" class="Symbol">}</a> <a id="1526" class="Symbol">{</a><a id="1527" href="IO.html#1527" class="Bound">A</a> <a id="1529" class="Symbol">:</a> <a id="1531" class="PrimitiveType">Set</a> <a id="1535" href="IO.html#1523" class="Bound">a</a><a id="1536" class="Symbol">}</a> <a id="1538" class="Symbol">→</a> <a id="1540" href="Codata.Musical.Colist.html#1866" class="Datatype">Colist</a> <a id="1547" class="Symbol">(</a><a id="1548" href="IO.html#952" class="Datatype">IO</a> <a id="1551" href="IO.html#1527" class="Bound">A</a><a id="1552" class="Symbol">)</a> <a id="1554" class="Symbol">→</a> <a id="1556" href="IO.html#952" class="Datatype">IO</a> <a id="1559" class="Symbol">(</a><a id="1560" href="Codata.Musical.Colist.html#1866" class="Datatype">Colist</a> <a id="1567" href="IO.html#1527" class="Bound">A</a><a id="1568" class="Symbol">)</a>
<a id="1570" href="IO.html#1509" class="Function">sequence</a> <a id="1579" href="Codata.Musical.Colist.html#1905" class="InductiveConstructor">[]</a>       <a id="1588" class="Symbol">=</a> <a id="1590" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="1597" href="Codata.Musical.Colist.html#1905" class="InductiveConstructor">[]</a>
<a id="1600" href="IO.html#1509" class="Function">sequence</a> <a id="1609" class="Symbol">(</a><a id="1610" href="IO.html#1610" class="Bound">c</a> <a id="1612" href="Codata.Musical.Colist.html#1922" class="InductiveConstructor Operator">∷</a> <a id="1614" href="IO.html#1614" class="Bound">cs</a><a id="1616" class="Symbol">)</a> <a id="1618" class="Symbol">=</a> <a id="1620" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="1622" href="IO.html#1610" class="Bound">c</a>                  <a id="1641" href="IO.html#1053" class="InductiveConstructor Operator">&gt;&gt;=</a> <a id="1645" class="Symbol">λ</a> <a id="1647" href="IO.html#1647" class="Bound">x</a>  <a id="1650" class="Symbol">→</a>
                    <a id="1672" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="1674" class="Symbol">(</a><a id="1675" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="1677" href="IO.html#1509" class="Function">sequence</a> <a id="1686" class="Symbol">(</a><a id="1687" href="Agda.Builtin.Coinduction.html#256" class="Field">♭</a> <a id="1689" href="IO.html#1614" class="Bound">cs</a><a id="1691" class="Symbol">)</a> <a id="1693" href="IO.html#1053" class="InductiveConstructor Operator">&gt;&gt;=</a> <a id="1697" class="Symbol">λ</a> <a id="1699" href="IO.html#1699" class="Bound">xs</a> <a id="1702" class="Symbol">→</a>
                    <a id="1724" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="1726" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="1733" class="Symbol">(</a><a id="1734" href="IO.html#1647" class="Bound">x</a> <a id="1736" href="Codata.Musical.Colist.html#1922" class="InductiveConstructor Operator">∷</a> <a id="1738" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="1740" href="IO.html#1699" class="Bound">xs</a><a id="1742" class="Symbol">))</a>

<a id="1746" class="Comment">-- The reason for not defining sequence′ in terms of sequence is</a>
<a id="1811" class="Comment">-- efficiency (the unused results could cause unnecessary memory use).</a>

<a id="sequence′"></a><a id="1883" href="IO.html#1883" class="Function">sequence′</a> <a id="1893" class="Symbol">:</a> <a id="1895" class="Symbol">∀</a> <a id="1897" class="Symbol">{</a><a id="1898" href="IO.html#1898" class="Bound">a</a><a id="1899" class="Symbol">}</a> <a id="1901" class="Symbol">{</a><a id="1902" href="IO.html#1902" class="Bound">A</a> <a id="1904" class="Symbol">:</a> <a id="1906" class="PrimitiveType">Set</a> <a id="1910" href="IO.html#1898" class="Bound">a</a><a id="1911" class="Symbol">}</a> <a id="1913" class="Symbol">→</a> <a id="1915" href="Codata.Musical.Colist.html#1866" class="Datatype">Colist</a> <a id="1922" class="Symbol">(</a><a id="1923" href="IO.html#952" class="Datatype">IO</a> <a id="1926" href="IO.html#1902" class="Bound">A</a><a id="1927" class="Symbol">)</a> <a id="1929" class="Symbol">→</a> <a id="1931" href="IO.html#952" class="Datatype">IO</a> <a id="1934" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="1936" href="IO.html#1883" class="Function">sequence′</a> <a id="1946" href="Codata.Musical.Colist.html#1905" class="InductiveConstructor">[]</a>       <a id="1955" class="Symbol">=</a> <a id="1957" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="1964" class="Symbol">_</a>
<a id="1966" href="IO.html#1883" class="Function">sequence′</a> <a id="1976" class="Symbol">(</a><a id="1977" href="IO.html#1977" class="Bound">c</a> <a id="1979" href="Codata.Musical.Colist.html#1922" class="InductiveConstructor Operator">∷</a> <a id="1981" href="IO.html#1981" class="Bound">cs</a><a id="1983" class="Symbol">)</a> <a id="1985" class="Symbol">=</a> <a id="1987" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="1989" href="IO.html#1977" class="Bound">c</a>                   <a id="2009" href="IO.html#1123" class="InductiveConstructor Operator">&gt;&gt;</a>
                     <a id="2033" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="2035" class="Symbol">(</a><a id="2036" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="2038" href="IO.html#1883" class="Function">sequence′</a> <a id="2048" class="Symbol">(</a><a id="2049" href="Agda.Builtin.Coinduction.html#256" class="Field">♭</a> <a id="2051" href="IO.html#1981" class="Bound">cs</a><a id="2053" class="Symbol">)</a> <a id="2055" href="IO.html#1123" class="InductiveConstructor Operator">&gt;&gt;</a>
                     <a id="2079" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="2081" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="2088" class="Symbol">_)</a>

<a id="2092" class="Keyword">module</a> <a id="2099" href="IO.html#2099" class="Module">_</a> <a id="2101" class="Symbol">{</a><a id="2102" href="IO.html#2102" class="Bound">a</a> <a id="2104" href="IO.html#2104" class="Bound">b</a><a id="2105" class="Symbol">}</a> <a id="2107" class="Symbol">{</a><a id="2108" href="IO.html#2108" class="Bound">A</a> <a id="2110" class="Symbol">:</a> <a id="2112" class="PrimitiveType">Set</a> <a id="2116" href="IO.html#2102" class="Bound">a</a><a id="2117" class="Symbol">}</a> <a id="2119" class="Symbol">{</a><a id="2120" href="IO.html#2120" class="Bound">B</a> <a id="2122" class="Symbol">:</a> <a id="2124" class="PrimitiveType">Set</a> <a id="2128" href="IO.html#2104" class="Bound">b</a><a id="2129" class="Symbol">}</a> <a id="2131" class="Keyword">where</a>

  <a id="2140" href="IO.html#2140" class="Function">mapM</a> <a id="2145" class="Symbol">:</a> <a id="2147" class="Symbol">(</a><a id="2148" href="IO.html#2108" class="Bound">A</a> <a id="2150" class="Symbol">→</a> <a id="2152" href="IO.html#952" class="Datatype">IO</a> <a id="2155" href="IO.html#2120" class="Bound">B</a><a id="2156" class="Symbol">)</a> <a id="2158" class="Symbol">→</a> <a id="2160" href="Codata.Musical.Colist.html#1866" class="Datatype">Colist</a> <a id="2167" href="IO.html#2108" class="Bound">A</a> <a id="2169" class="Symbol">→</a> <a id="2171" href="IO.html#952" class="Datatype">IO</a> <a id="2174" class="Symbol">(</a><a id="2175" href="Codata.Musical.Colist.html#1866" class="Datatype">Colist</a> <a id="2182" href="IO.html#2120" class="Bound">B</a><a id="2183" class="Symbol">)</a>
  <a id="2187" href="IO.html#2140" class="Function">mapM</a> <a id="2192" href="IO.html#2192" class="Bound">f</a> <a id="2194" class="Symbol">=</a> <a id="2196" href="IO.html#1509" class="Function">sequence</a> <a id="2205" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="2207" href="Codata.Musical.Colist.html#3754" class="Function">map</a> <a id="2211" href="IO.html#2192" class="Bound">f</a>

  <a id="2216" href="IO.html#2216" class="Function">mapM′</a> <a id="2222" class="Symbol">:</a> <a id="2224" class="Symbol">(</a><a id="2225" href="IO.html#2108" class="Bound">A</a> <a id="2227" class="Symbol">→</a> <a id="2229" href="IO.html#952" class="Datatype">IO</a> <a id="2232" href="IO.html#2120" class="Bound">B</a><a id="2233" class="Symbol">)</a> <a id="2235" class="Symbol">→</a> <a id="2237" href="Codata.Musical.Colist.html#1866" class="Datatype">Colist</a> <a id="2244" href="IO.html#2108" class="Bound">A</a> <a id="2246" class="Symbol">→</a> <a id="2248" href="IO.html#952" class="Datatype">IO</a> <a id="2251" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
  <a id="2255" href="IO.html#2216" class="Function">mapM′</a> <a id="2261" href="IO.html#2261" class="Bound">f</a> <a id="2263" class="Symbol">=</a> <a id="2265" href="IO.html#1883" class="Function">sequence′</a> <a id="2275" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="2277" href="Codata.Musical.Colist.html#3754" class="Function">map</a> <a id="2281" href="IO.html#2261" class="Bound">f</a>

<a id="2284" class="Comment">------------------------------------------------------------------------</a>
<a id="2357" class="Comment">-- Simple lazy IO</a>

<a id="2376" class="Comment">-- Note that the functions below produce commands which, when</a>
<a id="2438" class="Comment">-- executed, may raise exceptions.</a>

<a id="2474" class="Comment">-- Note also that the semantics of these functions depends on the</a>
<a id="2540" class="Comment">-- version of the Haskell base library. If the version is 4.2.0.0 (or</a>
<a id="2610" class="Comment">-- later?), then the functions use the character encoding specified by</a>
<a id="2681" class="Comment">-- the locale. For older versions of the library (going back to at</a>
<a id="2748" class="Comment">-- least version 3) the functions use ISO-8859-1.</a>

<a id="getContents"></a><a id="2799" href="IO.html#2799" class="Function">getContents</a> <a id="2811" class="Symbol">:</a> <a id="2813" href="IO.html#952" class="Datatype">IO</a> <a id="2816" href="Codata.Musical.Costring.html#468" class="Function">Costring</a>
<a id="2825" href="IO.html#2799" class="Function">getContents</a> <a id="2837" class="Symbol">=</a> <a id="2839" href="IO.html#993" class="InductiveConstructor">lift</a> <a id="2844" href="IO.Primitive.html#1693" class="Postulate">Prim.getContents</a>

<a id="readFile"></a><a id="2862" href="IO.html#2862" class="Function">readFile</a> <a id="2871" class="Symbol">:</a> <a id="2873" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="2880" class="Symbol">→</a> <a id="2882" href="IO.html#952" class="Datatype">IO</a> <a id="2885" href="Codata.Musical.Costring.html#468" class="Function">Costring</a>
<a id="2894" href="IO.html#2862" class="Function">readFile</a> <a id="2903" href="IO.html#2903" class="Bound">f</a> <a id="2905" class="Symbol">=</a> <a id="2907" href="IO.html#993" class="InductiveConstructor">lift</a> <a id="2912" class="Symbol">(</a><a id="2913" href="IO.Primitive.html#1721" class="Postulate">Prim.readFile</a> <a id="2927" href="IO.html#2903" class="Bound">f</a><a id="2928" class="Symbol">)</a>

<a id="2931" class="Comment">-- Reads a finite file. Raises an exception if the file path refers to</a>
<a id="3002" class="Comment">-- a non-physical file (like &quot;/dev/zero&quot;).</a>

<a id="readFiniteFile"></a><a id="3046" href="IO.html#3046" class="Function">readFiniteFile</a> <a id="3061" class="Symbol">:</a> <a id="3063" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="3070" class="Symbol">→</a> <a id="3072" href="IO.html#952" class="Datatype">IO</a> <a id="3075" href="Agda.Builtin.String.html#274" class="Postulate">String</a>
<a id="3082" href="IO.html#3046" class="Function">readFiniteFile</a> <a id="3097" href="IO.html#3097" class="Bound">f</a> <a id="3099" class="Symbol">=</a> <a id="3101" href="IO.html#993" class="InductiveConstructor">lift</a> <a id="3106" class="Symbol">(</a><a id="3107" href="IO.Primitive.html#2036" class="Postulate">Prim.readFiniteFile</a> <a id="3127" href="IO.html#3097" class="Bound">f</a><a id="3128" class="Symbol">)</a>

<a id="writeFile∞"></a><a id="3131" href="IO.html#3131" class="Function">writeFile∞</a> <a id="3142" class="Symbol">:</a> <a id="3144" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="3151" class="Symbol">→</a> <a id="3153" href="Codata.Musical.Costring.html#468" class="Function">Costring</a> <a id="3162" class="Symbol">→</a> <a id="3164" href="IO.html#952" class="Datatype">IO</a> <a id="3167" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3169" href="IO.html#3131" class="Function">writeFile∞</a> <a id="3180" href="IO.html#3180" class="Bound">f</a> <a id="3182" href="IO.html#3182" class="Bound">s</a> <a id="3184" class="Symbol">=</a>
  <a id="3188" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="3190" href="IO.html#993" class="InductiveConstructor">lift</a> <a id="3195" class="Symbol">(</a><a id="3196" href="IO.Primitive.html#1758" class="Postulate">Prim.writeFile</a> <a id="3211" href="IO.html#3180" class="Bound">f</a> <a id="3213" href="IO.html#3182" class="Bound">s</a><a id="3214" class="Symbol">)</a> <a id="3216" href="IO.html#1123" class="InductiveConstructor Operator">&gt;&gt;</a>
  <a id="3221" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="3223" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="3230" class="Symbol">_</a>

<a id="writeFile"></a><a id="3233" href="IO.html#3233" class="Function">writeFile</a> <a id="3243" class="Symbol">:</a> <a id="3245" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="3252" class="Symbol">→</a> <a id="3254" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="3261" class="Symbol">→</a> <a id="3263" href="IO.html#952" class="Datatype">IO</a> <a id="3266" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3268" href="IO.html#3233" class="Function">writeFile</a> <a id="3278" href="IO.html#3278" class="Bound">f</a> <a id="3280" href="IO.html#3280" class="Bound">s</a> <a id="3282" class="Symbol">=</a> <a id="3284" href="IO.html#3131" class="Function">writeFile∞</a> <a id="3295" href="IO.html#3278" class="Bound">f</a> <a id="3297" class="Symbol">(</a><a id="3298" href="Codata.Musical.Costring.html#519" class="Function">toCostring</a> <a id="3309" href="IO.html#3280" class="Bound">s</a><a id="3310" class="Symbol">)</a>

<a id="appendFile∞"></a><a id="3313" href="IO.html#3313" class="Function">appendFile∞</a> <a id="3325" class="Symbol">:</a> <a id="3327" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="3334" class="Symbol">→</a> <a id="3336" href="Codata.Musical.Costring.html#468" class="Function">Costring</a> <a id="3345" class="Symbol">→</a> <a id="3347" href="IO.html#952" class="Datatype">IO</a> <a id="3350" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3352" href="IO.html#3313" class="Function">appendFile∞</a> <a id="3364" href="IO.html#3364" class="Bound">f</a> <a id="3366" href="IO.html#3366" class="Bound">s</a> <a id="3368" class="Symbol">=</a>
  <a id="3372" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="3374" href="IO.html#993" class="InductiveConstructor">lift</a> <a id="3379" class="Symbol">(</a><a id="3380" href="IO.Primitive.html#1802" class="Postulate">Prim.appendFile</a> <a id="3396" href="IO.html#3364" class="Bound">f</a> <a id="3398" href="IO.html#3366" class="Bound">s</a><a id="3399" class="Symbol">)</a> <a id="3401" href="IO.html#1123" class="InductiveConstructor Operator">&gt;&gt;</a>
  <a id="3406" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="3408" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="3415" class="Symbol">_</a>

<a id="appendFile"></a><a id="3418" href="IO.html#3418" class="Function">appendFile</a> <a id="3429" class="Symbol">:</a> <a id="3431" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="3438" class="Symbol">→</a> <a id="3440" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="3447" class="Symbol">→</a> <a id="3449" href="IO.html#952" class="Datatype">IO</a> <a id="3452" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3454" href="IO.html#3418" class="Function">appendFile</a> <a id="3465" href="IO.html#3465" class="Bound">f</a> <a id="3467" href="IO.html#3467" class="Bound">s</a> <a id="3469" class="Symbol">=</a> <a id="3471" href="IO.html#3313" class="Function">appendFile∞</a> <a id="3483" href="IO.html#3465" class="Bound">f</a> <a id="3485" class="Symbol">(</a><a id="3486" href="Codata.Musical.Costring.html#519" class="Function">toCostring</a> <a id="3497" href="IO.html#3467" class="Bound">s</a><a id="3498" class="Symbol">)</a>

<a id="putStr∞"></a><a id="3501" href="IO.html#3501" class="Function">putStr∞</a> <a id="3509" class="Symbol">:</a> <a id="3511" href="Codata.Musical.Costring.html#468" class="Function">Costring</a> <a id="3520" class="Symbol">→</a> <a id="3522" href="IO.html#952" class="Datatype">IO</a> <a id="3525" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3527" href="IO.html#3501" class="Function">putStr∞</a> <a id="3535" href="IO.html#3535" class="Bound">s</a> <a id="3537" class="Symbol">=</a>
  <a id="3541" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="3543" href="IO.html#993" class="InductiveConstructor">lift</a> <a id="3548" class="Symbol">(</a><a id="3549" href="IO.Primitive.html#1846" class="Postulate">Prim.putStr</a> <a id="3561" href="IO.html#3535" class="Bound">s</a><a id="3562" class="Symbol">)</a> <a id="3564" href="IO.html#1123" class="InductiveConstructor Operator">&gt;&gt;</a>
  <a id="3569" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="3571" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="3578" class="Symbol">_</a>

<a id="putStr"></a><a id="3581" href="IO.html#3581" class="Function">putStr</a> <a id="3588" class="Symbol">:</a> <a id="3590" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="3597" class="Symbol">→</a> <a id="3599" href="IO.html#952" class="Datatype">IO</a> <a id="3602" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3604" href="IO.html#3581" class="Function">putStr</a> <a id="3611" href="IO.html#3611" class="Bound">s</a> <a id="3613" class="Symbol">=</a> <a id="3615" href="IO.html#3501" class="Function">putStr∞</a> <a id="3623" class="Symbol">(</a><a id="3624" href="Codata.Musical.Costring.html#519" class="Function">toCostring</a> <a id="3635" href="IO.html#3611" class="Bound">s</a><a id="3636" class="Symbol">)</a>

<a id="putStrLn∞"></a><a id="3639" href="IO.html#3639" class="Function">putStrLn∞</a> <a id="3649" class="Symbol">:</a> <a id="3651" href="Codata.Musical.Costring.html#468" class="Function">Costring</a> <a id="3660" class="Symbol">→</a> <a id="3662" href="IO.html#952" class="Datatype">IO</a> <a id="3665" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3667" href="IO.html#3639" class="Function">putStrLn∞</a> <a id="3677" href="IO.html#3677" class="Bound">s</a> <a id="3679" class="Symbol">=</a>
  <a id="3683" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="3685" href="IO.html#993" class="InductiveConstructor">lift</a> <a id="3690" class="Symbol">(</a><a id="3691" href="IO.Primitive.html#1881" class="Postulate">Prim.putStrLn</a> <a id="3705" href="IO.html#3677" class="Bound">s</a><a id="3706" class="Symbol">)</a> <a id="3708" href="IO.html#1123" class="InductiveConstructor Operator">&gt;&gt;</a>
  <a id="3713" href="Agda.Builtin.Coinduction.html#221" class="CoinductiveConstructor Operator">♯</a> <a id="3715" href="IO.html#1027" class="InductiveConstructor">return</a> <a id="3722" class="Symbol">_</a>

<a id="putStrLn"></a><a id="3725" href="IO.html#3725" class="Function">putStrLn</a> <a id="3734" class="Symbol">:</a> <a id="3736" href="Agda.Builtin.String.html#274" class="Postulate">String</a> <a id="3743" class="Symbol">→</a> <a id="3745" href="IO.html#952" class="Datatype">IO</a> <a id="3748" href="Data.Unit.Polymorphic.Base.html#480" class="Function">⊤</a>
<a id="3750" href="IO.html#3725" class="Function">putStrLn</a> <a id="3759" href="IO.html#3759" class="Bound">s</a> <a id="3761" class="Symbol">=</a> <a id="3763" href="IO.html#3639" class="Function">putStrLn∞</a> <a id="3773" class="Symbol">(</a><a id="3774" href="Codata.Musical.Costring.html#519" class="Function">toCostring</a> <a id="3785" href="IO.html#3759" class="Bound">s</a><a id="3786" class="Symbol">)</a>

<a id="3789" class="Comment">-- Note that the commands writeFile, appendFile, putStr and putStrLn</a>
<a id="3858" class="Comment">-- perform two conversions (string → costring → Haskell list). It may</a>
<a id="3928" class="Comment">-- be preferable to only perform one conversion.</a>
</pre></body></html>
<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Sized.Delay.Bisimilarity</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Bisimilarity for the Delay type</a>
<a id="141" class="Comment">------------------------------------------------------------------------</a>

<a id="215" class="Symbol">{-#</a> <a id="219" class="Keyword">OPTIONS</a> <a id="227" class="Pragma">--cubical-compatible</a> <a id="248" class="Pragma">--sized-types</a> <a id="262" class="Symbol">#-}</a>

<a id="267" class="Keyword">module</a> <a id="274" href="Codata.Sized.Delay.Bisimilarity.html" class="Module">Codata.Sized.Delay.Bisimilarity</a> <a id="306" class="Keyword">where</a>

<a id="313" class="Keyword">open</a> <a id="318" class="Keyword">import</a> <a id="325" href="Size.html" class="Module">Size</a>
<a id="330" class="Keyword">open</a> <a id="335" class="Keyword">import</a> <a id="342" href="Codata.Sized.Thunk.html" class="Module">Codata.Sized.Thunk</a>
<a id="361" class="Keyword">open</a> <a id="366" class="Keyword">import</a> <a id="373" href="Codata.Sized.Delay.html" class="Module">Codata.Sized.Delay</a>
<a id="392" class="Keyword">open</a> <a id="397" class="Keyword">import</a> <a id="404" href="Level.html" class="Module">Level</a>
<a id="410" class="Keyword">open</a> <a id="415" class="Keyword">import</a> <a id="422" href="Relation.Binary.Definitions.html" class="Module">Relation.Binary.Definitions</a>
  <a id="452" class="Keyword">using</a> <a id="458" class="Symbol">(</a><a id="459" href="Relation.Binary.Definitions.html#1428" class="Function">Reflexive</a><a id="468" class="Symbol">;</a> <a id="470" href="Relation.Binary.Definitions.html#1587" class="Function">Symmetric</a><a id="479" class="Symbol">;</a> <a id="481" href="Relation.Binary.Definitions.html#2103" class="Function">Transitive</a><a id="491" class="Symbol">;</a> <a id="493" href="Relation.Binary.Definitions.html#1513" class="Function">Sym</a><a id="496" class="Symbol">;</a> <a id="498" href="Relation.Binary.Definitions.html#1674" class="Function">Trans</a><a id="503" class="Symbol">)</a>
<a id="505" class="Keyword">open</a> <a id="510" class="Keyword">import</a> <a id="517" href="Relation.Binary.PropositionalEquality.Core.html" class="Module">Relation.Binary.PropositionalEquality.Core</a> <a id="560" class="Symbol">as</a> <a id="563" class="Module">Eq</a> <a id="566" class="Keyword">using</a> <a id="572" class="Symbol">(</a><a id="573" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">_≡_</a><a id="576" class="Symbol">)</a>

<a id="579" class="Keyword">data</a> <a id="Bisim"></a><a id="584" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="590" class="Symbol">{</a><a id="591" href="Codata.Sized.Delay.Bisimilarity.html#591" class="Bound">a</a> <a id="593" href="Codata.Sized.Delay.Bisimilarity.html#593" class="Bound">b</a> <a id="595" href="Codata.Sized.Delay.Bisimilarity.html#595" class="Bound">r</a><a id="596" class="Symbol">}</a> <a id="598" class="Symbol">{</a><a id="599" href="Codata.Sized.Delay.Bisimilarity.html#599" class="Bound">A</a> <a id="601" class="Symbol">:</a> <a id="603" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="607" href="Codata.Sized.Delay.Bisimilarity.html#591" class="Bound">a</a><a id="608" class="Symbol">}</a> <a id="610" class="Symbol">{</a><a id="611" href="Codata.Sized.Delay.Bisimilarity.html#611" class="Bound">B</a> <a id="613" class="Symbol">:</a> <a id="615" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="619" href="Codata.Sized.Delay.Bisimilarity.html#593" class="Bound">b</a><a id="620" class="Symbol">}</a> <a id="622" class="Symbol">(</a><a id="623" href="Codata.Sized.Delay.Bisimilarity.html#623" class="Bound">R</a> <a id="625" class="Symbol">:</a> <a id="627" href="Codata.Sized.Delay.Bisimilarity.html#599" class="Bound">A</a> <a id="629" class="Symbol">→</a> <a id="631" href="Codata.Sized.Delay.Bisimilarity.html#611" class="Bound">B</a> <a id="633" class="Symbol">→</a> <a id="635" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="639" href="Codata.Sized.Delay.Bisimilarity.html#595" class="Bound">r</a><a id="640" class="Symbol">)</a> <a id="642" href="Codata.Sized.Delay.Bisimilarity.html#642" class="Bound">i</a> <a id="644" class="Symbol">:</a>
           <a id="657" class="Symbol">(</a><a id="658" href="Codata.Sized.Delay.Bisimilarity.html#658" class="Bound">xs</a> <a id="661" class="Symbol">:</a> <a id="663" href="Codata.Sized.Delay.html#899" class="Datatype">Delay</a> <a id="669" href="Codata.Sized.Delay.Bisimilarity.html#599" class="Bound">A</a> <a id="671" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a><a id="672" class="Symbol">)</a> <a id="674" class="Symbol">(</a><a id="675" href="Codata.Sized.Delay.Bisimilarity.html#675" class="Bound">ys</a> <a id="678" class="Symbol">:</a> <a id="680" href="Codata.Sized.Delay.html#899" class="Datatype">Delay</a> <a id="686" href="Codata.Sized.Delay.Bisimilarity.html#611" class="Bound">B</a> <a id="688" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a><a id="689" class="Symbol">)</a> <a id="691" class="Symbol">→</a> <a id="693" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="697" class="Symbol">(</a><a id="698" href="Codata.Sized.Delay.Bisimilarity.html#591" class="Bound">a</a> <a id="700" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="702" href="Codata.Sized.Delay.Bisimilarity.html#593" class="Bound">b</a> <a id="704" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="706" href="Codata.Sized.Delay.Bisimilarity.html#595" class="Bound">r</a><a id="707" class="Symbol">)</a> <a id="709" class="Keyword">where</a>
  <a id="Bisim.now"></a><a id="717" href="Codata.Sized.Delay.Bisimilarity.html#717" class="InductiveConstructor">now</a>   <a id="723" class="Symbol">:</a> <a id="725" class="Symbol">∀</a> <a id="727" class="Symbol">{</a><a id="728" href="Codata.Sized.Delay.Bisimilarity.html#728" class="Bound">x</a> <a id="730" href="Codata.Sized.Delay.Bisimilarity.html#730" class="Bound">y</a><a id="731" class="Symbol">}</a> <a id="733" class="Symbol">→</a> <a id="735" href="Codata.Sized.Delay.Bisimilarity.html#623" class="Bound">R</a> <a id="737" href="Codata.Sized.Delay.Bisimilarity.html#728" class="Bound">x</a> <a id="739" href="Codata.Sized.Delay.Bisimilarity.html#730" class="Bound">y</a> <a id="741" class="Symbol">→</a> <a id="743" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="749" href="Codata.Sized.Delay.Bisimilarity.html#623" class="Bound">R</a> <a id="751" href="Codata.Sized.Delay.Bisimilarity.html#642" class="Bound">i</a> <a id="753" class="Symbol">(</a><a id="754" href="Codata.Sized.Delay.html#948" class="InductiveConstructor">now</a> <a id="758" href="Codata.Sized.Delay.Bisimilarity.html#728" class="Bound">x</a><a id="759" class="Symbol">)</a> <a id="761" class="Symbol">(</a><a id="762" href="Codata.Sized.Delay.html#948" class="InductiveConstructor">now</a> <a id="766" href="Codata.Sized.Delay.Bisimilarity.html#730" class="Bound">y</a><a id="767" class="Symbol">)</a>
  <a id="Bisim.later"></a><a id="771" href="Codata.Sized.Delay.Bisimilarity.html#771" class="InductiveConstructor">later</a> <a id="777" class="Symbol">:</a> <a id="779" class="Symbol">∀</a> <a id="781" class="Symbol">{</a><a id="782" href="Codata.Sized.Delay.Bisimilarity.html#782" class="Bound">xs</a> <a id="785" href="Codata.Sized.Delay.Bisimilarity.html#785" class="Bound">ys</a><a id="787" class="Symbol">}</a> <a id="789" class="Symbol">→</a> <a id="791" href="Codata.Sized.Thunk.html#759" class="Function">Thunk^R</a> <a id="799" class="Symbol">(</a><a id="800" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="806" href="Codata.Sized.Delay.Bisimilarity.html#623" class="Bound">R</a><a id="807" class="Symbol">)</a> <a id="809" href="Codata.Sized.Delay.Bisimilarity.html#642" class="Bound">i</a> <a id="811" href="Codata.Sized.Delay.Bisimilarity.html#782" class="Bound">xs</a> <a id="814" href="Codata.Sized.Delay.Bisimilarity.html#785" class="Bound">ys</a> <a id="817" class="Symbol">→</a> <a id="819" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="825" href="Codata.Sized.Delay.Bisimilarity.html#623" class="Bound">R</a> <a id="827" href="Codata.Sized.Delay.Bisimilarity.html#642" class="Bound">i</a> <a id="829" class="Symbol">(</a><a id="830" href="Codata.Sized.Delay.html#972" class="InductiveConstructor">later</a> <a id="836" href="Codata.Sized.Delay.Bisimilarity.html#782" class="Bound">xs</a><a id="838" class="Symbol">)</a> <a id="840" class="Symbol">(</a><a id="841" href="Codata.Sized.Delay.html#972" class="InductiveConstructor">later</a> <a id="847" href="Codata.Sized.Delay.Bisimilarity.html#785" class="Bound">ys</a><a id="849" class="Symbol">)</a>

<a id="852" class="Keyword">module</a> <a id="859" href="Codata.Sized.Delay.Bisimilarity.html#859" class="Module">_</a> <a id="861" class="Symbol">{</a><a id="862" href="Codata.Sized.Delay.Bisimilarity.html#862" class="Bound">a</a> <a id="864" href="Codata.Sized.Delay.Bisimilarity.html#864" class="Bound">r</a><a id="865" class="Symbol">}</a> <a id="867" class="Symbol">{</a><a id="868" href="Codata.Sized.Delay.Bisimilarity.html#868" class="Bound">A</a> <a id="870" class="Symbol">:</a> <a id="872" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="876" href="Codata.Sized.Delay.Bisimilarity.html#862" class="Bound">a</a><a id="877" class="Symbol">}</a> <a id="879" class="Symbol">{</a><a id="880" href="Codata.Sized.Delay.Bisimilarity.html#880" class="Bound">R</a> <a id="882" class="Symbol">:</a> <a id="884" href="Codata.Sized.Delay.Bisimilarity.html#868" class="Bound">A</a> <a id="886" class="Symbol">→</a> <a id="888" href="Codata.Sized.Delay.Bisimilarity.html#868" class="Bound">A</a> <a id="890" class="Symbol">→</a> <a id="892" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="896" href="Codata.Sized.Delay.Bisimilarity.html#864" class="Bound">r</a><a id="897" class="Symbol">}</a> <a id="899" class="Keyword">where</a>

 <a id="907" href="Codata.Sized.Delay.Bisimilarity.html#907" class="Function">reflexive</a> <a id="917" class="Symbol">:</a> <a id="919" href="Relation.Binary.Definitions.html#1428" class="Function">Reflexive</a> <a id="929" href="Codata.Sized.Delay.Bisimilarity.html#880" class="Bound">R</a> <a id="931" class="Symbol">→</a> <a id="933" class="Symbol">∀</a> <a id="935" class="Symbol">{</a><a id="936" href="Codata.Sized.Delay.Bisimilarity.html#936" class="Bound">i</a><a id="937" class="Symbol">}</a> <a id="939" class="Symbol">→</a> <a id="941" href="Relation.Binary.Definitions.html#1428" class="Function">Reflexive</a> <a id="951" class="Symbol">(</a><a id="952" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="958" href="Codata.Sized.Delay.Bisimilarity.html#880" class="Bound">R</a> <a id="960" href="Codata.Sized.Delay.Bisimilarity.html#936" class="Bound">i</a><a id="961" class="Symbol">)</a>
 <a id="964" href="Codata.Sized.Delay.Bisimilarity.html#907" class="Function">reflexive</a> <a id="974" href="Codata.Sized.Delay.Bisimilarity.html#974" class="Bound">refl^R</a> <a id="981" class="Symbol">{</a><a id="982" href="Codata.Sized.Delay.Bisimilarity.html#982" class="Bound">i</a><a id="983" class="Symbol">}</a> <a id="985" class="Symbol">{</a><a id="986" href="Codata.Sized.Delay.html#948" class="InductiveConstructor">now</a> <a id="990" href="Codata.Sized.Delay.Bisimilarity.html#990" class="Bound">r</a><a id="991" class="Symbol">}</a>    <a id="996" class="Symbol">=</a> <a id="998" href="Codata.Sized.Delay.Bisimilarity.html#717" class="InductiveConstructor">now</a> <a id="1002" href="Codata.Sized.Delay.Bisimilarity.html#974" class="Bound">refl^R</a>
 <a id="1010" href="Codata.Sized.Delay.Bisimilarity.html#907" class="Function">reflexive</a> <a id="1020" href="Codata.Sized.Delay.Bisimilarity.html#1020" class="Bound">refl^R</a> <a id="1027" class="Symbol">{</a><a id="1028" href="Codata.Sized.Delay.Bisimilarity.html#1028" class="Bound">i</a><a id="1029" class="Symbol">}</a> <a id="1031" class="Symbol">{</a><a id="1032" href="Codata.Sized.Delay.html#972" class="InductiveConstructor">later</a> <a id="1038" href="Codata.Sized.Delay.Bisimilarity.html#1038" class="Bound">rs</a><a id="1040" class="Symbol">}</a> <a id="1042" class="Symbol">=</a> <a id="1044" href="Codata.Sized.Delay.Bisimilarity.html#771" class="InductiveConstructor">later</a> <a id="1050" class="Symbol">λ</a> <a id="1052" class="Keyword">where</a> <a id="1058" class="Symbol">.</a><a id="1059" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1065" class="Symbol">→</a> <a id="1067" href="Codata.Sized.Delay.Bisimilarity.html#907" class="Function">reflexive</a> <a id="1077" href="Codata.Sized.Delay.Bisimilarity.html#1020" class="Bound">refl^R</a>

<a id="1085" class="Keyword">module</a> <a id="1092" href="Codata.Sized.Delay.Bisimilarity.html#1092" class="Module">_</a> <a id="1094" class="Symbol">{</a><a id="1095" href="Codata.Sized.Delay.Bisimilarity.html#1095" class="Bound">a</a> <a id="1097" href="Codata.Sized.Delay.Bisimilarity.html#1097" class="Bound">b</a><a id="1098" class="Symbol">}</a> <a id="1100" class="Symbol">{</a><a id="1101" href="Codata.Sized.Delay.Bisimilarity.html#1101" class="Bound">A</a> <a id="1103" class="Symbol">:</a> <a id="1105" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1109" href="Codata.Sized.Delay.Bisimilarity.html#1095" class="Bound">a</a><a id="1110" class="Symbol">}</a> <a id="1112" class="Symbol">{</a><a id="1113" href="Codata.Sized.Delay.Bisimilarity.html#1113" class="Bound">B</a> <a id="1115" class="Symbol">:</a> <a id="1117" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1121" href="Codata.Sized.Delay.Bisimilarity.html#1097" class="Bound">b</a><a id="1122" class="Symbol">}</a>
         <a id="1133" class="Symbol">{</a><a id="1134" href="Codata.Sized.Delay.Bisimilarity.html#1134" class="Bound">r</a><a id="1135" class="Symbol">}</a> <a id="1137" class="Symbol">{</a><a id="1138" href="Codata.Sized.Delay.Bisimilarity.html#1138" class="Bound">P</a> <a id="1140" class="Symbol">:</a> <a id="1142" href="Codata.Sized.Delay.Bisimilarity.html#1101" class="Bound">A</a> <a id="1144" class="Symbol">→</a> <a id="1146" href="Codata.Sized.Delay.Bisimilarity.html#1113" class="Bound">B</a> <a id="1148" class="Symbol">→</a> <a id="1150" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1154" href="Codata.Sized.Delay.Bisimilarity.html#1134" class="Bound">r</a><a id="1155" class="Symbol">}</a> <a id="1157" class="Symbol">{</a><a id="1158" href="Codata.Sized.Delay.Bisimilarity.html#1158" class="Bound">Q</a> <a id="1160" class="Symbol">:</a> <a id="1162" href="Codata.Sized.Delay.Bisimilarity.html#1113" class="Bound">B</a> <a id="1164" class="Symbol">→</a> <a id="1166" href="Codata.Sized.Delay.Bisimilarity.html#1101" class="Bound">A</a> <a id="1168" class="Symbol">→</a> <a id="1170" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1174" href="Codata.Sized.Delay.Bisimilarity.html#1134" class="Bound">r</a><a id="1175" class="Symbol">}</a> <a id="1177" class="Keyword">where</a>

 <a id="1185" href="Codata.Sized.Delay.Bisimilarity.html#1185" class="Function">symmetric</a> <a id="1195" class="Symbol">:</a> <a id="1197" href="Relation.Binary.Definitions.html#1513" class="Function">Sym</a> <a id="1201" href="Codata.Sized.Delay.Bisimilarity.html#1138" class="Bound">P</a> <a id="1203" href="Codata.Sized.Delay.Bisimilarity.html#1158" class="Bound">Q</a> <a id="1205" class="Symbol">→</a> <a id="1207" class="Symbol">∀</a> <a id="1209" class="Symbol">{</a><a id="1210" href="Codata.Sized.Delay.Bisimilarity.html#1210" class="Bound">i</a><a id="1211" class="Symbol">}</a> <a id="1213" class="Symbol">→</a> <a id="1215" href="Relation.Binary.Definitions.html#1513" class="Function">Sym</a> <a id="1219" class="Symbol">(</a><a id="1220" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="1226" href="Codata.Sized.Delay.Bisimilarity.html#1138" class="Bound">P</a> <a id="1228" href="Codata.Sized.Delay.Bisimilarity.html#1210" class="Bound">i</a><a id="1229" class="Symbol">)</a> <a id="1231" class="Symbol">(</a><a id="1232" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="1238" href="Codata.Sized.Delay.Bisimilarity.html#1158" class="Bound">Q</a> <a id="1240" href="Codata.Sized.Delay.Bisimilarity.html#1210" class="Bound">i</a><a id="1241" class="Symbol">)</a>
 <a id="1244" href="Codata.Sized.Delay.Bisimilarity.html#1185" class="Function">symmetric</a> <a id="1254" href="Codata.Sized.Delay.Bisimilarity.html#1254" class="Bound">sym^PQ</a> <a id="1261" class="Symbol">(</a><a id="1262" href="Codata.Sized.Delay.Bisimilarity.html#717" class="InductiveConstructor">now</a> <a id="1266" href="Codata.Sized.Delay.Bisimilarity.html#1266" class="Bound">p</a><a id="1267" class="Symbol">)</a>    <a id="1272" class="Symbol">=</a> <a id="1274" href="Codata.Sized.Delay.Bisimilarity.html#717" class="InductiveConstructor">now</a> <a id="1278" class="Symbol">(</a><a id="1279" href="Codata.Sized.Delay.Bisimilarity.html#1254" class="Bound">sym^PQ</a> <a id="1286" href="Codata.Sized.Delay.Bisimilarity.html#1266" class="Bound">p</a><a id="1287" class="Symbol">)</a>
 <a id="1290" href="Codata.Sized.Delay.Bisimilarity.html#1185" class="Function">symmetric</a> <a id="1300" href="Codata.Sized.Delay.Bisimilarity.html#1300" class="Bound">sym^PQ</a> <a id="1307" class="Symbol">(</a><a id="1308" href="Codata.Sized.Delay.Bisimilarity.html#771" class="InductiveConstructor">later</a> <a id="1314" href="Codata.Sized.Delay.Bisimilarity.html#1314" class="Bound">ps</a><a id="1316" class="Symbol">)</a> <a id="1318" class="Symbol">=</a> <a id="1320" href="Codata.Sized.Delay.Bisimilarity.html#771" class="InductiveConstructor">later</a> <a id="1326" class="Symbol">λ</a> <a id="1328" class="Keyword">where</a> <a id="1334" class="Symbol">.</a><a id="1335" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1341" class="Symbol">→</a> <a id="1343" href="Codata.Sized.Delay.Bisimilarity.html#1185" class="Function">symmetric</a> <a id="1353" href="Codata.Sized.Delay.Bisimilarity.html#1300" class="Bound">sym^PQ</a> <a id="1360" class="Symbol">(</a><a id="1361" href="Codata.Sized.Delay.Bisimilarity.html#1314" class="Bound">ps</a> <a id="1364" class="Symbol">.</a><a id="1365" href="Codata.Sized.Thunk.html#556" class="Field">force</a><a id="1370" class="Symbol">)</a>

<a id="1373" class="Keyword">module</a> <a id="1380" href="Codata.Sized.Delay.Bisimilarity.html#1380" class="Module">_</a> <a id="1382" class="Symbol">{</a><a id="1383" href="Codata.Sized.Delay.Bisimilarity.html#1383" class="Bound">a</a> <a id="1385" href="Codata.Sized.Delay.Bisimilarity.html#1385" class="Bound">b</a> <a id="1387" href="Codata.Sized.Delay.Bisimilarity.html#1387" class="Bound">c</a><a id="1388" class="Symbol">}</a> <a id="1390" class="Symbol">{</a><a id="1391" href="Codata.Sized.Delay.Bisimilarity.html#1391" class="Bound">A</a> <a id="1393" class="Symbol">:</a> <a id="1395" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1399" href="Codata.Sized.Delay.Bisimilarity.html#1383" class="Bound">a</a><a id="1400" class="Symbol">}</a> <a id="1402" class="Symbol">{</a><a id="1403" href="Codata.Sized.Delay.Bisimilarity.html#1403" class="Bound">B</a> <a id="1405" class="Symbol">:</a> <a id="1407" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1411" href="Codata.Sized.Delay.Bisimilarity.html#1385" class="Bound">b</a><a id="1412" class="Symbol">}</a> <a id="1414" class="Symbol">{</a><a id="1415" href="Codata.Sized.Delay.Bisimilarity.html#1415" class="Bound">C</a> <a id="1417" class="Symbol">:</a> <a id="1419" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1423" href="Codata.Sized.Delay.Bisimilarity.html#1387" class="Bound">c</a><a id="1424" class="Symbol">}</a>
         <a id="1435" class="Symbol">{</a><a id="1436" href="Codata.Sized.Delay.Bisimilarity.html#1436" class="Bound">r</a><a id="1437" class="Symbol">}</a> <a id="1439" class="Symbol">{</a><a id="1440" href="Codata.Sized.Delay.Bisimilarity.html#1440" class="Bound">P</a> <a id="1442" class="Symbol">:</a> <a id="1444" href="Codata.Sized.Delay.Bisimilarity.html#1391" class="Bound">A</a> <a id="1446" class="Symbol">→</a> <a id="1448" href="Codata.Sized.Delay.Bisimilarity.html#1403" class="Bound">B</a> <a id="1450" class="Symbol">→</a> <a id="1452" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1456" href="Codata.Sized.Delay.Bisimilarity.html#1436" class="Bound">r</a><a id="1457" class="Symbol">}</a> <a id="1459" class="Symbol">{</a><a id="1460" href="Codata.Sized.Delay.Bisimilarity.html#1460" class="Bound">Q</a> <a id="1462" class="Symbol">:</a> <a id="1464" href="Codata.Sized.Delay.Bisimilarity.html#1403" class="Bound">B</a> <a id="1466" class="Symbol">→</a> <a id="1468" href="Codata.Sized.Delay.Bisimilarity.html#1415" class="Bound">C</a> <a id="1470" class="Symbol">→</a> <a id="1472" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1476" href="Codata.Sized.Delay.Bisimilarity.html#1436" class="Bound">r</a><a id="1477" class="Symbol">}</a> <a id="1479" class="Symbol">{</a><a id="1480" href="Codata.Sized.Delay.Bisimilarity.html#1480" class="Bound">R</a> <a id="1482" class="Symbol">:</a> <a id="1484" href="Codata.Sized.Delay.Bisimilarity.html#1391" class="Bound">A</a> <a id="1486" class="Symbol">→</a> <a id="1488" href="Codata.Sized.Delay.Bisimilarity.html#1415" class="Bound">C</a> <a id="1490" class="Symbol">→</a> <a id="1492" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1496" href="Codata.Sized.Delay.Bisimilarity.html#1436" class="Bound">r</a><a id="1497" class="Symbol">}</a> <a id="1499" class="Keyword">where</a>

 <a id="1507" href="Codata.Sized.Delay.Bisimilarity.html#1507" class="Function">transitive</a> <a id="1518" class="Symbol">:</a> <a id="1520" href="Relation.Binary.Definitions.html#1674" class="Function">Trans</a> <a id="1526" href="Codata.Sized.Delay.Bisimilarity.html#1440" class="Bound">P</a> <a id="1528" href="Codata.Sized.Delay.Bisimilarity.html#1460" class="Bound">Q</a> <a id="1530" href="Codata.Sized.Delay.Bisimilarity.html#1480" class="Bound">R</a> <a id="1532" class="Symbol">→</a> <a id="1534" class="Symbol">∀</a> <a id="1536" class="Symbol">{</a><a id="1537" href="Codata.Sized.Delay.Bisimilarity.html#1537" class="Bound">i</a><a id="1538" class="Symbol">}</a> <a id="1540" class="Symbol">→</a> <a id="1542" href="Relation.Binary.Definitions.html#1674" class="Function">Trans</a> <a id="1548" class="Symbol">(</a><a id="1549" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="1555" href="Codata.Sized.Delay.Bisimilarity.html#1440" class="Bound">P</a> <a id="1557" href="Codata.Sized.Delay.Bisimilarity.html#1537" class="Bound">i</a><a id="1558" class="Symbol">)</a> <a id="1560" class="Symbol">(</a><a id="1561" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="1567" href="Codata.Sized.Delay.Bisimilarity.html#1460" class="Bound">Q</a> <a id="1569" href="Codata.Sized.Delay.Bisimilarity.html#1537" class="Bound">i</a><a id="1570" class="Symbol">)</a> <a id="1572" class="Symbol">(</a><a id="1573" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="1579" href="Codata.Sized.Delay.Bisimilarity.html#1480" class="Bound">R</a> <a id="1581" href="Codata.Sized.Delay.Bisimilarity.html#1537" class="Bound">i</a><a id="1582" class="Symbol">)</a>
 <a id="1585" href="Codata.Sized.Delay.Bisimilarity.html#1507" class="Function">transitive</a> <a id="1596" href="Codata.Sized.Delay.Bisimilarity.html#1596" class="Bound">trans^PQR</a> <a id="1606" class="Symbol">(</a><a id="1607" href="Codata.Sized.Delay.Bisimilarity.html#717" class="InductiveConstructor">now</a> <a id="1611" href="Codata.Sized.Delay.Bisimilarity.html#1611" class="Bound">p</a><a id="1612" class="Symbol">)</a>    <a id="1617" class="Symbol">(</a><a id="1618" href="Codata.Sized.Delay.Bisimilarity.html#717" class="InductiveConstructor">now</a> <a id="1622" href="Codata.Sized.Delay.Bisimilarity.html#1622" class="Bound">q</a><a id="1623" class="Symbol">)</a>    <a id="1628" class="Symbol">=</a> <a id="1630" href="Codata.Sized.Delay.Bisimilarity.html#717" class="InductiveConstructor">now</a> <a id="1634" class="Symbol">(</a><a id="1635" href="Codata.Sized.Delay.Bisimilarity.html#1596" class="Bound">trans^PQR</a> <a id="1645" href="Codata.Sized.Delay.Bisimilarity.html#1611" class="Bound">p</a> <a id="1647" href="Codata.Sized.Delay.Bisimilarity.html#1622" class="Bound">q</a><a id="1648" class="Symbol">)</a>
 <a id="1651" href="Codata.Sized.Delay.Bisimilarity.html#1507" class="Function">transitive</a> <a id="1662" href="Codata.Sized.Delay.Bisimilarity.html#1662" class="Bound">trans^PQR</a> <a id="1672" class="Symbol">(</a><a id="1673" href="Codata.Sized.Delay.Bisimilarity.html#771" class="InductiveConstructor">later</a> <a id="1679" href="Codata.Sized.Delay.Bisimilarity.html#1679" class="Bound">ps</a><a id="1681" class="Symbol">)</a> <a id="1683" class="Symbol">(</a><a id="1684" href="Codata.Sized.Delay.Bisimilarity.html#771" class="InductiveConstructor">later</a> <a id="1690" href="Codata.Sized.Delay.Bisimilarity.html#1690" class="Bound">qs</a><a id="1692" class="Symbol">)</a> <a id="1694" class="Symbol">=</a>
   <a id="1699" href="Codata.Sized.Delay.Bisimilarity.html#771" class="InductiveConstructor">later</a> <a id="1705" class="Symbol">λ</a> <a id="1707" class="Keyword">where</a> <a id="1713" class="Symbol">.</a><a id="1714" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1720" class="Symbol">→</a> <a id="1722" href="Codata.Sized.Delay.Bisimilarity.html#1507" class="Function">transitive</a> <a id="1733" href="Codata.Sized.Delay.Bisimilarity.html#1662" class="Bound">trans^PQR</a> <a id="1743" class="Symbol">(</a><a id="1744" href="Codata.Sized.Delay.Bisimilarity.html#1679" class="Bound">ps</a> <a id="1747" class="Symbol">.</a><a id="1748" href="Codata.Sized.Thunk.html#556" class="Field">force</a><a id="1753" class="Symbol">)</a> <a id="1755" class="Symbol">(</a><a id="1756" href="Codata.Sized.Delay.Bisimilarity.html#1690" class="Bound">qs</a> <a id="1759" class="Symbol">.</a><a id="1760" href="Codata.Sized.Thunk.html#556" class="Field">force</a><a id="1765" class="Symbol">)</a>


<a id="1769" class="Comment">-- Pointwise Equality as a Bisimilarity</a>
<a id="1809" class="Comment">------------------------------------------------------------------------</a>

<a id="1883" class="Keyword">module</a> <a id="1890" href="Codata.Sized.Delay.Bisimilarity.html#1890" class="Module">_</a> <a id="1892" class="Symbol">{</a><a id="1893" href="Codata.Sized.Delay.Bisimilarity.html#1893" class="Bound">ℓ</a><a id="1894" class="Symbol">}</a> <a id="1896" class="Symbol">{</a><a id="1897" href="Codata.Sized.Delay.Bisimilarity.html#1897" class="Bound">A</a> <a id="1899" class="Symbol">:</a> <a id="1901" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1905" href="Codata.Sized.Delay.Bisimilarity.html#1893" class="Bound">ℓ</a><a id="1906" class="Symbol">}</a> <a id="1908" class="Keyword">where</a>

 <a id="1916" class="Keyword">infix</a> <a id="1922" class="Number">1</a> <a id="1924" href="Codata.Sized.Delay.Bisimilarity.html#1931" class="Function Operator">_⊢_≈_</a>
 <a id="1931" href="Codata.Sized.Delay.Bisimilarity.html#1931" class="Function Operator">_⊢_≈_</a> <a id="1937" class="Symbol">:</a> <a id="1939" class="Symbol">∀</a> <a id="1941" href="Codata.Sized.Delay.Bisimilarity.html#1941" class="Bound">i</a> <a id="1943" class="Symbol">→</a> <a id="1945" href="Codata.Sized.Delay.html#899" class="Datatype">Delay</a> <a id="1951" href="Codata.Sized.Delay.Bisimilarity.html#1897" class="Bound">A</a> <a id="1953" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a> <a id="1955" class="Symbol">→</a> <a id="1957" href="Codata.Sized.Delay.html#899" class="Datatype">Delay</a> <a id="1963" href="Codata.Sized.Delay.Bisimilarity.html#1897" class="Bound">A</a> <a id="1965" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a> <a id="1967" class="Symbol">→</a> <a id="1969" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1973" href="Codata.Sized.Delay.Bisimilarity.html#1893" class="Bound">ℓ</a>
 <a id="1976" href="Codata.Sized.Delay.Bisimilarity.html#1931" class="Function Operator">_⊢_≈_</a> <a id="1982" class="Symbol">=</a> <a id="1984" href="Codata.Sized.Delay.Bisimilarity.html#584" class="Datatype">Bisim</a> <a id="1990" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">_≡_</a>

 <a id="1996" href="Codata.Sized.Delay.Bisimilarity.html#1996" class="Function">refl</a> <a id="2001" class="Symbol">:</a> <a id="2003" class="Symbol">∀</a> <a id="2005" class="Symbol">{</a><a id="2006" href="Codata.Sized.Delay.Bisimilarity.html#2006" class="Bound">i</a><a id="2007" class="Symbol">}</a> <a id="2009" class="Symbol">→</a> <a id="2011" href="Relation.Binary.Definitions.html#1428" class="Function">Reflexive</a> <a id="2021" class="Symbol">(</a><a id="2022" href="Codata.Sized.Delay.Bisimilarity.html#2006" class="Bound">i</a> <a id="2024" href="Codata.Sized.Delay.Bisimilarity.html#1931" class="Function Operator">⊢_≈_</a><a id="2028" class="Symbol">)</a>
 <a id="2031" href="Codata.Sized.Delay.Bisimilarity.html#1996" class="Function">refl</a> <a id="2036" class="Symbol">=</a> <a id="2038" href="Codata.Sized.Delay.Bisimilarity.html#907" class="Function">reflexive</a> <a id="2048" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">Eq.refl</a>

 <a id="2058" href="Codata.Sized.Delay.Bisimilarity.html#2058" class="Function">sym</a> <a id="2062" class="Symbol">:</a> <a id="2064" class="Symbol">∀</a> <a id="2066" class="Symbol">{</a><a id="2067" href="Codata.Sized.Delay.Bisimilarity.html#2067" class="Bound">i</a><a id="2068" class="Symbol">}</a> <a id="2070" class="Symbol">→</a> <a id="2072" href="Relation.Binary.Definitions.html#1587" class="Function">Symmetric</a> <a id="2082" class="Symbol">(</a><a id="2083" href="Codata.Sized.Delay.Bisimilarity.html#2067" class="Bound">i</a> <a id="2085" href="Codata.Sized.Delay.Bisimilarity.html#1931" class="Function Operator">⊢_≈_</a><a id="2089" class="Symbol">)</a>
 <a id="2092" href="Codata.Sized.Delay.Bisimilarity.html#2058" class="Function">sym</a> <a id="2096" class="Symbol">=</a> <a id="2098" href="Codata.Sized.Delay.Bisimilarity.html#1185" class="Function">symmetric</a> <a id="2108" href="Relation.Binary.PropositionalEquality.Core.html#1712" class="Function">Eq.sym</a>

 <a id="2117" href="Codata.Sized.Delay.Bisimilarity.html#2117" class="Function">trans</a> <a id="2123" class="Symbol">:</a> <a id="2125" class="Symbol">∀</a> <a id="2127" class="Symbol">{</a><a id="2128" href="Codata.Sized.Delay.Bisimilarity.html#2128" class="Bound">i</a><a id="2129" class="Symbol">}</a> <a id="2131" class="Symbol">→</a> <a id="2133" href="Relation.Binary.Definitions.html#2103" class="Function">Transitive</a> <a id="2144" class="Symbol">(</a><a id="2145" href="Codata.Sized.Delay.Bisimilarity.html#2128" class="Bound">i</a> <a id="2147" href="Codata.Sized.Delay.Bisimilarity.html#1931" class="Function Operator">⊢_≈_</a><a id="2151" class="Symbol">)</a>
 <a id="2154" href="Codata.Sized.Delay.Bisimilarity.html#2117" class="Function">trans</a> <a id="2160" class="Symbol">=</a> <a id="2162" href="Codata.Sized.Delay.Bisimilarity.html#1507" class="Function">transitive</a> <a id="2173" href="Relation.Binary.PropositionalEquality.Core.html#1757" class="Function">Eq.trans</a>
</pre></body></html>
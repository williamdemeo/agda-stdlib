<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Tactic.RingSolver.NonReflective</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- An implementation of the ring solver that requires you to manually</a>
<a id="176" class="Comment">-- pass the equation you wish to solve.</a>
<a id="216" class="Comment">------------------------------------------------------------------------</a>

<a id="290" class="Comment">-- You&#39;ll probably want to use `Tactic.RingSolver` instead which uses</a>
<a id="360" class="Comment">-- reflection to automatically extract the equation.</a>

<a id="414" class="Symbol">{-#</a> <a id="418" class="Keyword">OPTIONS</a> <a id="426" class="Pragma">--cubical-compatible</a> <a id="447" class="Pragma">--safe</a> <a id="454" class="Symbol">#-}</a>

<a id="459" class="Keyword">open</a> <a id="464" class="Keyword">import</a> <a id="471" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html" class="Module">Tactic.RingSolver.Core.AlmostCommutativeRing</a>

<a id="517" class="Keyword">module</a> <a id="524" href="Tactic.RingSolver.NonReflective.html" class="Module">Tactic.RingSolver.NonReflective</a>
  <a id="558" class="Symbol">{</a><a id="559" href="Tactic.RingSolver.NonReflective.html#559" class="Bound">ℓ₁</a> <a id="562" href="Tactic.RingSolver.NonReflective.html#562" class="Bound">ℓ₂</a><a id="564" class="Symbol">}</a> <a id="566" class="Symbol">(</a><a id="567" href="Tactic.RingSolver.NonReflective.html#567" class="Bound">ring</a> <a id="572" class="Symbol">:</a> <a id="574" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1288" class="Record">AlmostCommutativeRing</a> <a id="596" href="Tactic.RingSolver.NonReflective.html#559" class="Bound">ℓ₁</a> <a id="599" href="Tactic.RingSolver.NonReflective.html#562" class="Bound">ℓ₂</a><a id="601" class="Symbol">)</a>
  <a id="605" class="Symbol">(</a><a id="606" class="Keyword">let</a> <a id="610" class="Keyword">open</a> <a id="615" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1288" class="Module">AlmostCommutativeRing</a> <a id="637" href="Tactic.RingSolver.NonReflective.html#567" class="Bound">ring</a><a id="641" class="Symbol">)</a>
  <a id="645" class="Keyword">where</a>

<a id="652" class="Keyword">open</a> <a id="657" class="Keyword">import</a> <a id="664" href="Algebra.Morphism.html" class="Module">Algebra.Morphism</a>
<a id="681" class="Keyword">open</a> <a id="686" class="Keyword">import</a> <a id="693" href="Function.Base.html" class="Module">Function.Base</a> <a id="707" class="Keyword">using</a> <a id="713" class="Symbol">(</a><a id="714" href="Function.Base.html#704" class="Function">id</a><a id="716" class="Symbol">;</a> <a id="718" href="Function.Base.html#4322" class="Function Operator">_⟨_⟩_</a><a id="723" class="Symbol">)</a>
<a id="725" class="Keyword">open</a> <a id="730" class="Keyword">import</a> <a id="737" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="752" class="Keyword">using</a> <a id="758" class="Symbol">(</a><a id="759" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a><a id="763" class="Symbol">;</a> <a id="765" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a><a id="769" class="Symbol">;</a> <a id="771" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a><a id="776" class="Symbol">;</a> <a id="778" href="Data.Bool.Base.html#1348" class="Function">T</a><a id="779" class="Symbol">;</a> <a id="781" href="Data.Bool.Base.html#1505" class="Function Operator">if_then_else_</a><a id="794" class="Symbol">)</a>
<a id="796" class="Keyword">open</a> <a id="801" class="Keyword">import</a> <a id="808" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a>
<a id="824" class="Keyword">open</a> <a id="829" class="Keyword">import</a> <a id="836" href="Data.Empty.html" class="Module">Data.Empty</a> <a id="847" class="Keyword">using</a> <a id="853" class="Symbol">(</a><a id="854" href="Data.Empty.html#1050" class="Function">⊥-elim</a><a id="860" class="Symbol">)</a>
<a id="862" class="Keyword">open</a> <a id="867" class="Keyword">import</a> <a id="874" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="888" class="Keyword">using</a> <a id="894" class="Symbol">(</a><a id="895" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="896" class="Symbol">)</a>
<a id="898" class="Keyword">open</a> <a id="903" class="Keyword">import</a> <a id="910" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="928" class="Keyword">using</a> <a id="934" class="Symbol">(</a><a id="935" href="Data.Product.Base.html#1618" class="Function Operator">_×_</a><a id="938" class="Symbol">;</a> <a id="940" href="Data.Product.Base.html#636" class="Field">proj₁</a><a id="945" class="Symbol">;</a> <a id="947" href="Data.Product.Base.html#650" class="Field">proj₂</a><a id="952" class="Symbol">;</a> <a id="954" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="957" class="Symbol">)</a>
<a id="959" class="Keyword">open</a> <a id="964" class="Keyword">import</a> <a id="971" href="Data.Vec.Base.html" class="Module">Data.Vec.Base</a> <a id="985" class="Keyword">using</a> <a id="991" class="Symbol">(</a><a id="992" href="Data.Vec.Base.html#1111" class="Datatype">Vec</a><a id="995" class="Symbol">)</a>
<a id="997" class="Keyword">open</a> <a id="1002" class="Keyword">import</a> <a id="1009" href="Data.Vec.N-ary.html" class="Module">Data.Vec.N-ary</a>

<a id="1025" class="Keyword">open</a> <a id="1030" class="Keyword">import</a> <a id="1037" href="Tactic.RingSolver.Core.Polynomial.Parameters.html" class="Module">Tactic.RingSolver.Core.Polynomial.Parameters</a>
<a id="1082" class="Keyword">open</a> <a id="1087" class="Keyword">import</a> <a id="1094" href="Tactic.RingSolver.Core.Expression.html" class="Module">Tactic.RingSolver.Core.Expression</a> <a id="1128" class="Keyword">public</a>
<a id="1135" class="Keyword">open</a> <a id="1140" class="Keyword">import</a> <a id="1147" href="Algebra.Properties.Semiring.Exp.TCOptimised.html" class="Module">Algebra.Properties.Semiring.Exp.TCOptimised</a> <a id="1191" href="Algebra.Bundles.html#17932" class="Function">semiring</a>

<a id="1201" class="Keyword">module</a> <a id="Ops"></a><a id="1208" href="Tactic.RingSolver.NonReflective.html#1208" class="Module">Ops</a> <a id="1212" class="Keyword">where</a>
  <a id="Ops.zero-homo"></a><a id="1220" href="Tactic.RingSolver.NonReflective.html#1220" class="Function">zero-homo</a> <a id="1230" class="Symbol">:</a> <a id="1232" class="Symbol">∀</a> <a id="1234" href="Tactic.RingSolver.NonReflective.html#1234" class="Bound">x</a> <a id="1236" class="Symbol">→</a> <a id="1238" href="Data.Bool.Base.html#1348" class="Function">T</a> <a id="1240" class="Symbol">(</a><a id="1241" href="Data.Maybe.Base.html#1110" class="Function">is-just</a> <a id="1249" class="Symbol">(</a><a id="1250" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1670" class="Field Operator">0≟</a> <a id="1253" href="Tactic.RingSolver.NonReflective.html#1234" class="Bound">x</a><a id="1254" class="Symbol">))</a> <a id="1257" class="Symbol">→</a> <a id="1259" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1632" class="Field">0#</a> <a id="1262" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1462" class="Field Operator">≈</a> <a id="1264" href="Tactic.RingSolver.NonReflective.html#1234" class="Bound">x</a>
  <a id="1268" href="Tactic.RingSolver.NonReflective.html#1220" class="Function">zero-homo</a> <a id="1278" href="Tactic.RingSolver.NonReflective.html#1278" class="Bound">x</a> <a id="1280" class="Symbol">_</a> <a id="1282" class="Keyword">with</a> <a id="1287" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1670" class="Field Operator">0≟</a> <a id="1290" href="Tactic.RingSolver.NonReflective.html#1278" class="Bound">x</a>
  <a id="1294" href="Tactic.RingSolver.NonReflective.html#1220" class="Function">zero-homo</a> <a id="1304" href="Tactic.RingSolver.NonReflective.html#1304" class="Bound">x</a> <a id="1306" class="Symbol">_</a>  <a id="1309" class="Symbol">|</a> <a id="1311" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor">just</a> <a id="1316" href="Tactic.RingSolver.NonReflective.html#1316" class="Bound">p</a> <a id="1318" class="Symbol">=</a> <a id="1320" href="Tactic.RingSolver.NonReflective.html#1316" class="Bound">p</a>
  <a id="1324" href="Tactic.RingSolver.NonReflective.html#1220" class="Function">zero-homo</a> <a id="1334" href="Tactic.RingSolver.NonReflective.html#1334" class="Bound">x</a> <a id="1336" class="Symbol">()</a> <a id="1339" class="Symbol">|</a> <a id="1341" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">nothing</a>

  <a id="Ops.homo"></a><a id="1352" href="Tactic.RingSolver.NonReflective.html#1352" class="Function">homo</a> <a id="1357" class="Symbol">:</a> <a id="1359" href="Tactic.RingSolver.Core.Polynomial.Parameters.html#1092" class="Record">Homomorphism</a> <a id="1372" href="Tactic.RingSolver.NonReflective.html#559" class="Bound">ℓ₁</a> <a id="1375" href="Tactic.RingSolver.NonReflective.html#562" class="Bound">ℓ₂</a> <a id="1378" href="Tactic.RingSolver.NonReflective.html#559" class="Bound">ℓ₁</a> <a id="1381" href="Tactic.RingSolver.NonReflective.html#562" class="Bound">ℓ₂</a>
  <a id="1386" href="Tactic.RingSolver.NonReflective.html#1352" class="Function">homo</a> <a id="1391" class="Symbol">=</a> <a id="1393" class="Keyword">record</a>
    <a id="1404" class="Symbol">{</a> <a id="1406" href="Tactic.RingSolver.Core.Polynomial.Parameters.html#1167" class="Field">from</a> <a id="1411" class="Symbol">=</a> <a id="1413" class="Keyword">record</a>
      <a id="1426" class="Symbol">{</a> <a id="1428" href="Tactic.RingSolver.Core.Polynomial.Parameters.html#896" class="Field">rawRing</a> <a id="1436" class="Symbol">=</a> <a id="1438" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#2304" class="Function">AlmostCommutativeRing.rawRing</a> <a id="1468" href="Tactic.RingSolver.NonReflective.html#567" class="Bound">ring</a>
      <a id="1479" class="Symbol">;</a> <a id="1481" href="Tactic.RingSolver.Core.Polynomial.Parameters.html#924" class="Field">isZero</a>  <a id="1489" class="Symbol">=</a> <a id="1491" class="Symbol">λ</a> <a id="1493" href="Tactic.RingSolver.NonReflective.html#1493" class="Bound">x</a> <a id="1495" class="Symbol">→</a> <a id="1497" href="Data.Maybe.Base.html#1110" class="Function">is-just</a> <a id="1505" class="Symbol">(</a><a id="1506" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1670" class="Field Operator">0≟</a> <a id="1509" href="Tactic.RingSolver.NonReflective.html#1493" class="Bound">x</a><a id="1510" class="Symbol">)</a>
      <a id="1518" class="Symbol">}</a>
    <a id="1524" class="Symbol">;</a> <a id="1526" href="Tactic.RingSolver.Core.Polynomial.Parameters.html#1193" class="Field">to</a> <a id="1529" class="Symbol">=</a> <a id="1531" class="Keyword">record</a>
      <a id="1544" class="Symbol">{</a> <a id="1546" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1769" class="Field">isAlmostCommutativeRing</a> <a id="1570" class="Symbol">=</a> <a id="1572" class="Keyword">record</a>
          <a id="1589" class="Symbol">{</a> <a id="1591" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#927" class="Field">isCommutativeSemiring</a> <a id="1613" class="Symbol">=</a> <a id="1615" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#927" class="Function">isCommutativeSemiring</a>
          <a id="1647" class="Symbol">;</a> <a id="1649" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#995" class="Field">-‿cong</a>       <a id="1662" class="Symbol">=</a> <a id="1664" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#995" class="Function">-‿cong</a>
          <a id="1681" class="Symbol">;</a> <a id="1683" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1046" class="Field">-‿*-distribˡ</a> <a id="1696" class="Symbol">=</a> <a id="1698" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1046" class="Function">-‿*-distribˡ</a>
          <a id="1721" class="Symbol">;</a> <a id="1723" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1112" class="Field">-‿+-comm</a>     <a id="1736" class="Symbol">=</a> <a id="1738" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1112" class="Function">-‿+-comm</a>
          <a id="1757" class="Symbol">}</a>
      <a id="1765" class="Symbol">}</a>
    <a id="1771" class="Symbol">;</a> <a id="1773" href="Tactic.RingSolver.Core.Polynomial.Parameters.html#1310" class="Field">morphism</a>      <a id="1787" class="Symbol">=</a> <a id="1789" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#3211" class="Function">-raw-almostCommutative⟶</a> <a id="1813" href="Tactic.RingSolver.NonReflective.html#567" class="Bound">ring</a>
    <a id="1822" class="Symbol">;</a> <a id="1824" href="Tactic.RingSolver.Core.Polynomial.Parameters.html#1444" class="Field">Zero-C⟶Zero-R</a> <a id="1838" class="Symbol">=</a> <a id="1840" href="Tactic.RingSolver.NonReflective.html#1220" class="Function">zero-homo</a>
    <a id="1854" class="Symbol">}</a>
  <a id="1858" class="Keyword">open</a> <a id="1863" href="Tactic.RingSolver.Core.Expression.html#883" class="Module">Eval</a> <a id="1868" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#2304" class="Function">rawRing</a> <a id="1876" href="Function.Base.html#704" class="Function">id</a> <a id="1879" class="Keyword">public</a>

  <a id="1889" class="Keyword">open</a> <a id="1894" class="Keyword">import</a> <a id="1901" href="Tactic.RingSolver.Core.Polynomial.Base.html" class="Module">Tactic.RingSolver.Core.Polynomial.Base</a> <a id="1940" class="Symbol">(</a><a id="1941" href="Tactic.RingSolver.Core.Polynomial.Parameters.html#1167" class="Field">Homomorphism.from</a> <a id="1959" href="Tactic.RingSolver.NonReflective.html#1352" class="Function">homo</a><a id="1963" class="Symbol">)</a>

  <a id="Ops.norm"></a><a id="1968" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="1973" class="Symbol">:</a> <a id="1975" class="Symbol">∀</a> <a id="1977" class="Symbol">{</a><a id="1978" href="Tactic.RingSolver.NonReflective.html#1978" class="Bound">n</a><a id="1979" class="Symbol">}</a> <a id="1981" class="Symbol">→</a> <a id="1983" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="1988" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="1996" href="Tactic.RingSolver.NonReflective.html#1978" class="Bound">n</a> <a id="1998" class="Symbol">→</a> <a id="2000" href="Tactic.RingSolver.Core.Polynomial.Base.html#4207" class="Record">Poly</a> <a id="2005" href="Tactic.RingSolver.NonReflective.html#1978" class="Bound">n</a>
  <a id="2009" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2014" class="Symbol">(</a><a id="2015" href="Tactic.RingSolver.Core.Expression.html#558" class="InductiveConstructor">Κ</a> <a id="2017" href="Tactic.RingSolver.NonReflective.html#2017" class="Bound">x</a><a id="2018" class="Symbol">)</a>   <a id="2022" class="Symbol">=</a> <a id="2024" href="Tactic.RingSolver.Core.Polynomial.Base.html#13101" class="Function">κ</a> <a id="2026" href="Tactic.RingSolver.NonReflective.html#2017" class="Bound">x</a>
  <a id="2030" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2035" class="Symbol">(</a><a id="2036" href="Tactic.RingSolver.Core.Expression.html#609" class="InductiveConstructor">Ι</a> <a id="2038" href="Tactic.RingSolver.NonReflective.html#2038" class="Bound">x</a><a id="2039" class="Symbol">)</a>   <a id="2043" class="Symbol">=</a> <a id="2045" href="Tactic.RingSolver.Core.Polynomial.Base.html#13179" class="Function">ι</a> <a id="2047" href="Tactic.RingSolver.NonReflective.html#2038" class="Bound">x</a>
  <a id="2051" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2056" class="Symbol">(</a><a id="2057" href="Tactic.RingSolver.NonReflective.html#2057" class="Bound">x</a> <a id="2059" href="Tactic.RingSolver.Core.Expression.html#660" class="InductiveConstructor Operator">⊕</a> <a id="2061" href="Tactic.RingSolver.NonReflective.html#2061" class="Bound">y</a><a id="2062" class="Symbol">)</a> <a id="2064" class="Symbol">=</a> <a id="2066" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2071" href="Tactic.RingSolver.NonReflective.html#2057" class="Bound">x</a> <a id="2073" href="Tactic.RingSolver.Core.Polynomial.Base.html#8851" class="Function Operator">⊞</a> <a id="2075" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2080" href="Tactic.RingSolver.NonReflective.html#2061" class="Bound">y</a>
  <a id="2084" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2089" class="Symbol">(</a><a id="2090" href="Tactic.RingSolver.NonReflective.html#2090" class="Bound">x</a> <a id="2092" href="Tactic.RingSolver.Core.Expression.html#711" class="InductiveConstructor Operator">⊗</a> <a id="2094" href="Tactic.RingSolver.NonReflective.html#2094" class="Bound">y</a><a id="2095" class="Symbol">)</a> <a id="2097" class="Symbol">=</a> <a id="2099" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2104" href="Tactic.RingSolver.NonReflective.html#2090" class="Bound">x</a> <a id="2106" href="Tactic.RingSolver.Core.Polynomial.Base.html#12881" class="Function Operator">⊠</a> <a id="2108" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2113" href="Tactic.RingSolver.NonReflective.html#2094" class="Bound">y</a>
  <a id="2117" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2122" class="Symbol">(</a><a id="2123" href="Tactic.RingSolver.Core.Expression.html#825" class="InductiveConstructor Operator">⊝</a> <a id="2125" href="Tactic.RingSolver.NonReflective.html#2125" class="Bound">x</a><a id="2126" class="Symbol">)</a>   <a id="2130" class="Symbol">=</a> <a id="2132" href="Tactic.RingSolver.Core.Polynomial.Base.html#10729" class="Function Operator">⊟</a> <a id="2134" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2139" href="Tactic.RingSolver.NonReflective.html#2125" class="Bound">x</a>
  <a id="2143" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2148" class="Symbol">(</a><a id="2149" href="Tactic.RingSolver.NonReflective.html#2149" class="Bound">x</a> <a id="2151" href="Tactic.RingSolver.Core.Expression.html#768" class="InductiveConstructor Operator">⊛</a> <a id="2153" href="Tactic.RingSolver.NonReflective.html#2153" class="Bound">i</a><a id="2154" class="Symbol">)</a> <a id="2156" class="Symbol">=</a> <a id="2158" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2163" href="Tactic.RingSolver.NonReflective.html#2149" class="Bound">x</a> <a id="2165" href="Tactic.RingSolver.Core.Polynomial.Base.html#13799" class="Function Operator">⊡</a> <a id="2167" href="Tactic.RingSolver.NonReflective.html#2153" class="Bound">i</a>

  <a id="Ops.⟦_⇓⟧"></a><a id="2172" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">⟦_⇓⟧</a> <a id="2177" class="Symbol">:</a> <a id="2179" class="Symbol">∀</a> <a id="2181" class="Symbol">{</a><a id="2182" href="Tactic.RingSolver.NonReflective.html#2182" class="Bound">n</a><a id="2183" class="Symbol">}</a> <a id="2185" class="Symbol">→</a> <a id="2187" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="2192" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="2200" href="Tactic.RingSolver.NonReflective.html#2182" class="Bound">n</a> <a id="2202" class="Symbol">→</a> <a id="2204" href="Data.Vec.Base.html#1111" class="Datatype">Vec</a> <a id="2208" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="2216" href="Tactic.RingSolver.NonReflective.html#2182" class="Bound">n</a> <a id="2218" class="Symbol">→</a> <a id="2220" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a>
  <a id="2230" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">⟦</a> <a id="2232" href="Tactic.RingSolver.NonReflective.html#2232" class="Bound">expr</a> <a id="2237" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">⇓⟧</a> <a id="2240" class="Symbol">=</a> <a id="2242" href="Tactic.RingSolver.NonReflective.html#2352" class="Function Operator">⟦</a> <a id="2244" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2249" href="Tactic.RingSolver.NonReflective.html#2232" class="Bound">expr</a> <a id="2254" href="Tactic.RingSolver.NonReflective.html#2352" class="Function Operator">⟧ₚ</a> <a id="2257" class="Keyword">where</a>

    <a id="2268" class="Keyword">open</a> <a id="2273" class="Keyword">import</a> <a id="2280" href="Tactic.RingSolver.Core.Polynomial.Semantics.html" class="Module">Tactic.RingSolver.Core.Polynomial.Semantics</a> <a id="2324" href="Tactic.RingSolver.NonReflective.html#1352" class="Function">homo</a>
      <a id="2335" class="Keyword">renaming</a> <a id="2344" class="Symbol">(</a><a id="2345" href="Tactic.RingSolver.Core.Polynomial.Semantics.html#2009" class="Function Operator">⟦_⟧</a> <a id="2349" class="Symbol">to</a> <a id="2352" class="Function Operator">⟦_⟧ₚ</a><a id="2356" class="Symbol">)</a>

  <a id="Ops.correct"></a><a id="2361" href="Tactic.RingSolver.NonReflective.html#2361" class="Function">correct</a> <a id="2369" class="Symbol">:</a> <a id="2371" class="Symbol">∀</a> <a id="2373" class="Symbol">{</a><a id="2374" href="Tactic.RingSolver.NonReflective.html#2374" class="Bound">n</a><a id="2375" class="Symbol">}</a> <a id="2377" class="Symbol">(</a><a id="2378" href="Tactic.RingSolver.NonReflective.html#2378" class="Bound">expr</a> <a id="2383" class="Symbol">:</a> <a id="2385" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="2390" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="2398" href="Tactic.RingSolver.NonReflective.html#2374" class="Bound">n</a><a id="2399" class="Symbol">)</a> <a id="2401" href="Tactic.RingSolver.NonReflective.html#2401" class="Bound">ρ</a> <a id="2403" class="Symbol">→</a> <a id="2405" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">⟦</a> <a id="2407" href="Tactic.RingSolver.NonReflective.html#2378" class="Bound">expr</a> <a id="2412" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">⇓⟧</a> <a id="2415" href="Tactic.RingSolver.NonReflective.html#2401" class="Bound">ρ</a> <a id="2417" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1462" class="Field Operator">≈</a> <a id="2419" href="Tactic.RingSolver.Core.Expression.html#1073" class="Function Operator">⟦</a> <a id="2421" href="Tactic.RingSolver.NonReflective.html#2378" class="Bound">expr</a> <a id="2426" href="Tactic.RingSolver.Core.Expression.html#1073" class="Function Operator">⟧</a> <a id="2428" href="Tactic.RingSolver.NonReflective.html#2401" class="Bound">ρ</a>
  <a id="2432" href="Tactic.RingSolver.NonReflective.html#2361" class="Function">correct</a> <a id="2440" class="Symbol">{</a><a id="2441" class="Argument">n</a> <a id="2443" class="Symbol">=</a> <a id="2445" href="Tactic.RingSolver.NonReflective.html#2445" class="Bound">n</a><a id="2446" class="Symbol">}</a> <a id="2448" class="Symbol">=</a> <a id="2450" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a>
    <a id="2457" class="Keyword">where</a>
    <a id="2467" class="Keyword">open</a> <a id="2472" class="Keyword">import</a> <a id="2479" href="Tactic.RingSolver.Core.Polynomial.Homomorphism.html" class="Module">Tactic.RingSolver.Core.Polynomial.Homomorphism</a> <a id="2526" href="Tactic.RingSolver.NonReflective.html#1352" class="Function">homo</a>

    <a id="2536" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2539" class="Symbol">:</a> <a id="2541" class="Symbol">∀</a> <a id="2543" class="Symbol">(</a><a id="2544" href="Tactic.RingSolver.NonReflective.html#2544" class="Bound">expr</a> <a id="2549" class="Symbol">:</a> <a id="2551" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="2556" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="2564" href="Tactic.RingSolver.NonReflective.html#2445" class="Bound">n</a><a id="2565" class="Symbol">)</a> <a id="2567" href="Tactic.RingSolver.NonReflective.html#2567" class="Bound">ρ</a> <a id="2569" class="Symbol">→</a> <a id="2571" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">⟦</a> <a id="2573" href="Tactic.RingSolver.NonReflective.html#2544" class="Bound">expr</a> <a id="2578" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">⇓⟧</a> <a id="2581" href="Tactic.RingSolver.NonReflective.html#2567" class="Bound">ρ</a> <a id="2583" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1462" class="Field Operator">≈</a> <a id="2585" href="Tactic.RingSolver.Core.Expression.html#1073" class="Function Operator">⟦</a> <a id="2587" href="Tactic.RingSolver.NonReflective.html#2544" class="Bound">expr</a> <a id="2592" href="Tactic.RingSolver.Core.Expression.html#1073" class="Function Operator">⟧</a> <a id="2594" href="Tactic.RingSolver.NonReflective.html#2567" class="Bound">ρ</a>
    <a id="2600" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2603" class="Symbol">(</a><a id="2604" href="Tactic.RingSolver.Core.Expression.html#558" class="InductiveConstructor">Κ</a> <a id="2606" href="Tactic.RingSolver.NonReflective.html#2606" class="Bound">x</a><a id="2607" class="Symbol">)</a>   <a id="2611" href="Tactic.RingSolver.NonReflective.html#2611" class="Bound">ρ</a> <a id="2613" class="Symbol">=</a> <a id="2615" href="Tactic.RingSolver.Core.Polynomial.Homomorphism.Constants.html#663" class="Function">κ-hom</a> <a id="2621" href="Tactic.RingSolver.NonReflective.html#2606" class="Bound">x</a> <a id="2623" href="Tactic.RingSolver.NonReflective.html#2611" class="Bound">ρ</a>
    <a id="2629" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2632" class="Symbol">(</a><a id="2633" href="Tactic.RingSolver.Core.Expression.html#609" class="InductiveConstructor">Ι</a> <a id="2635" href="Tactic.RingSolver.NonReflective.html#2635" class="Bound">x</a><a id="2636" class="Symbol">)</a>   <a id="2640" href="Tactic.RingSolver.NonReflective.html#2640" class="Bound">ρ</a> <a id="2642" class="Symbol">=</a> <a id="2644" href="Tactic.RingSolver.Core.Polynomial.Homomorphism.Variables.html#953" class="Function">ι-hom</a> <a id="2650" href="Tactic.RingSolver.NonReflective.html#2635" class="Bound">x</a> <a id="2652" href="Tactic.RingSolver.NonReflective.html#2640" class="Bound">ρ</a>
    <a id="2658" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2661" class="Symbol">(</a><a id="2662" href="Tactic.RingSolver.NonReflective.html#2662" class="Bound">x</a> <a id="2664" href="Tactic.RingSolver.Core.Expression.html#660" class="InductiveConstructor Operator">⊕</a> <a id="2666" href="Tactic.RingSolver.NonReflective.html#2666" class="Bound">y</a><a id="2667" class="Symbol">)</a> <a id="2669" href="Tactic.RingSolver.NonReflective.html#2669" class="Bound">ρ</a> <a id="2671" class="Symbol">=</a> <a id="2673" href="Tactic.RingSolver.Core.Polynomial.Homomorphism.Addition.html#1290" class="Function">⊞-hom</a> <a id="2679" class="Symbol">(</a><a id="2680" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2685" href="Tactic.RingSolver.NonReflective.html#2662" class="Bound">x</a><a id="2686" class="Symbol">)</a> <a id="2688" class="Symbol">(</a><a id="2689" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2694" href="Tactic.RingSolver.NonReflective.html#2666" class="Bound">y</a><a id="2695" class="Symbol">)</a> <a id="2697" href="Tactic.RingSolver.NonReflective.html#2669" class="Bound">ρ</a> <a id="2699" href="Function.Base.html#4322" class="Function Operator">⟨</a> <a id="2701" href="Relation.Binary.Structures.html#1648" class="Function">trans</a> <a id="2707" href="Function.Base.html#4322" class="Function Operator">⟩</a> <a id="2709" class="Symbol">(</a><a id="2710" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2713" href="Tactic.RingSolver.NonReflective.html#2662" class="Bound">x</a> <a id="2715" href="Tactic.RingSolver.NonReflective.html#2669" class="Bound">ρ</a> <a id="2717" href="Function.Base.html#4322" class="Function Operator">⟨</a> <a id="2719" href="Algebra.Structures.html#11625" class="Function">+-cong</a> <a id="2726" href="Function.Base.html#4322" class="Function Operator">⟩</a> <a id="2728" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2731" href="Tactic.RingSolver.NonReflective.html#2666" class="Bound">y</a> <a id="2733" href="Tactic.RingSolver.NonReflective.html#2669" class="Bound">ρ</a><a id="2734" class="Symbol">)</a>
    <a id="2740" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2743" class="Symbol">(</a><a id="2744" href="Tactic.RingSolver.NonReflective.html#2744" class="Bound">x</a> <a id="2746" href="Tactic.RingSolver.Core.Expression.html#711" class="InductiveConstructor Operator">⊗</a> <a id="2748" href="Tactic.RingSolver.NonReflective.html#2748" class="Bound">y</a><a id="2749" class="Symbol">)</a> <a id="2751" href="Tactic.RingSolver.NonReflective.html#2751" class="Bound">ρ</a> <a id="2753" class="Symbol">=</a> <a id="2755" href="Tactic.RingSolver.Core.Polynomial.Homomorphism.Multiplication.html#9614" class="Function">⊠-hom</a> <a id="2761" class="Symbol">(</a><a id="2762" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2767" href="Tactic.RingSolver.NonReflective.html#2744" class="Bound">x</a><a id="2768" class="Symbol">)</a> <a id="2770" class="Symbol">(</a><a id="2771" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2776" href="Tactic.RingSolver.NonReflective.html#2748" class="Bound">y</a><a id="2777" class="Symbol">)</a> <a id="2779" href="Tactic.RingSolver.NonReflective.html#2751" class="Bound">ρ</a> <a id="2781" href="Function.Base.html#4322" class="Function Operator">⟨</a> <a id="2783" href="Relation.Binary.Structures.html#1648" class="Function">trans</a> <a id="2789" href="Function.Base.html#4322" class="Function Operator">⟩</a> <a id="2791" class="Symbol">(</a><a id="2792" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2795" href="Tactic.RingSolver.NonReflective.html#2744" class="Bound">x</a> <a id="2797" href="Tactic.RingSolver.NonReflective.html#2751" class="Bound">ρ</a> <a id="2799" href="Function.Base.html#4322" class="Function Operator">⟨</a> <a id="2801" href="Algebra.Structures.html#11190" class="Function">*-cong</a> <a id="2808" href="Function.Base.html#4322" class="Function Operator">⟩</a> <a id="2810" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2813" href="Tactic.RingSolver.NonReflective.html#2748" class="Bound">y</a> <a id="2815" href="Tactic.RingSolver.NonReflective.html#2751" class="Bound">ρ</a><a id="2816" class="Symbol">)</a>
    <a id="2822" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2825" class="Symbol">(</a><a id="2826" href="Tactic.RingSolver.Core.Expression.html#825" class="InductiveConstructor Operator">⊝</a> <a id="2828" href="Tactic.RingSolver.NonReflective.html#2828" class="Bound">x</a><a id="2829" class="Symbol">)</a>   <a id="2833" href="Tactic.RingSolver.NonReflective.html#2833" class="Bound">ρ</a> <a id="2835" class="Symbol">=</a> <a id="2837" href="Tactic.RingSolver.Core.Polynomial.Homomorphism.Negation.html#1764" class="Function">⊟-hom</a> <a id="2843" class="Symbol">(</a><a id="2844" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2849" href="Tactic.RingSolver.NonReflective.html#2828" class="Bound">x</a><a id="2850" class="Symbol">)</a> <a id="2852" href="Tactic.RingSolver.NonReflective.html#2833" class="Bound">ρ</a>   <a id="2856" href="Function.Base.html#4322" class="Function Operator">⟨</a> <a id="2858" href="Relation.Binary.Structures.html#1648" class="Function">trans</a> <a id="2864" href="Function.Base.html#4322" class="Function Operator">⟩</a> <a id="2866" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#995" class="Function">-‿cong</a> <a id="2873" class="Symbol">(</a><a id="2874" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2877" href="Tactic.RingSolver.NonReflective.html#2828" class="Bound">x</a> <a id="2879" href="Tactic.RingSolver.NonReflective.html#2833" class="Bound">ρ</a><a id="2880" class="Symbol">)</a>
    <a id="2886" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2889" class="Symbol">(</a><a id="2890" href="Tactic.RingSolver.NonReflective.html#2890" class="Bound">x</a> <a id="2892" href="Tactic.RingSolver.Core.Expression.html#768" class="InductiveConstructor Operator">⊛</a> <a id="2894" href="Tactic.RingSolver.NonReflective.html#2894" class="Bound">i</a><a id="2895" class="Symbol">)</a> <a id="2897" href="Tactic.RingSolver.NonReflective.html#2897" class="Bound">ρ</a> <a id="2899" class="Symbol">=</a> <a id="2901" href="Tactic.RingSolver.Core.Polynomial.Homomorphism.Exponentiation.html#3319" class="Function">⊡-hom</a> <a id="2907" class="Symbol">(</a><a id="2908" href="Tactic.RingSolver.NonReflective.html#1968" class="Function">norm</a> <a id="2913" href="Tactic.RingSolver.NonReflective.html#2890" class="Bound">x</a><a id="2914" class="Symbol">)</a> <a id="2916" href="Tactic.RingSolver.NonReflective.html#2894" class="Bound">i</a> <a id="2918" href="Tactic.RingSolver.NonReflective.html#2897" class="Bound">ρ</a> <a id="2920" href="Function.Base.html#4322" class="Function Operator">⟨</a> <a id="2922" href="Relation.Binary.Structures.html#1648" class="Function">trans</a> <a id="2928" href="Function.Base.html#4322" class="Function Operator">⟩</a> <a id="2930" href="Algebra.Properties.Semiring.Exp.TCOptimised.html#1170" class="Function">^-congˡ</a> <a id="2938" href="Tactic.RingSolver.NonReflective.html#2894" class="Bound">i</a> <a id="2940" class="Symbol">(</a><a id="2941" href="Tactic.RingSolver.NonReflective.html#2536" class="Function">go</a> <a id="2944" href="Tactic.RingSolver.NonReflective.html#2890" class="Bound">x</a> <a id="2946" href="Tactic.RingSolver.NonReflective.html#2897" class="Bound">ρ</a><a id="2947" class="Symbol">)</a>

  <a id="2952" class="Keyword">open</a> <a id="2957" class="Keyword">import</a> <a id="2964" href="Relation.Binary.Reflection.html" class="Module">Relation.Binary.Reflection</a> <a id="2991" href="Algebra.Structures.html#1390" class="Function">setoid</a> <a id="2998" href="Tactic.RingSolver.Core.Expression.html#609" class="InductiveConstructor">Ι</a> <a id="3000" href="Tactic.RingSolver.Core.Expression.html#1073" class="Function Operator">⟦_⟧</a> <a id="3004" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">⟦_⇓⟧</a> <a id="3009" href="Tactic.RingSolver.NonReflective.html#2361" class="Function">correct</a> <a id="3017" class="Keyword">public</a>

<a id="solve"></a><a id="3025" href="Tactic.RingSolver.NonReflective.html#3025" class="Function">solve</a> <a id="3031" class="Symbol">:</a> <a id="3033" class="Symbol">∀</a> <a id="3035" class="Symbol">(</a><a id="3036" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a> <a id="3038" class="Symbol">:</a> <a id="3040" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="3041" class="Symbol">)</a> <a id="3043" class="Symbol">→</a>
        <a id="3053" class="Symbol">(</a><a id="3054" href="Tactic.RingSolver.NonReflective.html#3054" class="Bound">f</a> <a id="3056" class="Symbol">:</a> <a id="3058" href="Data.Vec.N-ary.html#1165" class="Function">N-ary</a> <a id="3064" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a> <a id="3066" class="Symbol">(</a><a id="3067" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="3072" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="3080" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a><a id="3081" class="Symbol">)</a> <a id="3083" class="Symbol">(</a><a id="3084" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="3089" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="3097" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a> <a id="3099" href="Data.Product.Base.html#1618" class="Function Operator">×</a> <a id="3101" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="3106" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="3114" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a><a id="3115" class="Symbol">))</a> <a id="3118" class="Symbol">→</a>
        <a id="3128" href="Data.Vec.N-ary.html#2523" class="Function">Eqʰ</a> <a id="3132" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a> <a id="3134" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1462" class="Field Operator">_≈_</a> <a id="3138" class="Symbol">(</a><a id="3139" href="Data.Vec.N-ary.html#1376" class="Function">curryⁿ</a> <a id="3146" class="Symbol">(</a><a id="3147" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">Ops.⟦_⇓⟧</a> <a id="3156" class="Symbol">(</a><a id="3157" href="Data.Product.Base.html#636" class="Field">proj₁</a> <a id="3163" class="Symbol">(</a><a id="3164" href="Relation.Binary.Reflection.html#2136" class="Function">Ops.close</a> <a id="3174" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a> <a id="3176" href="Tactic.RingSolver.NonReflective.html#3054" class="Bound">f</a><a id="3177" class="Symbol">))))</a> <a id="3182" class="Symbol">(</a><a id="3183" href="Data.Vec.N-ary.html#1376" class="Function">curryⁿ</a> <a id="3190" class="Symbol">(</a><a id="3191" href="Tactic.RingSolver.NonReflective.html#2172" class="Function Operator">Ops.⟦_⇓⟧</a> <a id="3200" class="Symbol">(</a><a id="3201" href="Data.Product.Base.html#650" class="Field">proj₂</a> <a id="3207" class="Symbol">(</a><a id="3208" href="Relation.Binary.Reflection.html#2136" class="Function">Ops.close</a> <a id="3218" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a> <a id="3220" href="Tactic.RingSolver.NonReflective.html#3054" class="Bound">f</a><a id="3221" class="Symbol">))))</a> <a id="3226" class="Symbol">→</a>
        <a id="3236" href="Data.Vec.N-ary.html#2279" class="Function">Eq</a>  <a id="3240" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a> <a id="3242" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1462" class="Field Operator">_≈_</a> <a id="3246" class="Symbol">(</a><a id="3247" href="Data.Vec.N-ary.html#1376" class="Function">curryⁿ</a> <a id="3254" class="Symbol">(</a><a id="3255" href="Tactic.RingSolver.Core.Expression.html#1073" class="Function Operator">Ops.⟦_⟧</a>  <a id="3264" class="Symbol">(</a><a id="3265" href="Data.Product.Base.html#636" class="Field">proj₁</a> <a id="3271" class="Symbol">(</a><a id="3272" href="Relation.Binary.Reflection.html#2136" class="Function">Ops.close</a> <a id="3282" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a> <a id="3284" href="Tactic.RingSolver.NonReflective.html#3054" class="Bound">f</a><a id="3285" class="Symbol">))))</a> <a id="3290" class="Symbol">(</a><a id="3291" href="Data.Vec.N-ary.html#1376" class="Function">curryⁿ</a> <a id="3298" class="Symbol">(</a><a id="3299" href="Tactic.RingSolver.Core.Expression.html#1073" class="Function Operator">Ops.⟦_⟧</a>  <a id="3308" class="Symbol">(</a><a id="3309" href="Data.Product.Base.html#650" class="Field">proj₂</a> <a id="3315" class="Symbol">(</a><a id="3316" href="Relation.Binary.Reflection.html#2136" class="Function">Ops.close</a> <a id="3326" href="Tactic.RingSolver.NonReflective.html#3036" class="Bound">n</a> <a id="3328" href="Tactic.RingSolver.NonReflective.html#3054" class="Bound">f</a><a id="3329" class="Symbol">))))</a>
<a id="3334" href="Tactic.RingSolver.NonReflective.html#3025" class="Function">solve</a> <a id="3340" class="Symbol">=</a> <a id="3342" href="Relation.Binary.Reflection.html#2542" class="Function">Ops.solve</a>
<a id="3352" class="Symbol">{-#</a> <a id="3356" class="Keyword">INLINE</a> <a id="3363" href="Tactic.RingSolver.NonReflective.html#3025" class="Function">solve</a> <a id="3369" class="Symbol">#-}</a>

<a id="3374" class="Keyword">infixl</a> <a id="3381" class="Number">6</a> <a id="3383" href="Tactic.RingSolver.NonReflective.html#3387" class="Function Operator">_⊜_</a>
<a id="_⊜_"></a><a id="3387" href="Tactic.RingSolver.NonReflective.html#3387" class="Function Operator">_⊜_</a> <a id="3391" class="Symbol">:</a> <a id="3393" class="Symbol">∀</a> <a id="3395" class="Symbol">{</a><a id="3396" href="Tactic.RingSolver.NonReflective.html#3396" class="Bound">n</a> <a id="3398" class="Symbol">:</a> <a id="3400" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="3401" class="Symbol">}</a> <a id="3403" class="Symbol">→</a>
      <a id="3411" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="3416" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="3424" href="Tactic.RingSolver.NonReflective.html#3396" class="Bound">n</a> <a id="3426" class="Symbol">→</a>
      <a id="3434" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="3439" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="3447" href="Tactic.RingSolver.NonReflective.html#3396" class="Bound">n</a> <a id="3449" class="Symbol">→</a>
      <a id="3457" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="3462" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="3470" href="Tactic.RingSolver.NonReflective.html#3396" class="Bound">n</a> <a id="3472" href="Data.Product.Base.html#1618" class="Function Operator">×</a> <a id="3474" href="Tactic.RingSolver.Core.Expression.html#513" class="Datatype">Expr</a> <a id="3479" href="Tactic.RingSolver.Core.AlmostCommutativeRing.html#1426" class="Field">Carrier</a> <a id="3487" href="Tactic.RingSolver.NonReflective.html#3396" class="Bound">n</a>
<a id="3489" href="Tactic.RingSolver.NonReflective.html#3387" class="Function Operator">_⊜_</a> <a id="3493" class="Symbol">=</a> <a id="3495" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a>
<a id="3499" class="Symbol">{-#</a> <a id="3503" class="Keyword">INLINE</a> <a id="3510" href="Tactic.RingSolver.NonReflective.html#3387" class="Function Operator">_⊜_</a> <a id="3514" class="Symbol">#-}</a>
</pre></body></html>
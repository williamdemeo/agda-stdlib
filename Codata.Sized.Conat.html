<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Sized.Conat</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- The Conat type and some operations</a>
<a id="144" class="Comment">------------------------------------------------------------------------</a>

<a id="218" class="Symbol">{-#</a> <a id="222" class="Keyword">OPTIONS</a> <a id="230" class="Pragma">--without-K</a> <a id="242" class="Pragma">--sized-types</a> <a id="256" class="Symbol">#-}</a>

<a id="261" class="Keyword">module</a> <a id="268" href="Codata.Sized.Conat.html" class="Module">Codata.Sized.Conat</a> <a id="287" class="Keyword">where</a>

<a id="294" class="Keyword">open</a> <a id="299" class="Keyword">import</a> <a id="306" href="Size.html" class="Module">Size</a>
<a id="311" class="Keyword">open</a> <a id="316" class="Keyword">import</a> <a id="323" href="Codata.Sized.Thunk.html" class="Module">Codata.Sized.Thunk</a>

<a id="343" class="Keyword">open</a> <a id="348" class="Keyword">import</a> <a id="355" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="369" class="Keyword">using</a> <a id="375" class="Symbol">(</a><a id="376" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="378" class="Symbol">;</a> <a id="380" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a> <a id="385" class="Symbol">;</a> <a id="387" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a><a id="390" class="Symbol">)</a>
<a id="392" class="Keyword">open</a> <a id="397" class="Keyword">import</a> <a id="404" href="Relation.Nullary.html" class="Module">Relation.Nullary</a>

<a id="422" class="Comment">------------------------------------------------------------------------</a>
<a id="495" class="Comment">-- Definition and first values</a>

<a id="527" class="Keyword">data</a> <a id="Conat"></a><a id="532" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="538" class="Symbol">(</a><a id="539" href="Codata.Sized.Conat.html#539" class="Bound">i</a> <a id="541" class="Symbol">:</a> <a id="543" href="Agda.Builtin.Size.html#202" class="Postulate">Size</a><a id="547" class="Symbol">)</a> <a id="549" class="Symbol">:</a> <a id="551" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="555" class="Keyword">where</a>
  <a id="Conat.zero"></a><a id="563" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a> <a id="568" class="Symbol">:</a> <a id="570" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="576" href="Codata.Sized.Conat.html#539" class="Bound">i</a>
  <a id="Conat.suc"></a><a id="580" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="584" class="Symbol">:</a> <a id="586" href="Codata.Sized.Thunk.html#473" class="Record">Thunk</a> <a id="592" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="598" href="Codata.Sized.Conat.html#539" class="Bound">i</a> <a id="600" class="Symbol">→</a> <a id="602" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="608" href="Codata.Sized.Conat.html#539" class="Bound">i</a>

<a id="infinity"></a><a id="611" href="Codata.Sized.Conat.html#611" class="Function">infinity</a> <a id="620" class="Symbol">:</a> <a id="622" class="Symbol">∀</a> <a id="624" class="Symbol">{</a><a id="625" href="Codata.Sized.Conat.html#625" class="Bound">i</a><a id="626" class="Symbol">}</a> <a id="628" class="Symbol">→</a> <a id="630" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="636" href="Codata.Sized.Conat.html#625" class="Bound">i</a>
<a id="638" href="Codata.Sized.Conat.html#611" class="Function">infinity</a> <a id="647" class="Symbol">=</a> <a id="649" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="653" class="Symbol">λ</a> <a id="655" class="Keyword">where</a> <a id="661" class="Symbol">.</a><a id="662" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="668" class="Symbol">→</a> <a id="670" href="Codata.Sized.Conat.html#611" class="Function">infinity</a>

<a id="fromℕ"></a><a id="680" href="Codata.Sized.Conat.html#680" class="Function">fromℕ</a> <a id="686" class="Symbol">:</a> <a id="688" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="690" class="Symbol">→</a> <a id="692" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="698" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a>
<a id="700" href="Codata.Sized.Conat.html#680" class="Function">fromℕ</a> <a id="706" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a>    <a id="714" class="Symbol">=</a> <a id="716" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>
<a id="721" href="Codata.Sized.Conat.html#680" class="Function">fromℕ</a> <a id="727" class="Symbol">(</a><a id="728" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="732" href="Codata.Sized.Conat.html#732" class="Bound">n</a><a id="733" class="Symbol">)</a> <a id="735" class="Symbol">=</a> <a id="737" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="741" class="Symbol">λ</a> <a id="743" class="Keyword">where</a> <a id="749" class="Symbol">.</a><a id="750" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="756" class="Symbol">→</a> <a id="758" href="Codata.Sized.Conat.html#680" class="Function">fromℕ</a> <a id="764" href="Codata.Sized.Conat.html#732" class="Bound">n</a>

<a id="767" class="Comment">------------------------------------------------------------------------</a>
<a id="840" class="Comment">-- Arithmetic operations</a>

<a id="pred"></a><a id="866" href="Codata.Sized.Conat.html#866" class="Function">pred</a> <a id="871" class="Symbol">:</a> <a id="873" class="Symbol">∀</a> <a id="875" class="Symbol">{</a><a id="876" href="Codata.Sized.Conat.html#876" class="Bound">i</a><a id="877" class="Symbol">}</a> <a id="879" class="Symbol">{</a><a id="880" href="Codata.Sized.Conat.html#880" class="Bound">j</a> <a id="882" class="Symbol">:</a> <a id="884" href="Agda.Builtin.Size.html#236" class="Postulate Operator">Size&lt;</a> <a id="890" href="Codata.Sized.Conat.html#876" class="Bound">i</a><a id="891" class="Symbol">}</a> <a id="893" class="Symbol">→</a> <a id="895" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="901" href="Codata.Sized.Conat.html#876" class="Bound">i</a> <a id="903" class="Symbol">→</a> <a id="905" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="911" href="Codata.Sized.Conat.html#880" class="Bound">j</a>
<a id="913" href="Codata.Sized.Conat.html#866" class="Function">pred</a> <a id="918" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>    <a id="926" class="Symbol">=</a> <a id="928" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>
<a id="933" href="Codata.Sized.Conat.html#866" class="Function">pred</a> <a id="938" class="Symbol">(</a><a id="939" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="943" href="Codata.Sized.Conat.html#943" class="Bound">n</a><a id="944" class="Symbol">)</a> <a id="946" class="Symbol">=</a> <a id="948" href="Codata.Sized.Conat.html#943" class="Bound">n</a> <a id="950" class="Symbol">.</a><a id="951" href="Codata.Sized.Thunk.html#547" class="Field">force</a>

<a id="958" class="Keyword">infixl</a> <a id="965" class="Number">6</a> <a id="967" href="Codata.Sized.Conat.html#989" class="Function Operator">_∸_</a> <a id="971" href="Codata.Sized.Conat.html#1258" class="Function Operator">_+_</a>
<a id="975" class="Keyword">infixl</a> <a id="982" class="Number">7</a> <a id="984" href="Codata.Sized.Conat.html#1363" class="Function Operator">_*_</a>

<a id="_∸_"></a><a id="989" href="Codata.Sized.Conat.html#989" class="Function Operator">_∸_</a> <a id="993" class="Symbol">:</a> <a id="995" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1001" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="1003" class="Symbol">→</a> <a id="1005" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="1007" class="Symbol">→</a> <a id="1009" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1015" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a>
<a id="1017" href="Codata.Sized.Conat.html#1017" class="Bound">m</a> <a id="1019" href="Codata.Sized.Conat.html#989" class="Function Operator">∸</a> <a id="1021" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a>  <a id="1027" class="Symbol">=</a> <a id="1029" href="Codata.Sized.Conat.html#1017" class="Bound">m</a>
<a id="1031" href="Codata.Sized.Conat.html#1031" class="Bound">m</a> <a id="1033" href="Codata.Sized.Conat.html#989" class="Function Operator">∸</a> <a id="1035" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1039" href="Codata.Sized.Conat.html#1039" class="Bound">n</a> <a id="1041" class="Symbol">=</a> <a id="1043" href="Codata.Sized.Conat.html#866" class="Function">pred</a> <a id="1048" href="Codata.Sized.Conat.html#1031" class="Bound">m</a> <a id="1050" href="Codata.Sized.Conat.html#989" class="Function Operator">∸</a> <a id="1052" href="Codata.Sized.Conat.html#1039" class="Bound">n</a>

<a id="_ℕ+_"></a><a id="1055" href="Codata.Sized.Conat.html#1055" class="Function Operator">_ℕ+_</a> <a id="1060" class="Symbol">:</a> <a id="1062" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="1064" class="Symbol">→</a> <a id="1066" class="Symbol">∀</a> <a id="1068" class="Symbol">{</a><a id="1069" href="Codata.Sized.Conat.html#1069" class="Bound">i</a><a id="1070" class="Symbol">}</a> <a id="1072" class="Symbol">→</a> <a id="1074" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1080" href="Codata.Sized.Conat.html#1069" class="Bound">i</a> <a id="1082" class="Symbol">→</a> <a id="1084" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1090" href="Codata.Sized.Conat.html#1069" class="Bound">i</a>
<a id="1092" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a>  <a id="1098" href="Codata.Sized.Conat.html#1055" class="Function Operator">ℕ+</a> <a id="1101" href="Codata.Sized.Conat.html#1101" class="Bound">n</a> <a id="1103" class="Symbol">=</a> <a id="1105" href="Codata.Sized.Conat.html#1101" class="Bound">n</a>
<a id="1107" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1111" href="Codata.Sized.Conat.html#1111" class="Bound">m</a> <a id="1113" href="Codata.Sized.Conat.html#1055" class="Function Operator">ℕ+</a> <a id="1116" href="Codata.Sized.Conat.html#1116" class="Bound">n</a> <a id="1118" class="Symbol">=</a> <a id="1120" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1124" class="Symbol">λ</a> <a id="1126" class="Keyword">where</a> <a id="1132" class="Symbol">.</a><a id="1133" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1139" class="Symbol">→</a> <a id="1141" href="Codata.Sized.Conat.html#1111" class="Bound">m</a> <a id="1143" href="Codata.Sized.Conat.html#1055" class="Function Operator">ℕ+</a> <a id="1146" href="Codata.Sized.Conat.html#1116" class="Bound">n</a>

<a id="_+ℕ_"></a><a id="1149" href="Codata.Sized.Conat.html#1149" class="Function Operator">_+ℕ_</a> <a id="1154" class="Symbol">:</a> <a id="1156" class="Symbol">∀</a> <a id="1158" class="Symbol">{</a><a id="1159" href="Codata.Sized.Conat.html#1159" class="Bound">i</a><a id="1160" class="Symbol">}</a> <a id="1162" class="Symbol">→</a> <a id="1164" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1170" href="Codata.Sized.Conat.html#1159" class="Bound">i</a> <a id="1172" class="Symbol">→</a> <a id="1174" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="1176" class="Symbol">→</a> <a id="1178" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1184" href="Codata.Sized.Conat.html#1159" class="Bound">i</a>
<a id="1186" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>  <a id="1192" href="Codata.Sized.Conat.html#1149" class="Function Operator">+ℕ</a> <a id="1195" href="Codata.Sized.Conat.html#1195" class="Bound">n</a> <a id="1197" class="Symbol">=</a> <a id="1199" href="Codata.Sized.Conat.html#680" class="Function">fromℕ</a> <a id="1205" href="Codata.Sized.Conat.html#1195" class="Bound">n</a>
<a id="1207" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1211" href="Codata.Sized.Conat.html#1211" class="Bound">m</a> <a id="1213" href="Codata.Sized.Conat.html#1149" class="Function Operator">+ℕ</a> <a id="1216" href="Codata.Sized.Conat.html#1216" class="Bound">n</a> <a id="1218" class="Symbol">=</a> <a id="1220" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1224" class="Symbol">λ</a> <a id="1226" class="Keyword">where</a> <a id="1232" class="Symbol">.</a><a id="1233" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1239" class="Symbol">→</a> <a id="1241" class="Symbol">(</a><a id="1242" href="Codata.Sized.Conat.html#1211" class="Bound">m</a> <a id="1244" class="Symbol">.</a><a id="1245" href="Codata.Sized.Thunk.html#547" class="Field">force</a><a id="1250" class="Symbol">)</a> <a id="1252" href="Codata.Sized.Conat.html#1149" class="Function Operator">+ℕ</a> <a id="1255" href="Codata.Sized.Conat.html#1216" class="Bound">n</a>

<a id="_+_"></a><a id="1258" href="Codata.Sized.Conat.html#1258" class="Function Operator">_+_</a> <a id="1262" class="Symbol">:</a> <a id="1264" class="Symbol">∀</a> <a id="1266" class="Symbol">{</a><a id="1267" href="Codata.Sized.Conat.html#1267" class="Bound">i</a><a id="1268" class="Symbol">}</a> <a id="1270" class="Symbol">→</a> <a id="1272" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1278" href="Codata.Sized.Conat.html#1267" class="Bound">i</a> <a id="1280" class="Symbol">→</a> <a id="1282" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1288" href="Codata.Sized.Conat.html#1267" class="Bound">i</a> <a id="1290" class="Symbol">→</a> <a id="1292" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1298" href="Codata.Sized.Conat.html#1267" class="Bound">i</a>
<a id="1300" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>  <a id="1306" href="Codata.Sized.Conat.html#1258" class="Function Operator">+</a> <a id="1308" href="Codata.Sized.Conat.html#1308" class="Bound">n</a> <a id="1310" class="Symbol">=</a> <a id="1312" href="Codata.Sized.Conat.html#1308" class="Bound">n</a>
<a id="1314" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1318" href="Codata.Sized.Conat.html#1318" class="Bound">m</a> <a id="1320" href="Codata.Sized.Conat.html#1258" class="Function Operator">+</a> <a id="1322" href="Codata.Sized.Conat.html#1322" class="Bound">n</a> <a id="1324" class="Symbol">=</a> <a id="1326" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1330" class="Symbol">λ</a> <a id="1332" class="Keyword">where</a> <a id="1338" class="Symbol">.</a><a id="1339" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1345" class="Symbol">→</a> <a id="1347" class="Symbol">(</a><a id="1348" href="Codata.Sized.Conat.html#1318" class="Bound">m</a> <a id="1350" class="Symbol">.</a><a id="1351" href="Codata.Sized.Thunk.html#547" class="Field">force</a><a id="1356" class="Symbol">)</a> <a id="1358" href="Codata.Sized.Conat.html#1258" class="Function Operator">+</a> <a id="1360" href="Codata.Sized.Conat.html#1322" class="Bound">n</a>

<a id="_*_"></a><a id="1363" href="Codata.Sized.Conat.html#1363" class="Function Operator">_*_</a> <a id="1367" class="Symbol">:</a> <a id="1369" class="Symbol">∀</a> <a id="1371" class="Symbol">{</a><a id="1372" href="Codata.Sized.Conat.html#1372" class="Bound">i</a><a id="1373" class="Symbol">}</a> <a id="1375" class="Symbol">→</a> <a id="1377" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1383" href="Codata.Sized.Conat.html#1372" class="Bound">i</a> <a id="1385" class="Symbol">→</a> <a id="1387" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1393" href="Codata.Sized.Conat.html#1372" class="Bound">i</a> <a id="1395" class="Symbol">→</a> <a id="1397" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1403" href="Codata.Sized.Conat.html#1372" class="Bound">i</a>
<a id="1405" href="Codata.Sized.Conat.html#1405" class="Bound">m</a>     <a id="1411" href="Codata.Sized.Conat.html#1363" class="Function Operator">*</a> <a id="1413" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>  <a id="1419" class="Symbol">=</a> <a id="1421" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>
<a id="1426" href="Codata.Sized.Conat.html#563" class="CatchallClause InductiveConstructor">zero</a><a id="1430" class="CatchallClause">  </a><a id="1432" href="Codata.Sized.Conat.html#1363" class="CatchallClause Function Operator">*</a><a id="1433" class="CatchallClause"> </a><a id="1434" href="Codata.Sized.Conat.html#1434" class="CatchallClause Bound">n</a>     <a id="1440" class="Symbol">=</a> <a id="1442" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>
<a id="1447" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1451" href="Codata.Sized.Conat.html#1451" class="Bound">m</a> <a id="1453" href="Codata.Sized.Conat.html#1363" class="Function Operator">*</a> <a id="1455" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1459" href="Codata.Sized.Conat.html#1459" class="Bound">n</a> <a id="1461" class="Symbol">=</a> <a id="1463" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1467" class="Symbol">λ</a> <a id="1469" class="Keyword">where</a> <a id="1475" class="Symbol">.</a><a id="1476" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1482" class="Symbol">→</a> <a id="1484" href="Codata.Sized.Conat.html#1459" class="Bound">n</a> <a id="1486" class="Symbol">.</a><a id="1487" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1493" href="Codata.Sized.Conat.html#1258" class="Function Operator">+</a> <a id="1495" class="Symbol">(</a><a id="1496" href="Codata.Sized.Conat.html#1451" class="Bound">m</a> <a id="1498" class="Symbol">.</a><a id="1499" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1505" href="Codata.Sized.Conat.html#1363" class="Function Operator">*</a> <a id="1507" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1511" href="Codata.Sized.Conat.html#1459" class="Bound">n</a><a id="1512" class="Symbol">)</a>

<a id="1515" class="Comment">-- Max and Min</a>

<a id="1531" class="Keyword">infixl</a> <a id="1538" class="Number">6</a> <a id="1540" href="Codata.Sized.Conat.html#1558" class="Function Operator">_⊔_</a>
<a id="1544" class="Keyword">infixl</a> <a id="1551" class="Number">7</a> <a id="1553" href="Codata.Sized.Conat.html#1694" class="Function Operator">_⊓_</a>

<a id="_⊔_"></a><a id="1558" href="Codata.Sized.Conat.html#1558" class="Function Operator">_⊔_</a> <a id="1562" class="Symbol">:</a> <a id="1564" class="Symbol">∀</a> <a id="1566" class="Symbol">{</a><a id="1567" href="Codata.Sized.Conat.html#1567" class="Bound">i</a><a id="1568" class="Symbol">}</a> <a id="1570" class="Symbol">→</a> <a id="1572" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1578" href="Codata.Sized.Conat.html#1567" class="Bound">i</a> <a id="1580" class="Symbol">→</a> <a id="1582" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1588" href="Codata.Sized.Conat.html#1567" class="Bound">i</a> <a id="1590" class="Symbol">→</a> <a id="1592" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1598" href="Codata.Sized.Conat.html#1567" class="Bound">i</a>
<a id="1600" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>  <a id="1606" href="Codata.Sized.Conat.html#1558" class="Function Operator">⊔</a> <a id="1608" href="Codata.Sized.Conat.html#1608" class="Bound">n</a>     <a id="1614" class="Symbol">=</a> <a id="1616" href="Codata.Sized.Conat.html#1608" class="Bound">n</a>
<a id="1618" href="Codata.Sized.Conat.html#1618" class="CatchallClause Bound">m</a><a id="1619" class="CatchallClause">     </a><a id="1624" href="Codata.Sized.Conat.html#1558" class="CatchallClause Function Operator">⊔</a><a id="1625" class="CatchallClause"> </a><a id="1626" href="Codata.Sized.Conat.html#563" class="CatchallClause InductiveConstructor">zero</a>  <a id="1632" class="Symbol">=</a> <a id="1634" href="Codata.Sized.Conat.html#1618" class="Bound">m</a>
<a id="1636" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1640" href="Codata.Sized.Conat.html#1640" class="Bound">m</a> <a id="1642" href="Codata.Sized.Conat.html#1558" class="Function Operator">⊔</a> <a id="1644" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1648" href="Codata.Sized.Conat.html#1648" class="Bound">n</a> <a id="1650" class="Symbol">=</a> <a id="1652" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1656" class="Symbol">λ</a> <a id="1658" class="Keyword">where</a> <a id="1664" class="Symbol">.</a><a id="1665" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1671" class="Symbol">→</a> <a id="1673" href="Codata.Sized.Conat.html#1640" class="Bound">m</a> <a id="1675" class="Symbol">.</a><a id="1676" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1682" href="Codata.Sized.Conat.html#1558" class="Function Operator">⊔</a> <a id="1684" href="Codata.Sized.Conat.html#1648" class="Bound">n</a> <a id="1686" class="Symbol">.</a><a id="1687" href="Codata.Sized.Thunk.html#547" class="Field">force</a>

<a id="_⊓_"></a><a id="1694" href="Codata.Sized.Conat.html#1694" class="Function Operator">_⊓_</a> <a id="1698" class="Symbol">:</a> <a id="1700" class="Symbol">∀</a> <a id="1702" class="Symbol">{</a><a id="1703" href="Codata.Sized.Conat.html#1703" class="Bound">i</a><a id="1704" class="Symbol">}</a> <a id="1706" class="Symbol">→</a> <a id="1708" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1714" href="Codata.Sized.Conat.html#1703" class="Bound">i</a> <a id="1716" class="Symbol">→</a> <a id="1718" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1724" href="Codata.Sized.Conat.html#1703" class="Bound">i</a> <a id="1726" class="Symbol">→</a> <a id="1728" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1734" href="Codata.Sized.Conat.html#1703" class="Bound">i</a>
<a id="1736" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>  <a id="1742" href="Codata.Sized.Conat.html#1694" class="Function Operator">⊓</a> <a id="1744" href="Codata.Sized.Conat.html#1744" class="Bound">n</a>     <a id="1750" class="Symbol">=</a> <a id="1752" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>
<a id="1757" href="Codata.Sized.Conat.html#1757" class="CatchallClause Bound">m</a><a id="1758" class="CatchallClause">     </a><a id="1763" href="Codata.Sized.Conat.html#1694" class="CatchallClause Function Operator">⊓</a><a id="1764" class="CatchallClause"> </a><a id="1765" href="Codata.Sized.Conat.html#563" class="CatchallClause InductiveConstructor">zero</a>  <a id="1771" class="Symbol">=</a> <a id="1773" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>
<a id="1778" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1782" href="Codata.Sized.Conat.html#1782" class="Bound">m</a> <a id="1784" href="Codata.Sized.Conat.html#1694" class="Function Operator">⊓</a> <a id="1786" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1790" href="Codata.Sized.Conat.html#1790" class="Bound">n</a> <a id="1792" class="Symbol">=</a> <a id="1794" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="1798" class="Symbol">λ</a> <a id="1800" class="Keyword">where</a> <a id="1806" class="Symbol">.</a><a id="1807" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1813" class="Symbol">→</a> <a id="1815" href="Codata.Sized.Conat.html#1782" class="Bound">m</a> <a id="1817" class="Symbol">.</a><a id="1818" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="1824" href="Codata.Sized.Conat.html#1694" class="Function Operator">⊓</a> <a id="1826" href="Codata.Sized.Conat.html#1790" class="Bound">n</a> <a id="1828" class="Symbol">.</a><a id="1829" href="Codata.Sized.Thunk.html#547" class="Field">force</a>

<a id="1836" class="Comment">------------------------------------------------------------------------</a>
<a id="1909" class="Comment">-- Finiteness</a>

<a id="1924" class="Keyword">data</a> <a id="Finite"></a><a id="1929" href="Codata.Sized.Conat.html#1929" class="Datatype">Finite</a> <a id="1936" class="Symbol">:</a> <a id="1938" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="1944" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="1946" class="Symbol">→</a> <a id="1948" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1952" class="Keyword">where</a>
  <a id="Finite.zero"></a><a id="1960" href="Codata.Sized.Conat.html#1960" class="InductiveConstructor">zero</a> <a id="1965" class="Symbol">:</a> <a id="1967" href="Codata.Sized.Conat.html#1929" class="Datatype">Finite</a> <a id="1974" href="Codata.Sized.Conat.html#563" class="InductiveConstructor">zero</a>
  <a id="Finite.suc"></a><a id="1981" href="Codata.Sized.Conat.html#1981" class="InductiveConstructor">suc</a>  <a id="1986" class="Symbol">:</a> <a id="1988" class="Symbol">∀</a> <a id="1990" class="Symbol">{</a><a id="1991" href="Codata.Sized.Conat.html#1991" class="Bound">n</a><a id="1992" class="Symbol">}</a> <a id="1994" class="Symbol">→</a> <a id="1996" href="Codata.Sized.Conat.html#1929" class="Datatype">Finite</a> <a id="2003" class="Symbol">(</a><a id="2004" href="Codata.Sized.Conat.html#1991" class="Bound">n</a> <a id="2006" class="Symbol">.</a><a id="2007" href="Codata.Sized.Thunk.html#547" class="Field">force</a><a id="2012" class="Symbol">)</a> <a id="2014" class="Symbol">→</a> <a id="2016" href="Codata.Sized.Conat.html#1929" class="Datatype">Finite</a> <a id="2023" class="Symbol">(</a><a id="2024" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="2028" href="Codata.Sized.Conat.html#1991" class="Bound">n</a><a id="2029" class="Symbol">)</a>

<a id="toℕ"></a><a id="2032" href="Codata.Sized.Conat.html#2032" class="Function">toℕ</a> <a id="2036" class="Symbol">:</a> <a id="2038" class="Symbol">∀</a> <a id="2040" class="Symbol">{</a><a id="2041" href="Codata.Sized.Conat.html#2041" class="Bound">n</a><a id="2042" class="Symbol">}</a> <a id="2044" class="Symbol">→</a> <a id="2046" href="Codata.Sized.Conat.html#1929" class="Datatype">Finite</a> <a id="2053" href="Codata.Sized.Conat.html#2041" class="Bound">n</a> <a id="2055" class="Symbol">→</a> <a id="2057" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a>
<a id="2059" href="Codata.Sized.Conat.html#2032" class="Function">toℕ</a> <a id="2063" href="Codata.Sized.Conat.html#1960" class="InductiveConstructor">zero</a>    <a id="2071" class="Symbol">=</a> <a id="2073" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a>
<a id="2078" href="Codata.Sized.Conat.html#2032" class="Function">toℕ</a> <a id="2082" class="Symbol">(</a><a id="2083" href="Codata.Sized.Conat.html#1981" class="InductiveConstructor">suc</a> <a id="2087" href="Codata.Sized.Conat.html#2087" class="Bound">n</a><a id="2088" class="Symbol">)</a> <a id="2090" class="Symbol">=</a> <a id="2092" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2096" class="Symbol">(</a><a id="2097" href="Codata.Sized.Conat.html#2032" class="Function">toℕ</a> <a id="2101" href="Codata.Sized.Conat.html#2087" class="Bound">n</a><a id="2102" class="Symbol">)</a>

<a id="¬Finite∞"></a><a id="2105" href="Codata.Sized.Conat.html#2105" class="Function">¬Finite∞</a> <a id="2114" class="Symbol">:</a> <a id="2116" href="Relation.Nullary.Negation.Core.html#815" class="Function Operator">¬</a> <a id="2118" class="Symbol">(</a><a id="2119" href="Codata.Sized.Conat.html#1929" class="Datatype">Finite</a> <a id="2126" href="Codata.Sized.Conat.html#611" class="Function">infinity</a><a id="2134" class="Symbol">)</a>
<a id="2136" href="Codata.Sized.Conat.html#2105" class="Function">¬Finite∞</a> <a id="2145" class="Symbol">(</a><a id="2146" href="Codata.Sized.Conat.html#1981" class="InductiveConstructor">suc</a> <a id="2150" href="Codata.Sized.Conat.html#2150" class="Bound">p</a><a id="2151" class="Symbol">)</a> <a id="2153" class="Symbol">=</a> <a id="2155" href="Codata.Sized.Conat.html#2105" class="Function">¬Finite∞</a> <a id="2164" href="Codata.Sized.Conat.html#2150" class="Bound">p</a>

<a id="2167" class="Comment">------------------------------------------------------------------------</a>
<a id="2240" class="Comment">-- Order wrt to Nat</a>

<a id="2261" class="Keyword">data</a> <a id="_ℕ≤_"></a><a id="2266" href="Codata.Sized.Conat.html#2266" class="Datatype Operator">_ℕ≤_</a> <a id="2271" class="Symbol">:</a> <a id="2273" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="2275" class="Symbol">→</a> <a id="2277" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="2283" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="2285" class="Symbol">→</a> <a id="2287" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="2291" class="Keyword">where</a>
  <a id="_ℕ≤_.zℕ≤n"></a><a id="2299" href="Codata.Sized.Conat.html#2299" class="InductiveConstructor">zℕ≤n</a> <a id="2304" class="Symbol">:</a> <a id="2306" class="Symbol">∀</a> <a id="2308" class="Symbol">{</a><a id="2309" href="Codata.Sized.Conat.html#2309" class="Bound">n</a><a id="2310" class="Symbol">}</a> <a id="2312" class="Symbol">→</a> <a id="2314" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a> <a id="2319" href="Codata.Sized.Conat.html#2266" class="Datatype Operator">ℕ≤</a> <a id="2322" href="Codata.Sized.Conat.html#2309" class="Bound">n</a>
  <a id="_ℕ≤_.sℕ≤s"></a><a id="2326" href="Codata.Sized.Conat.html#2326" class="InductiveConstructor">sℕ≤s</a> <a id="2331" class="Symbol">:</a> <a id="2333" class="Symbol">∀</a> <a id="2335" class="Symbol">{</a><a id="2336" href="Codata.Sized.Conat.html#2336" class="Bound">k</a> <a id="2338" href="Codata.Sized.Conat.html#2338" class="Bound">n</a><a id="2339" class="Symbol">}</a> <a id="2341" class="Symbol">→</a> <a id="2343" href="Codata.Sized.Conat.html#2336" class="Bound">k</a> <a id="2345" href="Codata.Sized.Conat.html#2266" class="Datatype Operator">ℕ≤</a> <a id="2348" href="Codata.Sized.Conat.html#2338" class="Bound">n</a> <a id="2350" class="Symbol">.</a><a id="2351" href="Codata.Sized.Thunk.html#547" class="Field">force</a> <a id="2357" class="Symbol">→</a> <a id="2359" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2363" href="Codata.Sized.Conat.html#2336" class="Bound">k</a> <a id="2365" href="Codata.Sized.Conat.html#2266" class="Datatype Operator">ℕ≤</a> <a id="2368" href="Codata.Sized.Conat.html#580" class="InductiveConstructor">suc</a> <a id="2372" href="Codata.Sized.Conat.html#2338" class="Bound">n</a>

<a id="_ℕ&lt;_"></a><a id="2375" href="Codata.Sized.Conat.html#2375" class="Function Operator">_ℕ&lt;_</a> <a id="2380" class="Symbol">:</a> <a id="2382" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="2384" class="Symbol">→</a> <a id="2386" href="Codata.Sized.Conat.html#532" class="Datatype">Conat</a> <a id="2392" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="2394" class="Symbol">→</a> <a id="2396" href="Agda.Primitive.html#326" class="Primitive">Set</a>
<a id="2400" href="Codata.Sized.Conat.html#2400" class="Bound">k</a> <a id="2402" href="Codata.Sized.Conat.html#2375" class="Function Operator">ℕ&lt;</a> <a id="2405" href="Codata.Sized.Conat.html#2405" class="Bound">n</a> <a id="2407" class="Symbol">=</a> <a id="2409" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2413" href="Codata.Sized.Conat.html#2400" class="Bound">k</a> <a id="2415" href="Codata.Sized.Conat.html#2266" class="Datatype Operator">ℕ≤</a> <a id="2418" href="Codata.Sized.Conat.html#2405" class="Bound">n</a>

<a id="_ℕ≤infinity"></a><a id="2421" href="Codata.Sized.Conat.html#2421" class="Function Operator">_ℕ≤infinity</a> <a id="2433" class="Symbol">:</a> <a id="2435" class="Symbol">∀</a> <a id="2437" href="Codata.Sized.Conat.html#2437" class="Bound">k</a> <a id="2439" class="Symbol">→</a> <a id="2441" href="Codata.Sized.Conat.html#2437" class="Bound">k</a> <a id="2443" href="Codata.Sized.Conat.html#2266" class="Datatype Operator">ℕ≤</a> <a id="2446" href="Codata.Sized.Conat.html#611" class="Function">infinity</a>
<a id="2455" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a>  <a id="2461" href="Codata.Sized.Conat.html#2421" class="Function Operator">ℕ≤infinity</a> <a id="2472" class="Symbol">=</a> <a id="2474" href="Codata.Sized.Conat.html#2299" class="InductiveConstructor">zℕ≤n</a>
<a id="2479" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2483" href="Codata.Sized.Conat.html#2483" class="Bound">k</a> <a id="2485" href="Codata.Sized.Conat.html#2421" class="Function Operator">ℕ≤infinity</a> <a id="2496" class="Symbol">=</a> <a id="2498" href="Codata.Sized.Conat.html#2326" class="InductiveConstructor">sℕ≤s</a> <a id="2503" class="Symbol">(</a><a id="2504" href="Codata.Sized.Conat.html#2483" class="Bound">k</a> <a id="2506" href="Codata.Sized.Conat.html#2421" class="Function Operator">ℕ≤infinity</a><a id="2516" class="Symbol">)</a>
</pre></body></html>
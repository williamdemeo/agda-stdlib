<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>README.Data.Container.FreeMonad</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Example showing how the free monad construction on containers can be</a>
<a id="178" class="Comment">-- used</a>
<a id="186" class="Comment">------------------------------------------------------------------------</a>

<a id="260" class="Symbol">{-#</a> <a id="264" class="Keyword">OPTIONS</a> <a id="272" class="Pragma">--cubical-compatible</a> <a id="293" class="Pragma">--sized-types</a> <a id="307" class="Symbol">#-}</a>

<a id="312" class="Keyword">module</a> <a id="319" href="README.Data.Container.FreeMonad.html" class="Module">README.Data.Container.FreeMonad</a> <a id="351" class="Keyword">where</a>

<a id="358" class="Keyword">open</a> <a id="363" class="Keyword">import</a> <a id="370" href="Level.html" class="Module">Level</a> <a id="376" class="Keyword">using</a> <a id="382" class="Symbol">(</a><a id="383" href="Level.html#521" class="Function">0ℓ</a><a id="385" class="Symbol">)</a>
<a id="387" class="Keyword">open</a> <a id="392" class="Keyword">import</a> <a id="399" href="Effect.Monad.html" class="Module">Effect.Monad</a>
<a id="412" class="Keyword">open</a> <a id="417" class="Keyword">import</a> <a id="424" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="435" class="Keyword">open</a> <a id="440" class="Keyword">import</a> <a id="447" href="Data.Unit.html" class="Module">Data.Unit</a>
<a id="457" class="Keyword">open</a> <a id="462" class="Keyword">import</a> <a id="469" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="484" class="Keyword">using</a> <a id="490" class="Symbol">(</a><a id="491" href="Agda.Builtin.Bool.html#156" class="Datatype">Bool</a><a id="495" class="Symbol">;</a> <a id="497" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a><a id="501" class="Symbol">)</a>
<a id="503" class="Keyword">open</a> <a id="508" class="Keyword">import</a> <a id="515" href="Data.Nat.html" class="Module">Data.Nat</a>
<a id="524" class="Keyword">open</a> <a id="529" class="Keyword">import</a> <a id="536" href="Data.Sum.html" class="Module">Data.Sum</a> <a id="545" class="Keyword">using</a> <a id="551" class="Symbol">(</a><a id="552" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a><a id="556" class="Symbol">;</a> <a id="558" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a><a id="562" class="Symbol">)</a>
<a id="564" class="Keyword">open</a> <a id="569" class="Keyword">import</a> <a id="576" href="Data.Product.html" class="Module">Data.Product</a> <a id="589" class="Keyword">renaming</a> <a id="598" class="Symbol">(</a><a id="599" href="Data.Product.html#1176" class="Function Operator">_×_</a> <a id="603" class="Symbol">to</a> <a id="606" class="Function Operator">_⟨×⟩_</a><a id="611" class="Symbol">)</a>
<a id="613" class="Keyword">open</a> <a id="618" class="Keyword">import</a> <a id="625" href="Data.Container.html" class="Module">Data.Container</a> <a id="640" class="Keyword">using</a> <a id="646" class="Symbol">(</a><a id="647" href="Data.Container.Core.html#567" class="Record">Container</a><a id="656" class="Symbol">;</a> <a id="658" href="Data.Container.Core.html#631" class="InductiveConstructor Operator">_▷_</a><a id="661" class="Symbol">)</a>
<a id="663" class="Keyword">open</a> <a id="668" class="Keyword">import</a> <a id="675" href="Data.Container.Combinator.html" class="Module">Data.Container.Combinator</a>
<a id="701" class="Keyword">open</a> <a id="706" class="Keyword">import</a> <a id="713" href="Data.Container.FreeMonad.html" class="Module">Data.Container.FreeMonad</a> <a id="738" class="Symbol">as</a> <a id="741" class="Module">FreeMonad</a>
<a id="751" class="Keyword">open</a> <a id="756" class="Keyword">import</a> <a id="763" href="Data.W.html" class="Module">Data.W</a>
<a id="770" class="Keyword">open</a> <a id="775" class="Keyword">import</a> <a id="782" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="820" class="Symbol">as</a> <a id="823" class="Module">P</a>

<a id="826" class="Comment">------------------------------------------------------------------------</a>
<a id="899" class="Comment">-- Defining the signature of an effect and building trees describing</a>
<a id="968" class="Comment">-- computations leveraging that effect.</a>

<a id="1009" class="Comment">-- The signature of state and its (generic) operations.</a>

<a id="State"></a><a id="1066" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1072" class="Symbol">:</a> <a id="1074" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1078" class="Symbol">→</a> <a id="1080" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="1090" class="Symbol">_</a> <a id="1092" class="Symbol">_</a>
<a id="1094" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1100" href="README.Data.Container.FreeMonad.html#1100" class="Bound">S</a> <a id="1102" class="Symbol">=</a> <a id="1104" href="Agda.Builtin.Unit.html#158" class="Record">⊤</a> <a id="1106" href="README.Data.Container.FreeMonad.html#1128" class="Function Operator">⟶</a> <a id="1108" href="README.Data.Container.FreeMonad.html#1100" class="Bound">S</a> <a id="1110" href="Data.Container.Combinator.html#3119" class="Function Operator">⊎</a> <a id="1112" href="README.Data.Container.FreeMonad.html#1100" class="Bound">S</a> <a id="1114" href="README.Data.Container.FreeMonad.html#1128" class="Function Operator">⟶</a> <a id="1116" href="Agda.Builtin.Unit.html#158" class="Record">⊤</a>
  <a id="1120" class="Keyword">where</a>
  <a id="1128" href="README.Data.Container.FreeMonad.html#1128" class="Function Operator">_⟶_</a> <a id="1132" class="Symbol">:</a> <a id="1134" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1138" class="Symbol">→</a> <a id="1140" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1144" class="Symbol">→</a> <a id="1146" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="1156" class="Symbol">_</a> <a id="1158" class="Symbol">_</a>
  <a id="1162" href="README.Data.Container.FreeMonad.html#1162" class="Bound">I</a> <a id="1164" href="README.Data.Container.FreeMonad.html#1128" class="Function Operator">⟶</a> <a id="1166" href="README.Data.Container.FreeMonad.html#1166" class="Bound">O</a> <a id="1168" class="Symbol">=</a> <a id="1170" href="README.Data.Container.FreeMonad.html#1162" class="Bound">I</a> <a id="1172" href="Data.Container.Core.html#631" class="InductiveConstructor Operator">▷</a> <a id="1174" class="Symbol">λ</a> <a id="1176" href="README.Data.Container.FreeMonad.html#1176" class="Bound">_</a> <a id="1178" class="Symbol">→</a> <a id="1180" href="README.Data.Container.FreeMonad.html#1166" class="Bound">O</a>

<a id="get"></a><a id="1183" href="README.Data.Container.FreeMonad.html#1183" class="Function">get</a> <a id="1187" class="Symbol">:</a> <a id="1189" class="Symbol">∀</a> <a id="1191" class="Symbol">{</a><a id="1192" href="README.Data.Container.FreeMonad.html#1192" class="Bound">S</a><a id="1193" class="Symbol">}</a> <a id="1195" class="Symbol">→</a> <a id="1197" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1203" href="README.Data.Container.FreeMonad.html#1192" class="Bound">S</a> <a id="1205" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="1207" href="README.Data.Container.FreeMonad.html#1192" class="Bound">S</a>
<a id="1209" href="README.Data.Container.FreeMonad.html#1183" class="Function">get</a> <a id="1213" class="Symbol">=</a> <a id="1215" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="1222" class="Symbol">(</a><a id="1223" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="1228" class="Symbol">_</a> <a id="1230" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1232" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a><a id="1236" class="Symbol">)</a>

<a id="put"></a><a id="1239" href="README.Data.Container.FreeMonad.html#1239" class="Function">put</a> <a id="1243" class="Symbol">:</a> <a id="1245" class="Symbol">∀</a> <a id="1247" class="Symbol">{</a><a id="1248" href="README.Data.Container.FreeMonad.html#1248" class="Bound">S</a><a id="1249" class="Symbol">}</a> <a id="1251" class="Symbol">→</a> <a id="1253" href="README.Data.Container.FreeMonad.html#1248" class="Bound">S</a> <a id="1255" class="Symbol">→</a> <a id="1257" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1263" href="README.Data.Container.FreeMonad.html#1248" class="Bound">S</a> <a id="1265" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="1267" href="Agda.Builtin.Unit.html#158" class="Record">⊤</a>
<a id="1269" href="README.Data.Container.FreeMonad.html#1239" class="Function">put</a> <a id="1273" href="README.Data.Container.FreeMonad.html#1273" class="Bound">s</a> <a id="1275" class="Symbol">=</a> <a id="1277" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="1284" class="Symbol">(</a><a id="1285" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="1290" href="README.Data.Container.FreeMonad.html#1273" class="Bound">s</a> <a id="1292" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1294" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a><a id="1298" class="Symbol">)</a>

<a id="1301" class="Comment">-- Using the above we can, for example, write a stateful program that</a>
<a id="1371" class="Comment">-- delivers a boolean.</a>
<a id="prog"></a><a id="1394" href="README.Data.Container.FreeMonad.html#1394" class="Function">prog</a> <a id="1399" class="Symbol">:</a> <a id="1401" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1407" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1409" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="1411" href="Agda.Builtin.Bool.html#156" class="Datatype">Bool</a>
<a id="1416" href="README.Data.Container.FreeMonad.html#1394" class="Function">prog</a> <a id="1421" class="Symbol">=</a>
  <a id="1425" href="README.Data.Container.FreeMonad.html#1183" class="Function">get</a>         <a id="1437" href="Data.Container.FreeMonad.html#3830" class="Function Operator">&gt;&gt;=</a> <a id="1441" class="Symbol">λ</a> <a id="1443" href="README.Data.Container.FreeMonad.html#1443" class="Bound">n</a> <a id="1445" class="Symbol">→</a>
  <a id="1449" href="README.Data.Container.FreeMonad.html#1239" class="Function">put</a> <a id="1453" class="Symbol">(</a><a id="1454" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1458" href="README.Data.Container.FreeMonad.html#1443" class="Bound">n</a><a id="1459" class="Symbol">)</a> <a id="1461" href="Effect.Monad.Indexed.html#745" class="Function Operator">&gt;&gt;</a>
  <a id="1466" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="1471" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a>
  <a id="1478" class="Keyword">where</a>
  <a id="1486" class="Keyword">open</a> <a id="1491" href="Effect.Monad.html#821" class="Module">RawMonad</a> <a id="1500" href="Data.Container.FreeMonad.html#4237" class="Function">rawMonad</a> <a id="1509" class="Keyword">using</a> <a id="1515" class="Symbol">(</a><a id="1516" href="Effect.Monad.Indexed.html#745" class="Function Operator">_&gt;&gt;_</a><a id="1520" class="Symbol">)</a>

<a id="runState"></a><a id="1523" href="README.Data.Container.FreeMonad.html#1523" class="Function">runState</a> <a id="1532" class="Symbol">:</a> <a id="1534" class="Symbol">{</a><a id="1535" href="README.Data.Container.FreeMonad.html#1535" class="Bound">S</a> <a id="1537" href="README.Data.Container.FreeMonad.html#1537" class="Bound">X</a> <a id="1539" class="Symbol">:</a> <a id="1541" href="Agda.Primitive.html#320" class="Primitive">Set</a><a id="1544" class="Symbol">}</a> <a id="1546" class="Symbol">→</a> <a id="1548" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="1554" href="README.Data.Container.FreeMonad.html#1535" class="Bound">S</a> <a id="1556" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="1558" href="README.Data.Container.FreeMonad.html#1537" class="Bound">X</a> <a id="1560" class="Symbol">→</a> <a id="1562" class="Symbol">(</a><a id="1563" href="README.Data.Container.FreeMonad.html#1535" class="Bound">S</a> <a id="1565" class="Symbol">→</a> <a id="1567" href="README.Data.Container.FreeMonad.html#1537" class="Bound">X</a> <a id="1569" href="README.Data.Container.FreeMonad.html#606" class="Function Operator">⟨×⟩</a> <a id="1573" href="README.Data.Container.FreeMonad.html#1535" class="Bound">S</a><a id="1574" class="Symbol">)</a>
<a id="1576" href="README.Data.Container.FreeMonad.html#1523" class="Function">runState</a> <a id="1585" class="Symbol">(</a><a id="1586" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="1591" href="README.Data.Container.FreeMonad.html#1591" class="Bound">x</a><a id="1592" class="Symbol">)</a>                <a id="1609" class="Symbol">=</a> <a id="1611" class="Symbol">λ</a> <a id="1613" href="README.Data.Container.FreeMonad.html#1613" class="Bound">s</a> <a id="1615" class="Symbol">→</a> <a id="1617" href="README.Data.Container.FreeMonad.html#1591" class="Bound">x</a> <a id="1619" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1621" href="README.Data.Container.FreeMonad.html#1613" class="Bound">s</a>
<a id="1623" href="README.Data.Container.FreeMonad.html#1523" class="Function">runState</a> <a id="1632" class="Symbol">(</a><a id="1633" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="1640" class="Symbol">((</a><a id="1642" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="1647" class="Symbol">_)</a> <a id="1650" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1652" href="README.Data.Container.FreeMonad.html#1652" class="Bound">k</a><a id="1653" class="Symbol">))</a> <a id="1656" class="Symbol">=</a> <a id="1658" class="Symbol">λ</a> <a id="1660" href="README.Data.Container.FreeMonad.html#1660" class="Bound">s</a> <a id="1662" class="Symbol">→</a> <a id="1664" href="README.Data.Container.FreeMonad.html#1523" class="Function">runState</a> <a id="1673" class="Symbol">(</a><a id="1674" href="README.Data.Container.FreeMonad.html#1652" class="Bound">k</a> <a id="1676" href="README.Data.Container.FreeMonad.html#1660" class="Bound">s</a><a id="1677" class="Symbol">)</a> <a id="1679" href="README.Data.Container.FreeMonad.html#1660" class="Bound">s</a>
<a id="1681" href="README.Data.Container.FreeMonad.html#1523" class="Function">runState</a> <a id="1690" class="Symbol">(</a><a id="1691" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="1698" class="Symbol">((</a><a id="1700" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="1705" href="README.Data.Container.FreeMonad.html#1705" class="Bound">s</a><a id="1706" class="Symbol">)</a> <a id="1708" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1710" href="README.Data.Container.FreeMonad.html#1710" class="Bound">k</a><a id="1711" class="Symbol">))</a> <a id="1714" class="Symbol">=</a> <a id="1716" class="Symbol">λ</a> <a id="1718" href="README.Data.Container.FreeMonad.html#1718" class="Bound">_</a> <a id="1720" class="Symbol">→</a> <a id="1722" href="README.Data.Container.FreeMonad.html#1523" class="Function">runState</a> <a id="1731" class="Symbol">(</a><a id="1732" href="README.Data.Container.FreeMonad.html#1710" class="Bound">k</a> <a id="1734" class="Symbol">_)</a> <a id="1737" href="README.Data.Container.FreeMonad.html#1705" class="Bound">s</a>

<a id="test"></a><a id="1740" href="README.Data.Container.FreeMonad.html#1740" class="Function">test</a> <a id="1745" class="Symbol">:</a> <a id="1747" href="README.Data.Container.FreeMonad.html#1523" class="Function">runState</a> <a id="1756" href="README.Data.Container.FreeMonad.html#1394" class="Function">prog</a> <a id="1761" class="Number">0</a> <a id="1763" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1765" class="Symbol">(</a><a id="1766" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a> <a id="1771" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1773" class="Number">1</a><a id="1774" class="Symbol">)</a>
<a id="1776" href="README.Data.Container.FreeMonad.html#1740" class="Function">test</a> <a id="1781" class="Symbol">=</a> <a id="1783" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">P.refl</a>

<a id="1791" class="Comment">-- It should be noted that @State S ⋆ X@ is not the state monad. If we</a>
<a id="1862" class="Comment">-- could quotient @State S ⋆ X@ by the seven axioms of state (see</a>
<a id="1928" class="Comment">-- Plotkin and Power&#39;s &quot;Notions of Computation Determine Monads&quot;, 2002)</a>
<a id="2000" class="Comment">-- then we would get the state monad.</a>

<a id="2039" class="Comment">------------------------------------------------------------------------</a>
<a id="2112" class="Comment">-- Defining effectful inductive data structures</a>

<a id="2161" class="Comment">-- The definition of `C ⋆ A` is strictly positive in `A`, meaning that we</a>
<a id="2235" class="Comment">-- can use `C ⋆_` when defining (co)inductive datatypes.</a>


<a id="2294" class="Keyword">open</a> <a id="2299" class="Keyword">import</a> <a id="2306" href="Size.html" class="Module">Size</a>
<a id="2311" class="Keyword">open</a> <a id="2316" class="Keyword">import</a> <a id="2323" href="Codata.Sized.Thunk.html" class="Module">Codata.Sized.Thunk</a>
<a id="2342" class="Keyword">open</a> <a id="2347" class="Keyword">import</a> <a id="2354" href="Data.Vec.Base.html" class="Module">Data.Vec.Base</a> <a id="2368" class="Keyword">using</a> <a id="2374" class="Symbol">(</a><a id="2375" href="Data.Vec.Base.html#1066" class="Datatype">Vec</a><a id="2378" class="Symbol">;</a> <a id="2380" href="Data.Vec.Base.html#1102" class="InductiveConstructor">[]</a><a id="2382" class="Symbol">;</a> <a id="2384" href="Data.Vec.Base.html#1121" class="InductiveConstructor Operator">_∷_</a><a id="2387" class="Symbol">)</a>


<a id="2391" class="Comment">-- A `Tap C A` is a infinite source of `A`s provided that we can perform</a>
<a id="2464" class="Comment">-- the effectful computations described by `C`.</a>
<a id="2512" class="Comment">-- The first one can be accessed readily but the rest of them is hidden</a>
<a id="2584" class="Comment">-- under layers of `C` computations.</a>

<a id="2622" class="Keyword">module</a> <a id="2629" href="README.Data.Container.FreeMonad.html#2629" class="Module">_</a> <a id="2631" class="Symbol">(</a><a id="2632" href="README.Data.Container.FreeMonad.html#2632" class="Bound">C</a> <a id="2634" class="Symbol">:</a> <a id="2636" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="2646" href="Level.html#521" class="Function">0ℓ</a> <a id="2649" href="Level.html#521" class="Function">0ℓ</a><a id="2651" class="Symbol">)</a> <a id="2653" class="Symbol">(</a><a id="2654" href="README.Data.Container.FreeMonad.html#2654" class="Bound">A</a> <a id="2656" class="Symbol">:</a> <a id="2658" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2662" href="Level.html#521" class="Function">0ℓ</a><a id="2664" class="Symbol">)</a> <a id="2666" class="Keyword">where</a>

  <a id="2675" class="Keyword">data</a> <a id="2680" href="README.Data.Container.FreeMonad.html#2680" class="Datatype">Tap</a> <a id="2684" class="Symbol">(</a><a id="2685" href="README.Data.Container.FreeMonad.html#2685" class="Bound">i</a> <a id="2687" class="Symbol">:</a> <a id="2689" href="Agda.Builtin.Size.html#196" class="Postulate">Size</a><a id="2693" class="Symbol">)</a> <a id="2695" class="Symbol">:</a> <a id="2697" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2701" href="Level.html#521" class="Function">0ℓ</a> <a id="2704" class="Keyword">where</a>
    <a id="2714" href="README.Data.Container.FreeMonad.html#2714" class="InductiveConstructor Operator">_∷_</a> <a id="2718" class="Symbol">:</a> <a id="2720" href="README.Data.Container.FreeMonad.html#2654" class="Bound">A</a> <a id="2722" class="Symbol">→</a> <a id="2724" href="Codata.Sized.Thunk.html#482" class="Record">Thunk</a> <a id="2730" class="Symbol">(λ</a> <a id="2733" href="README.Data.Container.FreeMonad.html#2733" class="Bound">i</a> <a id="2735" class="Symbol">→</a> <a id="2737" href="README.Data.Container.FreeMonad.html#2632" class="Bound">C</a> <a id="2739" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2741" href="README.Data.Container.FreeMonad.html#2680" class="Datatype">Tap</a> <a id="2745" href="README.Data.Container.FreeMonad.html#2733" class="Bound">i</a><a id="2746" class="Symbol">)</a> <a id="2748" href="README.Data.Container.FreeMonad.html#2685" class="Bound">i</a> <a id="2750" class="Symbol">→</a> <a id="2752" href="README.Data.Container.FreeMonad.html#2680" class="Datatype">Tap</a> <a id="2756" href="README.Data.Container.FreeMonad.html#2685" class="Bound">i</a>

<a id="2759" class="Comment">-- We can run a given tap for a set number of steps and collect the elements</a>
<a id="2836" class="Comment">-- thus generated along the way. This gives us a `C ⋆_` computation of a vector.</a>

<a id="2918" class="Keyword">module</a> <a id="2925" href="README.Data.Container.FreeMonad.html#2925" class="Module">_</a> <a id="2927" class="Symbol">{</a><a id="2928" href="README.Data.Container.FreeMonad.html#2928" class="Bound">C</a> <a id="2930" class="Symbol">:</a> <a id="2932" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="2942" href="Level.html#521" class="Function">0ℓ</a> <a id="2945" href="Level.html#521" class="Function">0ℓ</a><a id="2947" class="Symbol">}</a> <a id="2949" class="Symbol">{</a><a id="2950" href="README.Data.Container.FreeMonad.html#2950" class="Bound">A</a> <a id="2952" class="Symbol">:</a> <a id="2954" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2958" href="Level.html#521" class="Function">0ℓ</a><a id="2960" class="Symbol">}</a> <a id="2962" class="Keyword">where</a>

  <a id="2971" href="README.Data.Container.FreeMonad.html#2971" class="Function">take</a> <a id="2976" class="Symbol">:</a> <a id="2978" href="README.Data.Container.FreeMonad.html#2680" class="Datatype">Tap</a> <a id="2982" href="README.Data.Container.FreeMonad.html#2928" class="Bound">C</a> <a id="2984" href="README.Data.Container.FreeMonad.html#2950" class="Bound">A</a> <a id="2986" class="Symbol">_</a> <a id="2988" class="Symbol">→</a> <a id="2990" class="Symbol">(</a><a id="2991" href="README.Data.Container.FreeMonad.html#2991" class="Bound">n</a> <a id="2993" class="Symbol">:</a> <a id="2995" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="2996" class="Symbol">)</a> <a id="2998" class="Symbol">→</a> <a id="3000" href="README.Data.Container.FreeMonad.html#2928" class="Bound">C</a> <a id="3002" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3004" href="Data.Vec.Base.html#1066" class="Datatype">Vec</a> <a id="3008" href="README.Data.Container.FreeMonad.html#2950" class="Bound">A</a> <a id="3010" href="README.Data.Container.FreeMonad.html#2991" class="Bound">n</a>
  <a id="3014" href="README.Data.Container.FreeMonad.html#2971" class="Function">take</a> <a id="3019" class="Symbol">_</a> <a id="3021" class="Number">0</a> <a id="3023" class="Symbol">=</a> <a id="3025" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3030" href="Data.Vec.Base.html#1102" class="InductiveConstructor">[]</a>
  <a id="3035" href="README.Data.Container.FreeMonad.html#2971" class="Function">take</a> <a id="3040" class="Symbol">(</a><a id="3041" href="README.Data.Container.FreeMonad.html#3041" class="Bound">x</a> <a id="3043" href="README.Data.Container.FreeMonad.html#2714" class="InductiveConstructor Operator">∷</a> <a id="3045" class="Symbol">_)</a> <a id="3048" class="Number">1</a> <a id="3050" class="Symbol">=</a> <a id="3052" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3057" class="Symbol">(</a><a id="3058" href="README.Data.Container.FreeMonad.html#3041" class="Bound">x</a> <a id="3060" href="Data.Vec.Base.html#1121" class="InductiveConstructor Operator">∷</a> <a id="3062" href="Data.Vec.Base.html#1102" class="InductiveConstructor">[]</a><a id="3064" class="Symbol">)</a>
  <a id="3068" href="README.Data.Container.FreeMonad.html#2971" class="CatchallClause Function">take</a><a id="3072" class="CatchallClause"> </a><a id="3073" class="CatchallClause Symbol">(</a><a id="3074" href="README.Data.Container.FreeMonad.html#3074" class="CatchallClause Bound">x</a><a id="3075" class="CatchallClause"> </a><a id="3076" href="README.Data.Container.FreeMonad.html#2714" class="CatchallClause InductiveConstructor Operator">∷</a><a id="3077" class="CatchallClause"> </a><a id="3078" href="README.Data.Container.FreeMonad.html#3078" class="CatchallClause Bound">mxs</a><a id="3081" class="CatchallClause Symbol">)</a><a id="3082" class="CatchallClause"> </a><a id="3083" class="CatchallClause Symbol">(</a><a id="3084" href="Agda.Builtin.Nat.html#217" class="CatchallClause InductiveConstructor">suc</a><a id="3087" class="CatchallClause"> </a><a id="3088" href="README.Data.Container.FreeMonad.html#3088" class="CatchallClause Bound">n</a><a id="3089" class="CatchallClause Symbol">)</a> <a id="3091" class="Symbol">=</a> <a id="3093" class="Keyword">do</a>
    <a id="3100" href="README.Data.Container.FreeMonad.html#3100" class="Bound">xs</a> <a id="3103" href="Data.Container.FreeMonad.html#3830" class="Function Operator">←</a> <a id="3105" href="README.Data.Container.FreeMonad.html#3078" class="Bound">mxs</a> <a id="3109" class="Symbol">.</a><a id="3110" href="Codata.Sized.Thunk.html#556" class="Field">force</a>
    <a id="3120" href="README.Data.Container.FreeMonad.html#3120" class="Bound">rest</a> <a id="3125" href="Data.Container.FreeMonad.html#3830" class="Function Operator">←</a> <a id="3127" href="README.Data.Container.FreeMonad.html#2971" class="Function">take</a> <a id="3132" href="README.Data.Container.FreeMonad.html#3100" class="Bound">xs</a> <a id="3135" href="README.Data.Container.FreeMonad.html#3088" class="Bound">n</a>
    <a id="3141" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3146" class="Symbol">(</a><a id="3147" href="README.Data.Container.FreeMonad.html#3074" class="Bound">x</a> <a id="3149" href="Data.Vec.Base.html#1121" class="InductiveConstructor Operator">∷</a> <a id="3151" href="README.Data.Container.FreeMonad.html#3120" class="Bound">rest</a><a id="3155" class="Symbol">)</a>

<a id="3158" class="Comment">-- A stream of all the natural numbers starting from a given value is an</a>
<a id="3231" class="Comment">-- example of a tap.</a>

<a id="natsFrom"></a><a id="3253" href="README.Data.Container.FreeMonad.html#3253" class="Function">natsFrom</a> <a id="3262" class="Symbol">:</a> <a id="3264" class="Symbol">∀</a> <a id="3266" class="Symbol">{</a><a id="3267" href="README.Data.Container.FreeMonad.html#3267" class="Bound">i</a><a id="3268" class="Symbol">}</a> <a id="3270" class="Symbol">→</a> <a id="3272" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="3278" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="3280" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3282" href="README.Data.Container.FreeMonad.html#2680" class="Datatype">Tap</a> <a id="3286" class="Symbol">(</a><a id="3287" href="README.Data.Container.FreeMonad.html#1066" class="Function">State</a> <a id="3293" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="3294" class="Symbol">)</a> <a id="3296" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="3298" href="README.Data.Container.FreeMonad.html#3267" class="Bound">i</a>
<a id="3300" href="README.Data.Container.FreeMonad.html#3253" class="Function">natsFrom</a> <a id="3309" class="Symbol">=</a> <a id="3311" class="Keyword">let</a> <a id="3315" class="Keyword">open</a> <a id="3320" href="Effect.Monad.html#821" class="Module">RawMonad</a> <a id="3329" href="Data.Container.FreeMonad.html#4237" class="Function">rawMonad</a> <a id="3338" class="Keyword">using</a> <a id="3344" class="Symbol">(</a><a id="3345" href="Effect.Monad.Indexed.html#745" class="Function Operator">_&gt;&gt;_</a><a id="3349" class="Symbol">)</a> <a id="3351" class="Keyword">in</a> <a id="3354" class="Keyword">do</a>
  <a id="3359" href="README.Data.Container.FreeMonad.html#3359" class="Bound">n</a> <a id="3361" href="Data.Container.FreeMonad.html#3830" class="Function Operator">←</a> <a id="3363" href="README.Data.Container.FreeMonad.html#1183" class="Function">get</a>
  <a id="3369" href="README.Data.Container.FreeMonad.html#1239" class="Function">put</a> <a id="3373" class="Symbol">(</a><a id="3374" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="3378" href="README.Data.Container.FreeMonad.html#3359" class="Bound">n</a><a id="3379" class="Symbol">)</a>
  <a id="3383" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3388" class="Symbol">(</a><a id="3389" href="README.Data.Container.FreeMonad.html#3359" class="Bound">n</a> <a id="3391" href="README.Data.Container.FreeMonad.html#2714" class="InductiveConstructor Operator">∷</a> <a id="3393" class="Symbol">λ</a> <a id="3395" class="Keyword">where</a> <a id="3401" class="Symbol">.</a><a id="3402" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="3408" class="Symbol">→</a> <a id="3410" href="README.Data.Container.FreeMonad.html#3253" class="Function">natsFrom</a><a id="3418" class="Symbol">)</a>

<a id="3421" class="Comment">-- We can use `take` to capture an initial segment of the `natsFrom` tap</a>
<a id="3494" class="Comment">-- and, after running the state operations, observe that it does generate</a>
<a id="3568" class="Comment">-- successive numbers.</a>

<a id="3592" href="README.Data.Container.FreeMonad.html#3592" class="Function">_</a> <a id="3594" class="Symbol">:</a> <a id="3596" class="Symbol">∀</a> <a id="3598" href="README.Data.Container.FreeMonad.html#3598" class="Bound">k</a> <a id="3600" class="Symbol">→</a>
    <a id="3606" href="README.Data.Container.FreeMonad.html#1523" class="Function">runState</a> <a id="3615" class="Symbol">(</a><a id="3616" href="README.Data.Container.FreeMonad.html#3253" class="Function">natsFrom</a> <a id="3625" href="Data.Container.FreeMonad.html#3830" class="Function Operator">&gt;&gt;=</a> <a id="3629" class="Symbol">λ</a> <a id="3631" href="README.Data.Container.FreeMonad.html#3631" class="Bound">ns</a> <a id="3634" class="Symbol">→</a> <a id="3636" href="README.Data.Container.FreeMonad.html#2971" class="Function">take</a> <a id="3641" href="README.Data.Container.FreeMonad.html#3631" class="Bound">ns</a> <a id="3644" class="Number">5</a><a id="3645" class="Symbol">)</a> <a id="3647" href="README.Data.Container.FreeMonad.html#3598" class="Bound">k</a>
  <a id="3651" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="3653" class="Symbol">(</a><a id="3654" href="README.Data.Container.FreeMonad.html#3598" class="Bound">k</a> <a id="3656" class="InductiveConstructor Operator">∷</a> <a id="3658" class="Number">1</a> <a id="3660" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3662" href="README.Data.Container.FreeMonad.html#3598" class="Bound">k</a> <a id="3664" class="InductiveConstructor Operator">∷</a> <a id="3666" class="Number">2</a> <a id="3668" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3670" href="README.Data.Container.FreeMonad.html#3598" class="Bound">k</a> <a id="3672" class="InductiveConstructor Operator">∷</a> <a id="3674" class="Number">3</a> <a id="3676" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3678" href="README.Data.Container.FreeMonad.html#3598" class="Bound">k</a> <a id="3680" class="InductiveConstructor Operator">∷</a> <a id="3682" class="Number">4</a> <a id="3684" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3686" href="README.Data.Container.FreeMonad.html#3598" class="Bound">k</a> <a id="3688" class="InductiveConstructor Operator">∷</a> <a id="3690" href="Data.Vec.Base.html#1102" class="InductiveConstructor">[]</a> <a id="3693" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3695" class="Number">5</a> <a id="3697" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">+</a> <a id="3699" href="README.Data.Container.FreeMonad.html#3598" class="Bound">k</a><a id="3700" class="Symbol">)</a>
<a id="3702" class="Symbol">_</a> <a id="3704" class="Symbol">=</a> <a id="3706" class="Symbol">λ</a> <a id="3708" href="README.Data.Container.FreeMonad.html#3708" class="Bound">k</a> <a id="3710" class="Symbol">→</a> <a id="3712" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
</pre></body></html>
<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Product.Effectful.Left</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Left-biased universe-sensitive functor and monad instances for the</a>
<a id="176" class="Comment">-- Product type.</a>
<a id="193" class="Comment">--</a>
<a id="196" class="Comment">-- To minimize the universe level of the RawFunctor, we require that</a>
<a id="265" class="Comment">-- elements of B are &quot;lifted&quot; to a copy of B at a higher universe level</a>
<a id="337" class="Comment">-- (a ⊔ b). See the Data.Product.Effectful.Examples for how this is</a>
<a id="405" class="Comment">-- done.</a>
<a id="414" class="Comment">------------------------------------------------------------------------</a>

<a id="488" class="Symbol">{-#</a> <a id="492" class="Keyword">OPTIONS</a> <a id="500" class="Pragma">--cubical-compatible</a> <a id="521" class="Pragma">--safe</a> <a id="528" class="Symbol">#-}</a>

<a id="533" class="Keyword">open</a> <a id="538" class="Keyword">import</a> <a id="545" href="Algebra.html" class="Module">Algebra</a>
<a id="553" class="Keyword">open</a> <a id="558" class="Keyword">import</a> <a id="565" href="Level.html" class="Module">Level</a>

<a id="572" class="Keyword">module</a> <a id="579" href="Data.Product.Effectful.Left.html" class="Module">Data.Product.Effectful.Left</a>
  <a id="609" class="Symbol">{</a><a id="610" href="Data.Product.Effectful.Left.html#610" class="Bound">a</a> <a id="612" href="Data.Product.Effectful.Left.html#612" class="Bound">e</a><a id="613" class="Symbol">}</a> <a id="615" class="Symbol">(</a><a id="616" href="Data.Product.Effectful.Left.html#616" class="Bound">A</a> <a id="618" class="Symbol">:</a> <a id="620" href="Algebra.Bundles.html#5577" class="Record">RawMonoid</a> <a id="630" href="Data.Product.Effectful.Left.html#610" class="Bound">a</a> <a id="632" href="Data.Product.Effectful.Left.html#612" class="Bound">e</a><a id="633" class="Symbol">)</a> <a id="635" class="Symbol">(</a><a id="636" href="Data.Product.Effectful.Left.html#636" class="Bound">b</a> <a id="638" class="Symbol">:</a> <a id="640" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="645" class="Symbol">)</a> <a id="647" class="Keyword">where</a>

<a id="654" class="Keyword">open</a> <a id="659" class="Keyword">import</a> <a id="666" href="Data.Product.html" class="Module">Data.Product</a>
<a id="679" class="Keyword">import</a> <a id="686" href="Data.Product.Effectful.Left.Base.html" class="Module">Data.Product.Effectful.Left.Base</a> <a id="719" class="Symbol">as</a> <a id="722" class="Module">Base</a>
<a id="727" class="Keyword">open</a> <a id="732" class="Keyword">import</a> <a id="739" href="Effect.Applicative.html" class="Module">Effect.Applicative</a> <a id="758" class="Keyword">using</a> <a id="764" class="Symbol">(</a><a id="765" href="Effect.Applicative.html#499" class="Function">RawApplicative</a><a id="779" class="Symbol">)</a>
<a id="781" class="Keyword">open</a> <a id="786" class="Keyword">import</a> <a id="793" href="Effect.Monad.html" class="Module">Effect.Monad</a> <a id="806" class="Keyword">using</a> <a id="812" class="Symbol">(</a><a id="813" href="Effect.Monad.html#437" class="Function">RawMonad</a><a id="821" class="Symbol">;</a> <a id="823" href="Effect.Monad.html#516" class="Function">RawMonadT</a><a id="832" class="Symbol">)</a>
<a id="834" class="Keyword">open</a> <a id="839" class="Keyword">import</a> <a id="846" href="Function.Base.html" class="Module">Function.Base</a> <a id="860" class="Keyword">using</a> <a id="866" class="Symbol">(</a><a id="867" href="Function.Base.html#704" class="Function">id</a><a id="869" class="Symbol">;</a> <a id="871" href="Function.Base.html#1638" class="Function">flip</a><a id="875" class="Symbol">;</a> <a id="877" href="Function.Base.html#1115" class="Function Operator">_∘_</a><a id="880" class="Symbol">;</a> <a id="882" href="Function.Base.html#3646" class="Function Operator">_∘′_</a><a id="886" class="Symbol">)</a>
<a id="888" class="Keyword">import</a> <a id="895" href="Function.Identity.Effectful.html" class="Module">Function.Identity.Effectful</a> <a id="923" class="Symbol">as</a> <a id="926" class="Module">Id</a>

<a id="930" class="Keyword">open</a> <a id="935" href="Algebra.Bundles.html#5577" class="Module">RawMonoid</a> <a id="945" href="Data.Product.Effectful.Left.html#616" class="Bound">A</a>

<a id="948" class="Comment">------------------------------------------------------------------------</a>
<a id="1021" class="Comment">-- Re-export the base contents publically</a>

<a id="1064" class="Keyword">open</a> <a id="1069" href="Data.Product.Effectful.Left.Base.html" class="Module">Base</a> <a id="1074" href="Algebra.Bundles.html#5659" class="Function">Carrier</a> <a id="1082" href="Data.Product.Effectful.Left.html#636" class="Bound">b</a> <a id="1084" class="Keyword">public</a>

<a id="1092" class="Comment">------------------------------------------------------------------------</a>
<a id="1165" class="Comment">-- Basic records</a>

<a id="applicative"></a><a id="1183" href="Data.Product.Effectful.Left.html#1183" class="Function">applicative</a> <a id="1195" class="Symbol">:</a> <a id="1197" href="Effect.Applicative.html#499" class="Function">RawApplicative</a> <a id="1212" href="Data.Product.Effectful.Left.Base.html#900" class="Function">Productₗ</a>
<a id="1221" href="Data.Product.Effectful.Left.html#1183" class="Function">applicative</a> <a id="1233" class="Symbol">=</a> <a id="1235" class="Keyword">record</a>
  <a id="1244" class="Symbol">{</a> <a id="1246" href="Effect.Applicative.Indexed.html#1000" class="Field">pure</a> <a id="1251" class="Symbol">=</a> <a id="1253" href="Algebra.Bundles.html#5733" class="Function">ε</a> <a id="1255" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,_</a>
  <a id="1260" class="Symbol">;</a> <a id="1262" href="Effect.Applicative.Indexed.html#1031" class="Field Operator">_⊛_</a>  <a id="1267" class="Symbol">=</a> <a id="1269" href="Data.Product.html#3069" class="Function">zip</a> <a id="1273" href="Algebra.Bundles.html#5707" class="Function Operator">_∙_</a> <a id="1277" href="Function.Base.html#704" class="Function">id</a>
  <a id="1282" class="Symbol">}</a>

<a id="1285" class="Comment">-- The monad instance also requires some mucking about with universe levels.</a>
<a id="monadT"></a><a id="1362" href="Data.Product.Effectful.Left.html#1362" class="Function">monadT</a> <a id="1369" class="Symbol">:</a> <a id="1371" href="Effect.Monad.html#516" class="Function">RawMonadT</a> <a id="1381" class="Symbol">(</a><a id="1382" href="Function.Base.html#3646" class="Function Operator">_∘′</a> <a id="1386" href="Data.Product.Effectful.Left.Base.html#900" class="Function">Productₗ</a><a id="1394" class="Symbol">)</a>
<a id="1396" href="Data.Product.Effectful.Left.html#1362" class="Function">monadT</a> <a id="1403" href="Data.Product.Effectful.Left.html#1403" class="Bound">M</a> <a id="1405" class="Symbol">=</a> <a id="1407" class="Keyword">record</a>
  <a id="1416" class="Symbol">{</a> <a id="1418" href="Effect.Monad.Indexed.html#654" class="Field">return</a> <a id="1425" class="Symbol">=</a> <a id="1427" href="Effect.Applicative.Indexed.html#1000" class="Function">pure</a> <a id="1432" href="Function.Base.html#3646" class="Function Operator">∘′</a> <a id="1435" class="Symbol">(</a><a id="1436" href="Algebra.Bundles.html#5733" class="Function">ε</a> <a id="1438" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,_</a><a id="1440" class="Symbol">)</a>
  <a id="1444" class="Symbol">;</a> <a id="1446" href="Effect.Monad.Indexed.html#687" class="Field Operator">_&gt;&gt;=_</a>  <a id="1453" class="Symbol">=</a> <a id="1455" class="Symbol">λ</a> <a id="1457" href="Data.Product.Effectful.Left.html#1457" class="Bound">ma</a> <a id="1460" href="Data.Product.Effectful.Left.html#1460" class="Bound">f</a> <a id="1462" class="Symbol">→</a> <a id="1464" href="Data.Product.Effectful.Left.html#1457" class="Bound">ma</a> <a id="1467" href="Effect.Monad.Indexed.html#687" class="Field Operator">&gt;&gt;=</a> <a id="1471" href="Data.Product.html#3431" class="Function">uncurry</a> <a id="1479" class="Symbol">λ</a> <a id="1481" href="Data.Product.Effectful.Left.html#1481" class="Bound">a</a> <a id="1483" href="Data.Product.Effectful.Left.html#1483" class="Bound">x</a> <a id="1485" class="Symbol">→</a> <a id="1487" href="Data.Product.html#2634" class="Function">map₁</a> <a id="1492" class="Symbol">(</a><a id="1493" href="Data.Product.Effectful.Left.html#1481" class="Bound">a</a> <a id="1495" href="Algebra.Bundles.html#5707" class="Function Operator">∙_</a><a id="1497" class="Symbol">)</a> <a id="1499" href="Effect.Functor.html#607" class="Function Operator">&lt;$&gt;</a> <a id="1503" href="Data.Product.Effectful.Left.html#1460" class="Bound">f</a> <a id="1505" href="Data.Product.Effectful.Left.html#1483" class="Bound">x</a>
  <a id="1509" class="Symbol">}</a> <a id="1511" class="Keyword">where</a> <a id="1517" class="Keyword">open</a> <a id="1522" href="Effect.Monad.html#821" class="Module">RawMonad</a> <a id="1531" href="Data.Product.Effectful.Left.html#1403" class="Bound">M</a>

<a id="monad"></a><a id="1534" href="Data.Product.Effectful.Left.html#1534" class="Function">monad</a> <a id="1540" class="Symbol">:</a> <a id="1542" href="Effect.Monad.html#437" class="Function">RawMonad</a> <a id="1551" href="Data.Product.Effectful.Left.Base.html#900" class="Function">Productₗ</a>
<a id="1560" href="Data.Product.Effectful.Left.html#1534" class="Function">monad</a> <a id="1566" class="Symbol">=</a> <a id="1568" href="Data.Product.Effectful.Left.html#1362" class="Function">monadT</a> <a id="1575" href="Function.Identity.Effectful.html#649" class="Function">Id.monad</a>

<a id="1585" class="Comment">------------------------------------------------------------------------</a>
<a id="1658" class="Comment">-- Get access to other monadic functions</a>

<a id="1700" class="Keyword">module</a> <a id="TraversableA"></a><a id="1707" href="Data.Product.Effectful.Left.html#1707" class="Module">TraversableA</a> <a id="1720" class="Symbol">{</a><a id="1721" href="Data.Product.Effectful.Left.html#1721" class="Bound">F</a><a id="1722" class="Symbol">}</a> <a id="1724" class="Symbol">(</a><a id="1725" href="Data.Product.Effectful.Left.html#1725" class="Bound">App</a> <a id="1729" class="Symbol">:</a> <a id="1731" href="Effect.Applicative.html#499" class="Function">RawApplicative</a> <a id="1746" class="Symbol">{</a><a id="1747" href="Data.Product.Effectful.Left.html#610" class="Bound">a</a> <a id="1749" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1751" href="Data.Product.Effectful.Left.html#636" class="Bound">b</a><a id="1752" class="Symbol">}</a> <a id="1754" href="Data.Product.Effectful.Left.html#1721" class="Bound">F</a><a id="1755" class="Symbol">)</a> <a id="1757" class="Keyword">where</a>

  <a id="1766" class="Keyword">open</a> <a id="1771" href="Effect.Applicative.html#607" class="Module">RawApplicative</a> <a id="1786" href="Data.Product.Effectful.Left.html#1725" class="Bound">App</a>

  <a id="TraversableA.sequenceA"></a><a id="1793" href="Data.Product.Effectful.Left.html#1793" class="Function">sequenceA</a> <a id="1803" class="Symbol">:</a> <a id="1805" class="Symbol">∀</a> <a id="1807" class="Symbol">{</a><a id="1808" href="Data.Product.Effectful.Left.html#1808" class="Bound">A</a><a id="1809" class="Symbol">}</a> <a id="1811" class="Symbol">→</a> <a id="1813" href="Data.Product.Effectful.Left.Base.html#900" class="Function">Productₗ</a> <a id="1822" class="Symbol">(</a><a id="1823" href="Data.Product.Effectful.Left.html#1721" class="Bound">F</a> <a id="1825" href="Data.Product.Effectful.Left.html#1808" class="Bound">A</a><a id="1826" class="Symbol">)</a> <a id="1828" class="Symbol">→</a> <a id="1830" href="Data.Product.Effectful.Left.html#1721" class="Bound">F</a> <a id="1832" class="Symbol">(</a><a id="1833" href="Data.Product.Effectful.Left.Base.html#900" class="Function">Productₗ</a> <a id="1842" href="Data.Product.Effectful.Left.html#1808" class="Bound">A</a><a id="1843" class="Symbol">)</a>
  <a id="1847" href="Data.Product.Effectful.Left.html#1793" class="Function">sequenceA</a> <a id="1857" class="Symbol">(</a><a id="1858" href="Data.Product.Effectful.Left.html#1858" class="Bound">x</a> <a id="1860" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="1862" href="Data.Product.Effectful.Left.html#1862" class="Bound">fa</a><a id="1864" class="Symbol">)</a> <a id="1866" class="Symbol">=</a> <a id="1868" class="Symbol">(</a><a id="1869" href="Data.Product.Effectful.Left.html#1858" class="Bound">x</a> <a id="1871" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,_</a><a id="1873" class="Symbol">)</a> <a id="1875" href="Effect.Functor.html#607" class="Function Operator">&lt;$&gt;</a> <a id="1879" href="Data.Product.Effectful.Left.html#1862" class="Bound">fa</a>

  <a id="TraversableA.mapA"></a><a id="1885" href="Data.Product.Effectful.Left.html#1885" class="Function">mapA</a> <a id="1890" class="Symbol">:</a> <a id="1892" class="Symbol">∀</a> <a id="1894" class="Symbol">{</a><a id="1895" href="Data.Product.Effectful.Left.html#1895" class="Bound">A</a> <a id="1897" href="Data.Product.Effectful.Left.html#1897" class="Bound">B</a><a id="1898" class="Symbol">}</a> <a id="1900" class="Symbol">→</a> <a id="1902" class="Symbol">(</a><a id="1903" href="Data.Product.Effectful.Left.html#1895" class="Bound">A</a> <a id="1905" class="Symbol">→</a> <a id="1907" href="Data.Product.Effectful.Left.html#1721" class="Bound">F</a> <a id="1909" href="Data.Product.Effectful.Left.html#1897" class="Bound">B</a><a id="1910" class="Symbol">)</a> <a id="1912" class="Symbol">→</a> <a id="1914" href="Data.Product.Effectful.Left.Base.html#900" class="Function">Productₗ</a> <a id="1923" href="Data.Product.Effectful.Left.html#1895" class="Bound">A</a> <a id="1925" class="Symbol">→</a> <a id="1927" href="Data.Product.Effectful.Left.html#1721" class="Bound">F</a> <a id="1929" class="Symbol">(</a><a id="1930" href="Data.Product.Effectful.Left.Base.html#900" class="Function">Productₗ</a> <a id="1939" href="Data.Product.Effectful.Left.html#1897" class="Bound">B</a><a id="1940" class="Symbol">)</a>
  <a id="1944" href="Data.Product.Effectful.Left.html#1885" class="Function">mapA</a> <a id="1949" href="Data.Product.Effectful.Left.html#1949" class="Bound">f</a> <a id="1951" class="Symbol">=</a> <a id="1953" href="Data.Product.Effectful.Left.html#1793" class="Function">sequenceA</a> <a id="1963" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="1965" href="Data.Product.html#2684" class="Function">map₂</a> <a id="1970" href="Data.Product.Effectful.Left.html#1949" class="Bound">f</a>

  <a id="TraversableA.forA"></a><a id="1975" href="Data.Product.Effectful.Left.html#1975" class="Function">forA</a> <a id="1980" class="Symbol">:</a> <a id="1982" class="Symbol">∀</a> <a id="1984" class="Symbol">{</a><a id="1985" href="Data.Product.Effectful.Left.html#1985" class="Bound">A</a> <a id="1987" href="Data.Product.Effectful.Left.html#1987" class="Bound">B</a><a id="1988" class="Symbol">}</a> <a id="1990" class="Symbol">→</a> <a id="1992" href="Data.Product.Effectful.Left.Base.html#900" class="Function">Productₗ</a> <a id="2001" href="Data.Product.Effectful.Left.html#1985" class="Bound">A</a> <a id="2003" class="Symbol">→</a> <a id="2005" class="Symbol">(</a><a id="2006" href="Data.Product.Effectful.Left.html#1985" class="Bound">A</a> <a id="2008" class="Symbol">→</a> <a id="2010" href="Data.Product.Effectful.Left.html#1721" class="Bound">F</a> <a id="2012" href="Data.Product.Effectful.Left.html#1987" class="Bound">B</a><a id="2013" class="Symbol">)</a> <a id="2015" class="Symbol">→</a> <a id="2017" href="Data.Product.Effectful.Left.html#1721" class="Bound">F</a> <a id="2019" class="Symbol">(</a><a id="2020" href="Data.Product.Effectful.Left.Base.html#900" class="Function">Productₗ</a> <a id="2029" href="Data.Product.Effectful.Left.html#1987" class="Bound">B</a><a id="2030" class="Symbol">)</a>
  <a id="2034" href="Data.Product.Effectful.Left.html#1975" class="Function">forA</a> <a id="2039" class="Symbol">=</a> <a id="2041" href="Function.Base.html#1638" class="Function">flip</a> <a id="2046" href="Data.Product.Effectful.Left.html#1885" class="Function">mapA</a>

<a id="2052" class="Keyword">module</a> <a id="TraversableM"></a><a id="2059" href="Data.Product.Effectful.Left.html#2059" class="Module">TraversableM</a> <a id="2072" class="Symbol">{</a><a id="2073" href="Data.Product.Effectful.Left.html#2073" class="Bound">M</a><a id="2074" class="Symbol">}</a> <a id="2076" class="Symbol">(</a><a id="2077" href="Data.Product.Effectful.Left.html#2077" class="Bound">Mon</a> <a id="2081" class="Symbol">:</a> <a id="2083" href="Effect.Monad.html#437" class="Function">RawMonad</a> <a id="2092" class="Symbol">{</a><a id="2093" href="Data.Product.Effectful.Left.html#610" class="Bound">a</a> <a id="2095" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2097" href="Data.Product.Effectful.Left.html#636" class="Bound">b</a><a id="2098" class="Symbol">}</a> <a id="2100" href="Data.Product.Effectful.Left.html#2073" class="Bound">M</a><a id="2101" class="Symbol">)</a> <a id="2103" class="Keyword">where</a>

  <a id="2112" class="Keyword">open</a> <a id="2117" href="Effect.Monad.html#821" class="Module">RawMonad</a> <a id="2126" href="Data.Product.Effectful.Left.html#2077" class="Bound">Mon</a>

  <a id="2133" class="Keyword">open</a> <a id="2138" href="Data.Product.Effectful.Left.html#1707" class="Module">TraversableA</a> <a id="2151" href="Effect.Monad.Indexed.html#1151" class="Function">rawIApplicative</a> <a id="2167" class="Keyword">public</a>
    <a id="2178" class="Keyword">renaming</a>
    <a id="2191" class="Symbol">(</a> <a id="2193" href="Data.Product.Effectful.Left.html#1793" class="Function">sequenceA</a> <a id="2203" class="Symbol">to</a> <a id="2206" class="Function">sequenceM</a>
    <a id="2220" class="Symbol">;</a> <a id="2222" href="Data.Product.Effectful.Left.html#1885" class="Function">mapA</a>      <a id="2232" class="Symbol">to</a> <a id="2235" class="Function">mapM</a>
    <a id="2244" class="Symbol">;</a> <a id="2246" href="Data.Product.Effectful.Left.html#1975" class="Function">forA</a>      <a id="2256" class="Symbol">to</a> <a id="2259" class="Function">forM</a>
    <a id="2268" class="Symbol">)</a>
</pre></body></html>
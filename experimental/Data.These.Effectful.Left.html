<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.These.Effectful.Left</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Left-biased universe-sensitive functor and monad instances for These.</a>
<a id="179" class="Comment">--</a>
<a id="182" class="Comment">------------------------------------------------------------------------</a>

<a id="256" class="Comment">-- To minimize the universe level of the RawFunctor, we require that</a>
<a id="325" class="Comment">-- elements of B are &quot;lifted&quot; to a copy of B at a higher universe level</a>
<a id="397" class="Comment">-- (a ⊔ b).</a>
<a id="409" class="Comment">-- See the Data.Product.Effectful.Examples for how this is done in a</a>
<a id="478" class="Comment">-- Product-based similar setting.</a>

<a id="513" class="Comment">-- This functor can be understood as a notion of computation which can</a>
<a id="584" class="Comment">-- either fail (this), succeed (that) or accumulate warnings whilst</a>
<a id="652" class="Comment">-- delivering a successful computation (these).</a>

<a id="701" class="Comment">-- It is a good alternative to Data.Product.Effectful when the notion</a>
<a id="771" class="Comment">-- of warnings does not have a neutral element (e.g. List⁺).</a>

<a id="833" class="Symbol">{-#</a> <a id="837" class="Keyword">OPTIONS</a> <a id="845" class="Pragma">--cubical-compatible</a> <a id="866" class="Pragma">--safe</a> <a id="873" class="Symbol">#-}</a>

<a id="878" class="Keyword">open</a> <a id="883" class="Keyword">import</a> <a id="890" href="Level.html" class="Module">Level</a>
<a id="896" class="Keyword">open</a> <a id="901" class="Keyword">import</a> <a id="908" href="Algebra.html" class="Module">Algebra</a>

<a id="917" class="Keyword">module</a> <a id="924" href="Data.These.Effectful.Left.html" class="Module">Data.These.Effectful.Left</a> <a id="950" class="Symbol">{</a><a id="951" href="Data.These.Effectful.Left.html#951" class="Bound">c</a> <a id="953" href="Data.These.Effectful.Left.html#953" class="Bound">ℓ</a><a id="954" class="Symbol">}</a> <a id="956" class="Symbol">(</a><a id="957" href="Data.These.Effectful.Left.html#957" class="Bound">W</a> <a id="959" class="Symbol">:</a> <a id="961" href="Algebra.Bundles.html#4007" class="Record">Semigroup</a> <a id="971" href="Data.These.Effectful.Left.html#951" class="Bound">c</a> <a id="973" href="Data.These.Effectful.Left.html#953" class="Bound">ℓ</a><a id="974" class="Symbol">)</a> <a id="976" class="Symbol">(</a><a id="977" href="Data.These.Effectful.Left.html#977" class="Bound">b</a> <a id="979" class="Symbol">:</a> <a id="981" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="986" class="Symbol">)</a> <a id="988" class="Keyword">where</a>

<a id="995" class="Keyword">open</a> <a id="1000" href="Algebra.Bundles.html#4007" class="Module">Semigroup</a> <a id="1010" href="Data.These.Effectful.Left.html#957" class="Bound">W</a>
<a id="1012" class="Keyword">open</a> <a id="1017" class="Keyword">import</a> <a id="1024" href="Data.These.Effectful.Left.Base.html" class="Module">Data.These.Effectful.Left.Base</a> <a id="1055" href="Algebra.Bundles.html#4089" class="Function">Carrier</a> <a id="1063" href="Data.These.Effectful.Left.html#977" class="Bound">b</a> <a id="1065" class="Keyword">public</a>

<a id="1073" class="Keyword">open</a> <a id="1078" class="Keyword">import</a> <a id="1085" href="Data.These.Base.html" class="Module">Data.These.Base</a>
<a id="1101" class="Keyword">open</a> <a id="1106" class="Keyword">import</a> <a id="1113" href="Effect.Applicative.html" class="Module">Effect.Applicative</a>
<a id="1132" class="Keyword">open</a> <a id="1137" class="Keyword">import</a> <a id="1144" href="Effect.Monad.html" class="Module">Effect.Monad</a>

<a id="1158" class="Keyword">module</a> <a id="1165" href="Data.These.Effectful.Left.html#1165" class="Module">_</a> <a id="1167" class="Symbol">{</a><a id="1168" href="Data.These.Effectful.Left.html#1168" class="Bound">a</a> <a id="1170" href="Data.These.Effectful.Left.html#1170" class="Bound">b</a><a id="1171" class="Symbol">}</a> <a id="1173" class="Symbol">{</a><a id="1174" href="Data.These.Effectful.Left.html#1174" class="Bound">A</a> <a id="1176" class="Symbol">:</a> <a id="1178" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1182" href="Data.These.Effectful.Left.html#1168" class="Bound">a</a><a id="1183" class="Symbol">}</a> <a id="1185" class="Symbol">{</a><a id="1186" href="Data.These.Effectful.Left.html#1186" class="Bound">B</a> <a id="1188" class="Symbol">:</a> <a id="1190" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1194" href="Data.These.Effectful.Left.html#1170" class="Bound">b</a><a id="1195" class="Symbol">}</a> <a id="1197" class="Keyword">where</a>

<a id="applicative"></a><a id="1204" href="Data.These.Effectful.Left.html#1204" class="Function">applicative</a> <a id="1216" class="Symbol">:</a> <a id="1218" href="Effect.Applicative.html#499" class="Function">RawApplicative</a> <a id="1233" href="Data.These.Effectful.Left.Base.html#810" class="Function">Theseₗ</a>
<a id="1240" href="Data.These.Effectful.Left.html#1204" class="Function">applicative</a> <a id="1252" class="Symbol">=</a> <a id="1254" class="Keyword">record</a>
  <a id="1263" class="Symbol">{</a> <a id="1265" href="Effect.Applicative.Indexed.html#1000" class="Field">pure</a> <a id="1270" class="Symbol">=</a> <a id="1272" href="Data.These.Base.html#623" class="InductiveConstructor">that</a>
  <a id="1279" class="Symbol">;</a> <a id="1281" href="Effect.Applicative.Indexed.html#1031" class="Field Operator">_⊛_</a>  <a id="1286" class="Symbol">=</a> <a id="1288" href="Data.These.Effectful.Left.html#1304" class="Function">ap</a>
  <a id="1293" class="Symbol">}</a> <a id="1295" class="Keyword">where</a>

  <a id="1304" href="Data.These.Effectful.Left.html#1304" class="Function">ap</a> <a id="1307" class="Symbol">:</a> <a id="1309" class="Symbol">∀</a> <a id="1311" class="Symbol">{</a><a id="1312" href="Data.These.Effectful.Left.html#1312" class="Bound">A</a> <a id="1314" href="Data.These.Effectful.Left.html#1314" class="Bound">B</a><a id="1315" class="Symbol">}→</a> <a id="1318" href="Data.These.Effectful.Left.Base.html#810" class="Function">Theseₗ</a> <a id="1325" class="Symbol">(</a><a id="1326" href="Data.These.Effectful.Left.html#1312" class="Bound">A</a> <a id="1328" class="Symbol">→</a> <a id="1330" href="Data.These.Effectful.Left.html#1314" class="Bound">B</a><a id="1331" class="Symbol">)</a> <a id="1333" class="Symbol">→</a> <a id="1335" href="Data.These.Effectful.Left.Base.html#810" class="Function">Theseₗ</a> <a id="1342" href="Data.These.Effectful.Left.html#1312" class="Bound">A</a> <a id="1344" class="Symbol">→</a> <a id="1346" href="Data.These.Effectful.Left.Base.html#810" class="Function">Theseₗ</a> <a id="1353" href="Data.These.Effectful.Left.html#1314" class="Bound">B</a>
  <a id="1357" href="Data.These.Effectful.Left.html#1304" class="Function">ap</a> <a id="1360" class="Symbol">(</a><a id="1361" href="Data.These.Base.html#595" class="InductiveConstructor">this</a> <a id="1366" href="Data.These.Effectful.Left.html#1366" class="Bound">w</a><a id="1367" class="Symbol">)</a>    <a id="1372" href="Data.These.Effectful.Left.html#1372" class="Bound">t</a> <a id="1374" class="Symbol">=</a> <a id="1376" href="Data.These.Base.html#595" class="InductiveConstructor">this</a> <a id="1381" href="Data.These.Effectful.Left.html#1366" class="Bound">w</a>
  <a id="1385" href="Data.These.Effectful.Left.html#1304" class="Function">ap</a> <a id="1388" class="Symbol">(</a><a id="1389" href="Data.These.Base.html#623" class="InductiveConstructor">that</a> <a id="1394" href="Data.These.Effectful.Left.html#1394" class="Bound">f</a><a id="1395" class="Symbol">)</a>    <a id="1400" href="Data.These.Effectful.Left.html#1400" class="Bound">t</a> <a id="1402" class="Symbol">=</a> <a id="1404" href="Data.These.Base.html#1067" class="Function">map₂</a> <a id="1409" href="Data.These.Effectful.Left.html#1394" class="Bound">f</a> <a id="1411" href="Data.These.Effectful.Left.html#1400" class="Bound">t</a>
  <a id="1415" href="Data.These.Effectful.Left.html#1304" class="Function">ap</a> <a id="1418" class="Symbol">(</a><a id="1419" href="Data.These.Base.html#651" class="InductiveConstructor">these</a> <a id="1425" href="Data.These.Effectful.Left.html#1425" class="Bound">w</a> <a id="1427" href="Data.These.Effectful.Left.html#1427" class="Bound">f</a><a id="1428" class="Symbol">)</a> <a id="1430" href="Data.These.Effectful.Left.html#1430" class="Bound">t</a> <a id="1432" class="Symbol">=</a> <a id="1434" href="Data.These.Base.html#844" class="Function">map</a> <a id="1438" class="Symbol">(</a><a id="1439" href="Data.These.Effectful.Left.html#1425" class="Bound">w</a> <a id="1441" href="Algebra.Bundles.html#4145" class="Function Operator">∙_</a><a id="1443" class="Symbol">)</a> <a id="1445" href="Data.These.Effectful.Left.html#1427" class="Bound">f</a> <a id="1447" href="Data.These.Effectful.Left.html#1430" class="Bound">t</a>

<a id="monad"></a><a id="1450" href="Data.These.Effectful.Left.html#1450" class="Function">monad</a> <a id="1456" class="Symbol">:</a> <a id="1458" href="Effect.Monad.html#437" class="Function">RawMonad</a> <a id="1467" href="Data.These.Effectful.Left.Base.html#810" class="Function">Theseₗ</a>
<a id="1474" href="Data.These.Effectful.Left.html#1450" class="Function">monad</a> <a id="1480" class="Symbol">=</a> <a id="1482" class="Keyword">record</a>
  <a id="1491" class="Symbol">{</a> <a id="1493" href="Effect.Monad.Indexed.html#654" class="Field">return</a> <a id="1500" class="Symbol">=</a> <a id="1502" href="Data.These.Base.html#623" class="InductiveConstructor">that</a>
  <a id="1509" class="Symbol">;</a> <a id="1511" href="Effect.Monad.Indexed.html#687" class="Field Operator">_&gt;&gt;=_</a>  <a id="1518" class="Symbol">=</a> <a id="1520" href="Data.These.Effectful.Left.html#1538" class="Function">bind</a>
  <a id="1527" class="Symbol">}</a> <a id="1529" class="Keyword">where</a>

  <a id="1538" href="Data.These.Effectful.Left.html#1538" class="Function">bind</a> <a id="1543" class="Symbol">:</a> <a id="1545" class="Symbol">∀</a> <a id="1547" class="Symbol">{</a><a id="1548" href="Data.These.Effectful.Left.html#1548" class="Bound">A</a> <a id="1550" href="Data.These.Effectful.Left.html#1550" class="Bound">B</a><a id="1551" class="Symbol">}</a> <a id="1553" class="Symbol">→</a> <a id="1555" href="Data.These.Effectful.Left.Base.html#810" class="Function">Theseₗ</a> <a id="1562" href="Data.These.Effectful.Left.html#1548" class="Bound">A</a> <a id="1564" class="Symbol">→</a> <a id="1566" class="Symbol">(</a><a id="1567" href="Data.These.Effectful.Left.html#1548" class="Bound">A</a> <a id="1569" class="Symbol">→</a> <a id="1571" href="Data.These.Effectful.Left.Base.html#810" class="Function">Theseₗ</a> <a id="1578" href="Data.These.Effectful.Left.html#1550" class="Bound">B</a><a id="1579" class="Symbol">)</a> <a id="1581" class="Symbol">→</a> <a id="1583" href="Data.These.Effectful.Left.Base.html#810" class="Function">Theseₗ</a> <a id="1590" href="Data.These.Effectful.Left.html#1550" class="Bound">B</a>
  <a id="1594" href="Data.These.Effectful.Left.html#1538" class="Function">bind</a> <a id="1599" class="Symbol">(</a><a id="1600" href="Data.These.Base.html#595" class="InductiveConstructor">this</a> <a id="1605" href="Data.These.Effectful.Left.html#1605" class="Bound">w</a><a id="1606" class="Symbol">)</a>    <a id="1611" href="Data.These.Effectful.Left.html#1611" class="Bound">f</a> <a id="1613" class="Symbol">=</a> <a id="1615" href="Data.These.Base.html#595" class="InductiveConstructor">this</a> <a id="1620" href="Data.These.Effectful.Left.html#1605" class="Bound">w</a>
  <a id="1624" href="Data.These.Effectful.Left.html#1538" class="Function">bind</a> <a id="1629" class="Symbol">(</a><a id="1630" href="Data.These.Base.html#623" class="InductiveConstructor">that</a> <a id="1635" href="Data.These.Effectful.Left.html#1635" class="Bound">t</a><a id="1636" class="Symbol">)</a>    <a id="1641" href="Data.These.Effectful.Left.html#1641" class="Bound">f</a> <a id="1643" class="Symbol">=</a> <a id="1645" href="Data.These.Effectful.Left.html#1641" class="Bound">f</a> <a id="1647" href="Data.These.Effectful.Left.html#1635" class="Bound">t</a>
  <a id="1651" href="Data.These.Effectful.Left.html#1538" class="Function">bind</a> <a id="1656" class="Symbol">(</a><a id="1657" href="Data.These.Base.html#651" class="InductiveConstructor">these</a> <a id="1663" href="Data.These.Effectful.Left.html#1663" class="Bound">w</a> <a id="1665" href="Data.These.Effectful.Left.html#1665" class="Bound">t</a><a id="1666" class="Symbol">)</a> <a id="1668" href="Data.These.Effectful.Left.html#1668" class="Bound">f</a> <a id="1670" class="Symbol">=</a> <a id="1672" href="Data.These.Base.html#1005" class="Function">map₁</a> <a id="1677" class="Symbol">(</a><a id="1678" href="Data.These.Effectful.Left.html#1663" class="Bound">w</a> <a id="1680" href="Algebra.Bundles.html#4145" class="Function Operator">∙_</a><a id="1682" class="Symbol">)</a> <a id="1684" class="Symbol">(</a><a id="1685" href="Data.These.Effectful.Left.html#1668" class="Bound">f</a> <a id="1687" href="Data.These.Effectful.Left.html#1665" class="Bound">t</a><a id="1688" class="Symbol">)</a>
</pre></body></html>
<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Container.FreeMonad</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- The free monad construction on containers</a>
<a id="151" class="Comment">------------------------------------------------------------------------</a>

<a id="225" class="Symbol">{-#</a> <a id="229" class="Keyword">OPTIONS</a> <a id="237" class="Pragma">--cubical-compatible</a> <a id="258" class="Pragma">--safe</a> <a id="265" class="Symbol">#-}</a>

<a id="270" class="Keyword">module</a> <a id="277" href="Data.Container.FreeMonad.html" class="Module">Data.Container.FreeMonad</a> <a id="302" class="Keyword">where</a>

<a id="309" class="Keyword">open</a> <a id="314" class="Keyword">import</a> <a id="321" href="Level.html" class="Module">Level</a> <a id="327" class="Keyword">using</a> <a id="333" class="Symbol">(</a><a id="334" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="339" class="Symbol">;</a> <a id="341" href="Agda.Primitive.html#804" class="Primitive Operator">_⊔_</a><a id="344" class="Symbol">)</a>
<a id="346" class="Keyword">open</a> <a id="351" class="Keyword">import</a> <a id="358" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="372" class="Keyword">using</a> <a id="378" class="Symbol">(</a><a id="379" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a><a id="383" class="Symbol">;</a> <a id="385" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="390" class="Symbol">;</a> <a id="392" href="Data.Sum.Base.html#1098" class="Function Operator">[_,_]′</a><a id="398" class="Symbol">)</a>
<a id="400" class="Keyword">open</a> <a id="405" class="Keyword">import</a> <a id="412" href="Data.Product.html" class="Module">Data.Product</a> <a id="425" class="Keyword">using</a> <a id="431" class="Symbol">(</a><a id="432" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">_,_</a><a id="435" class="Symbol">;</a> <a id="437" href="Data.Product.html#2256" class="Function Operator">-,_</a><a id="440" class="Symbol">)</a>
<a id="442" class="Keyword">open</a> <a id="447" class="Keyword">import</a> <a id="454" href="Data.Container.html" class="Module">Data.Container</a> <a id="469" class="Keyword">using</a> <a id="475" class="Symbol">(</a><a id="476" href="Data.Container.Core.html#567" class="Record">Container</a><a id="485" class="Symbol">;</a> <a id="487" href="Data.Container.Core.html#764" class="Function Operator">⟦_⟧</a><a id="490" class="Symbol">;</a> <a id="492" href="Data.Container.html#1529" class="Function">μ</a><a id="493" class="Symbol">)</a>
<a id="495" class="Keyword">open</a> <a id="500" class="Keyword">import</a> <a id="507" href="Data.Container.Relation.Unary.All.html" class="Module">Data.Container.Relation.Unary.All</a> <a id="541" class="Keyword">using</a> <a id="547" class="Symbol">(</a><a id="548" href="Data.Container.Relation.Unary.All.html#552" class="Record">□</a><a id="549" class="Symbol">;</a> <a id="551" href="Data.Container.Relation.Unary.All.html#671" class="InductiveConstructor">all</a><a id="554" class="Symbol">)</a>
<a id="556" class="Keyword">open</a> <a id="561" class="Keyword">import</a> <a id="568" href="Data.Container.Combinator.html" class="Module">Data.Container.Combinator</a> <a id="594" class="Keyword">using</a> <a id="600" class="Symbol">(</a><a id="601" href="Data.Container.Combinator.html#1018" class="Function">const</a><a id="606" class="Symbol">;</a> <a id="608" href="Data.Container.Combinator.html#3119" class="Function Operator">_⊎_</a><a id="611" class="Symbol">)</a>
<a id="613" class="Keyword">open</a> <a id="618" class="Keyword">import</a> <a id="625" href="Data.W.html" class="Module">Data.W</a> <a id="632" class="Symbol">as</a> <a id="635" class="Module">W</a> <a id="637" class="Keyword">using</a> <a id="643" class="Symbol">(</a><a id="644" href="Data.W.html#808" class="InductiveConstructor">sup</a><a id="647" class="Symbol">)</a>
<a id="649" class="Keyword">open</a> <a id="654" class="Keyword">import</a> <a id="661" href="Effect.Functor.html" class="Module">Effect.Functor</a> <a id="676" class="Keyword">using</a> <a id="682" class="Symbol">(</a><a id="683" href="Effect.Functor.html#498" class="Record">RawFunctor</a><a id="693" class="Symbol">)</a>
<a id="695" class="Keyword">open</a> <a id="700" class="Keyword">import</a> <a id="707" href="Effect.Applicative.html" class="Module">Effect.Applicative</a> <a id="726" class="Keyword">using</a> <a id="732" class="Symbol">(</a><a id="733" href="Effect.Applicative.html#499" class="Function">RawApplicative</a><a id="747" class="Symbol">)</a>
<a id="749" class="Keyword">open</a> <a id="754" class="Keyword">import</a> <a id="761" href="Effect.Monad.html" class="Module">Effect.Monad</a> <a id="774" class="Keyword">using</a> <a id="780" class="Symbol">(</a><a id="781" href="Effect.Monad.html#437" class="Function">RawMonad</a><a id="789" class="Symbol">)</a>
<a id="791" class="Keyword">open</a> <a id="796" class="Keyword">import</a> <a id="803" href="Function.Base.html" class="Module">Function.Base</a> <a id="817" class="Symbol">as</a> <a id="820" class="Module">Function</a> <a id="829" class="Keyword">using</a> <a id="835" class="Symbol">(</a><a id="836" href="Function.Base.html#2003" class="Function Operator">_$_</a><a id="839" class="Symbol">;</a> <a id="841" href="Function.Base.html#1115" class="Function Operator">_∘_</a><a id="844" class="Symbol">)</a>

<a id="847" class="Keyword">variable</a>
  <a id="858" href="Data.Container.FreeMonad.html#858" class="Generalizable">x</a> <a id="860" href="Data.Container.FreeMonad.html#860" class="Generalizable">y</a> <a id="862" href="Data.Container.FreeMonad.html#862" class="Generalizable">s</a> <a id="864" href="Data.Container.FreeMonad.html#864" class="Generalizable">p</a> <a id="866" href="Data.Container.FreeMonad.html#866" class="Generalizable">ℓ</a> <a id="868" class="Symbol">:</a> <a id="870" href="Agda.Primitive.html#591" class="Postulate">Level</a>
  <a id="878" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="880" class="Symbol">:</a> <a id="882" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="892" href="Data.Container.FreeMonad.html#862" class="Generalizable">s</a> <a id="894" href="Data.Container.FreeMonad.html#864" class="Generalizable">p</a>
  <a id="898" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="900" class="Symbol">:</a> <a id="902" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="906" href="Data.Container.FreeMonad.html#858" class="Generalizable">x</a>
  <a id="910" href="Data.Container.FreeMonad.html#910" class="Generalizable">Y</a> <a id="912" class="Symbol">:</a> <a id="914" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="918" href="Data.Container.FreeMonad.html#860" class="Generalizable">y</a>

<a id="921" class="Keyword">infixl</a> <a id="928" class="Number">1</a> <a id="930" href="Data.Container.FreeMonad.html#1981" class="Function Operator">_⋆C_</a>
<a id="935" class="Keyword">infix</a>  <a id="942" class="Number">1</a> <a id="944" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">_⋆_</a>

<a id="949" class="Comment">------------------------------------------------------------------------</a>
<a id="1022" class="Comment">-- The free monad construction over a container and a set is, in</a>
<a id="1087" class="Comment">-- universal algebra terminology, also known as the term algebra over a</a>
<a id="1159" class="Comment">-- signature (a container) and a set (of variable symbols). The return</a>
<a id="1230" class="Comment">-- of the free monad corresponds to variables and the bind operator</a>
<a id="1298" class="Comment">-- corresponds to (parallel) substitution.</a>

<a id="1342" class="Comment">-- A useful intuition is to think of containers describing single</a>
<a id="1408" class="Comment">-- operations and the free monad construction over a container and a set</a>
<a id="1481" class="Comment">-- describing a tree of operations as nodes and elements of the set as</a>
<a id="1552" class="Comment">-- leaves. If one starts at the root, then any path will pass finitely</a>
<a id="1623" class="Comment">-- many nodes (operations described by the container) and eventually end</a>
<a id="1696" class="Comment">-- up in a leaf (element of the set) -- hence the Kleene star notation</a>
<a id="1767" class="Comment">-- (the type can be read as a regular expression).</a>

<a id="1819" class="Comment">------------------------------------------------------------------------</a>
<a id="1892" class="Comment">-- Type definition</a>

<a id="1912" class="Comment">-- The free moand can be defined as the least fixpoint `μ (C ⋆C X)`</a>

<a id="_⋆C_"></a><a id="1981" href="Data.Container.FreeMonad.html#1981" class="Function Operator">_⋆C_</a> <a id="1986" class="Symbol">:</a> <a id="1988" class="Symbol">∀</a> <a id="1990" class="Symbol">{</a><a id="1991" href="Data.Container.FreeMonad.html#1991" class="Bound">x</a> <a id="1993" href="Data.Container.FreeMonad.html#1993" class="Bound">s</a> <a id="1995" href="Data.Container.FreeMonad.html#1995" class="Bound">p</a><a id="1996" class="Symbol">}</a> <a id="1998" class="Symbol">→</a> <a id="2000" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="2010" href="Data.Container.FreeMonad.html#1993" class="Bound">s</a> <a id="2012" href="Data.Container.FreeMonad.html#1995" class="Bound">p</a> <a id="2014" class="Symbol">→</a> <a id="2016" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2020" href="Data.Container.FreeMonad.html#1991" class="Bound">x</a> <a id="2022" class="Symbol">→</a> <a id="2024" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="2034" class="Symbol">(</a><a id="2035" href="Data.Container.FreeMonad.html#1993" class="Bound">s</a> <a id="2037" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2039" href="Data.Container.FreeMonad.html#1991" class="Bound">x</a><a id="2040" class="Symbol">)</a> <a id="2042" href="Data.Container.FreeMonad.html#1995" class="Bound">p</a>
<a id="2044" href="Data.Container.FreeMonad.html#2044" class="Bound">C</a> <a id="2046" href="Data.Container.FreeMonad.html#1981" class="Function Operator">⋆C</a> <a id="2049" href="Data.Container.FreeMonad.html#2049" class="Bound">X</a> <a id="2051" class="Symbol">=</a> <a id="2053" href="Data.Container.Combinator.html#1018" class="Function">const</a> <a id="2059" href="Data.Container.FreeMonad.html#2049" class="Bound">X</a> <a id="2061" href="Data.Container.Combinator.html#3119" class="Function Operator">⊎</a> <a id="2063" href="Data.Container.FreeMonad.html#2044" class="Bound">C</a>

<a id="2066" class="Comment">-- However Agda&#39;s positivity checker is currently too weak to observe</a>
<a id="2136" class="Comment">-- that `X` is used in a strictly positive manner in `μ (C ⋆C X)` as</a>
<a id="2205" class="Comment">-- demonstrated in #693.</a>
<a id="2230" class="Comment">-- So we provide instead an inductive definition that we prove to be</a>
<a id="2299" class="Comment">-- equivalent to the μ-based one.</a>

<a id="2334" class="Keyword">data</a> <a id="_⋆_"></a><a id="2339" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">_⋆_</a> <a id="2343" class="Symbol">(</a><a id="2344" href="Data.Container.FreeMonad.html#2344" class="Bound">C</a> <a id="2346" class="Symbol">:</a> <a id="2348" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="2358" href="Data.Container.FreeMonad.html#862" class="Generalizable">s</a> <a id="2360" href="Data.Container.FreeMonad.html#864" class="Generalizable">p</a><a id="2361" class="Symbol">)</a> <a id="2363" class="Symbol">(</a><a id="2364" href="Data.Container.FreeMonad.html#2364" class="Bound">X</a> <a id="2366" class="Symbol">:</a> <a id="2368" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2372" href="Data.Container.FreeMonad.html#858" class="Generalizable">x</a><a id="2373" class="Symbol">)</a> <a id="2375" class="Symbol">:</a> <a id="2377" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2381" class="Symbol">(</a><a id="2382" href="Data.Container.FreeMonad.html#2372" class="Bound">x</a> <a id="2384" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2386" href="Data.Container.FreeMonad.html#2358" class="Bound">s</a> <a id="2388" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="2390" href="Data.Container.FreeMonad.html#2360" class="Bound">p</a><a id="2391" class="Symbol">)</a> <a id="2393" class="Keyword">where</a>
  <a id="_⋆_.pure"></a><a id="2401" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="2406" class="Symbol">:</a> <a id="2408" href="Data.Container.FreeMonad.html#2364" class="Bound">X</a> <a id="2410" class="Symbol">→</a> <a id="2412" href="Data.Container.FreeMonad.html#2344" class="Bound">C</a> <a id="2414" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2416" href="Data.Container.FreeMonad.html#2364" class="Bound">X</a>
  <a id="_⋆_.impure"></a><a id="2420" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="2427" class="Symbol">:</a> <a id="2429" href="Data.Container.Core.html#764" class="Function Operator">⟦</a> <a id="2431" href="Data.Container.FreeMonad.html#2344" class="Bound">C</a> <a id="2433" href="Data.Container.Core.html#764" class="Function Operator">⟧</a> <a id="2435" class="Symbol">(</a><a id="2436" href="Data.Container.FreeMonad.html#2344" class="Bound">C</a> <a id="2438" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2440" href="Data.Container.FreeMonad.html#2364" class="Bound">X</a><a id="2441" class="Symbol">)</a> <a id="2443" class="Symbol">→</a> <a id="2445" href="Data.Container.FreeMonad.html#2344" class="Bound">C</a> <a id="2447" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2449" href="Data.Container.FreeMonad.html#2364" class="Bound">X</a>

<a id="2452" class="Comment">------------------------------------------------------------------------</a>
<a id="2525" class="Comment">-- Equivalent types</a>

<a id="2546" class="Comment">-- We can prove that `C ⋆ X` is equivalent to one layer of `C ⋆C X` with</a>
<a id="2619" class="Comment">-- subterms of tyep `C ⋆ X`.</a>

<a id="inj"></a><a id="2649" href="Data.Container.FreeMonad.html#2649" class="Function">inj</a> <a id="2653" class="Symbol">:</a> <a id="2655" class="Symbol">{</a><a id="2656" href="Data.Container.FreeMonad.html#2656" class="Bound">X</a> <a id="2658" class="Symbol">:</a> <a id="2660" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2664" href="Data.Container.FreeMonad.html#858" class="Generalizable">x</a><a id="2665" class="Symbol">}</a> <a id="2667" class="Symbol">→</a> <a id="2669" href="Data.Container.Core.html#764" class="Function Operator">⟦</a> <a id="2671" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="2673" href="Data.Container.FreeMonad.html#1981" class="Function Operator">⋆C</a> <a id="2676" href="Data.Container.FreeMonad.html#2656" class="Bound">X</a> <a id="2678" href="Data.Container.Core.html#764" class="Function Operator">⟧</a> <a id="2680" class="Symbol">(</a><a id="2681" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="2683" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2685" href="Data.Container.FreeMonad.html#2656" class="Bound">X</a><a id="2686" class="Symbol">)</a> <a id="2688" class="Symbol">→</a> <a id="2690" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="2692" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2694" href="Data.Container.FreeMonad.html#2656" class="Bound">X</a>
<a id="2696" href="Data.Container.FreeMonad.html#2649" class="Function">inj</a> <a id="2700" class="Symbol">(</a><a id="2701" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="2706" href="Data.Container.FreeMonad.html#2706" class="Bound">x</a> <a id="2708" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2710" class="Symbol">_)</a> <a id="2713" class="Symbol">=</a> <a id="2715" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="2720" href="Data.Container.FreeMonad.html#2706" class="Bound">x</a>
<a id="2722" href="Data.Container.FreeMonad.html#2649" class="Function">inj</a> <a id="2726" class="Symbol">(</a><a id="2727" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="2732" href="Data.Container.FreeMonad.html#2732" class="Bound">c</a> <a id="2734" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2736" href="Data.Container.FreeMonad.html#2736" class="Bound">r</a><a id="2737" class="Symbol">)</a> <a id="2739" class="Symbol">=</a> <a id="2741" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="2748" class="Symbol">(</a><a id="2749" href="Data.Container.FreeMonad.html#2732" class="Bound">c</a> <a id="2751" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2753" href="Data.Container.FreeMonad.html#2736" class="Bound">r</a><a id="2754" class="Symbol">)</a>

<a id="out"></a><a id="2757" href="Data.Container.FreeMonad.html#2757" class="Function">out</a> <a id="2761" class="Symbol">:</a> <a id="2763" class="Symbol">{</a><a id="2764" href="Data.Container.FreeMonad.html#2764" class="Bound">X</a> <a id="2766" class="Symbol">:</a> <a id="2768" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="2772" href="Data.Container.FreeMonad.html#858" class="Generalizable">x</a><a id="2773" class="Symbol">}</a> <a id="2775" class="Symbol">→</a> <a id="2777" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="2779" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2781" href="Data.Container.FreeMonad.html#2764" class="Bound">X</a> <a id="2783" class="Symbol">→</a> <a id="2785" href="Data.Container.Core.html#764" class="Function Operator">⟦</a> <a id="2787" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="2789" href="Data.Container.FreeMonad.html#1981" class="Function Operator">⋆C</a> <a id="2792" href="Data.Container.FreeMonad.html#2764" class="Bound">X</a> <a id="2794" href="Data.Container.Core.html#764" class="Function Operator">⟧</a> <a id="2796" class="Symbol">(</a><a id="2797" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="2799" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2801" href="Data.Container.FreeMonad.html#2764" class="Bound">X</a><a id="2802" class="Symbol">)</a>
<a id="2804" href="Data.Container.FreeMonad.html#2757" class="Function">out</a> <a id="2808" class="Symbol">(</a><a id="2809" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="2814" href="Data.Container.FreeMonad.html#2814" class="Bound">x</a><a id="2815" class="Symbol">)</a> <a id="2817" class="Symbol">=</a> <a id="2819" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="2824" href="Data.Container.FreeMonad.html#2814" class="Bound">x</a> <a id="2826" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2828" class="Symbol">λ</a> <a id="2830" class="Symbol">()</a>
<a id="2833" href="Data.Container.FreeMonad.html#2757" class="Function">out</a> <a id="2837" class="Symbol">(</a><a id="2838" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="2845" class="Symbol">(</a><a id="2846" href="Data.Container.FreeMonad.html#2846" class="Bound">c</a> <a id="2848" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2850" href="Data.Container.FreeMonad.html#2850" class="Bound">r</a><a id="2851" class="Symbol">))</a> <a id="2854" class="Symbol">=</a> <a id="2856" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="2861" href="Data.Container.FreeMonad.html#2846" class="Bound">c</a> <a id="2863" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2865" href="Data.Container.FreeMonad.html#2850" class="Bound">r</a>

<a id="2868" class="Comment">-- We can fully convert back and forth between `C ⋆ X` and `μ (C ⋆C X)`.</a>

<a id="toμ"></a><a id="2942" href="Data.Container.FreeMonad.html#2942" class="Function">toμ</a> <a id="2946" class="Symbol">:</a> <a id="2948" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="2950" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="2952" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="2954" class="Symbol">→</a> <a id="2956" href="Data.Container.html#1529" class="Function">μ</a> <a id="2958" class="Symbol">(</a><a id="2959" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="2961" href="Data.Container.FreeMonad.html#1981" class="Function Operator">⋆C</a> <a id="2964" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a><a id="2965" class="Symbol">)</a>
<a id="2967" href="Data.Container.FreeMonad.html#2942" class="Function">toμ</a> <a id="2971" class="Symbol">(</a><a id="2972" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="2977" href="Data.Container.FreeMonad.html#2977" class="Bound">x</a><a id="2978" class="Symbol">)</a> <a id="2980" class="Symbol">=</a> <a id="2982" href="Data.W.html#808" class="InductiveConstructor">sup</a> <a id="2986" class="Symbol">(</a><a id="2987" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="2992" href="Data.Container.FreeMonad.html#2977" class="Bound">x</a> <a id="2994" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2996" class="Symbol">λ</a> <a id="2998" class="Symbol">())</a>
<a id="3002" href="Data.Container.FreeMonad.html#2942" class="Function">toμ</a> <a id="3006" class="Symbol">(</a><a id="3007" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="3014" class="Symbol">(</a><a id="3015" href="Data.Container.FreeMonad.html#3015" class="Bound">c</a> <a id="3017" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3019" href="Data.Container.FreeMonad.html#3019" class="Bound">r</a><a id="3020" class="Symbol">))</a> <a id="3023" class="Symbol">=</a> <a id="3025" href="Data.W.html#808" class="InductiveConstructor">sup</a> <a id="3029" class="Symbol">(</a><a id="3030" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="3035" href="Data.Container.FreeMonad.html#3015" class="Bound">c</a> <a id="3037" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3039" href="Data.Container.FreeMonad.html#2942" class="Function">toμ</a> <a id="3043" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="3045" href="Data.Container.FreeMonad.html#3019" class="Bound">r</a><a id="3046" class="Symbol">)</a>

<a id="fromμ"></a><a id="3049" href="Data.Container.FreeMonad.html#3049" class="Function">fromμ</a> <a id="3055" class="Symbol">:</a> <a id="3057" href="Data.Container.html#1529" class="Function">μ</a> <a id="3059" class="Symbol">(</a><a id="3060" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3062" href="Data.Container.FreeMonad.html#1981" class="Function Operator">⋆C</a> <a id="3065" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a><a id="3066" class="Symbol">)</a> <a id="3068" class="Symbol">→</a> <a id="3070" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3072" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3074" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a>
<a id="3076" href="Data.Container.FreeMonad.html#3049" class="Function">fromμ</a> <a id="3082" class="Symbol">=</a> <a id="3084" href="Data.W.html#1477" class="Function">W.foldr</a> <a id="3092" href="Data.Container.FreeMonad.html#2649" class="Function">inj</a>

<a id="3097" class="Comment">-- We can recover an induction principle similar to the one given in `Data.W`.</a>
<a id="3176" class="Comment">-- We curry these ones by distinguishing the pure vs. impure case</a>

<a id="3243" class="Keyword">module</a> <a id="3250" href="Data.Container.FreeMonad.html#3250" class="Module">_</a> <a id="3252" class="Symbol">(</a><a id="3253" href="Data.Container.FreeMonad.html#3253" class="Bound">P</a> <a id="3255" class="Symbol">:</a> <a id="3257" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3259" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3261" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="3263" class="Symbol">→</a> <a id="3265" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="3269" href="Data.Container.FreeMonad.html#866" class="Generalizable">ℓ</a><a id="3270" class="Symbol">)</a>
         <a id="3281" class="Symbol">(</a><a id="3282" href="Data.Container.FreeMonad.html#3282" class="Bound">algP</a> <a id="3287" class="Symbol">:</a> <a id="3289" class="Symbol">∀</a> <a id="3291" href="Data.Container.FreeMonad.html#3291" class="Bound">x</a> <a id="3293" class="Symbol">→</a> <a id="3295" href="Data.Container.FreeMonad.html#3253" class="Bound">P</a> <a id="3297" class="Symbol">(</a><a id="3298" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3303" href="Data.Container.FreeMonad.html#3291" class="Bound">x</a><a id="3304" class="Symbol">))</a>
         <a id="3316" class="Symbol">(</a><a id="3317" href="Data.Container.FreeMonad.html#3317" class="Bound">algI</a> <a id="3322" class="Symbol">:</a> <a id="3324" class="Symbol">∀</a> <a id="3326" class="Symbol">{</a><a id="3327" href="Data.Container.FreeMonad.html#3327" class="Bound">t</a><a id="3328" class="Symbol">}</a> <a id="3330" class="Symbol">→</a> <a id="3332" href="Data.Container.Relation.Unary.All.html#552" class="Record">□</a> <a id="3334" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3336" href="Data.Container.FreeMonad.html#3253" class="Bound">P</a> <a id="3338" href="Data.Container.FreeMonad.html#3327" class="Bound">t</a> <a id="3340" class="Symbol">→</a> <a id="3342" href="Data.Container.FreeMonad.html#3253" class="Bound">P</a> <a id="3344" class="Symbol">(</a><a id="3345" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="3352" href="Data.Container.FreeMonad.html#3327" class="Bound">t</a><a id="3353" class="Symbol">))</a> <a id="3356" class="Keyword">where</a>

 <a id="3364" href="Data.Container.FreeMonad.html#3364" class="Function">induction</a> <a id="3374" class="Symbol">:</a> <a id="3376" class="Symbol">(</a><a id="3377" href="Data.Container.FreeMonad.html#3377" class="Bound">t</a> <a id="3379" class="Symbol">:</a> <a id="3381" href="Data.Container.FreeMonad.html#3257" class="Bound">C</a> <a id="3383" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3385" href="Data.Container.FreeMonad.html#3261" class="Bound">X</a><a id="3386" class="Symbol">)</a> <a id="3388" class="Symbol">→</a> <a id="3390" href="Data.Container.FreeMonad.html#3253" class="Bound">P</a> <a id="3392" href="Data.Container.FreeMonad.html#3377" class="Bound">t</a>
 <a id="3395" href="Data.Container.FreeMonad.html#3364" class="Function">induction</a> <a id="3405" class="Symbol">(</a><a id="3406" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3411" href="Data.Container.FreeMonad.html#3411" class="Bound">x</a><a id="3412" class="Symbol">)</a> <a id="3414" class="Symbol">=</a> <a id="3416" href="Data.Container.FreeMonad.html#3282" class="Bound">algP</a> <a id="3421" href="Data.Container.FreeMonad.html#3411" class="Bound">x</a>
 <a id="3424" href="Data.Container.FreeMonad.html#3364" class="Function">induction</a> <a id="3434" class="Symbol">(</a><a id="3435" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="3442" class="Symbol">(</a><a id="3443" href="Data.Container.FreeMonad.html#3443" class="Bound">c</a> <a id="3445" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3447" href="Data.Container.FreeMonad.html#3447" class="Bound">r</a><a id="3448" class="Symbol">))</a> <a id="3451" class="Symbol">=</a> <a id="3453" href="Data.Container.FreeMonad.html#3317" class="Bound">algI</a> <a id="3458" href="Function.Base.html#2003" class="Function Operator">$</a> <a id="3460" href="Data.Container.Relation.Unary.All.html#671" class="InductiveConstructor">all</a> <a id="3464" class="Symbol">(</a><a id="3465" href="Data.Container.FreeMonad.html#3364" class="Function">induction</a> <a id="3475" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="3477" href="Data.Container.FreeMonad.html#3447" class="Bound">r</a><a id="3478" class="Symbol">)</a>

<a id="3481" class="Keyword">module</a> <a id="3488" href="Data.Container.FreeMonad.html#3488" class="Module">_</a> <a id="3490" class="Symbol">{</a><a id="3491" href="Data.Container.FreeMonad.html#3491" class="Bound">P</a> <a id="3493" class="Symbol">:</a> <a id="3495" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="3499" href="Data.Container.FreeMonad.html#866" class="Generalizable">ℓ</a><a id="3500" class="Symbol">}</a>
         <a id="3511" class="Symbol">(</a><a id="3512" href="Data.Container.FreeMonad.html#3512" class="Bound">algP</a> <a id="3517" class="Symbol">:</a> <a id="3519" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="3521" class="Symbol">→</a> <a id="3523" href="Data.Container.FreeMonad.html#3491" class="Bound">P</a><a id="3524" class="Symbol">)</a>
         <a id="3535" class="Symbol">(</a><a id="3536" href="Data.Container.FreeMonad.html#3536" class="Bound">algI</a> <a id="3541" class="Symbol">:</a> <a id="3543" href="Data.Container.Core.html#764" class="Function Operator">⟦</a> <a id="3545" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3547" href="Data.Container.Core.html#764" class="Function Operator">⟧</a> <a id="3549" href="Data.Container.FreeMonad.html#3491" class="Bound">P</a> <a id="3551" class="Symbol">→</a> <a id="3553" href="Data.Container.FreeMonad.html#3491" class="Bound">P</a><a id="3554" class="Symbol">)</a> <a id="3556" class="Keyword">where</a>

 <a id="3564" href="Data.Container.FreeMonad.html#3564" class="Function">foldr</a> <a id="3570" class="Symbol">:</a> <a id="3572" href="Data.Container.FreeMonad.html#3545" class="Bound">C</a> <a id="3574" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3576" href="Data.Container.FreeMonad.html#3519" class="Bound">X</a> <a id="3578" class="Symbol">→</a> <a id="3580" href="Data.Container.FreeMonad.html#3491" class="Bound">P</a>
 <a id="3583" href="Data.Container.FreeMonad.html#3564" class="Function">foldr</a> <a id="3589" class="Symbol">=</a> <a id="3591" href="Data.Container.FreeMonad.html#3364" class="Function">induction</a> <a id="3601" class="Symbol">(</a><a id="3602" href="Function.Base.html#725" class="Function">Function.const</a> <a id="3617" href="Data.Container.FreeMonad.html#3491" class="Bound">P</a><a id="3618" class="Symbol">)</a> <a id="3620" href="Data.Container.FreeMonad.html#3512" class="Bound">algP</a> <a id="3625" class="Symbol">(</a><a id="3626" href="Data.Container.FreeMonad.html#3536" class="Bound">algI</a> <a id="3631" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="3633" href="Data.Product.html#2256" class="Function Operator">-,_</a> <a id="3637" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="3639" href="Data.Container.Relation.Unary.All.html#683" class="Field">□.proof</a><a id="3646" class="Symbol">)</a>

<a id="_&lt;$&gt;_"></a><a id="3649" href="Data.Container.FreeMonad.html#3649" class="Function Operator">_&lt;$&gt;_</a> <a id="3655" class="Symbol">:</a> <a id="3657" class="Symbol">(</a><a id="3658" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="3660" class="Symbol">→</a> <a id="3662" href="Data.Container.FreeMonad.html#910" class="Generalizable">Y</a><a id="3663" class="Symbol">)</a> <a id="3665" class="Symbol">→</a> <a id="3667" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3669" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3671" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="3673" class="Symbol">→</a> <a id="3675" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3677" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3679" href="Data.Container.FreeMonad.html#910" class="Generalizable">Y</a>
<a id="3681" href="Data.Container.FreeMonad.html#3681" class="Bound">f</a> <a id="3683" href="Data.Container.FreeMonad.html#3649" class="Function Operator">&lt;$&gt;</a> <a id="3687" href="Data.Container.FreeMonad.html#3687" class="Bound">t</a> <a id="3689" class="Symbol">=</a> <a id="3691" href="Data.Container.FreeMonad.html#3564" class="Function">foldr</a> <a id="3697" class="Symbol">(</a><a id="3698" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3703" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="3705" href="Data.Container.FreeMonad.html#3681" class="Bound">f</a><a id="3706" class="Symbol">)</a> <a id="3708" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="3715" href="Data.Container.FreeMonad.html#3687" class="Bound">t</a>

<a id="_&lt;*&gt;_"></a><a id="3718" href="Data.Container.FreeMonad.html#3718" class="Function Operator">_&lt;*&gt;_</a> <a id="3724" class="Symbol">:</a> <a id="3726" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3728" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3730" class="Symbol">(</a><a id="3731" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="3733" class="Symbol">→</a> <a id="3735" href="Data.Container.FreeMonad.html#910" class="Generalizable">Y</a><a id="3736" class="Symbol">)</a> <a id="3738" class="Symbol">→</a> <a id="3740" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3742" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3744" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="3746" class="Symbol">→</a> <a id="3748" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3750" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3752" href="Data.Container.FreeMonad.html#910" class="Generalizable">Y</a>
<a id="3754" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3759" href="Data.Container.FreeMonad.html#3759" class="Bound">f</a> <a id="3761" href="Data.Container.FreeMonad.html#3718" class="Function Operator">&lt;*&gt;</a> <a id="3765" href="Data.Container.FreeMonad.html#3765" class="Bound">t</a> <a id="3767" class="Symbol">=</a> <a id="3769" href="Data.Container.FreeMonad.html#3759" class="Bound">f</a> <a id="3771" href="Data.Container.FreeMonad.html#3649" class="Function Operator">&lt;$&gt;</a> <a id="3775" href="Data.Container.FreeMonad.html#3765" class="Bound">t</a>
<a id="3777" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="3784" class="Symbol">(</a><a id="3785" href="Data.Container.FreeMonad.html#3785" class="Bound">c</a> <a id="3787" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3789" href="Data.Container.FreeMonad.html#3789" class="Bound">r</a><a id="3790" class="Symbol">)</a> <a id="3792" href="Data.Container.FreeMonad.html#3718" class="Function Operator">&lt;*&gt;</a> <a id="3796" href="Data.Container.FreeMonad.html#3796" class="Bound">t</a> <a id="3798" class="Symbol">=</a> <a id="3800" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="3807" class="Symbol">(</a><a id="3808" href="Data.Container.FreeMonad.html#3785" class="Bound">c</a> <a id="3810" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3812" class="Symbol">λ</a> <a id="3814" href="Data.Container.FreeMonad.html#3814" class="Bound">v</a> <a id="3816" class="Symbol">→</a> <a id="3818" href="Data.Container.FreeMonad.html#3789" class="Bound">r</a> <a id="3820" href="Data.Container.FreeMonad.html#3814" class="Bound">v</a> <a id="3822" href="Data.Container.FreeMonad.html#3718" class="Function Operator">&lt;*&gt;</a> <a id="3826" href="Data.Container.FreeMonad.html#3796" class="Bound">t</a><a id="3827" class="Symbol">)</a>

<a id="_&gt;&gt;=_"></a><a id="3830" href="Data.Container.FreeMonad.html#3830" class="Function Operator">_&gt;&gt;=_</a> <a id="3836" class="Symbol">:</a> <a id="3838" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3840" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3842" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="3844" class="Symbol">→</a> <a id="3846" class="Symbol">(</a><a id="3847" href="Data.Container.FreeMonad.html#898" class="Generalizable">X</a> <a id="3849" class="Symbol">→</a> <a id="3851" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3853" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3855" href="Data.Container.FreeMonad.html#910" class="Generalizable">Y</a><a id="3856" class="Symbol">)</a> <a id="3858" class="Symbol">→</a> <a id="3860" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a> <a id="3862" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">⋆</a> <a id="3864" href="Data.Container.FreeMonad.html#910" class="Generalizable">Y</a>
<a id="3866" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="3871" href="Data.Container.FreeMonad.html#3871" class="Bound">x</a> <a id="3873" href="Data.Container.FreeMonad.html#3830" class="Function Operator">&gt;&gt;=</a> <a id="3877" href="Data.Container.FreeMonad.html#3877" class="Bound">f</a> <a id="3879" class="Symbol">=</a> <a id="3881" href="Data.Container.FreeMonad.html#3877" class="Bound">f</a> <a id="3883" href="Data.Container.FreeMonad.html#3871" class="Bound">x</a>
<a id="3885" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="3892" class="Symbol">(</a><a id="3893" href="Data.Container.FreeMonad.html#3893" class="Bound">c</a> <a id="3895" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3897" href="Data.Container.FreeMonad.html#3897" class="Bound">r</a><a id="3898" class="Symbol">)</a> <a id="3900" href="Data.Container.FreeMonad.html#3830" class="Function Operator">&gt;&gt;=</a> <a id="3904" href="Data.Container.FreeMonad.html#3904" class="Bound">f</a> <a id="3906" class="Symbol">=</a> <a id="3908" href="Data.Container.FreeMonad.html#2420" class="InductiveConstructor">impure</a> <a id="3915" class="Symbol">(</a><a id="3916" href="Data.Container.FreeMonad.html#3893" class="Bound">c</a> <a id="3918" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="3920" class="Symbol">λ</a> <a id="3922" href="Data.Container.FreeMonad.html#3922" class="Bound">v</a> <a id="3924" class="Symbol">→</a> <a id="3926" href="Data.Container.FreeMonad.html#3897" class="Bound">r</a> <a id="3928" href="Data.Container.FreeMonad.html#3922" class="Bound">v</a> <a id="3930" href="Data.Container.FreeMonad.html#3830" class="Function Operator">&gt;&gt;=</a> <a id="3934" href="Data.Container.FreeMonad.html#3904" class="Bound">f</a><a id="3935" class="Symbol">)</a>

<a id="3938" class="Comment">------------------------------------------------------------------------</a>
<a id="4011" class="Comment">-- Structure</a>

<a id="rawFunctor"></a><a id="4025" href="Data.Container.FreeMonad.html#4025" class="Function">rawFunctor</a> <a id="4036" class="Symbol">:</a> <a id="4038" href="Effect.Functor.html#498" class="Record">RawFunctor</a> <a id="4049" class="Symbol">(</a><a id="4050" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">_⋆_</a> <a id="4054" class="Symbol">{</a><a id="4055" class="Argument">x</a> <a id="4057" class="Symbol">=</a> <a id="4059" href="Data.Container.FreeMonad.html#858" class="Generalizable">x</a><a id="4060" class="Symbol">}</a> <a id="4062" href="Data.Container.FreeMonad.html#878" class="Generalizable">C</a><a id="4063" class="Symbol">)</a>
<a id="4065" href="Data.Container.FreeMonad.html#4025" class="Function">rawFunctor</a> <a id="4076" class="Symbol">=</a> <a id="4078" class="Keyword">record</a> <a id="4085" class="Symbol">{</a> <a id="4087" href="Effect.Functor.html#607" class="Field Operator">_&lt;$&gt;_</a> <a id="4093" class="Symbol">=</a> <a id="4095" href="Data.Container.FreeMonad.html#3649" class="Function Operator">_&lt;$&gt;_</a> <a id="4101" class="Symbol">}</a>

<a id="rawApplicative"></a><a id="4104" href="Data.Container.FreeMonad.html#4104" class="Function">rawApplicative</a> <a id="4119" class="Symbol">:</a> <a id="4121" class="Symbol">{</a><a id="4122" href="Data.Container.FreeMonad.html#4122" class="Bound">C</a> <a id="4124" class="Symbol">:</a> <a id="4126" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="4136" href="Data.Container.FreeMonad.html#862" class="Generalizable">s</a> <a id="4138" href="Data.Container.FreeMonad.html#864" class="Generalizable">p</a><a id="4139" class="Symbol">}</a> <a id="4141" class="Symbol">→</a> <a id="4143" href="Effect.Applicative.html#499" class="Function">RawApplicative</a> <a id="4158" class="Symbol">(</a><a id="4159" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">_⋆_</a> <a id="4163" class="Symbol">{</a><a id="4164" class="Argument">x</a> <a id="4166" class="Symbol">=</a> <a id="4168" href="Data.Container.FreeMonad.html#858" class="Generalizable">x</a> <a id="4170" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="4172" href="Data.Container.FreeMonad.html#862" class="Generalizable">s</a> <a id="4174" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="4176" href="Data.Container.FreeMonad.html#864" class="Generalizable">p</a><a id="4177" class="Symbol">}</a> <a id="4179" href="Data.Container.FreeMonad.html#4122" class="Bound">C</a><a id="4180" class="Symbol">)</a>
<a id="4182" href="Data.Container.FreeMonad.html#4104" class="Function">rawApplicative</a> <a id="4197" class="Symbol">=</a> <a id="4199" class="Keyword">record</a> <a id="4206" class="Symbol">{</a> <a id="4208" href="Effect.Applicative.Indexed.html#1000" class="Field">pure</a> <a id="4213" class="Symbol">=</a> <a id="4215" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="4220" class="Symbol">;</a> <a id="4222" href="Effect.Applicative.Indexed.html#1031" class="Field Operator">_⊛_</a> <a id="4226" class="Symbol">=</a> <a id="4228" href="Data.Container.FreeMonad.html#3718" class="Function Operator">_&lt;*&gt;_</a> <a id="4234" class="Symbol">}</a>

<a id="rawMonad"></a><a id="4237" href="Data.Container.FreeMonad.html#4237" class="Function">rawMonad</a> <a id="4246" class="Symbol">:</a> <a id="4248" class="Symbol">{</a><a id="4249" href="Data.Container.FreeMonad.html#4249" class="Bound">C</a> <a id="4251" class="Symbol">:</a> <a id="4253" href="Data.Container.Core.html#567" class="Record">Container</a> <a id="4263" href="Data.Container.FreeMonad.html#862" class="Generalizable">s</a> <a id="4265" href="Data.Container.FreeMonad.html#864" class="Generalizable">p</a><a id="4266" class="Symbol">}</a> <a id="4268" class="Symbol">→</a> <a id="4270" href="Effect.Monad.html#437" class="Function">RawMonad</a> <a id="4279" class="Symbol">(</a><a id="4280" href="Data.Container.FreeMonad.html#2339" class="Datatype Operator">_⋆_</a> <a id="4284" class="Symbol">{</a><a id="4285" class="Argument">x</a> <a id="4287" class="Symbol">=</a> <a id="4289" href="Data.Container.FreeMonad.html#858" class="Generalizable">x</a> <a id="4291" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="4293" href="Data.Container.FreeMonad.html#862" class="Generalizable">s</a> <a id="4295" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="4297" href="Data.Container.FreeMonad.html#864" class="Generalizable">p</a><a id="4298" class="Symbol">}</a> <a id="4300" href="Data.Container.FreeMonad.html#4249" class="Bound">C</a><a id="4301" class="Symbol">)</a>
<a id="4303" href="Data.Container.FreeMonad.html#4237" class="Function">rawMonad</a> <a id="4312" class="Symbol">=</a> <a id="4314" class="Keyword">record</a> <a id="4321" class="Symbol">{</a> <a id="4323" href="Effect.Monad.Indexed.html#654" class="Field">return</a> <a id="4330" class="Symbol">=</a> <a id="4332" href="Data.Container.FreeMonad.html#2401" class="InductiveConstructor">pure</a> <a id="4337" class="Symbol">;</a> <a id="4339" href="Effect.Monad.Indexed.html#687" class="Field Operator">_&gt;&gt;=_</a> <a id="4345" class="Symbol">=</a> <a id="4347" href="Data.Container.FreeMonad.html#3830" class="Function Operator">_&gt;&gt;=_</a> <a id="4353" class="Symbol">}</a>
</pre></body></html>
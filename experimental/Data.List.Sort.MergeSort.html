<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.List.Sort.MergeSort</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- An implementation of merge sort along with proofs of correctness.</a>
<a id="175" class="Comment">------------------------------------------------------------------------</a>

<a id="249" class="Comment">-- Unless you are need a particular property of MergeSort, you should</a>
<a id="319" class="Comment">-- import and use the sorting algorithm from `Data.List.Sort` instead</a>
<a id="389" class="Comment">-- of this file.</a>

<a id="407" class="Symbol">{-#</a> <a id="411" class="Keyword">OPTIONS</a> <a id="419" class="Pragma">--cubical-compatible</a> <a id="440" class="Pragma">--safe</a> <a id="447" class="Symbol">#-}</a>

<a id="452" class="Keyword">open</a> <a id="457" class="Keyword">import</a> <a id="464" href="Data.Bool.html" class="Module">Data.Bool</a> <a id="474" class="Keyword">using</a> <a id="480" class="Symbol">(</a><a id="481" href="Agda.Builtin.Bool.html#181" class="InductiveConstructor">true</a><a id="485" class="Symbol">;</a> <a id="487" href="Agda.Builtin.Bool.html#175" class="InductiveConstructor">false</a><a id="492" class="Symbol">)</a>
<a id="494" class="Keyword">open</a> <a id="499" class="Keyword">import</a> <a id="506" href="Data.List.Base.html" class="Module">Data.List.Base</a>
<a id="521" class="Keyword">open</a> <a id="526" class="Keyword">import</a> <a id="533" href="Data.List.Properties.html" class="Module">Data.List.Properties</a> <a id="554" class="Keyword">using</a> <a id="560" class="Symbol">(</a><a id="561" href="Data.List.Properties.html#31231" class="Function">length-partition</a><a id="577" class="Symbol">;</a> <a id="579" href="Data.List.Properties.html#5511" class="Function">++-assoc</a><a id="587" class="Symbol">;</a> <a id="589" href="Data.List.Properties.html#19708" class="Function">concat-[-]</a><a id="599" class="Symbol">)</a>
<a id="601" class="Keyword">open</a> <a id="606" class="Keyword">import</a> <a id="613" href="Data.List.Relation.Unary.Linked.html" class="Module">Data.List.Relation.Unary.Linked</a> <a id="645" class="Keyword">using</a> <a id="651" class="Symbol">(</a><a id="652" href="Data.List.Relation.Unary.Linked.html#1451" class="InductiveConstructor">[]</a><a id="654" class="Symbol">;</a> <a id="656" href="Data.List.Relation.Unary.Linked.html#1471" class="InductiveConstructor">[-]</a><a id="659" class="Symbol">)</a>
<a id="661" class="Keyword">import</a> <a id="668" href="Data.List.Relation.Unary.Sorted.TotalOrder.Properties.html" class="Module">Data.List.Relation.Unary.Sorted.TotalOrder.Properties</a> <a id="722" class="Symbol">as</a> <a id="725" class="Module">Sorted</a>
<a id="732" class="Keyword">open</a> <a id="737" class="Keyword">import</a> <a id="744" href="Data.List.Relation.Unary.All.html" class="Module">Data.List.Relation.Unary.All</a> <a id="773" class="Symbol">as</a> <a id="776" class="Module">All</a> <a id="780" class="Keyword">using</a> <a id="786" class="Symbol">(</a><a id="787" href="Data.List.Relation.Unary.All.html#1501" class="Datatype">All</a><a id="790" class="Symbol">;</a> <a id="792" href="Data.List.Relation.Unary.All.html#1564" class="InductiveConstructor">[]</a><a id="794" class="Symbol">;</a> <a id="796" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">_∷_</a><a id="799" class="Symbol">)</a>
<a id="801" class="Keyword">import</a> <a id="808" href="Data.List.Relation.Unary.All.Properties.html" class="Module">Data.List.Relation.Unary.All.Properties</a> <a id="848" class="Symbol">as</a> <a id="851" class="Module">All</a>
<a id="855" class="Keyword">open</a> <a id="860" class="Keyword">import</a> <a id="867" href="Data.List.Relation.Binary.Permutation.Propositional.html" class="Module">Data.List.Relation.Binary.Permutation.Propositional</a>
<a id="919" class="Keyword">import</a> <a id="926" href="Data.List.Relation.Binary.Permutation.Propositional.Properties.html" class="Module">Data.List.Relation.Binary.Permutation.Propositional.Properties</a> <a id="989" class="Symbol">as</a> <a id="992" class="Module">Perm</a>
<a id="997" class="Keyword">open</a> <a id="1002" class="Keyword">import</a> <a id="1009" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a> <a id="1025" class="Keyword">using</a> <a id="1031" class="Symbol">(</a><a id="1032" href="Agda.Builtin.Maybe.html#156" class="InductiveConstructor">just</a><a id="1036" class="Symbol">)</a>
<a id="1038" class="Keyword">open</a> <a id="1043" class="Keyword">import</a> <a id="1050" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="1067" class="Keyword">using</a> <a id="1073" class="Symbol">(</a><a id="1074" href="Relation.Nullary.html#1590" class="Field">does</a><a id="1078" class="Symbol">)</a>
<a id="1080" class="Keyword">open</a> <a id="1085" class="Keyword">import</a> <a id="1092" href="Data.Nat.html" class="Module">Data.Nat</a> <a id="1101" class="Keyword">using</a> <a id="1107" class="Symbol">(</a><a id="1108" href="Data.Nat.Base.html#1583" class="Function Operator">_&lt;_</a><a id="1111" class="Symbol">;</a> <a id="1113" href="Data.Nat.Base.html#1862" class="Function Operator">_&gt;_</a><a id="1116" class="Symbol">;</a> <a id="1118" href="Data.Nat.Base.html#1655" class="InductiveConstructor">z&lt;s</a><a id="1121" class="Symbol">;</a> <a id="1123" href="Data.Nat.Base.html#1695" class="InductiveConstructor">s&lt;s</a><a id="1126" class="Symbol">)</a>
<a id="1128" class="Keyword">open</a> <a id="1133" class="Keyword">import</a> <a id="1140" href="Data.Nat.Induction.html" class="Module">Data.Nat.Induction</a>
<a id="1159" class="Keyword">open</a> <a id="1164" class="Keyword">import</a> <a id="1171" href="Data.Nat.Properties.html" class="Module">Data.Nat.Properties</a> <a id="1191" class="Keyword">using</a> <a id="1197" class="Symbol">(</a><a id="1198" href="Data.Nat.Properties.html#7610" class="Function">≤-step</a><a id="1204" class="Symbol">)</a>
<a id="1206" class="Keyword">open</a> <a id="1211" class="Keyword">import</a> <a id="1218" href="Data.Product.html" class="Module">Data.Product</a> <a id="1231" class="Symbol">as</a> <a id="1234" class="Module">Prod</a> <a id="1239" class="Keyword">using</a> <a id="1245" class="Symbol">(</a><a id="1246" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">_,_</a><a id="1249" class="Symbol">)</a>
<a id="1251" class="Keyword">open</a> <a id="1256" class="Keyword">import</a> <a id="1263" href="Function.Base.html" class="Module">Function.Base</a> <a id="1277" class="Keyword">using</a> <a id="1283" class="Symbol">(</a><a id="1284" href="Function.Base.html#1115" class="Function Operator">_∘_</a><a id="1287" class="Symbol">)</a>
<a id="1289" class="Keyword">open</a> <a id="1294" class="Keyword">import</a> <a id="1301" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="1317" class="Keyword">using</a> <a id="1323" class="Symbol">(</a><a id="1324" href="Relation.Binary.Bundles.html#6016" class="Record">DecTotalOrder</a><a id="1337" class="Symbol">)</a>
<a id="1339" class="Keyword">open</a> <a id="1344" class="Keyword">import</a> <a id="1351" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="1368" class="Keyword">using</a> <a id="1374" class="Symbol">(</a><a id="1375" href="Relation.Nullary.html#665" class="Function Operator">¬_</a><a id="1377" class="Symbol">)</a>

<a id="1380" class="Keyword">module</a> <a id="1387" href="Data.List.Sort.MergeSort.html" class="Module">Data.List.Sort.MergeSort</a>
  <a id="1414" class="Symbol">{</a><a id="1415" href="Data.List.Sort.MergeSort.html#1415" class="Bound">a</a> <a id="1417" href="Data.List.Sort.MergeSort.html#1417" class="Bound">ℓ₁</a> <a id="1420" href="Data.List.Sort.MergeSort.html#1420" class="Bound">ℓ₂</a><a id="1422" class="Symbol">}</a> <a id="1424" class="Symbol">(</a><a id="1425" href="Data.List.Sort.MergeSort.html#1425" class="Bound">O</a> <a id="1427" class="Symbol">:</a> <a id="1429" href="Relation.Binary.Bundles.html#6016" class="Record">DecTotalOrder</a> <a id="1443" href="Data.List.Sort.MergeSort.html#1415" class="Bound">a</a> <a id="1445" href="Data.List.Sort.MergeSort.html#1417" class="Bound">ℓ₁</a> <a id="1448" href="Data.List.Sort.MergeSort.html#1420" class="Bound">ℓ₂</a><a id="1450" class="Symbol">)</a> <a id="1452" class="Keyword">where</a>

<a id="1459" class="Keyword">open</a> <a id="1464" href="Relation.Binary.Bundles.html#6016" class="Module">DecTotalOrder</a> <a id="1478" href="Data.List.Sort.MergeSort.html#1425" class="Bound">O</a> <a id="1480" class="Keyword">renaming</a> <a id="1489" class="Symbol">(</a><a id="1490" href="Relation.Binary.Bundles.html#6100" class="Field">Carrier</a> <a id="1498" class="Symbol">to</a> <a id="1501" class="Field">A</a><a id="1502" class="Symbol">)</a>

<a id="1505" class="Keyword">open</a> <a id="1510" class="Keyword">import</a> <a id="1517" href="Data.List.Sort.Base.html" class="Module">Data.List.Sort.Base</a> <a id="1537" href="Relation.Binary.Bundles.html#6344" class="Function">totalOrder</a>
<a id="1548" class="Keyword">open</a> <a id="1553" class="Keyword">import</a> <a id="1560" href="Data.List.Relation.Unary.Sorted.TotalOrder.html" class="Module">Data.List.Relation.Unary.Sorted.TotalOrder</a> <a id="1603" href="Relation.Binary.Bundles.html#6344" class="Function">totalOrder</a> <a id="1614" class="Keyword">hiding</a> <a id="1621" class="Symbol">(</a><a id="1622" href="Data.List.Relation.Unary.Sorted.TotalOrder.html#937" class="Function">head</a><a id="1626" class="Symbol">)</a>
<a id="1628" class="Keyword">open</a> <a id="1633" class="Keyword">import</a> <a id="1640" href="Relation.Binary.Properties.DecTotalOrder.html" class="Module">Relation.Binary.Properties.DecTotalOrder</a> <a id="1681" href="Data.List.Sort.MergeSort.html#1425" class="Bound">O</a> <a id="1683" class="Keyword">using</a> <a id="1689" class="Symbol">(</a><a id="1690" href="Relation.Binary.Properties.Poset.html#1549" class="Function Operator">_≰_</a><a id="1693" class="Symbol">;</a> <a id="1695" href="Relation.Binary.Properties.TotalOrder.html#2493" class="Function">≰⇒≥</a><a id="1698" class="Symbol">;</a> <a id="1700" href="Relation.Binary.Properties.Poset.html#1583" class="Function">≰-respˡ-≈</a><a id="1709" class="Symbol">)</a>

<a id="1712" class="Keyword">open</a> <a id="1717" href="Data.List.Relation.Binary.Permutation.Propositional.html#2249" class="Module">PermutationReasoning</a>

<a id="1739" class="Comment">------------------------------------------------------------------------</a>
<a id="1812" class="Comment">-- Definition</a>

<a id="mergePairs"></a><a id="1827" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="1838" class="Symbol">:</a> <a id="1840" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="1845" class="Symbol">(</a><a id="1846" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="1851" href="Data.List.Sort.MergeSort.html#1501" class="Field">A</a><a id="1852" class="Symbol">)</a> <a id="1854" class="Symbol">→</a> <a id="1856" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="1861" class="Symbol">(</a><a id="1862" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="1867" href="Data.List.Sort.MergeSort.html#1501" class="Field">A</a><a id="1868" class="Symbol">)</a>
<a id="1870" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="1881" class="Symbol">(</a><a id="1882" href="Data.List.Sort.MergeSort.html#1882" class="Bound">xs</a> <a id="1885" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1887" href="Data.List.Sort.MergeSort.html#1887" class="Bound">ys</a> <a id="1890" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1892" href="Data.List.Sort.MergeSort.html#1892" class="Bound">xss</a><a id="1895" class="Symbol">)</a> <a id="1897" class="Symbol">=</a> <a id="1899" href="Data.List.Base.html#3869" class="Function">merge</a> <a id="1905" href="Relation.Binary.Structures.html#5644" class="Function Operator">_≤?_</a> <a id="1910" href="Data.List.Sort.MergeSort.html#1882" class="Bound">xs</a> <a id="1913" href="Data.List.Sort.MergeSort.html#1887" class="Bound">ys</a> <a id="1916" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1918" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="1929" href="Data.List.Sort.MergeSort.html#1892" class="Bound">xss</a>
<a id="1933" href="Data.List.Sort.MergeSort.html#1827" class="CatchallClause Function">mergePairs</a><a id="1943" class="CatchallClause"> </a><a id="1944" href="Data.List.Sort.MergeSort.html#1944" class="CatchallClause Bound">xss</a>             <a id="1960" class="Symbol">=</a> <a id="1962" href="Data.List.Sort.MergeSort.html#1944" class="Bound">xss</a>

<a id="1967" class="Keyword">private</a>
  <a id="length-mergePairs"></a><a id="1977" href="Data.List.Sort.MergeSort.html#1977" class="Function">length-mergePairs</a> <a id="1995" class="Symbol">:</a> <a id="1997" class="Symbol">∀</a> <a id="1999" href="Data.List.Sort.MergeSort.html#1999" class="Bound">xs</a> <a id="2002" href="Data.List.Sort.MergeSort.html#2002" class="Bound">ys</a> <a id="2005" href="Data.List.Sort.MergeSort.html#2005" class="Bound">xss</a> <a id="2009" class="Symbol">→</a> <a id="2011" href="Data.List.Base.html#4888" class="Function">length</a> <a id="2018" class="Symbol">(</a><a id="2019" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="2030" class="Symbol">(</a><a id="2031" href="Data.List.Sort.MergeSort.html#1999" class="Bound">xs</a> <a id="2034" class="InductiveConstructor Operator">∷</a> <a id="2036" href="Data.List.Sort.MergeSort.html#2002" class="Bound">ys</a> <a id="2039" class="InductiveConstructor Operator">∷</a> <a id="2041" href="Data.List.Sort.MergeSort.html#2005" class="Bound">xss</a><a id="2044" class="Symbol">))</a> <a id="2047" href="Data.Nat.Base.html#1583" class="Function Operator">&lt;</a> <a id="2049" href="Data.List.Base.html#4888" class="Function">length</a> <a id="2056" class="Symbol">(</a><a id="2057" href="Data.List.Sort.MergeSort.html#1999" class="Bound">xs</a> <a id="2060" class="InductiveConstructor Operator">∷</a> <a id="2062" href="Data.List.Sort.MergeSort.html#2002" class="Bound">ys</a> <a id="2065" class="InductiveConstructor Operator">∷</a> <a id="2067" href="Data.List.Sort.MergeSort.html#2005" class="Bound">xss</a><a id="2070" class="Symbol">)</a>
  <a id="2074" href="Data.List.Sort.MergeSort.html#1977" class="Function">length-mergePairs</a> <a id="2092" class="Symbol">_</a> <a id="2094" class="Symbol">_</a> <a id="2096" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>              <a id="2112" class="Symbol">=</a> <a id="2114" href="Data.Nat.Base.html#1695" class="InductiveConstructor">s&lt;s</a> <a id="2118" href="Data.Nat.Base.html#1655" class="InductiveConstructor">z&lt;s</a>
  <a id="2124" href="Data.List.Sort.MergeSort.html#1977" class="Function">length-mergePairs</a> <a id="2142" class="Symbol">_</a> <a id="2144" class="Symbol">_</a> <a id="2146" class="Symbol">(</a><a id="2147" href="Data.List.Sort.MergeSort.html#2147" class="Bound">xss</a> <a id="2151" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2153" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a><a id="2155" class="Symbol">)</a>      <a id="2162" class="Symbol">=</a> <a id="2164" href="Data.Nat.Base.html#1695" class="InductiveConstructor">s&lt;s</a> <a id="2168" class="Symbol">(</a><a id="2169" href="Data.Nat.Base.html#1695" class="InductiveConstructor">s&lt;s</a> <a id="2173" href="Data.Nat.Base.html#1655" class="InductiveConstructor">z&lt;s</a><a id="2176" class="Symbol">)</a>
  <a id="2180" href="Data.List.Sort.MergeSort.html#1977" class="Function">length-mergePairs</a> <a id="2198" class="Symbol">_</a> <a id="2200" class="Symbol">_</a> <a id="2202" class="Symbol">(</a><a id="2203" href="Data.List.Sort.MergeSort.html#2203" class="Bound">xs</a> <a id="2206" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2208" href="Data.List.Sort.MergeSort.html#2208" class="Bound">ys</a> <a id="2211" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2213" href="Data.List.Sort.MergeSort.html#2213" class="Bound">xss</a><a id="2216" class="Symbol">)</a> <a id="2218" class="Symbol">=</a> <a id="2220" href="Data.Nat.Base.html#1695" class="InductiveConstructor">s&lt;s</a> <a id="2224" class="Symbol">(</a><a id="2225" href="Data.Nat.Properties.html#7610" class="Function">≤-step</a> <a id="2232" class="Symbol">(</a><a id="2233" href="Data.List.Sort.MergeSort.html#1977" class="Function">length-mergePairs</a> <a id="2251" href="Data.List.Sort.MergeSort.html#2203" class="Bound">xs</a> <a id="2254" href="Data.List.Sort.MergeSort.html#2208" class="Bound">ys</a> <a id="2257" href="Data.List.Sort.MergeSort.html#2213" class="Bound">xss</a><a id="2260" class="Symbol">))</a>

<a id="mergeAll"></a><a id="2264" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a> <a id="2273" class="Symbol">:</a> <a id="2275" class="Symbol">(</a><a id="2276" href="Data.List.Sort.MergeSort.html#2276" class="Bound">xss</a> <a id="2280" class="Symbol">:</a> <a id="2282" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="2287" class="Symbol">(</a><a id="2288" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="2293" href="Data.List.Sort.MergeSort.html#1501" class="Field">A</a><a id="2294" class="Symbol">))</a> <a id="2297" class="Symbol">→</a> <a id="2299" href="Induction.WellFounded.html#1009" class="Datatype">Acc</a> <a id="2303" href="Data.Nat.Base.html#1583" class="Function Operator">_&lt;_</a> <a id="2307" class="Symbol">(</a><a id="2308" href="Data.List.Base.html#4888" class="Function">length</a> <a id="2315" href="Data.List.Sort.MergeSort.html#2276" class="Bound">xss</a><a id="2318" class="Symbol">)</a> <a id="2320" class="Symbol">→</a> <a id="2322" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="2327" href="Data.List.Sort.MergeSort.html#1501" class="Field">A</a>
<a id="2329" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a> <a id="2338" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>        <a id="2348" class="Symbol">_</a>               <a id="2364" class="Symbol">=</a> <a id="2366" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>
<a id="2369" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a> <a id="2378" class="Symbol">(</a><a id="2379" href="Data.List.Sort.MergeSort.html#2379" class="Bound">xs</a> <a id="2382" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2384" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a><a id="2386" class="Symbol">)</a> <a id="2388" class="Symbol">_</a>               <a id="2404" class="Symbol">=</a> <a id="2406" href="Data.List.Sort.MergeSort.html#2379" class="Bound">xs</a>
<a id="2409" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a> <a id="2418" class="Symbol">(</a><a id="2419" href="Data.List.Sort.MergeSort.html#2419" class="Bound">xs</a> <a id="2422" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2424" href="Data.List.Sort.MergeSort.html#2424" class="Bound">ys</a> <a id="2427" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2429" href="Data.List.Sort.MergeSort.html#2429" class="Bound">xss</a><a id="2432" class="Symbol">)</a> <a id="2434" class="Symbol">(</a><a id="2435" href="Induction.WellFounded.html#1071" class="InductiveConstructor">acc</a> <a id="2439" href="Data.List.Sort.MergeSort.html#2439" class="Bound">rec</a><a id="2442" class="Symbol">)</a> <a id="2444" class="Symbol">=</a> <a id="2446" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a>
  <a id="2457" class="Symbol">(</a><a id="2458" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="2469" class="Symbol">(</a><a id="2470" href="Data.List.Sort.MergeSort.html#2419" class="Bound">xs</a> <a id="2473" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2475" href="Data.List.Sort.MergeSort.html#2424" class="Bound">ys</a> <a id="2478" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2480" href="Data.List.Sort.MergeSort.html#2429" class="Bound">xss</a><a id="2483" class="Symbol">))</a> <a id="2486" class="Symbol">(</a><a id="2487" href="Data.List.Sort.MergeSort.html#2439" class="Bound">rec</a> <a id="2491" class="Symbol">_</a> <a id="2493" class="Symbol">(</a><a id="2494" href="Data.List.Sort.MergeSort.html#1977" class="Function">length-mergePairs</a> <a id="2512" href="Data.List.Sort.MergeSort.html#2419" class="Bound">xs</a> <a id="2515" href="Data.List.Sort.MergeSort.html#2424" class="Bound">ys</a> <a id="2518" href="Data.List.Sort.MergeSort.html#2429" class="Bound">xss</a><a id="2521" class="Symbol">))</a>

<a id="sort"></a><a id="2525" href="Data.List.Sort.MergeSort.html#2525" class="Function">sort</a> <a id="2530" class="Symbol">:</a> <a id="2532" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="2537" href="Data.List.Sort.MergeSort.html#1501" class="Field">A</a> <a id="2539" class="Symbol">→</a> <a id="2541" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="2546" href="Data.List.Sort.MergeSort.html#1501" class="Field">A</a>
<a id="2548" href="Data.List.Sort.MergeSort.html#2525" class="Function">sort</a> <a id="2553" href="Data.List.Sort.MergeSort.html#2553" class="Bound">xs</a> <a id="2556" class="Symbol">=</a> <a id="2558" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a> <a id="2567" class="Symbol">(</a><a id="2568" href="Data.List.Base.html#1572" class="Function">map</a> <a id="2572" href="Data.List.Base.html#5049" class="Function Operator">[_]</a> <a id="2576" href="Data.List.Sort.MergeSort.html#2553" class="Bound">xs</a><a id="2578" class="Symbol">)</a> <a id="2580" class="Symbol">(</a><a id="2581" href="Data.Nat.Induction.html#2738" class="Function">&lt;-wellFounded-fast</a> <a id="2600" class="Symbol">_)</a>

<a id="2604" class="Comment">------------------------------------------------------------------------</a>
<a id="2677" class="Comment">-- Permutation property</a>

<a id="mergePairs-↭"></a><a id="2702" href="Data.List.Sort.MergeSort.html#2702" class="Function">mergePairs-↭</a> <a id="2715" class="Symbol">:</a> <a id="2717" class="Symbol">∀</a> <a id="2719" href="Data.List.Sort.MergeSort.html#2719" class="Bound">xss</a> <a id="2723" class="Symbol">→</a> <a id="2725" href="Data.List.Base.html#4418" class="Function">concat</a> <a id="2732" class="Symbol">(</a><a id="2733" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="2744" href="Data.List.Sort.MergeSort.html#2719" class="Bound">xss</a><a id="2747" class="Symbol">)</a> <a id="2749" href="Data.List.Relation.Binary.Permutation.Propositional.html#1050" class="Datatype Operator">↭</a> <a id="2751" href="Data.List.Base.html#4418" class="Function">concat</a> <a id="2758" href="Data.List.Sort.MergeSort.html#2719" class="Bound">xss</a>
<a id="2762" href="Data.List.Sort.MergeSort.html#2702" class="Function">mergePairs-↭</a> <a id="2775" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>              <a id="2791" class="Symbol">=</a> <a id="2793" href="Data.List.Relation.Binary.Permutation.Propositional.html#1426" class="Function">↭-refl</a>
<a id="2800" href="Data.List.Sort.MergeSort.html#2702" class="Function">mergePairs-↭</a> <a id="2813" class="Symbol">(</a><a id="2814" href="Data.List.Sort.MergeSort.html#2814" class="Bound">xs</a> <a id="2817" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2819" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a><a id="2821" class="Symbol">)</a>       <a id="2829" class="Symbol">=</a> <a id="2831" href="Data.List.Relation.Binary.Permutation.Propositional.html#1426" class="Function">↭-refl</a>
<a id="2838" href="Data.List.Sort.MergeSort.html#2702" class="Function">mergePairs-↭</a> <a id="2851" class="Symbol">(</a><a id="2852" href="Data.List.Sort.MergeSort.html#2852" class="Bound">xs</a> <a id="2855" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2857" href="Data.List.Sort.MergeSort.html#2857" class="Bound">ys</a> <a id="2860" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2862" href="Data.List.Sort.MergeSort.html#2862" class="Bound">xss</a><a id="2865" class="Symbol">)</a> <a id="2867" class="Symbol">=</a> <a id="2869" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="2877" href="Data.List.Base.html#3869" class="Function">merge</a> <a id="2883" class="Symbol">_</a> <a id="2885" href="Data.List.Sort.MergeSort.html#2852" class="Bound">xs</a> <a id="2888" href="Data.List.Sort.MergeSort.html#2857" class="Bound">ys</a> <a id="2891" href="Data.List.Base.html#1834" class="Function Operator">++</a> <a id="2894" href="Data.List.Base.html#4418" class="Function">concat</a> <a id="2901" class="Symbol">(</a><a id="2902" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="2913" href="Data.List.Sort.MergeSort.html#2862" class="Bound">xss</a><a id="2916" class="Symbol">)</a> <a id="2918" href="Data.List.Relation.Binary.Permutation.Propositional.html#2442" class="Function">↭⟨</a> <a id="2921" href="Data.List.Relation.Binary.Permutation.Propositional.Properties.html#6341" class="Function">Perm.++⁺</a> <a id="2930" class="Symbol">(</a><a id="2931" href="Data.List.Relation.Binary.Permutation.Propositional.Properties.html#12652" class="Function">Perm.merge-↭</a> <a id="2944" class="Symbol">_</a> <a id="2946" href="Data.List.Sort.MergeSort.html#2852" class="Bound">xs</a> <a id="2949" href="Data.List.Sort.MergeSort.html#2857" class="Bound">ys</a><a id="2951" class="Symbol">)</a> <a id="2953" class="Symbol">(</a><a id="2954" href="Data.List.Sort.MergeSort.html#2702" class="Function">mergePairs-↭</a> <a id="2967" href="Data.List.Sort.MergeSort.html#2862" class="Bound">xss</a><a id="2970" class="Symbol">)</a> <a id="2972" href="Data.List.Relation.Binary.Permutation.Propositional.html#2442" class="Function">⟩</a>
  <a id="2976" class="Symbol">(</a><a id="2977" href="Data.List.Sort.MergeSort.html#2852" class="Bound">xs</a> <a id="2980" href="Data.List.Base.html#1834" class="Function Operator">++</a> <a id="2983" href="Data.List.Sort.MergeSort.html#2857" class="Bound">ys</a><a id="2985" class="Symbol">)</a>    <a id="2990" href="Data.List.Base.html#1834" class="Function Operator">++</a> <a id="2993" href="Data.List.Base.html#4418" class="Function">concat</a> <a id="3000" href="Data.List.Sort.MergeSort.html#2862" class="Bound">xss</a>              <a id="3017" href="Relation.Binary.Reasoning.Base.Single.html#2191" class="Function">≡⟨</a> <a id="3020" href="Data.List.Properties.html#5511" class="Function">++-assoc</a> <a id="3029" href="Data.List.Sort.MergeSort.html#2852" class="Bound">xs</a> <a id="3032" href="Data.List.Sort.MergeSort.html#2857" class="Bound">ys</a> <a id="3035" class="Symbol">(</a><a id="3036" href="Data.List.Base.html#4418" class="Function">concat</a> <a id="3043" href="Data.List.Sort.MergeSort.html#2862" class="Bound">xss</a><a id="3046" class="Symbol">)</a> <a id="3048" href="Relation.Binary.Reasoning.Base.Single.html#2191" class="Function">⟩</a>
  <a id="3052" href="Data.List.Sort.MergeSort.html#2852" class="Bound">xs</a> <a id="3055" href="Data.List.Base.html#1834" class="Function Operator">++</a> <a id="3058" href="Data.List.Sort.MergeSort.html#2857" class="Bound">ys</a>      <a id="3066" href="Data.List.Base.html#1834" class="Function Operator">++</a> <a id="3069" href="Data.List.Base.html#4418" class="Function">concat</a> <a id="3076" href="Data.List.Sort.MergeSort.html#2862" class="Bound">xss</a>              <a id="3093" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>

<a id="mergeAll-↭"></a><a id="3096" href="Data.List.Sort.MergeSort.html#3096" class="Function">mergeAll-↭</a> <a id="3107" class="Symbol">:</a> <a id="3109" class="Symbol">∀</a> <a id="3111" href="Data.List.Sort.MergeSort.html#3111" class="Bound">xss</a> <a id="3115" class="Symbol">(</a><a id="3116" href="Data.List.Sort.MergeSort.html#3116" class="Bound">rec</a> <a id="3120" class="Symbol">:</a> <a id="3122" href="Induction.WellFounded.html#1009" class="Datatype">Acc</a> <a id="3126" href="Data.Nat.Base.html#1583" class="Function Operator">_&lt;_</a> <a id="3130" class="Symbol">(</a><a id="3131" href="Data.List.Base.html#4888" class="Function">length</a> <a id="3138" href="Data.List.Sort.MergeSort.html#3111" class="Bound">xss</a><a id="3141" class="Symbol">))</a> <a id="3144" class="Symbol">→</a> <a id="3146" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a> <a id="3155" href="Data.List.Sort.MergeSort.html#3111" class="Bound">xss</a> <a id="3159" href="Data.List.Sort.MergeSort.html#3116" class="Bound">rec</a> <a id="3163" href="Data.List.Relation.Binary.Permutation.Propositional.html#1050" class="Datatype Operator">↭</a> <a id="3165" href="Data.List.Base.html#4418" class="Function">concat</a> <a id="3172" href="Data.List.Sort.MergeSort.html#3111" class="Bound">xss</a>
<a id="3176" href="Data.List.Sort.MergeSort.html#3096" class="Function">mergeAll-↭</a> <a id="3187" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>              <a id="3203" class="Symbol">_</a> <a id="3205" class="Symbol">=</a> <a id="3207" href="Data.List.Relation.Binary.Permutation.Propositional.html#1426" class="Function">↭-refl</a>
<a id="3214" href="Data.List.Sort.MergeSort.html#3096" class="Function">mergeAll-↭</a> <a id="3225" class="Symbol">(</a><a id="3226" href="Data.List.Sort.MergeSort.html#3226" class="Bound">xs</a> <a id="3229" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3231" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a><a id="3233" class="Symbol">)</a>       <a id="3241" class="Symbol">_</a> <a id="3243" class="Symbol">=</a> <a id="3245" href="Data.List.Relation.Binary.Permutation.Propositional.html#1464" class="Function">↭-sym</a> <a id="3251" class="Symbol">(</a><a id="3252" href="Data.List.Relation.Binary.Permutation.Propositional.Properties.html#10091" class="Function">Perm.++-identityʳ</a> <a id="3270" href="Data.List.Sort.MergeSort.html#3226" class="Bound">xs</a><a id="3272" class="Symbol">)</a>
<a id="3274" href="Data.List.Sort.MergeSort.html#3096" class="Function">mergeAll-↭</a> <a id="3285" class="Symbol">(</a><a id="3286" href="Data.List.Sort.MergeSort.html#3286" class="Bound">xs</a> <a id="3289" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3291" href="Data.List.Sort.MergeSort.html#3291" class="Bound">ys</a> <a id="3294" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3296" href="Data.List.Sort.MergeSort.html#3296" class="Bound">xss</a><a id="3299" class="Symbol">)</a> <a id="3301" class="Symbol">(</a><a id="3302" href="Induction.WellFounded.html#1071" class="InductiveConstructor">acc</a> <a id="3306" href="Data.List.Sort.MergeSort.html#3306" class="Bound">rec</a><a id="3309" class="Symbol">)</a> <a id="3311" class="Symbol">=</a> <a id="3313" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="3321" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a> <a id="3330" class="Symbol">(</a><a id="3331" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="3342" class="Symbol">(</a><a id="3343" href="Data.List.Sort.MergeSort.html#3286" class="Bound">xs</a> <a id="3346" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3348" href="Data.List.Sort.MergeSort.html#3291" class="Bound">ys</a> <a id="3351" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3353" href="Data.List.Sort.MergeSort.html#3296" class="Bound">xss</a><a id="3356" class="Symbol">))</a> <a id="3359" class="Symbol">_</a> <a id="3361" href="Data.List.Relation.Binary.Permutation.Propositional.html#2442" class="Function">↭⟨</a> <a id="3364" href="Data.List.Sort.MergeSort.html#3096" class="Function">mergeAll-↭</a> <a id="3375" class="Symbol">(</a><a id="3376" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="3387" class="Symbol">(</a><a id="3388" href="Data.List.Sort.MergeSort.html#3286" class="Bound">xs</a> <a id="3391" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3393" href="Data.List.Sort.MergeSort.html#3291" class="Bound">ys</a> <a id="3396" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3398" href="Data.List.Sort.MergeSort.html#3296" class="Bound">xss</a><a id="3401" class="Symbol">))</a> <a id="3404" class="Symbol">_</a> <a id="3406" href="Data.List.Relation.Binary.Permutation.Propositional.html#2442" class="Function">⟩</a>
  <a id="3410" href="Data.List.Base.html#4418" class="Function">concat</a>   <a id="3419" class="Symbol">(</a><a id="3420" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="3431" class="Symbol">(</a><a id="3432" href="Data.List.Sort.MergeSort.html#3286" class="Bound">xs</a> <a id="3435" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3437" href="Data.List.Sort.MergeSort.html#3291" class="Bound">ys</a> <a id="3440" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3442" href="Data.List.Sort.MergeSort.html#3296" class="Bound">xss</a><a id="3445" class="Symbol">))</a>   <a id="3450" href="Data.List.Relation.Binary.Permutation.Propositional.html#2442" class="Function">↭⟨</a> <a id="3453" href="Data.List.Sort.MergeSort.html#2702" class="Function">mergePairs-↭</a> <a id="3466" class="Symbol">(</a><a id="3467" href="Data.List.Sort.MergeSort.html#3286" class="Bound">xs</a> <a id="3470" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3472" href="Data.List.Sort.MergeSort.html#3291" class="Bound">ys</a> <a id="3475" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3477" href="Data.List.Sort.MergeSort.html#3296" class="Bound">xss</a><a id="3480" class="Symbol">)</a> <a id="3482" href="Data.List.Relation.Binary.Permutation.Propositional.html#2442" class="Function">⟩</a>
  <a id="3486" href="Data.List.Base.html#4418" class="Function">concat</a>   <a id="3495" class="Symbol">(</a><a id="3496" href="Data.List.Sort.MergeSort.html#3286" class="Bound">xs</a> <a id="3499" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3501" href="Data.List.Sort.MergeSort.html#3291" class="Bound">ys</a> <a id="3504" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="3506" href="Data.List.Sort.MergeSort.html#3296" class="Bound">xss</a><a id="3509" class="Symbol">)</a>                <a id="3526" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>

<a id="sort-↭"></a><a id="3529" href="Data.List.Sort.MergeSort.html#3529" class="Function">sort-↭</a> <a id="3536" class="Symbol">:</a> <a id="3538" class="Symbol">∀</a> <a id="3540" href="Data.List.Sort.MergeSort.html#3540" class="Bound">xs</a> <a id="3543" class="Symbol">→</a> <a id="3545" href="Data.List.Sort.MergeSort.html#2525" class="Function">sort</a> <a id="3550" href="Data.List.Sort.MergeSort.html#3540" class="Bound">xs</a> <a id="3553" href="Data.List.Relation.Binary.Permutation.Propositional.html#1050" class="Datatype Operator">↭</a> <a id="3555" href="Data.List.Sort.MergeSort.html#3540" class="Bound">xs</a>
<a id="3558" href="Data.List.Sort.MergeSort.html#3529" class="Function">sort-↭</a> <a id="3565" href="Data.List.Sort.MergeSort.html#3565" class="Bound">xs</a> <a id="3568" class="Symbol">=</a> <a id="3570" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="3578" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a> <a id="3587" class="Symbol">(</a><a id="3588" href="Data.List.Base.html#1572" class="Function">map</a> <a id="3592" href="Data.List.Base.html#5049" class="Function Operator">[_]</a> <a id="3596" href="Data.List.Sort.MergeSort.html#3565" class="Bound">xs</a><a id="3598" class="Symbol">)</a> <a id="3600" class="Symbol">_</a> <a id="3602" href="Data.List.Relation.Binary.Permutation.Propositional.html#2442" class="Function">↭⟨</a> <a id="3605" href="Data.List.Sort.MergeSort.html#3096" class="Function">mergeAll-↭</a> <a id="3616" class="Symbol">(</a><a id="3617" href="Data.List.Base.html#1572" class="Function">map</a> <a id="3621" href="Data.List.Base.html#5049" class="Function Operator">[_]</a> <a id="3625" href="Data.List.Sort.MergeSort.html#3565" class="Bound">xs</a><a id="3627" class="Symbol">)</a> <a id="3629" class="Symbol">_</a> <a id="3631" href="Data.List.Relation.Binary.Permutation.Propositional.html#2442" class="Function">⟩</a>
  <a id="3635" href="Data.List.Base.html#4418" class="Function">concat</a> <a id="3642" class="Symbol">(</a><a id="3643" href="Data.List.Base.html#1572" class="Function">map</a> <a id="3647" href="Data.List.Base.html#5049" class="Function Operator">[_]</a> <a id="3651" href="Data.List.Sort.MergeSort.html#3565" class="Bound">xs</a><a id="3653" class="Symbol">)</a>     <a id="3659" href="Relation.Binary.Reasoning.Base.Single.html#2191" class="Function">≡⟨</a> <a id="3662" href="Data.List.Properties.html#19708" class="Function">concat-[-]</a> <a id="3673" href="Data.List.Sort.MergeSort.html#3565" class="Bound">xs</a> <a id="3676" href="Relation.Binary.Reasoning.Base.Single.html#2191" class="Function">⟩</a>
  <a id="3680" href="Data.List.Sort.MergeSort.html#3565" class="Bound">xs</a>                      <a id="3704" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>

<a id="3707" class="Comment">------------------------------------------------------------------------</a>
<a id="3780" class="Comment">-- Sorted property</a>

<a id="mergePairs-↗"></a><a id="3800" href="Data.List.Sort.MergeSort.html#3800" class="Function">mergePairs-↗</a> <a id="3813" class="Symbol">:</a> <a id="3815" class="Symbol">∀</a> <a id="3817" class="Symbol">{</a><a id="3818" href="Data.List.Sort.MergeSort.html#3818" class="Bound">xss</a><a id="3821" class="Symbol">}</a> <a id="3823" class="Symbol">→</a> <a id="3825" href="Data.List.Relation.Unary.All.html#1501" class="Datatype">All</a> <a id="3829" href="Data.List.Relation.Unary.Sorted.TotalOrder.html#763" class="Function">Sorted</a> <a id="3836" href="Data.List.Sort.MergeSort.html#3818" class="Bound">xss</a> <a id="3840" class="Symbol">→</a> <a id="3842" href="Data.List.Relation.Unary.All.html#1501" class="Datatype">All</a> <a id="3846" href="Data.List.Relation.Unary.Sorted.TotalOrder.html#763" class="Function">Sorted</a> <a id="3853" class="Symbol">(</a><a id="3854" href="Data.List.Sort.MergeSort.html#1827" class="Function">mergePairs</a> <a id="3865" href="Data.List.Sort.MergeSort.html#3818" class="Bound">xss</a><a id="3868" class="Symbol">)</a>
<a id="3870" href="Data.List.Sort.MergeSort.html#3800" class="Function">mergePairs-↗</a> <a id="3883" href="Data.List.Relation.Unary.All.html#1564" class="InductiveConstructor">[]</a>                 <a id="3902" class="Symbol">=</a> <a id="3904" href="Data.List.Relation.Unary.All.html#1564" class="InductiveConstructor">[]</a>
<a id="3907" href="Data.List.Sort.MergeSort.html#3800" class="Function">mergePairs-↗</a> <a id="3920" class="Symbol">(</a><a id="3921" href="Data.List.Sort.MergeSort.html#3921" class="Bound">xs↗</a> <a id="3925" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="3927" href="Data.List.Relation.Unary.All.html#1564" class="InductiveConstructor">[]</a><a id="3929" class="Symbol">)</a>         <a id="3939" class="Symbol">=</a> <a id="3941" href="Data.List.Sort.MergeSort.html#3921" class="Bound">xs↗</a> <a id="3945" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="3947" href="Data.List.Relation.Unary.All.html#1564" class="InductiveConstructor">[]</a>
<a id="3950" href="Data.List.Sort.MergeSort.html#3800" class="Function">mergePairs-↗</a> <a id="3963" class="Symbol">(</a><a id="3964" href="Data.List.Sort.MergeSort.html#3964" class="Bound">xs↗</a> <a id="3968" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="3970" href="Data.List.Sort.MergeSort.html#3970" class="Bound">ys↗</a> <a id="3974" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="3976" href="Data.List.Sort.MergeSort.html#3976" class="Bound">xss↗</a><a id="3980" class="Symbol">)</a> <a id="3982" class="Symbol">=</a> <a id="3984" href="Data.List.Relation.Unary.Sorted.TotalOrder.Properties.html#4115" class="Function">Sorted.merge⁺</a> <a id="3998" href="Data.List.Sort.MergeSort.html#1425" class="Bound">O</a> <a id="4000" href="Data.List.Sort.MergeSort.html#3964" class="Bound">xs↗</a> <a id="4004" href="Data.List.Sort.MergeSort.html#3970" class="Bound">ys↗</a> <a id="4008" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="4010" href="Data.List.Sort.MergeSort.html#3800" class="Function">mergePairs-↗</a> <a id="4023" href="Data.List.Sort.MergeSort.html#3976" class="Bound">xss↗</a>

<a id="mergeAll-↗"></a><a id="4029" href="Data.List.Sort.MergeSort.html#4029" class="Function">mergeAll-↗</a> <a id="4040" class="Symbol">:</a> <a id="4042" class="Symbol">∀</a> <a id="4044" class="Symbol">{</a><a id="4045" href="Data.List.Sort.MergeSort.html#4045" class="Bound">xss</a><a id="4048" class="Symbol">}</a> <a id="4050" class="Symbol">(</a><a id="4051" href="Data.List.Sort.MergeSort.html#4051" class="Bound">rec</a> <a id="4055" class="Symbol">:</a> <a id="4057" href="Induction.WellFounded.html#1009" class="Datatype">Acc</a> <a id="4061" href="Data.Nat.Base.html#1583" class="Function Operator">_&lt;_</a> <a id="4065" class="Symbol">(</a><a id="4066" href="Data.List.Base.html#4888" class="Function">length</a> <a id="4073" href="Data.List.Sort.MergeSort.html#4045" class="Bound">xss</a><a id="4076" class="Symbol">))</a> <a id="4079" class="Symbol">→</a>
             <a id="4094" href="Data.List.Relation.Unary.All.html#1501" class="Datatype">All</a> <a id="4098" href="Data.List.Relation.Unary.Sorted.TotalOrder.html#763" class="Function">Sorted</a> <a id="4105" href="Data.List.Sort.MergeSort.html#4045" class="Bound">xss</a> <a id="4109" class="Symbol">→</a> <a id="4111" href="Data.List.Relation.Unary.Sorted.TotalOrder.html#763" class="Function">Sorted</a> <a id="4118" class="Symbol">(</a><a id="4119" href="Data.List.Sort.MergeSort.html#2264" class="Function">mergeAll</a> <a id="4128" href="Data.List.Sort.MergeSort.html#4045" class="Bound">xss</a> <a id="4132" href="Data.List.Sort.MergeSort.html#4051" class="Bound">rec</a><a id="4135" class="Symbol">)</a>
<a id="4137" href="Data.List.Sort.MergeSort.html#4029" class="Function">mergeAll-↗</a> <a id="4148" href="Data.List.Sort.MergeSort.html#4148" class="Bound">rec</a>       <a id="4158" href="Data.List.Relation.Unary.All.html#1564" class="InductiveConstructor">[]</a>                 <a id="4177" class="Symbol">=</a> <a id="4179" href="Data.List.Relation.Unary.Linked.html#1451" class="InductiveConstructor">[]</a>
<a id="4182" href="Data.List.Sort.MergeSort.html#4029" class="Function">mergeAll-↗</a> <a id="4193" href="Data.List.Sort.MergeSort.html#4193" class="Bound">rec</a>       <a id="4203" class="Symbol">(</a><a id="4204" href="Data.List.Sort.MergeSort.html#4204" class="Bound">xs↗</a> <a id="4208" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="4210" href="Data.List.Relation.Unary.All.html#1564" class="InductiveConstructor">[]</a><a id="4212" class="Symbol">)</a>         <a id="4222" class="Symbol">=</a> <a id="4224" href="Data.List.Sort.MergeSort.html#4204" class="Bound">xs↗</a>
<a id="4228" href="Data.List.Sort.MergeSort.html#4029" class="Function">mergeAll-↗</a> <a id="4239" class="Symbol">(</a><a id="4240" href="Induction.WellFounded.html#1071" class="InductiveConstructor">acc</a> <a id="4244" href="Data.List.Sort.MergeSort.html#4244" class="Bound">rec</a><a id="4247" class="Symbol">)</a> <a id="4249" class="Symbol">(</a><a id="4250" href="Data.List.Sort.MergeSort.html#4250" class="Bound">xs↗</a> <a id="4254" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="4256" href="Data.List.Sort.MergeSort.html#4256" class="Bound">ys↗</a> <a id="4260" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="4262" href="Data.List.Sort.MergeSort.html#4262" class="Bound">xss↗</a><a id="4266" class="Symbol">)</a> <a id="4268" class="Symbol">=</a> <a id="4270" href="Data.List.Sort.MergeSort.html#4029" class="Function">mergeAll-↗</a> <a id="4281" class="Symbol">_</a> <a id="4283" class="Symbol">(</a><a id="4284" href="Data.List.Sort.MergeSort.html#3800" class="Function">mergePairs-↗</a> <a id="4297" class="Symbol">(</a><a id="4298" href="Data.List.Sort.MergeSort.html#4250" class="Bound">xs↗</a> <a id="4302" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="4304" href="Data.List.Sort.MergeSort.html#4256" class="Bound">ys↗</a> <a id="4308" href="Data.List.Relation.Unary.All.html#1581" class="InductiveConstructor Operator">∷</a> <a id="4310" href="Data.List.Sort.MergeSort.html#4262" class="Bound">xss↗</a><a id="4314" class="Symbol">))</a>

<a id="sort-↗"></a><a id="4318" href="Data.List.Sort.MergeSort.html#4318" class="Function">sort-↗</a> <a id="4325" class="Symbol">:</a> <a id="4327" class="Symbol">∀</a> <a id="4329" href="Data.List.Sort.MergeSort.html#4329" class="Bound">xs</a> <a id="4332" class="Symbol">→</a> <a id="4334" href="Data.List.Relation.Unary.Sorted.TotalOrder.html#763" class="Function">Sorted</a> <a id="4341" class="Symbol">(</a><a id="4342" href="Data.List.Sort.MergeSort.html#2525" class="Function">sort</a> <a id="4347" href="Data.List.Sort.MergeSort.html#4329" class="Bound">xs</a><a id="4349" class="Symbol">)</a>
<a id="4351" href="Data.List.Sort.MergeSort.html#4318" class="Function">sort-↗</a> <a id="4358" href="Data.List.Sort.MergeSort.html#4358" class="Bound">xs</a> <a id="4361" class="Symbol">=</a> <a id="4363" href="Data.List.Sort.MergeSort.html#4029" class="Function">mergeAll-↗</a> <a id="4374" class="Symbol">_</a> <a id="4376" class="Symbol">(</a><a id="4377" href="Data.List.Relation.Unary.All.Properties.html#13869" class="Function">All.map⁺</a> <a id="4386" class="Symbol">(</a><a id="4387" href="Data.List.Relation.Unary.All.html#6432" class="Function">All.universal</a> <a id="4401" class="Symbol">(λ</a> <a id="4404" href="Data.List.Sort.MergeSort.html#4404" class="Bound">_</a> <a id="4406" class="Symbol">→</a> <a id="4408" href="Data.List.Relation.Unary.Linked.html#1471" class="InductiveConstructor">[-]</a><a id="4411" class="Symbol">)</a> <a id="4413" href="Data.List.Sort.MergeSort.html#4358" class="Bound">xs</a><a id="4415" class="Symbol">))</a>

<a id="4419" class="Comment">------------------------------------------------------------------------</a>
<a id="4492" class="Comment">-- Algorithm</a>

<a id="mergeSort"></a><a id="4506" href="Data.List.Sort.MergeSort.html#4506" class="Function">mergeSort</a> <a id="4516" class="Symbol">:</a> <a id="4518" href="Data.List.Sort.Base.html#771" class="Record">SortingAlgorithm</a>
<a id="4535" href="Data.List.Sort.MergeSort.html#4506" class="Function">mergeSort</a> <a id="4545" class="Symbol">=</a> <a id="4547" class="Keyword">record</a>
  <a id="4556" class="Symbol">{</a> <a id="4558" href="Data.List.Sort.Base.html#826" class="Field">sort</a>   <a id="4565" class="Symbol">=</a> <a id="4567" href="Data.List.Sort.MergeSort.html#2525" class="Function">sort</a>
  <a id="4574" class="Symbol">;</a> <a id="4576" href="Data.List.Sort.Base.html#914" class="Field">sort-↭</a> <a id="4583" class="Symbol">=</a> <a id="4585" href="Data.List.Sort.MergeSort.html#3529" class="Function">sort-↭</a>
  <a id="4594" class="Symbol">;</a> <a id="4596" href="Data.List.Sort.Base.html#987" class="Field">sort-↗</a> <a id="4603" class="Symbol">=</a> <a id="4605" href="Data.List.Sort.MergeSort.html#4318" class="Function">sort-↗</a>
  <a id="4614" class="Symbol">}</a>
</pre></body></html>
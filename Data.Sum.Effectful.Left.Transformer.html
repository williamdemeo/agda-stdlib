<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Sum.Effectful.Left.Transformer</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- An effectful view of the Sum type (Left-biased)</a>
<a id="157" class="Comment">------------------------------------------------------------------------</a>

<a id="231" class="Symbol">{-#</a> <a id="235" class="Keyword">OPTIONS</a> <a id="243" class="Pragma">--without-K</a> <a id="255" class="Pragma">--safe</a> <a id="262" class="Symbol">#-}</a>

<a id="267" class="Keyword">open</a> <a id="272" class="Keyword">import</a> <a id="279" href="Level.html" class="Module">Level</a>

<a id="286" class="Keyword">module</a> <a id="293" href="Data.Sum.Effectful.Left.Transformer.html" class="Module">Data.Sum.Effectful.Left.Transformer</a> <a id="329" class="Symbol">{</a><a id="330" href="Data.Sum.Effectful.Left.Transformer.html#330" class="Bound">a</a><a id="331" class="Symbol">}</a> <a id="333" class="Symbol">(</a><a id="334" href="Data.Sum.Effectful.Left.Transformer.html#334" class="Bound">A</a> <a id="336" class="Symbol">:</a> <a id="338" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="342" href="Data.Sum.Effectful.Left.Transformer.html#330" class="Bound">a</a><a id="343" class="Symbol">)</a> <a id="345" class="Symbol">(</a><a id="346" href="Data.Sum.Effectful.Left.Transformer.html#346" class="Bound">b</a> <a id="348" class="Symbol">:</a> <a id="350" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="355" class="Symbol">)</a> <a id="357" class="Keyword">where</a>

<a id="364" class="Keyword">open</a> <a id="369" class="Keyword">import</a> <a id="376" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a>
<a id="390" class="Keyword">open</a> <a id="395" class="Keyword">import</a> <a id="402" href="Effect.Choice.html" class="Module">Effect.Choice</a>
<a id="416" class="Keyword">open</a> <a id="421" class="Keyword">import</a> <a id="428" href="Effect.Empty.html" class="Module">Effect.Empty</a>
<a id="441" class="Keyword">open</a> <a id="446" class="Keyword">import</a> <a id="453" href="Effect.Functor.html" class="Module">Effect.Functor</a>
<a id="468" class="Keyword">open</a> <a id="473" class="Keyword">import</a> <a id="480" href="Effect.Applicative.html" class="Module">Effect.Applicative</a>
<a id="499" class="Keyword">open</a> <a id="504" class="Keyword">import</a> <a id="511" href="Effect.Monad.html" class="Module">Effect.Monad</a>
<a id="524" class="Keyword">open</a> <a id="529" class="Keyword">import</a> <a id="536" href="Function.Base.html" class="Module">Function.Base</a>

<a id="551" class="Keyword">private</a>
  <a id="561" class="Keyword">variable</a>
    <a id="574" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a> <a id="576" class="Symbol">:</a> <a id="578" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="582" class="Symbol">(</a><a id="583" href="Data.Sum.Effectful.Left.Transformer.html#330" class="Bound">a</a> <a id="585" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="587" href="Data.Sum.Effectful.Left.Transformer.html#346" class="Bound">b</a><a id="588" class="Symbol">)</a> <a id="590" class="Symbol">→</a> <a id="592" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="596" class="Symbol">(</a><a id="597" href="Data.Sum.Effectful.Left.Transformer.html#330" class="Bound">a</a> <a id="599" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="601" href="Data.Sum.Effectful.Left.Transformer.html#346" class="Bound">b</a><a id="602" class="Symbol">)</a>

<a id="605" class="Comment">-- To minimize the universe level of the RawFunctor, we require that elements of</a>
<a id="686" class="Comment">-- B are &quot;lifted&quot; to a copy of B at a higher universe level (a ⊔ b). See the</a>
<a id="763" class="Comment">-- examples for how this is done.</a>

<a id="798" class="Keyword">open</a> <a id="803" class="Keyword">import</a> <a id="810" href="Data.Sum.Effectful.Left.html" class="Module">Data.Sum.Effectful.Left</a> <a id="834" href="Data.Sum.Effectful.Left.Transformer.html#334" class="Bound">A</a> <a id="836" href="Data.Sum.Effectful.Left.Transformer.html#346" class="Bound">b</a> <a id="838" class="Keyword">using</a> <a id="844" class="Symbol">(</a><a id="845" href="Data.Sum.Effectful.Left.html#844" class="Function">Sumₗ</a><a id="849" class="Symbol">)</a>

<a id="852" class="Comment">------------------------------------------------------------------------</a>
<a id="925" class="Comment">-- Left-biased monad transformer instance for _⊎_</a>

<a id="976" class="Keyword">record</a> <a id="SumₗT"></a><a id="983" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Record">SumₗT</a>
         <a id="998" class="Symbol">(</a><a id="999" href="Data.Sum.Effectful.Left.Transformer.html#999" class="Bound">M</a> <a id="1001" class="Symbol">:</a> <a id="1003" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1007" class="Symbol">(</a><a id="1008" href="Data.Sum.Effectful.Left.Transformer.html#330" class="Bound">a</a> <a id="1010" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1012" href="Data.Sum.Effectful.Left.Transformer.html#346" class="Bound">b</a><a id="1013" class="Symbol">)</a> <a id="1015" class="Symbol">→</a> <a id="1017" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1021" class="Symbol">(</a><a id="1022" href="Data.Sum.Effectful.Left.Transformer.html#330" class="Bound">a</a> <a id="1024" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1026" href="Data.Sum.Effectful.Left.Transformer.html#346" class="Bound">b</a><a id="1027" class="Symbol">))</a>
         <a id="1039" class="Symbol">(</a><a id="1040" href="Data.Sum.Effectful.Left.Transformer.html#1040" class="Bound">B</a> <a id="1042" class="Symbol">:</a> <a id="1044" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1048" class="Symbol">(</a><a id="1049" href="Data.Sum.Effectful.Left.Transformer.html#330" class="Bound">a</a> <a id="1051" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1053" href="Data.Sum.Effectful.Left.Transformer.html#346" class="Bound">b</a><a id="1054" class="Symbol">))</a> <a id="1057" class="Symbol">:</a> <a id="1059" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1063" class="Symbol">(</a><a id="1064" href="Data.Sum.Effectful.Left.Transformer.html#330" class="Bound">a</a> <a id="1066" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1068" href="Data.Sum.Effectful.Left.Transformer.html#346" class="Bound">b</a><a id="1069" class="Symbol">)</a> <a id="1071" class="Keyword">where</a>
  <a id="1079" class="Keyword">constructor</a> <a id="mkSumₗT"></a><a id="1091" href="Data.Sum.Effectful.Left.Transformer.html#1091" class="InductiveConstructor">mkSumₗT</a>
  <a id="1101" class="Keyword">field</a> <a id="SumₗT.runSumₗT"></a><a id="1107" href="Data.Sum.Effectful.Left.Transformer.html#1107" class="Field">runSumₗT</a> <a id="1116" class="Symbol">:</a> <a id="1118" href="Data.Sum.Effectful.Left.Transformer.html#999" class="Bound">M</a> <a id="1120" class="Symbol">(</a><a id="1121" href="Data.Sum.Effectful.Left.html#844" class="Function">Sumₗ</a> <a id="1126" href="Data.Sum.Effectful.Left.Transformer.html#1040" class="Bound">B</a><a id="1127" class="Symbol">)</a>
<a id="1129" class="Keyword">open</a> <a id="1134" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Module">SumₗT</a> <a id="1140" class="Keyword">public</a>

<a id="1148" class="Comment">------------------------------------------------------------------------</a>
<a id="1221" class="Comment">-- Structure</a>

<a id="functor"></a><a id="1235" href="Data.Sum.Effectful.Left.Transformer.html#1235" class="Function">functor</a> <a id="1243" class="Symbol">:</a> <a id="1245" href="Effect.Functor.html#570" class="Record">RawFunctor</a> <a id="1256" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a> <a id="1258" class="Symbol">→</a> <a id="1260" href="Effect.Functor.html#570" class="Record">RawFunctor</a> <a id="1271" class="Symbol">(</a><a id="1272" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Record">SumₗT</a> <a id="1278" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a><a id="1279" class="Symbol">)</a>
<a id="1281" href="Data.Sum.Effectful.Left.Transformer.html#1235" class="Function">functor</a> <a id="1289" href="Data.Sum.Effectful.Left.Transformer.html#1289" class="Bound">M</a> <a id="1291" class="Symbol">=</a> <a id="1293" class="Keyword">record</a>
  <a id="1302" class="Symbol">{</a> <a id="1304" href="Effect.Functor.html#678" class="Field Operator">_&lt;$&gt;_</a> <a id="1310" class="Symbol">=</a> <a id="1312" class="Symbol">λ</a> <a id="1314" href="Data.Sum.Effectful.Left.Transformer.html#1314" class="Bound">f</a> <a id="1316" class="Symbol">→</a> <a id="1318" href="Data.Sum.Effectful.Left.Transformer.html#1091" class="InductiveConstructor">mkSumₗT</a> <a id="1326" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="1329" class="Symbol">(</a><a id="1330" href="Data.Sum.Base.html#1374" class="Function">map₂</a> <a id="1335" href="Data.Sum.Effectful.Left.Transformer.html#1314" class="Bound">f</a> <a id="1337" href="Effect.Functor.html#678" class="Field Operator">&lt;$&gt;_</a><a id="1341" class="Symbol">)</a> <a id="1343" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="1346" href="Data.Sum.Effectful.Left.Transformer.html#1107" class="Field">runSumₗT</a>
  <a id="1357" class="Symbol">}</a> <a id="1359" class="Keyword">where</a> <a id="1365" class="Keyword">open</a> <a id="1370" href="Effect.Functor.html#570" class="Module">RawFunctor</a> <a id="1381" href="Data.Sum.Effectful.Left.Transformer.html#1289" class="Bound">M</a>

<a id="applicative"></a><a id="1384" href="Data.Sum.Effectful.Left.Transformer.html#1384" class="Function">applicative</a> <a id="1396" class="Symbol">:</a> <a id="1398" href="Effect.Applicative.html#970" class="Record">RawApplicative</a> <a id="1413" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a> <a id="1415" class="Symbol">→</a> <a id="1417" href="Effect.Applicative.html#970" class="Record">RawApplicative</a> <a id="1432" class="Symbol">(</a><a id="1433" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Record">SumₗT</a> <a id="1439" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a><a id="1440" class="Symbol">)</a>
<a id="1442" href="Data.Sum.Effectful.Left.Transformer.html#1384" class="Function">applicative</a> <a id="1454" href="Data.Sum.Effectful.Left.Transformer.html#1454" class="Bound">M</a> <a id="1456" class="Symbol">=</a> <a id="1458" class="Keyword">record</a>
  <a id="1467" class="Symbol">{</a> <a id="1469" href="Effect.Applicative.html#1108" class="Field">rawFunctor</a> <a id="1480" class="Symbol">=</a> <a id="1482" href="Data.Sum.Effectful.Left.Transformer.html#1235" class="Function">functor</a> <a id="1490" href="Effect.Applicative.html#1108" class="Field">rawFunctor</a>
  <a id="1503" class="Symbol">;</a> <a id="1505" href="Effect.Applicative.html#1138" class="Field">pure</a> <a id="1510" class="Symbol">=</a> <a id="1512" href="Data.Sum.Effectful.Left.Transformer.html#1091" class="InductiveConstructor">mkSumₗT</a> <a id="1520" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="1523" href="Effect.Applicative.html#1138" class="Field">pure</a> <a id="1528" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="1531" href="Data.Sum.Base.html#691" class="InductiveConstructor">inj₂</a>
  <a id="1538" class="Symbol">;</a> <a id="1540" href="Effect.Applicative.html#1157" class="Field Operator">_&lt;*&gt;_</a> <a id="1546" class="Symbol">=</a> <a id="1548" class="Symbol">λ</a> <a id="1550" href="Data.Sum.Effectful.Left.Transformer.html#1550" class="Bound">mf</a> <a id="1553" href="Data.Sum.Effectful.Left.Transformer.html#1553" class="Bound">mx</a> <a id="1556" class="Symbol">→</a> <a id="1558" href="Data.Sum.Effectful.Left.Transformer.html#1091" class="InductiveConstructor">mkSumₗT</a> <a id="1566" class="Symbol">(</a><a id="1567" href="Data.Sum.Base.html#971" class="Function Operator">[</a> <a id="1569" href="Function.Base.html#716" class="Function">const</a> <a id="1575" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="1578" href="Data.Sum.Base.html#666" class="InductiveConstructor">inj₁</a> <a id="1583" href="Data.Sum.Base.html#971" class="Function Operator">,</a> <a id="1585" href="Data.Sum.Base.html#1374" class="Function">map₂</a> <a id="1590" href="Data.Sum.Base.html#971" class="Function Operator">]′</a> <a id="1593" href="Effect.Functor.html#678" class="Function Operator">&lt;$&gt;</a> <a id="1597" href="Data.Sum.Effectful.Left.Transformer.html#1107" class="Field">runSumₗT</a> <a id="1606" href="Data.Sum.Effectful.Left.Transformer.html#1550" class="Bound">mf</a> <a id="1609" href="Effect.Applicative.html#1157" class="Field Operator">&lt;*&gt;</a> <a id="1613" href="Data.Sum.Effectful.Left.Transformer.html#1107" class="Field">runSumₗT</a> <a id="1622" href="Data.Sum.Effectful.Left.Transformer.html#1553" class="Bound">mx</a><a id="1624" class="Symbol">)</a>
  <a id="1628" class="Symbol">}</a> <a id="1630" class="Keyword">where</a> <a id="1636" class="Keyword">open</a> <a id="1641" href="Effect.Applicative.html#970" class="Module">RawApplicative</a> <a id="1656" href="Data.Sum.Effectful.Left.Transformer.html#1454" class="Bound">M</a>

<a id="empty"></a><a id="1659" href="Data.Sum.Effectful.Left.Transformer.html#1659" class="Function">empty</a> <a id="1665" class="Symbol">:</a> <a id="1667" href="Effect.Applicative.html#970" class="Record">RawApplicative</a> <a id="1682" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a> <a id="1684" class="Symbol">→</a> <a id="1686" href="Data.Sum.Effectful.Left.Transformer.html#334" class="Bound">A</a> <a id="1688" class="Symbol">→</a> <a id="1690" href="Effect.Empty.html#375" class="Record">RawEmpty</a> <a id="1699" class="Symbol">(</a><a id="1700" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Record">SumₗT</a> <a id="1706" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a><a id="1707" class="Symbol">)</a>
<a id="1709" href="Data.Sum.Effectful.Left.Transformer.html#1659" class="Function">empty</a> <a id="1715" href="Data.Sum.Effectful.Left.Transformer.html#1715" class="Bound">M</a> <a id="1717" href="Data.Sum.Effectful.Left.Transformer.html#1717" class="Bound">a</a> <a id="1719" class="Symbol">=</a> <a id="1721" class="Keyword">record</a>
  <a id="1730" class="Symbol">{</a> <a id="1732" href="Effect.Empty.html#442" class="Field">empty</a> <a id="1738" class="Symbol">=</a> <a id="1740" href="Data.Sum.Effectful.Left.Transformer.html#1091" class="InductiveConstructor">mkSumₗT</a> <a id="1748" class="Symbol">(</a><a id="1749" href="Effect.Applicative.html#1138" class="Function">pure</a> <a id="1754" class="Symbol">(</a><a id="1755" href="Data.Sum.Base.html#666" class="InductiveConstructor">inj₁</a> <a id="1760" href="Data.Sum.Effectful.Left.Transformer.html#1717" class="Bound">a</a><a id="1761" class="Symbol">))</a>
  <a id="1766" class="Symbol">}</a> <a id="1768" class="Keyword">where</a> <a id="1774" class="Keyword">open</a> <a id="1779" href="Effect.Applicative.html#970" class="Module">RawApplicative</a> <a id="1794" href="Data.Sum.Effectful.Left.Transformer.html#1715" class="Bound">M</a>

<a id="choice"></a><a id="1797" href="Data.Sum.Effectful.Left.Transformer.html#1797" class="Function">choice</a> <a id="1804" class="Symbol">:</a> <a id="1806" href="Effect.Applicative.html#970" class="Record">RawApplicative</a> <a id="1821" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a> <a id="1823" class="Symbol">→</a> <a id="1825" href="Effect.Choice.html#405" class="Record">RawChoice</a> <a id="1835" class="Symbol">(</a><a id="1836" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Record">SumₗT</a> <a id="1842" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a><a id="1843" class="Symbol">)</a>
<a id="1845" href="Data.Sum.Effectful.Left.Transformer.html#1797" class="Function">choice</a> <a id="1852" href="Data.Sum.Effectful.Left.Transformer.html#1852" class="Bound">M</a> <a id="1854" class="Symbol">=</a> <a id="1856" class="Keyword">record</a>
  <a id="1865" class="Symbol">{</a> <a id="1867" href="Effect.Choice.html#494" class="Field Operator">_&lt;|&gt;_</a> <a id="1873" class="Symbol">=</a> <a id="1875" class="Symbol">λ</a> <a id="1877" href="Data.Sum.Effectful.Left.Transformer.html#1877" class="Bound">ma₁</a> <a id="1881" href="Data.Sum.Effectful.Left.Transformer.html#1881" class="Bound">ma₂</a> <a id="1885" class="Symbol">→</a> <a id="1887" href="Data.Sum.Effectful.Left.Transformer.html#1091" class="InductiveConstructor">mkSumₗT</a> <a id="1895" class="Symbol">(</a><a id="1896" href="Data.Sum.Base.html#971" class="Function Operator">[</a> <a id="1898" href="Function.Base.html#1629" class="Function">flip</a> <a id="1903" href="Function.Base.html#716" class="Function">const</a> <a id="1909" href="Data.Sum.Base.html#971" class="Function Operator">,</a> <a id="1911" href="Function.Base.html#716" class="Function">const</a> <a id="1917" href="Function.Base.html#1106" class="Function Operator">∘</a> <a id="1919" href="Data.Sum.Base.html#691" class="InductiveConstructor">inj₂</a> <a id="1924" href="Data.Sum.Base.html#971" class="Function Operator">]′</a> <a id="1927" href="Effect.Functor.html#678" class="Function Operator">&lt;$&gt;</a> <a id="1931" href="Data.Sum.Effectful.Left.Transformer.html#1107" class="Field">runSumₗT</a> <a id="1940" href="Data.Sum.Effectful.Left.Transformer.html#1877" class="Bound">ma₁</a> <a id="1944" href="Effect.Applicative.html#1157" class="Field Operator">&lt;*&gt;</a> <a id="1948" href="Data.Sum.Effectful.Left.Transformer.html#1107" class="Field">runSumₗT</a> <a id="1957" href="Data.Sum.Effectful.Left.Transformer.html#1881" class="Bound">ma₂</a><a id="1960" class="Symbol">)</a>
  <a id="1964" class="Symbol">}</a> <a id="1966" class="Keyword">where</a> <a id="1972" class="Keyword">open</a> <a id="1977" href="Effect.Applicative.html#970" class="Module">RawApplicative</a> <a id="1992" href="Data.Sum.Effectful.Left.Transformer.html#1852" class="Bound">M</a>

<a id="applicativeZero"></a><a id="1995" href="Data.Sum.Effectful.Left.Transformer.html#1995" class="Function">applicativeZero</a> <a id="2011" class="Symbol">:</a> <a id="2013" href="Effect.Applicative.html#970" class="Record">RawApplicative</a> <a id="2028" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a> <a id="2030" class="Symbol">→</a> <a id="2032" href="Data.Sum.Effectful.Left.Transformer.html#334" class="Bound">A</a> <a id="2034" class="Symbol">→</a> <a id="2036" href="Effect.Applicative.html#2181" class="Record">RawApplicativeZero</a> <a id="2055" class="Symbol">(</a><a id="2056" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Record">SumₗT</a> <a id="2062" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a><a id="2063" class="Symbol">)</a>
<a id="2065" href="Data.Sum.Effectful.Left.Transformer.html#1995" class="Function">applicativeZero</a> <a id="2081" href="Data.Sum.Effectful.Left.Transformer.html#2081" class="Bound">M</a> <a id="2083" href="Data.Sum.Effectful.Left.Transformer.html#2083" class="Bound">a</a> <a id="2085" class="Symbol">=</a> <a id="2087" class="Keyword">record</a>
  <a id="2096" class="Symbol">{</a> <a id="2098" href="Effect.Applicative.html#2256" class="Field">rawApplicative</a> <a id="2113" class="Symbol">=</a> <a id="2115" href="Data.Sum.Effectful.Left.Transformer.html#1384" class="Function">applicative</a> <a id="2127" href="Data.Sum.Effectful.Left.Transformer.html#2081" class="Bound">M</a>
  <a id="2131" class="Symbol">;</a> <a id="2133" href="Effect.Applicative.html#2294" class="Field">rawEmpty</a> <a id="2142" class="Symbol">=</a> <a id="2144" href="Data.Sum.Effectful.Left.Transformer.html#1659" class="Function">empty</a> <a id="2150" href="Data.Sum.Effectful.Left.Transformer.html#2081" class="Bound">M</a> <a id="2152" href="Data.Sum.Effectful.Left.Transformer.html#2083" class="Bound">a</a>
  <a id="2156" class="Symbol">}</a>

<a id="alternative"></a><a id="2159" href="Data.Sum.Effectful.Left.Transformer.html#2159" class="Function">alternative</a> <a id="2171" class="Symbol">:</a> <a id="2173" href="Effect.Applicative.html#970" class="Record">RawApplicative</a> <a id="2188" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a> <a id="2190" class="Symbol">→</a> <a id="2192" href="Data.Sum.Effectful.Left.Transformer.html#334" class="Bound">A</a> <a id="2194" class="Symbol">→</a> <a id="2196" href="Effect.Applicative.html#2585" class="Record">RawAlternative</a> <a id="2211" class="Symbol">(</a><a id="2212" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Record">SumₗT</a> <a id="2218" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a><a id="2219" class="Symbol">)</a>
<a id="2221" href="Data.Sum.Effectful.Left.Transformer.html#2159" class="Function">alternative</a> <a id="2233" href="Data.Sum.Effectful.Left.Transformer.html#2233" class="Bound">M</a> <a id="2235" href="Data.Sum.Effectful.Left.Transformer.html#2235" class="Bound">a</a> <a id="2237" class="Symbol">=</a> <a id="2239" class="Keyword">record</a>
  <a id="2248" class="Symbol">{</a> <a id="2250" href="Effect.Applicative.html#2656" class="Field">rawApplicativeZero</a> <a id="2269" class="Symbol">=</a> <a id="2271" href="Data.Sum.Effectful.Left.Transformer.html#1995" class="Function">applicativeZero</a> <a id="2287" href="Data.Sum.Effectful.Left.Transformer.html#2233" class="Bound">M</a> <a id="2289" href="Data.Sum.Effectful.Left.Transformer.html#2235" class="Bound">a</a>
  <a id="2293" class="Symbol">;</a> <a id="2295" href="Effect.Applicative.html#2702" class="Field">rawChoice</a> <a id="2305" class="Symbol">=</a> <a id="2307" href="Data.Sum.Effectful.Left.Transformer.html#1797" class="Function">choice</a> <a id="2314" href="Data.Sum.Effectful.Left.Transformer.html#2233" class="Bound">M</a>
  <a id="2318" class="Symbol">}</a>

<a id="monad"></a><a id="2321" href="Data.Sum.Effectful.Left.Transformer.html#2321" class="Function">monad</a> <a id="2327" class="Symbol">:</a> <a id="2329" href="Effect.Monad.html#766" class="Record">RawMonad</a> <a id="2338" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a> <a id="2340" class="Symbol">→</a> <a id="2342" href="Effect.Monad.html#766" class="Record">RawMonad</a> <a id="2351" class="Symbol">(</a><a id="2352" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Record">SumₗT</a> <a id="2358" href="Data.Sum.Effectful.Left.Transformer.html#574" class="Generalizable">M</a><a id="2359" class="Symbol">)</a>
<a id="2361" href="Data.Sum.Effectful.Left.Transformer.html#2321" class="Function">monad</a> <a id="2367" href="Data.Sum.Effectful.Left.Transformer.html#2367" class="Bound">M</a> <a id="2369" class="Symbol">=</a> <a id="2371" class="Keyword">record</a>
  <a id="2380" class="Symbol">{</a> <a id="2382" href="Effect.Monad.html#882" class="Field">rawApplicative</a> <a id="2397" class="Symbol">=</a> <a id="2399" href="Data.Sum.Effectful.Left.Transformer.html#1384" class="Function">applicative</a> <a id="2411" href="Effect.Monad.html#882" class="Field">rawApplicative</a>
  <a id="2428" class="Symbol">;</a> <a id="2430" href="Effect.Monad.html#920" class="Field Operator">_&gt;&gt;=_</a> <a id="2436" class="Symbol">=</a> <a id="2438" class="Symbol">λ</a> <a id="2440" href="Data.Sum.Effectful.Left.Transformer.html#2440" class="Bound">ma</a> <a id="2443" href="Data.Sum.Effectful.Left.Transformer.html#2443" class="Bound">f</a> <a id="2445" class="Symbol">→</a> <a id="2447" href="Data.Sum.Effectful.Left.Transformer.html#1091" class="InductiveConstructor">mkSumₗT</a> <a id="2455" href="Function.Base.html#1994" class="Function Operator">$</a> <a id="2457" class="Keyword">do</a>
                       <a id="2483" href="Data.Sum.Effectful.Left.Transformer.html#2483" class="Bound">a</a> <a id="2485" href="Effect.Monad.html#920" class="Field Operator">←</a> <a id="2487" href="Data.Sum.Effectful.Left.Transformer.html#1107" class="Field">runSumₗT</a> <a id="2496" href="Data.Sum.Effectful.Left.Transformer.html#2440" class="Bound">ma</a>
                       <a id="2522" href="Data.Sum.Base.html#971" class="Function Operator">[</a> <a id="2524" href="Effect.Applicative.html#1138" class="Function">pure</a> <a id="2529" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="2532" href="Data.Sum.Base.html#666" class="InductiveConstructor">inj₁</a> <a id="2537" href="Data.Sum.Base.html#971" class="Function Operator">,</a> <a id="2539" href="Data.Sum.Effectful.Left.Transformer.html#1107" class="Field">runSumₗT</a> <a id="2548" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="2551" href="Data.Sum.Effectful.Left.Transformer.html#2443" class="Bound">f</a> <a id="2553" href="Data.Sum.Base.html#971" class="Function Operator">]′</a> <a id="2556" href="Data.Sum.Effectful.Left.Transformer.html#2483" class="Bound">a</a>
  <a id="2560" class="Symbol">}</a> <a id="2562" class="Keyword">where</a> <a id="2568" class="Keyword">open</a> <a id="2573" href="Effect.Monad.html#766" class="Module">RawMonad</a> <a id="2582" href="Data.Sum.Effectful.Left.Transformer.html#2367" class="Bound">M</a>

<a id="monadT"></a><a id="2585" href="Data.Sum.Effectful.Left.Transformer.html#2585" class="Function">monadT</a> <a id="2592" class="Symbol">:</a> <a id="2594" href="Effect.Monad.html#3039" class="Function">RawMonadT</a> <a id="2604" href="Data.Sum.Effectful.Left.Transformer.html#983" class="Record">SumₗT</a>
<a id="2610" href="Data.Sum.Effectful.Left.Transformer.html#2585" class="Function">monadT</a> <a id="2617" href="Data.Sum.Effectful.Left.Transformer.html#2617" class="Bound">M</a> <a id="2619" class="Symbol">=</a> <a id="2621" class="Keyword">record</a>
  <a id="2630" class="Symbol">{</a> <a id="2632" href="Effect.Monad.html#2960" class="Field">lift</a> <a id="2637" class="Symbol">=</a> <a id="2639" href="Data.Sum.Effectful.Left.Transformer.html#1091" class="InductiveConstructor">mkSumₗT</a> <a id="2647" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="2650" class="Symbol">(</a><a id="2651" href="Data.Sum.Base.html#691" class="InductiveConstructor">inj₂</a> <a id="2656" href="Effect.Functor.html#678" class="Function Operator">&lt;$&gt;_</a><a id="2660" class="Symbol">)</a>
  <a id="2664" class="Symbol">;</a> <a id="2666" href="Effect.Monad.html#2982" class="Field">rawMonad</a> <a id="2675" class="Symbol">=</a> <a id="2677" href="Data.Sum.Effectful.Left.Transformer.html#2321" class="Function">monad</a> <a id="2683" href="Data.Sum.Effectful.Left.Transformer.html#2617" class="Bound">M</a>
  <a id="2687" class="Symbol">}</a> <a id="2689" class="Keyword">where</a> <a id="2695" class="Keyword">open</a> <a id="2700" href="Effect.Monad.html#766" class="Module">RawMonad</a> <a id="2709" href="Data.Sum.Effectful.Left.Transformer.html#2617" class="Bound">M</a>
</pre></body></html>
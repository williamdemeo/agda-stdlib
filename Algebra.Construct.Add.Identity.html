<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Construct.Add.Identity</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Definition of algebraic structures we get from freely adding an</a>
<a id="173" class="Comment">-- identity element</a>
<a id="193" class="Comment">------------------------------------------------------------------------</a>

<a id="267" class="Symbol">{-#</a> <a id="271" class="Keyword">OPTIONS</a> <a id="279" class="Pragma">--without-K</a> <a id="291" class="Pragma">--safe</a> <a id="298" class="Symbol">#-}</a>

<a id="303" class="Keyword">module</a> <a id="310" href="Algebra.Construct.Add.Identity.html" class="Module">Algebra.Construct.Add.Identity</a> <a id="341" class="Keyword">where</a>

<a id="348" class="Keyword">open</a> <a id="353" class="Keyword">import</a> <a id="360" href="Algebra.Bundles.html" class="Module">Algebra.Bundles</a>
<a id="376" class="Keyword">open</a> <a id="381" class="Keyword">import</a> <a id="388" href="Algebra.Core.html" class="Module">Algebra.Core</a> <a id="401" class="Keyword">using</a> <a id="407" class="Symbol">(</a><a id="408" href="Algebra.Core.html#518" class="Function">Op₂</a><a id="411" class="Symbol">)</a>
<a id="413" class="Keyword">open</a> <a id="418" class="Keyword">import</a> <a id="425" href="Algebra.Definitions.html" class="Module">Algebra.Definitions</a>
<a id="445" class="Keyword">open</a> <a id="450" class="Keyword">import</a> <a id="457" href="Algebra.Structures.html" class="Module">Algebra.Structures</a>
<a id="476" class="Keyword">open</a> <a id="481" class="Keyword">import</a> <a id="488" href="Relation.Binary.Construct.Add.Point.Equality.html" class="Module">Relation.Binary.Construct.Add.Point.Equality</a> <a id="533" class="Keyword">renaming</a> <a id="542" class="Symbol">(</a><a id="543" href="Relation.Binary.Construct.Add.Point.Equality.html#836" class="Datatype Operator">_≈∙_</a> <a id="548" class="Symbol">to</a> <a id="551" class="Datatype Operator">lift≈</a><a id="556" class="Symbol">)</a>
<a id="558" class="Keyword">open</a> <a id="563" class="Keyword">import</a> <a id="570" href="Data.Product.html" class="Module">Data.Product</a> <a id="583" class="Keyword">using</a> <a id="589" class="Symbol">(</a><a id="590" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,_</a><a id="593" class="Symbol">)</a>
<a id="595" class="Keyword">open</a> <a id="600" class="Keyword">import</a> <a id="607" href="Level.html" class="Module">Level</a> <a id="613" class="Keyword">using</a> <a id="619" class="Symbol">(</a><a id="620" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="625" class="Symbol">;</a> <a id="627" href="Agda.Primitive.html#810" class="Primitive Operator">_⊔_</a><a id="630" class="Symbol">)</a>
<a id="632" class="Keyword">open</a> <a id="637" class="Keyword">import</a> <a id="644" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a>
<a id="665" class="Keyword">open</a> <a id="670" class="Keyword">import</a> <a id="677" href="Relation.Binary.Definitions.html" class="Module">Relation.Binary.Definitions</a>
<a id="705" class="Keyword">open</a> <a id="710" class="Keyword">import</a> <a id="717" href="Relation.Binary.Structures.html" class="Module">Relation.Binary.Structures</a>
<a id="744" class="Keyword">open</a> <a id="749" class="Keyword">import</a> <a id="756" href="Relation.Nullary.Construct.Add.Point.html" class="Module">Relation.Nullary.Construct.Add.Point</a>

<a id="794" class="Keyword">private</a>
  <a id="804" class="Keyword">variable</a>
    <a id="817" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a> <a id="819" href="Algebra.Construct.Add.Identity.html#819" class="Generalizable">ℓ</a> <a id="821" class="Symbol">:</a> <a id="823" href="Agda.Primitive.html#597" class="Postulate">Level</a>
    <a id="833" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">A</a> <a id="835" class="Symbol">:</a> <a id="837" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="841" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a>

<a id="liftOp"></a><a id="844" href="Algebra.Construct.Add.Identity.html#844" class="Function">liftOp</a> <a id="851" class="Symbol">:</a> <a id="853" href="Algebra.Core.html#518" class="Function">Op₂</a> <a id="857" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">A</a> <a id="859" class="Symbol">→</a> <a id="861" href="Algebra.Core.html#518" class="Function">Op₂</a> <a id="865" class="Symbol">(</a><a id="866" href="Agda.Builtin.Maybe.html#136" class="Datatype">Pointed</a> <a id="874" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">A</a><a id="875" class="Symbol">)</a>
<a id="877" href="Algebra.Construct.Add.Identity.html#844" class="Function">liftOp</a> <a id="884" href="Algebra.Construct.Add.Identity.html#884" class="Bound">op</a> <a id="887" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="889" href="Algebra.Construct.Add.Identity.html#889" class="Bound">p</a> <a id="891" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="893" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="895" href="Algebra.Construct.Add.Identity.html#895" class="Bound">q</a> <a id="897" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="899" class="Symbol">=</a> <a id="901" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="903" href="Algebra.Construct.Add.Identity.html#884" class="Bound">op</a> <a id="906" href="Algebra.Construct.Add.Identity.html#889" class="Bound">p</a> <a id="908" href="Algebra.Construct.Add.Identity.html#895" class="Bound">q</a> <a id="910" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a>
<a id="912" href="Algebra.Construct.Add.Identity.html#844" class="Function">liftOp</a> <a id="919" class="Symbol">_</a>  <a id="922" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="924" href="Algebra.Construct.Add.Identity.html#924" class="Bound">p</a> <a id="926" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="928" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="934" class="Symbol">=</a> <a id="936" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="938" href="Algebra.Construct.Add.Identity.html#924" class="Bound">p</a> <a id="940" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a>
<a id="942" href="Algebra.Construct.Add.Identity.html#844" class="Function">liftOp</a> <a id="949" class="Symbol">_</a>  <a id="952" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="958" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="960" href="Algebra.Construct.Add.Identity.html#960" class="Bound">q</a> <a id="962" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="964" class="Symbol">=</a> <a id="966" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="968" href="Algebra.Construct.Add.Identity.html#960" class="Bound">q</a> <a id="970" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a>
<a id="972" href="Algebra.Construct.Add.Identity.html#844" class="Function">liftOp</a> <a id="979" class="Symbol">_</a>  <a id="982" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="988" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="994" class="Symbol">=</a> <a id="996" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>

<a id="999" class="Keyword">module</a> <a id="1006" href="Algebra.Construct.Add.Identity.html#1006" class="Module">_</a> <a id="1008" class="Symbol">{</a><a id="1009" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1013" class="Symbol">:</a> <a id="1015" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="1019" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">A</a> <a id="1021" href="Algebra.Construct.Add.Identity.html#819" class="Generalizable">ℓ</a><a id="1022" class="Symbol">}</a> <a id="1024" class="Symbol">{</a><a id="1025" href="Algebra.Construct.Add.Identity.html#1025" class="Bound">op</a> <a id="1028" class="Symbol">:</a> <a id="1030" href="Algebra.Core.html#518" class="Function">Op₂</a> <a id="1034" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">A</a><a id="1035" class="Symbol">}</a> <a id="1037" class="Symbol">(</a><a id="1038" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a> <a id="1045" class="Symbol">:</a> <a id="1047" href="Relation.Binary.Definitions.html#1404" class="Function">Reflexive</a> <a id="1057" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a><a id="1060" class="Symbol">)</a> <a id="1062" class="Keyword">where</a>
  <a id="1070" class="Keyword">private</a>
    <a id="1082" href="Algebra.Construct.Add.Identity.html#1082" class="Function Operator">_≈∙_</a> <a id="1087" class="Symbol">=</a> <a id="1089" href="Algebra.Construct.Add.Identity.html#551" class="Datatype">lift≈</a> <a id="1095" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a>
    <a id="1103" href="Algebra.Construct.Add.Identity.html#1103" class="Function">op∙</a> <a id="1107" class="Symbol">=</a> <a id="1109" href="Algebra.Construct.Add.Identity.html#844" class="Function">liftOp</a> <a id="1116" href="Algebra.Construct.Add.Identity.html#1025" class="Bound">op</a>

    <a id="1124" href="Algebra.Construct.Add.Identity.html#1124" class="Function">lift-≈</a> <a id="1131" class="Symbol">:</a> <a id="1133" class="Symbol">∀</a> <a id="1135" class="Symbol">{</a><a id="1136" href="Algebra.Construct.Add.Identity.html#1136" class="Bound">x</a> <a id="1138" href="Algebra.Construct.Add.Identity.html#1138" class="Bound">y</a> <a id="1140" class="Symbol">:</a> <a id="1142" href="Algebra.Construct.Add.Identity.html#1019" class="Bound">A</a><a id="1143" class="Symbol">}</a> <a id="1145" class="Symbol">→</a> <a id="1147" href="Algebra.Construct.Add.Identity.html#1136" class="Bound">x</a> <a id="1149" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">≈</a> <a id="1151" href="Algebra.Construct.Add.Identity.html#1138" class="Bound">y</a> <a id="1153" class="Symbol">→</a> <a id="1155" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1157" href="Algebra.Construct.Add.Identity.html#1136" class="Bound">x</a> <a id="1159" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1161" href="Algebra.Construct.Add.Identity.html#1082" class="Function Operator">≈∙</a> <a id="1164" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1166" href="Algebra.Construct.Add.Identity.html#1138" class="Bound">y</a> <a id="1168" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a>
    <a id="1174" href="Algebra.Construct.Add.Identity.html#1124" class="Function">lift-≈</a> <a id="1181" class="Symbol">=</a> <a id="1183" href="Relation.Binary.Construct.Add.Point.Equality.html#914" class="InductiveConstructor Operator">[_]</a>

  <a id="1190" href="Algebra.Construct.Add.Identity.html#1190" class="Function">cong₂</a> <a id="1196" class="Symbol">:</a> <a id="1198" href="Algebra.Definitions.html#1187" class="Function">Congruent₂</a> <a id="1209" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1213" href="Algebra.Construct.Add.Identity.html#1025" class="Bound">op</a> <a id="1216" class="Symbol">→</a> <a id="1218" href="Algebra.Definitions.html#1187" class="Function">Congruent₂</a> <a id="1229" href="Algebra.Construct.Add.Identity.html#1082" class="Function Operator">_≈∙_</a> <a id="1234" class="Symbol">(</a><a id="1235" href="Algebra.Construct.Add.Identity.html#1103" class="Function">op∙</a><a id="1238" class="Symbol">)</a>
  <a id="1242" href="Algebra.Construct.Add.Identity.html#1190" class="Function">cong₂</a> <a id="1248" href="Algebra.Construct.Add.Identity.html#1248" class="Bound">R-cong</a> <a id="1255" href="Relation.Binary.Construct.Add.Point.Equality.html#914" class="InductiveConstructor Operator">[</a> <a id="1257" href="Algebra.Construct.Add.Identity.html#1257" class="Bound">eq-l</a> <a id="1262" href="Relation.Binary.Construct.Add.Point.Equality.html#914" class="InductiveConstructor Operator">]</a> <a id="1264" href="Relation.Binary.Construct.Add.Point.Equality.html#914" class="InductiveConstructor Operator">[</a> <a id="1266" href="Algebra.Construct.Add.Identity.html#1266" class="Bound">eq-r</a> <a id="1271" href="Relation.Binary.Construct.Add.Point.Equality.html#914" class="InductiveConstructor Operator">]</a> <a id="1273" class="Symbol">=</a> <a id="1275" href="Algebra.Construct.Add.Identity.html#1124" class="Function">lift-≈</a> <a id="1282" class="Symbol">(</a><a id="1283" href="Algebra.Construct.Add.Identity.html#1248" class="Bound">R-cong</a> <a id="1290" href="Algebra.Construct.Add.Identity.html#1257" class="Bound">eq-l</a> <a id="1295" href="Algebra.Construct.Add.Identity.html#1266" class="Bound">eq-r</a><a id="1299" class="Symbol">)</a>
  <a id="1303" href="Algebra.Construct.Add.Identity.html#1190" class="Function">cong₂</a> <a id="1309" href="Algebra.Construct.Add.Identity.html#1309" class="Bound">R-cong</a> <a id="1316" href="Relation.Binary.Construct.Add.Point.Equality.html#914" class="InductiveConstructor Operator">[</a> <a id="1318" href="Algebra.Construct.Add.Identity.html#1318" class="Bound">eq</a>   <a id="1323" href="Relation.Binary.Construct.Add.Point.Equality.html#914" class="InductiveConstructor Operator">]</a> <a id="1325" href="Relation.Binary.Construct.Add.Point.Equality.html#875" class="InductiveConstructor">∙≈∙</a>      <a id="1334" class="Symbol">=</a> <a id="1336" href="Algebra.Construct.Add.Identity.html#1124" class="Function">lift-≈</a> <a id="1343" href="Algebra.Construct.Add.Identity.html#1318" class="Bound">eq</a>
  <a id="1348" href="Algebra.Construct.Add.Identity.html#1190" class="Function">cong₂</a> <a id="1354" href="Algebra.Construct.Add.Identity.html#1354" class="Bound">R-cong</a> <a id="1361" href="Relation.Binary.Construct.Add.Point.Equality.html#875" class="InductiveConstructor">∙≈∙</a>      <a id="1370" href="Relation.Binary.Construct.Add.Point.Equality.html#914" class="InductiveConstructor Operator">[</a> <a id="1372" href="Algebra.Construct.Add.Identity.html#1372" class="Bound">eq</a>   <a id="1377" href="Relation.Binary.Construct.Add.Point.Equality.html#914" class="InductiveConstructor Operator">]</a> <a id="1379" class="Symbol">=</a> <a id="1381" href="Algebra.Construct.Add.Identity.html#1124" class="Function">lift-≈</a> <a id="1388" href="Algebra.Construct.Add.Identity.html#1372" class="Bound">eq</a>
  <a id="1393" href="Algebra.Construct.Add.Identity.html#1190" class="Function">cong₂</a> <a id="1399" href="Algebra.Construct.Add.Identity.html#1399" class="Bound">R-cong</a> <a id="1406" href="Relation.Binary.Construct.Add.Point.Equality.html#875" class="InductiveConstructor">∙≈∙</a>      <a id="1415" href="Relation.Binary.Construct.Add.Point.Equality.html#875" class="InductiveConstructor">∙≈∙</a>      <a id="1424" class="Symbol">=</a> <a id="1426" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="1434" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1438" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>

  <a id="1448" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="1454" class="Symbol">:</a> <a id="1456" href="Algebra.Definitions.html#1433" class="Function">Associative</a> <a id="1468" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1472" href="Algebra.Construct.Add.Identity.html#1025" class="Bound">op</a> <a id="1475" class="Symbol">→</a> <a id="1477" href="Algebra.Definitions.html#1433" class="Function">Associative</a> <a id="1489" href="Algebra.Construct.Add.Identity.html#1082" class="Function Operator">_≈∙_</a> <a id="1494" class="Symbol">(</a><a id="1495" href="Algebra.Construct.Add.Identity.html#1103" class="Function">op∙</a><a id="1498" class="Symbol">)</a>
  <a id="1502" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="1508" href="Algebra.Construct.Add.Identity.html#1508" class="Bound">assoc</a> <a id="1514" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1516" href="Algebra.Construct.Add.Identity.html#1516" class="Bound">p</a> <a id="1518" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1520" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1522" href="Algebra.Construct.Add.Identity.html#1522" class="Bound">q</a> <a id="1524" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1526" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1528" href="Algebra.Construct.Add.Identity.html#1528" class="Bound">r</a> <a id="1530" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1532" class="Symbol">=</a> <a id="1534" href="Algebra.Construct.Add.Identity.html#1124" class="Function">lift-≈</a> <a id="1541" class="Symbol">(</a><a id="1542" href="Algebra.Construct.Add.Identity.html#1508" class="Bound">assoc</a> <a id="1548" href="Algebra.Construct.Add.Identity.html#1516" class="Bound">p</a> <a id="1550" href="Algebra.Construct.Add.Identity.html#1522" class="Bound">q</a> <a id="1552" href="Algebra.Construct.Add.Identity.html#1528" class="Bound">r</a><a id="1553" class="Symbol">)</a>
  <a id="1557" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="1563" class="Symbol">_</a>     <a id="1569" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1571" href="Algebra.Construct.Add.Identity.html#1571" class="Bound">p</a> <a id="1573" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1575" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1577" href="Algebra.Construct.Add.Identity.html#1577" class="Bound">q</a> <a id="1579" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1581" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1587" class="Symbol">=</a> <a id="1589" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="1597" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1601" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>
  <a id="1610" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="1616" class="Symbol">_</a>     <a id="1622" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1624" href="Algebra.Construct.Add.Identity.html#1624" class="Bound">p</a> <a id="1626" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1628" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1634" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1636" href="Algebra.Construct.Add.Identity.html#1636" class="Bound">r</a> <a id="1638" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1640" class="Symbol">=</a> <a id="1642" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="1650" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1654" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>
  <a id="1663" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="1669" class="Symbol">_</a>     <a id="1675" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1677" href="Algebra.Construct.Add.Identity.html#1677" class="Bound">p</a> <a id="1679" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1681" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1687" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1693" class="Symbol">=</a> <a id="1695" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="1703" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1707" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>
  <a id="1716" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="1722" class="Symbol">_</a>     <a id="1728" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1734" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1736" href="Algebra.Construct.Add.Identity.html#1736" class="Bound">r</a> <a id="1738" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1740" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1742" href="Algebra.Construct.Add.Identity.html#1742" class="Bound">q</a> <a id="1744" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1746" class="Symbol">=</a> <a id="1748" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="1756" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1760" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>
  <a id="1769" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="1775" class="Symbol">_</a>     <a id="1781" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1787" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1789" href="Algebra.Construct.Add.Identity.html#1789" class="Bound">q</a> <a id="1791" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1793" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1799" class="Symbol">=</a> <a id="1801" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="1809" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1813" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>
  <a id="1822" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="1828" class="Symbol">_</a>     <a id="1834" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1840" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1846" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1848" href="Algebra.Construct.Add.Identity.html#1848" class="Bound">r</a> <a id="1850" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1852" class="Symbol">=</a> <a id="1854" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="1862" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1866" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>
  <a id="1875" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="1881" class="Symbol">_</a>     <a id="1887" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1893" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1899" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="1905" class="Symbol">=</a> <a id="1907" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="1915" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1919" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>

  <a id="1929" href="Algebra.Construct.Add.Identity.html#1929" class="Function">identityˡ</a> <a id="1939" class="Symbol">:</a> <a id="1941" href="Algebra.Definitions.html#1593" class="Function">LeftIdentity</a> <a id="1954" href="Algebra.Construct.Add.Identity.html#1082" class="Function Operator">_≈∙_</a> <a id="1959" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a> <a id="1961" class="Symbol">(</a><a id="1962" href="Algebra.Construct.Add.Identity.html#1103" class="Function">op∙</a><a id="1965" class="Symbol">)</a>
  <a id="1969" href="Algebra.Construct.Add.Identity.html#1929" class="Function">identityˡ</a> <a id="1979" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="1981" href="Algebra.Construct.Add.Identity.html#1981" class="Bound">p</a> <a id="1983" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="1985" class="Symbol">=</a> <a id="1987" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="1995" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="1999" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>
  <a id="2008" href="Algebra.Construct.Add.Identity.html#1929" class="Function">identityˡ</a> <a id="2018" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="2024" class="Symbol">=</a> <a id="2026" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="2034" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="2038" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>

  <a id="2048" href="Algebra.Construct.Add.Identity.html#2048" class="Function">identityʳ</a> <a id="2058" class="Symbol">:</a> <a id="2060" href="Algebra.Definitions.html#1666" class="Function">RightIdentity</a> <a id="2074" href="Algebra.Construct.Add.Identity.html#1082" class="Function Operator">_≈∙_</a> <a id="2079" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a> <a id="2081" class="Symbol">(</a><a id="2082" href="Algebra.Construct.Add.Identity.html#1103" class="Function">op∙</a><a id="2085" class="Symbol">)</a>
  <a id="2089" href="Algebra.Construct.Add.Identity.html#2048" class="Function">identityʳ</a> <a id="2099" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">[</a> <a id="2101" href="Algebra.Construct.Add.Identity.html#2101" class="Bound">p</a> <a id="2103" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor Operator">]</a> <a id="2105" class="Symbol">=</a> <a id="2107" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="2115" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="2119" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>
  <a id="2128" href="Algebra.Construct.Add.Identity.html#2048" class="Function">identityʳ</a> <a id="2138" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>     <a id="2144" class="Symbol">=</a> <a id="2146" href="Relation.Binary.Construct.Add.Point.Equality.html#1133" class="Function">≈∙-refl</a> <a id="2154" href="Algebra.Construct.Add.Identity.html#1009" class="Bound Operator">_≈_</a> <a id="2158" href="Algebra.Construct.Add.Identity.html#1038" class="Bound">refl-≈</a>

  <a id="2168" href="Algebra.Construct.Add.Identity.html#2168" class="Function">identity</a> <a id="2177" class="Symbol">:</a> <a id="2179" href="Algebra.Definitions.html#1741" class="Function">Identity</a> <a id="2188" href="Algebra.Construct.Add.Identity.html#1082" class="Function Operator">_≈∙_</a> <a id="2193" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a> <a id="2195" class="Symbol">(</a><a id="2196" href="Algebra.Construct.Add.Identity.html#1103" class="Function">op∙</a><a id="2199" class="Symbol">)</a>
  <a id="2203" href="Algebra.Construct.Add.Identity.html#2168" class="Function">identity</a> <a id="2212" class="Symbol">=</a> <a id="2214" href="Algebra.Construct.Add.Identity.html#1929" class="Function">identityˡ</a> <a id="2224" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2226" href="Algebra.Construct.Add.Identity.html#2048" class="Function">identityʳ</a>

<a id="2237" class="Keyword">module</a> <a id="2244" href="Algebra.Construct.Add.Identity.html#2244" class="Module">_</a> <a id="2246" class="Symbol">{</a><a id="2247" href="Algebra.Construct.Add.Identity.html#2247" class="Bound Operator">_≈_</a> <a id="2251" class="Symbol">:</a> <a id="2253" href="Relation.Binary.Core.html#882" class="Function">Rel</a> <a id="2257" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">A</a> <a id="2259" href="Algebra.Construct.Add.Identity.html#819" class="Generalizable">ℓ</a><a id="2260" class="Symbol">}</a> <a id="2262" class="Symbol">{</a><a id="2263" href="Algebra.Construct.Add.Identity.html#2263" class="Bound">op</a> <a id="2266" class="Symbol">:</a> <a id="2268" href="Algebra.Core.html#518" class="Function">Op₂</a> <a id="2272" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">A</a><a id="2273" class="Symbol">}</a> <a id="2275" class="Keyword">where</a>
  <a id="2283" class="Keyword">private</a>
    <a id="2295" href="Algebra.Construct.Add.Identity.html#2295" class="Function Operator">_≈∙_</a> <a id="2300" class="Symbol">=</a> <a id="2302" href="Algebra.Construct.Add.Identity.html#551" class="Datatype">lift≈</a> <a id="2308" href="Algebra.Construct.Add.Identity.html#2247" class="Bound Operator">_≈_</a>
    <a id="2316" href="Algebra.Construct.Add.Identity.html#2316" class="Function">op∙</a> <a id="2320" class="Symbol">=</a> <a id="2322" href="Algebra.Construct.Add.Identity.html#844" class="Function">liftOp</a> <a id="2329" href="Algebra.Construct.Add.Identity.html#2263" class="Bound">op</a>

  <a id="2335" href="Algebra.Construct.Add.Identity.html#2335" class="Function">isMagma</a> <a id="2343" class="Symbol">:</a> <a id="2345" href="Algebra.Structures.html#1117" class="Record">IsMagma</a> <a id="2353" href="Algebra.Construct.Add.Identity.html#2247" class="Bound Operator">_≈_</a> <a id="2357" href="Algebra.Construct.Add.Identity.html#2263" class="Bound">op</a> <a id="2360" class="Symbol">→</a> <a id="2362" href="Algebra.Structures.html#1117" class="Record">IsMagma</a> <a id="2370" href="Algebra.Construct.Add.Identity.html#2295" class="Function Operator">_≈∙_</a> <a id="2375" href="Algebra.Construct.Add.Identity.html#2316" class="Function">op∙</a>
  <a id="2381" href="Algebra.Construct.Add.Identity.html#2335" class="Function">isMagma</a> <a id="2389" href="Algebra.Construct.Add.Identity.html#2389" class="Bound">M</a> <a id="2391" class="Symbol">=</a>
    <a id="2397" class="Keyword">record</a>
    <a id="2408" class="Symbol">{</a> <a id="2410" href="Algebra.Structures.html#1169" class="Field">isEquivalence</a> <a id="2424" class="Symbol">=</a> <a id="2426" href="Relation.Binary.Construct.Add.Point.Equality.html#2082" class="Function">≈∙-isEquivalence</a> <a id="2443" href="Algebra.Construct.Add.Identity.html#2247" class="Bound Operator">_≈_</a> <a id="2447" href="Algebra.Structures.html#1169" class="Field">M.isEquivalence</a>
    <a id="2467" class="Symbol">;</a> <a id="2469" href="Algebra.Structures.html#1207" class="Field">∙-cong</a>        <a id="2483" class="Symbol">=</a> <a id="2485" href="Algebra.Construct.Add.Identity.html#1190" class="Function">cong₂</a> <a id="2491" href="Relation.Binary.Structures.html#1582" class="Function">M.refl</a> <a id="2498" href="Algebra.Structures.html#1207" class="Field">M.∙-cong</a>
    <a id="2511" class="Symbol">}</a> <a id="2513" class="Keyword">where</a> <a id="2519" class="Keyword">module</a> <a id="2526" href="Algebra.Construct.Add.Identity.html#2526" class="Module">M</a> <a id="2528" class="Symbol">=</a> <a id="2530" href="Algebra.Structures.html#1117" class="Module">IsMagma</a> <a id="2538" href="Algebra.Construct.Add.Identity.html#2389" class="Bound">M</a>

  <a id="2543" href="Algebra.Construct.Add.Identity.html#2543" class="Function">isSemigroup</a> <a id="2555" class="Symbol">:</a> <a id="2557" href="Algebra.Structures.html#2789" class="Record">IsSemigroup</a> <a id="2569" href="Algebra.Construct.Add.Identity.html#2247" class="Bound Operator">_≈_</a> <a id="2573" href="Algebra.Construct.Add.Identity.html#2263" class="Bound">op</a> <a id="2576" class="Symbol">→</a> <a id="2578" href="Algebra.Structures.html#2789" class="Record">IsSemigroup</a> <a id="2590" href="Algebra.Construct.Add.Identity.html#2295" class="Function Operator">_≈∙_</a> <a id="2595" href="Algebra.Construct.Add.Identity.html#2316" class="Function">op∙</a>
  <a id="2601" href="Algebra.Construct.Add.Identity.html#2543" class="Function">isSemigroup</a> <a id="2613" href="Algebra.Construct.Add.Identity.html#2613" class="Bound">S</a> <a id="2615" class="Symbol">=</a> <a id="2617" class="Keyword">record</a>
    <a id="2628" class="Symbol">{</a> <a id="2630" href="Algebra.Structures.html#2845" class="Field">isMagma</a> <a id="2638" class="Symbol">=</a> <a id="2640" href="Algebra.Construct.Add.Identity.html#2335" class="Function">isMagma</a> <a id="2648" href="Algebra.Structures.html#2845" class="Field">S.isMagma</a>
    <a id="2662" class="Symbol">;</a> <a id="2664" href="Algebra.Structures.html#2869" class="Field">assoc</a>   <a id="2672" class="Symbol">=</a> <a id="2674" href="Algebra.Construct.Add.Identity.html#1448" class="Function">assoc</a> <a id="2680" href="Relation.Binary.Structures.html#1582" class="Function">S.refl</a> <a id="2687" href="Algebra.Structures.html#2869" class="Field">S.assoc</a>
    <a id="2699" class="Symbol">}</a> <a id="2701" class="Keyword">where</a> <a id="2707" class="Keyword">module</a> <a id="2714" href="Algebra.Construct.Add.Identity.html#2714" class="Module">S</a> <a id="2716" class="Symbol">=</a> <a id="2718" href="Algebra.Structures.html#2789" class="Module">IsSemigroup</a> <a id="2730" href="Algebra.Construct.Add.Identity.html#2613" class="Bound">S</a>

  <a id="2735" href="Algebra.Construct.Add.Identity.html#2735" class="Function">isMonoid</a> <a id="2744" class="Symbol">:</a> <a id="2746" href="Algebra.Structures.html#2789" class="Record">IsSemigroup</a> <a id="2758" href="Algebra.Construct.Add.Identity.html#2247" class="Bound Operator">_≈_</a> <a id="2762" href="Algebra.Construct.Add.Identity.html#2263" class="Bound">op</a> <a id="2765" class="Symbol">→</a> <a id="2767" href="Algebra.Structures.html#3866" class="Record">IsMonoid</a> <a id="2776" href="Algebra.Construct.Add.Identity.html#2295" class="Function Operator">_≈∙_</a> <a id="2781" href="Algebra.Construct.Add.Identity.html#2316" class="Function">op∙</a> <a id="2785" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">∙</a>
  <a id="2789" href="Algebra.Construct.Add.Identity.html#2735" class="Function">isMonoid</a> <a id="2798" href="Algebra.Construct.Add.Identity.html#2798" class="Bound">S</a> <a id="2800" class="Symbol">=</a> <a id="2802" class="Keyword">record</a>
    <a id="2813" class="Symbol">{</a> <a id="2815" href="Algebra.Structures.html#3927" class="Field">isSemigroup</a> <a id="2827" class="Symbol">=</a> <a id="2829" href="Algebra.Construct.Add.Identity.html#2543" class="Function">isSemigroup</a> <a id="2841" href="Algebra.Construct.Add.Identity.html#2798" class="Bound">S</a>
    <a id="2847" class="Symbol">;</a> <a id="2849" href="Algebra.Structures.html#3959" class="Field">identity</a>    <a id="2861" class="Symbol">=</a> <a id="2863" href="Algebra.Construct.Add.Identity.html#2168" class="Function">identity</a> <a id="2872" href="Relation.Binary.Structures.html#1582" class="Function">S.refl</a>
    <a id="2883" class="Symbol">}</a> <a id="2885" class="Keyword">where</a> <a id="2891" class="Keyword">module</a> <a id="2898" href="Algebra.Construct.Add.Identity.html#2898" class="Module">S</a> <a id="2900" class="Symbol">=</a> <a id="2902" href="Algebra.Structures.html#2789" class="Module">IsSemigroup</a> <a id="2914" href="Algebra.Construct.Add.Identity.html#2798" class="Bound">S</a>

<a id="semigroup"></a><a id="2917" href="Algebra.Construct.Add.Identity.html#2917" class="Function">semigroup</a> <a id="2927" class="Symbol">:</a> <a id="2929" href="Algebra.Bundles.html#4095" class="Record">Semigroup</a> <a id="2939" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a> <a id="2941" class="Symbol">(</a><a id="2942" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a> <a id="2944" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="2946" href="Algebra.Construct.Add.Identity.html#819" class="Generalizable">ℓ</a><a id="2947" class="Symbol">)</a> <a id="2949" class="Symbol">→</a> <a id="2951" href="Algebra.Bundles.html#4095" class="Record">Semigroup</a> <a id="2961" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a> <a id="2963" class="Symbol">(</a><a id="2964" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a> <a id="2966" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="2968" href="Algebra.Construct.Add.Identity.html#819" class="Generalizable">ℓ</a><a id="2969" class="Symbol">)</a>
<a id="2971" href="Algebra.Construct.Add.Identity.html#2917" class="Function">semigroup</a> <a id="2981" href="Algebra.Construct.Add.Identity.html#2981" class="Bound">S</a> <a id="2983" class="Symbol">=</a> <a id="2985" class="Keyword">record</a>
  <a id="2994" class="Symbol">{</a> <a id="2996" href="Algebra.Bundles.html#4177" class="Field">Carrier</a> <a id="3004" class="Symbol">=</a> <a id="3006" href="Agda.Builtin.Maybe.html#136" class="Datatype">Pointed</a> <a id="3014" href="Algebra.Bundles.html#4177" class="Field">S.Carrier</a>
  <a id="3026" class="Symbol">;</a> <a id="3028" href="Algebra.Bundles.html#4263" class="Field">isSemigroup</a> <a id="3040" class="Symbol">=</a> <a id="3042" href="Algebra.Construct.Add.Identity.html#2543" class="Function">isSemigroup</a> <a id="3054" href="Algebra.Bundles.html#4263" class="Field">S.isSemigroup</a>
  <a id="3070" class="Symbol">}</a> <a id="3072" class="Keyword">where</a> <a id="3078" class="Keyword">module</a> <a id="3085" href="Algebra.Construct.Add.Identity.html#3085" class="Module">S</a> <a id="3087" class="Symbol">=</a> <a id="3089" href="Algebra.Bundles.html#4095" class="Module">Semigroup</a> <a id="3099" href="Algebra.Construct.Add.Identity.html#2981" class="Bound">S</a>

<a id="monoid"></a><a id="3102" href="Algebra.Construct.Add.Identity.html#3102" class="Function">monoid</a> <a id="3109" class="Symbol">:</a> <a id="3111" href="Algebra.Bundles.html#4095" class="Record">Semigroup</a> <a id="3121" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a> <a id="3123" class="Symbol">(</a><a id="3124" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a> <a id="3126" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="3128" href="Algebra.Construct.Add.Identity.html#819" class="Generalizable">ℓ</a><a id="3129" class="Symbol">)</a> <a id="3131" class="Symbol">→</a> <a id="3133" href="Algebra.Bundles.html#6007" class="Record">Monoid</a> <a id="3140" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a> <a id="3142" class="Symbol">(</a><a id="3143" href="Algebra.Construct.Add.Identity.html#817" class="Generalizable">a</a> <a id="3145" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="3147" href="Algebra.Construct.Add.Identity.html#819" class="Generalizable">ℓ</a><a id="3148" class="Symbol">)</a>
<a id="3150" href="Algebra.Construct.Add.Identity.html#3102" class="Function">monoid</a> <a id="3157" href="Algebra.Construct.Add.Identity.html#3157" class="Bound">S</a> <a id="3159" class="Symbol">=</a> <a id="3161" class="Keyword">record</a>
  <a id="3170" class="Symbol">{</a> <a id="3172" href="Algebra.Bundles.html#6186" class="Field">isMonoid</a> <a id="3181" class="Symbol">=</a> <a id="3183" href="Algebra.Construct.Add.Identity.html#2735" class="Function">isMonoid</a> <a id="3192" href="Algebra.Bundles.html#4263" class="Field">S.isSemigroup</a>
  <a id="3208" class="Symbol">}</a> <a id="3210" class="Keyword">where</a> <a id="3216" class="Keyword">module</a> <a id="3223" href="Algebra.Construct.Add.Identity.html#3223" class="Module">S</a> <a id="3225" class="Symbol">=</a> <a id="3227" href="Algebra.Bundles.html#4095" class="Module">Semigroup</a> <a id="3237" href="Algebra.Construct.Add.Identity.html#3157" class="Bound">S</a>
</pre></body></html>
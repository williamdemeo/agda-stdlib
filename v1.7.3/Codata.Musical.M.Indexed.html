<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Musical.M.Indexed</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Indexed M-types (the dual of indexed W-types aka Petersson-Synek</a>
<a id="174" class="Comment">-- trees).</a>
<a id="185" class="Comment">------------------------------------------------------------------------</a>

<a id="259" class="Symbol">{-#</a> <a id="263" class="Keyword">OPTIONS</a> <a id="271" class="Pragma">--cubical-compatible</a> <a id="292" class="Pragma">--safe</a> <a id="299" class="Pragma">--guardedness</a> <a id="313" class="Symbol">#-}</a>

<a id="318" class="Keyword">module</a> <a id="325" href="Codata.Musical.M.Indexed.html" class="Module">Codata.Musical.M.Indexed</a> <a id="350" class="Keyword">where</a>

<a id="357" class="Keyword">open</a> <a id="362" class="Keyword">import</a> <a id="369" href="Level.html" class="Module">Level</a>
<a id="375" class="Keyword">open</a> <a id="380" class="Keyword">import</a> <a id="387" href="Codata.Musical.Notation.html" class="Module">Codata.Musical.Notation</a>
<a id="411" class="Keyword">open</a> <a id="416" class="Keyword">import</a> <a id="423" href="Data.Product.html" class="Module">Data.Product</a>
<a id="436" class="Keyword">open</a> <a id="441" class="Keyword">import</a> <a id="448" href="Data.Container.Indexed.Core.html" class="Module">Data.Container.Indexed.Core</a>
<a id="476" class="Keyword">open</a> <a id="481" class="Keyword">import</a> <a id="488" href="Function.html" class="Module">Function</a>
<a id="497" class="Keyword">open</a> <a id="502" class="Keyword">import</a> <a id="509" href="Relation.Unary.html" class="Module">Relation.Unary</a>

<a id="525" class="Comment">-- The family of indexed M-types.</a>

<a id="560" class="Keyword">module</a> <a id="567" href="Codata.Musical.M.Indexed.html#567" class="Module">_</a> <a id="569" class="Symbol">{</a><a id="570" href="Codata.Musical.M.Indexed.html#570" class="Bound">ℓ</a> <a id="572" href="Codata.Musical.M.Indexed.html#572" class="Bound">c</a> <a id="574" href="Codata.Musical.M.Indexed.html#574" class="Bound">r</a><a id="575" class="Symbol">}</a> <a id="577" class="Symbol">{</a><a id="578" href="Codata.Musical.M.Indexed.html#578" class="Bound">O</a> <a id="580" class="Symbol">:</a> <a id="582" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="586" href="Codata.Musical.M.Indexed.html#570" class="Bound">ℓ</a><a id="587" class="Symbol">}</a> <a id="589" class="Symbol">(</a><a id="590" href="Codata.Musical.M.Indexed.html#590" class="Bound">C</a> <a id="592" class="Symbol">:</a> <a id="594" href="Data.Container.Indexed.Core.html#387" class="Record">Container</a> <a id="604" href="Codata.Musical.M.Indexed.html#578" class="Bound">O</a> <a id="606" href="Codata.Musical.M.Indexed.html#578" class="Bound">O</a> <a id="608" href="Codata.Musical.M.Indexed.html#572" class="Bound">c</a> <a id="610" href="Codata.Musical.M.Indexed.html#574" class="Bound">r</a><a id="611" class="Symbol">)</a> <a id="613" class="Keyword">where</a>

  <a id="622" class="Keyword">data</a> <a id="627" href="Codata.Musical.M.Indexed.html#627" class="Datatype">M</a> <a id="629" class="Symbol">(</a><a id="630" href="Codata.Musical.M.Indexed.html#630" class="Bound">o</a> <a id="632" class="Symbol">:</a> <a id="634" href="Codata.Musical.M.Indexed.html#578" class="Bound">O</a><a id="635" class="Symbol">)</a> <a id="637" class="Symbol">:</a> <a id="639" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="643" class="Symbol">(</a><a id="644" href="Codata.Musical.M.Indexed.html#570" class="Bound">ℓ</a> <a id="646" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="648" href="Codata.Musical.M.Indexed.html#572" class="Bound">c</a> <a id="650" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="652" href="Codata.Musical.M.Indexed.html#574" class="Bound">r</a><a id="653" class="Symbol">)</a> <a id="655" class="Keyword">where</a>
    <a id="665" href="Codata.Musical.M.Indexed.html#665" class="InductiveConstructor">inf</a> <a id="669" class="Symbol">:</a> <a id="671" href="Data.Container.Indexed.Core.html#706" class="Function Operator">⟦</a> <a id="673" href="Codata.Musical.M.Indexed.html#590" class="Bound">C</a> <a id="675" href="Data.Container.Indexed.Core.html#706" class="Function Operator">⟧</a> <a id="677" class="Symbol">(</a><a id="678" href="Agda.Builtin.Coinduction.html#196" class="Record">∞</a> <a id="680" href="Function.Base.html#1040" class="Function Operator">∘</a> <a id="682" href="Codata.Musical.M.Indexed.html#627" class="Datatype">M</a><a id="683" class="Symbol">)</a> <a id="685" href="Codata.Musical.M.Indexed.html#630" class="Bound">o</a> <a id="687" class="Symbol">→</a> <a id="689" href="Codata.Musical.M.Indexed.html#627" class="Datatype">M</a> <a id="691" href="Codata.Musical.M.Indexed.html#630" class="Bound">o</a>

  <a id="696" class="Keyword">open</a> <a id="701" href="Data.Container.Indexed.Core.html#387" class="Module">Container</a> <a id="711" href="Codata.Musical.M.Indexed.html#590" class="Bound">C</a>

  <a id="716" class="Comment">-- Projections.</a>

  <a id="735" href="Codata.Musical.M.Indexed.html#735" class="Function">head</a> <a id="740" class="Symbol">:</a> <a id="742" href="Codata.Musical.M.Indexed.html#627" class="Datatype">M</a> <a id="744" href="Relation.Unary.html#1751" class="Function Operator">⊆</a> <a id="746" href="Data.Container.Indexed.Core.html#526" class="Field">Command</a>
  <a id="756" href="Codata.Musical.M.Indexed.html#735" class="Function">head</a> <a id="761" class="Symbol">(</a><a id="762" href="Codata.Musical.M.Indexed.html#665" class="InductiveConstructor">inf</a> <a id="766" class="Symbol">(</a><a id="767" href="Codata.Musical.M.Indexed.html#767" class="Bound">c</a> <a id="769" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="771" class="Symbol">_))</a> <a id="775" class="Symbol">=</a> <a id="777" href="Codata.Musical.M.Indexed.html#767" class="Bound">c</a>

  <a id="782" href="Codata.Musical.M.Indexed.html#782" class="Function">tail</a> <a id="787" class="Symbol">:</a> <a id="789" class="Symbol">∀</a> <a id="791" class="Symbol">{</a><a id="792" href="Codata.Musical.M.Indexed.html#792" class="Bound">o</a><a id="793" class="Symbol">}</a> <a id="795" class="Symbol">(</a><a id="796" href="Codata.Musical.M.Indexed.html#796" class="Bound">m</a> <a id="798" class="Symbol">:</a> <a id="800" href="Codata.Musical.M.Indexed.html#627" class="Datatype">M</a> <a id="802" href="Codata.Musical.M.Indexed.html#792" class="Bound">o</a><a id="803" class="Symbol">)</a> <a id="805" class="Symbol">(</a><a id="806" href="Codata.Musical.M.Indexed.html#806" class="Bound">r</a> <a id="808" class="Symbol">:</a> <a id="810" href="Data.Container.Indexed.Core.html#557" class="Field">Response</a> <a id="819" class="Symbol">(</a><a id="820" href="Codata.Musical.M.Indexed.html#735" class="Function">head</a> <a id="825" href="Codata.Musical.M.Indexed.html#796" class="Bound">m</a><a id="826" class="Symbol">))</a> <a id="829" class="Symbol">→</a> <a id="831" href="Codata.Musical.M.Indexed.html#627" class="Datatype">M</a> <a id="833" class="Symbol">(</a><a id="834" href="Data.Container.Indexed.Core.html#598" class="Field">next</a> <a id="839" class="Symbol">(</a><a id="840" href="Codata.Musical.M.Indexed.html#735" class="Function">head</a> <a id="845" href="Codata.Musical.M.Indexed.html#796" class="Bound">m</a><a id="846" class="Symbol">)</a> <a id="848" href="Codata.Musical.M.Indexed.html#806" class="Bound">r</a><a id="849" class="Symbol">)</a>
  <a id="853" href="Codata.Musical.M.Indexed.html#782" class="Function">tail</a> <a id="858" class="Symbol">(</a><a id="859" href="Codata.Musical.M.Indexed.html#665" class="InductiveConstructor">inf</a> <a id="863" class="Symbol">(_</a> <a id="866" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="868" href="Codata.Musical.M.Indexed.html#868" class="Bound">k</a><a id="869" class="Symbol">))</a> <a id="872" href="Codata.Musical.M.Indexed.html#872" class="Bound">r</a> <a id="874" class="Symbol">=</a> <a id="876" href="Agda.Builtin.Coinduction.html#264" class="Field">♭</a> <a id="878" class="Symbol">(</a><a id="879" href="Codata.Musical.M.Indexed.html#868" class="Bound">k</a> <a id="881" href="Codata.Musical.M.Indexed.html#872" class="Bound">r</a><a id="882" class="Symbol">)</a>

  <a id="887" href="Codata.Musical.M.Indexed.html#887" class="Function">force</a> <a id="893" class="Symbol">:</a> <a id="895" href="Codata.Musical.M.Indexed.html#627" class="Datatype">M</a> <a id="897" href="Relation.Unary.html#1751" class="Function Operator">⊆</a> <a id="899" href="Data.Container.Indexed.Core.html#706" class="Function Operator">⟦</a> <a id="901" href="Codata.Musical.M.Indexed.html#590" class="Bound">C</a> <a id="903" href="Data.Container.Indexed.Core.html#706" class="Function Operator">⟧</a> <a id="905" href="Codata.Musical.M.Indexed.html#627" class="Datatype">M</a>
  <a id="909" href="Codata.Musical.M.Indexed.html#887" class="Function">force</a> <a id="915" class="Symbol">(</a><a id="916" href="Codata.Musical.M.Indexed.html#665" class="InductiveConstructor">inf</a> <a id="920" class="Symbol">(</a><a id="921" href="Codata.Musical.M.Indexed.html#921" class="Bound">c</a> <a id="923" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="925" href="Codata.Musical.M.Indexed.html#925" class="Bound">k</a><a id="926" class="Symbol">))</a> <a id="929" class="Symbol">=</a> <a id="931" href="Codata.Musical.M.Indexed.html#921" class="Bound">c</a> <a id="933" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="935" class="Symbol">λ</a> <a id="937" href="Codata.Musical.M.Indexed.html#937" class="Bound">r</a> <a id="939" class="Symbol">→</a> <a id="941" href="Agda.Builtin.Coinduction.html#264" class="Field">♭</a> <a id="943" class="Symbol">(</a><a id="944" href="Codata.Musical.M.Indexed.html#925" class="Bound">k</a> <a id="946" href="Codata.Musical.M.Indexed.html#937" class="Bound">r</a><a id="947" class="Symbol">)</a>

  <a id="952" class="Comment">-- Coiteration.</a>

  <a id="971" href="Codata.Musical.M.Indexed.html#971" class="Function">coit</a> <a id="976" class="Symbol">:</a> <a id="978" class="Symbol">∀</a> <a id="980" class="Symbol">{</a><a id="981" href="Codata.Musical.M.Indexed.html#981" class="Bound">ℓ</a><a id="982" class="Symbol">}</a> <a id="984" class="Symbol">{</a><a id="985" href="Codata.Musical.M.Indexed.html#985" class="Bound">X</a> <a id="987" class="Symbol">:</a> <a id="989" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="994" href="Codata.Musical.M.Indexed.html#578" class="Bound">O</a> <a id="996" href="Codata.Musical.M.Indexed.html#981" class="Bound">ℓ</a><a id="997" class="Symbol">}</a> <a id="999" class="Symbol">→</a> <a id="1001" href="Codata.Musical.M.Indexed.html#985" class="Bound">X</a> <a id="1003" href="Relation.Unary.html#1751" class="Function Operator">⊆</a> <a id="1005" href="Data.Container.Indexed.Core.html#706" class="Function Operator">⟦</a> <a id="1007" href="Codata.Musical.M.Indexed.html#590" class="Bound">C</a> <a id="1009" href="Data.Container.Indexed.Core.html#706" class="Function Operator">⟧</a> <a id="1011" href="Codata.Musical.M.Indexed.html#985" class="Bound">X</a> <a id="1013" class="Symbol">→</a> <a id="1015" href="Codata.Musical.M.Indexed.html#985" class="Bound">X</a> <a id="1017" href="Relation.Unary.html#1751" class="Function Operator">⊆</a> <a id="1019" href="Codata.Musical.M.Indexed.html#627" class="Datatype">M</a>
  <a id="1023" href="Codata.Musical.M.Indexed.html#971" class="Function">coit</a> <a id="1028" href="Codata.Musical.M.Indexed.html#1028" class="Bound">ψ</a> <a id="1030" href="Codata.Musical.M.Indexed.html#1030" class="Bound">x</a> <a id="1032" class="Symbol">=</a> <a id="1034" href="Codata.Musical.M.Indexed.html#665" class="InductiveConstructor">inf</a> <a id="1038" class="Symbol">(</a><a id="1039" href="Agda.Builtin.Sigma.html#251" class="Field">proj₁</a> <a id="1045" href="Codata.Musical.M.Indexed.html#1093" class="Function">cs</a> <a id="1048" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1050" class="Symbol">λ</a> <a id="1052" href="Codata.Musical.M.Indexed.html#1052" class="Bound">r</a> <a id="1054" class="Symbol">→</a> <a id="1056" href="Agda.Builtin.Coinduction.html#229" class="CoinductiveConstructor Operator">♯</a> <a id="1058" href="Codata.Musical.M.Indexed.html#971" class="Function">coit</a> <a id="1063" href="Codata.Musical.M.Indexed.html#1028" class="Bound">ψ</a> <a id="1065" class="Symbol">(</a><a id="1066" href="Agda.Builtin.Sigma.html#263" class="Field">proj₂</a> <a id="1072" href="Codata.Musical.M.Indexed.html#1093" class="Function">cs</a> <a id="1075" href="Codata.Musical.M.Indexed.html#1052" class="Bound">r</a><a id="1076" class="Symbol">))</a>
    <a id="1083" class="Keyword">where</a>
    <a id="1093" href="Codata.Musical.M.Indexed.html#1093" class="Function">cs</a> <a id="1096" class="Symbol">=</a> <a id="1098" href="Codata.Musical.M.Indexed.html#1028" class="Bound">ψ</a> <a id="1100" href="Codata.Musical.M.Indexed.html#1030" class="Bound">x</a>
</pre></body></html>
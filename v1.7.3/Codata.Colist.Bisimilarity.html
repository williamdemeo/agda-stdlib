<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Colist.Bisimilarity</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Bisimilarity for Colists</a>
<a id="134" class="Comment">------------------------------------------------------------------------</a>

<a id="208" class="Symbol">{-#</a> <a id="212" class="Keyword">OPTIONS</a> <a id="220" class="Pragma">--cubical-compatible</a> <a id="241" class="Pragma">--sized-types</a> <a id="255" class="Symbol">#-}</a>

<a id="260" class="Keyword">module</a> <a id="267" href="Codata.Colist.Bisimilarity.html" class="Module">Codata.Colist.Bisimilarity</a> <a id="294" class="Keyword">where</a>

<a id="301" class="Keyword">open</a> <a id="306" class="Keyword">import</a> <a id="313" href="Level.html" class="Module">Level</a> <a id="319" class="Keyword">using</a> <a id="325" class="Symbol">(</a><a id="326" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="331" class="Symbol">;</a> <a id="333" href="Agda.Primitive.html#961" class="Primitive Operator">_⊔_</a><a id="336" class="Symbol">)</a>
<a id="338" class="Keyword">open</a> <a id="343" class="Keyword">import</a> <a id="350" href="Size.html" class="Module">Size</a>
<a id="355" class="Keyword">open</a> <a id="360" class="Keyword">import</a> <a id="367" href="Codata.Thunk.html" class="Module">Codata.Thunk</a>
<a id="380" class="Keyword">open</a> <a id="385" class="Keyword">import</a> <a id="392" href="Codata.Colist.html" class="Module">Codata.Colist</a>
<a id="406" class="Keyword">open</a> <a id="411" class="Keyword">import</a> <a id="418" href="Data.List.Base.html" class="Module">Data.List.Base</a> <a id="433" class="Keyword">using</a> <a id="439" class="Symbol">(</a><a id="440" href="Agda.Builtin.List.html#147" class="Datatype">List</a><a id="444" class="Symbol">;</a> <a id="446" href="Data.List.Base.html#10392" class="InductiveConstructor">[]</a><a id="448" class="Symbol">;</a> <a id="450" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">_∷_</a><a id="453" class="Symbol">)</a>
<a id="455" class="Keyword">open</a> <a id="460" class="Keyword">import</a> <a id="467" href="Data.List.Relation.Binary.Pointwise.html" class="Module">Data.List.Relation.Binary.Pointwise</a> <a id="503" class="Keyword">using</a> <a id="509" class="Symbol">(</a><a id="510" href="Data.List.Relation.Binary.Pointwise.Base.html#784" class="Datatype">Pointwise</a><a id="519" class="Symbol">;</a> <a id="521" href="Data.List.Relation.Binary.Pointwise.Base.html#893" class="InductiveConstructor">[]</a><a id="523" class="Symbol">;</a> <a id="525" href="Data.List.Relation.Binary.Pointwise.Base.html#919" class="InductiveConstructor Operator">_∷_</a><a id="528" class="Symbol">)</a>
<a id="530" class="Keyword">open</a> <a id="535" class="Keyword">import</a> <a id="542" href="Data.List.NonEmpty.html" class="Module">Data.List.NonEmpty</a> <a id="561" class="Symbol">as</a> <a id="564" class="Module">List⁺</a>  <a id="571" class="Keyword">using</a> <a id="577" class="Symbol">(</a><a id="578" href="Data.List.NonEmpty.Base.html#1088" class="Record">List⁺</a><a id="583" class="Symbol">;</a> <a id="585" href="Data.List.NonEmpty.Base.html#1134" class="InductiveConstructor Operator">_∷_</a><a id="588" class="Symbol">)</a>
<a id="590" class="Keyword">open</a> <a id="595" class="Keyword">import</a> <a id="602" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="618" class="Keyword">open</a> <a id="623" class="Keyword">import</a> <a id="630" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="668" class="Symbol">as</a> <a id="671" class="Module">Eq</a> <a id="674" class="Keyword">using</a> <a id="680" class="Symbol">(</a><a id="681" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">_≡_</a><a id="684" class="Symbol">)</a>

<a id="687" class="Keyword">private</a>
  <a id="697" class="Keyword">variable</a>
    <a id="710" href="Codata.Colist.Bisimilarity.html#710" class="Generalizable">a</a> <a id="712" href="Codata.Colist.Bisimilarity.html#712" class="Generalizable">b</a> <a id="714" href="Codata.Colist.Bisimilarity.html#714" class="Generalizable">c</a> <a id="716" href="Codata.Colist.Bisimilarity.html#716" class="Generalizable">p</a> <a id="718" href="Codata.Colist.Bisimilarity.html#718" class="Generalizable">q</a> <a id="720" href="Codata.Colist.Bisimilarity.html#720" class="Generalizable">r</a> <a id="722" class="Symbol">:</a> <a id="724" href="Agda.Primitive.html#742" class="Postulate">Level</a>
    <a id="734" href="Codata.Colist.Bisimilarity.html#734" class="Generalizable">A</a> <a id="736" class="Symbol">:</a> <a id="738" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="742" href="Codata.Colist.Bisimilarity.html#710" class="Generalizable">a</a>
    <a id="748" href="Codata.Colist.Bisimilarity.html#748" class="Generalizable">B</a> <a id="750" class="Symbol">:</a> <a id="752" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="756" href="Codata.Colist.Bisimilarity.html#712" class="Generalizable">b</a>
    <a id="762" href="Codata.Colist.Bisimilarity.html#762" class="Generalizable">C</a> <a id="764" class="Symbol">:</a> <a id="766" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="770" href="Codata.Colist.Bisimilarity.html#714" class="Generalizable">c</a>
    <a id="776" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a> <a id="778" class="Symbol">:</a> <a id="780" href="Agda.Builtin.Size.html#213" class="Postulate">Size</a>

<a id="786" class="Keyword">data</a> <a id="Bisim"></a><a id="791" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="797" class="Symbol">{</a><a id="798" href="Codata.Colist.Bisimilarity.html#798" class="Bound">A</a> <a id="800" class="Symbol">:</a> <a id="802" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="806" href="Codata.Colist.Bisimilarity.html#710" class="Generalizable">a</a><a id="807" class="Symbol">}</a> <a id="809" class="Symbol">{</a><a id="810" href="Codata.Colist.Bisimilarity.html#810" class="Bound">B</a> <a id="812" class="Symbol">:</a> <a id="814" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="818" href="Codata.Colist.Bisimilarity.html#712" class="Generalizable">b</a><a id="819" class="Symbol">}</a> <a id="821" class="Symbol">(</a><a id="822" href="Codata.Colist.Bisimilarity.html#822" class="Bound">R</a> <a id="824" class="Symbol">:</a> <a id="826" href="Relation.Binary.Core.html#775" class="Function">REL</a> <a id="830" href="Codata.Colist.Bisimilarity.html#798" class="Bound">A</a> <a id="832" href="Codata.Colist.Bisimilarity.html#810" class="Bound">B</a> <a id="834" href="Codata.Colist.Bisimilarity.html#720" class="Generalizable">r</a><a id="835" class="Symbol">)</a> <a id="837" class="Symbol">(</a><a id="838" href="Codata.Colist.Bisimilarity.html#838" class="Bound">i</a> <a id="840" class="Symbol">:</a> <a id="842" href="Agda.Builtin.Size.html#213" class="Postulate">Size</a><a id="846" class="Symbol">)</a> <a id="848" class="Symbol">:</a>
           <a id="861" href="Relation.Binary.Core.html#775" class="Function">REL</a> <a id="865" class="Symbol">(</a><a id="866" href="Codata.Colist.html#1290" class="Datatype">Colist</a> <a id="873" href="Codata.Colist.Bisimilarity.html#798" class="Bound">A</a> <a id="875" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a><a id="876" class="Symbol">)</a> <a id="878" class="Symbol">(</a><a id="879" href="Codata.Colist.html#1290" class="Datatype">Colist</a> <a id="886" href="Codata.Colist.Bisimilarity.html#810" class="Bound">B</a> <a id="888" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a><a id="889" class="Symbol">)</a> <a id="891" class="Symbol">(</a><a id="892" href="Codata.Colist.Bisimilarity.html#834" class="Bound">r</a> <a id="894" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="896" href="Codata.Colist.Bisimilarity.html#806" class="Bound">a</a> <a id="898" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="900" href="Codata.Colist.Bisimilarity.html#818" class="Bound">b</a><a id="901" class="Symbol">)</a> <a id="903" class="Keyword">where</a>
  <a id="Bisim.[]"></a><a id="911" href="Codata.Colist.Bisimilarity.html#911" class="InductiveConstructor">[]</a>  <a id="915" class="Symbol">:</a> <a id="917" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="923" href="Codata.Colist.Bisimilarity.html#822" class="Bound">R</a> <a id="925" href="Codata.Colist.Bisimilarity.html#838" class="Bound">i</a> <a id="927" href="Codata.Colist.html#1336" class="InductiveConstructor">[]</a> <a id="930" href="Codata.Colist.html#1336" class="InductiveConstructor">[]</a>
  <a id="Bisim._∷_"></a><a id="935" href="Codata.Colist.Bisimilarity.html#935" class="InductiveConstructor Operator">_∷_</a> <a id="939" class="Symbol">:</a> <a id="941" class="Symbol">∀</a> <a id="943" class="Symbol">{</a><a id="944" href="Codata.Colist.Bisimilarity.html#944" class="Bound">x</a> <a id="946" href="Codata.Colist.Bisimilarity.html#946" class="Bound">y</a> <a id="948" href="Codata.Colist.Bisimilarity.html#948" class="Bound">xs</a> <a id="951" href="Codata.Colist.Bisimilarity.html#951" class="Bound">ys</a><a id="953" class="Symbol">}</a> <a id="955" class="Symbol">→</a> <a id="957" href="Codata.Colist.Bisimilarity.html#822" class="Bound">R</a> <a id="959" href="Codata.Colist.Bisimilarity.html#944" class="Bound">x</a> <a id="961" href="Codata.Colist.Bisimilarity.html#946" class="Bound">y</a> <a id="963" class="Symbol">→</a> <a id="965" href="Codata.Thunk.html#753" class="Function">Thunk^R</a> <a id="973" class="Symbol">(</a><a id="974" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="980" href="Codata.Colist.Bisimilarity.html#822" class="Bound">R</a><a id="981" class="Symbol">)</a> <a id="983" href="Codata.Colist.Bisimilarity.html#838" class="Bound">i</a> <a id="985" href="Codata.Colist.Bisimilarity.html#948" class="Bound">xs</a> <a id="988" href="Codata.Colist.Bisimilarity.html#951" class="Bound">ys</a> <a id="991" class="Symbol">→</a>
        <a id="1001" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="1007" href="Codata.Colist.Bisimilarity.html#822" class="Bound">R</a> <a id="1009" href="Codata.Colist.Bisimilarity.html#838" class="Bound">i</a> <a id="1011" class="Symbol">(</a><a id="1012" href="Codata.Colist.Bisimilarity.html#944" class="Bound">x</a> <a id="1014" href="Codata.Colist.html#1355" class="InductiveConstructor Operator">∷</a> <a id="1016" href="Codata.Colist.Bisimilarity.html#948" class="Bound">xs</a><a id="1018" class="Symbol">)</a> <a id="1020" class="Symbol">(</a><a id="1021" href="Codata.Colist.Bisimilarity.html#946" class="Bound">y</a> <a id="1023" href="Codata.Colist.html#1355" class="InductiveConstructor Operator">∷</a> <a id="1025" href="Codata.Colist.Bisimilarity.html#951" class="Bound">ys</a><a id="1027" class="Symbol">)</a>

<a id="1030" class="Keyword">module</a> <a id="1037" href="Codata.Colist.Bisimilarity.html#1037" class="Module">_</a> <a id="1039" class="Symbol">{</a><a id="1040" href="Codata.Colist.Bisimilarity.html#1040" class="Bound">R</a> <a id="1042" class="Symbol">:</a> <a id="1044" href="Relation.Binary.Core.html#891" class="Function">Rel</a> <a id="1048" href="Codata.Colist.Bisimilarity.html#734" class="Generalizable">A</a> <a id="1050" href="Codata.Colist.Bisimilarity.html#720" class="Generalizable">r</a><a id="1051" class="Symbol">}</a> <a id="1053" class="Keyword">where</a>

 <a id="1061" href="Codata.Colist.Bisimilarity.html#1061" class="Function">reflexive</a> <a id="1071" class="Symbol">:</a> <a id="1073" href="Relation.Binary.Definitions.html#1348" class="Function">Reflexive</a> <a id="1083" href="Codata.Colist.Bisimilarity.html#1040" class="Bound">R</a> <a id="1085" class="Symbol">→</a> <a id="1087" href="Relation.Binary.Definitions.html#1348" class="Function">Reflexive</a> <a id="1097" class="Symbol">(</a><a id="1098" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="1104" href="Codata.Colist.Bisimilarity.html#1040" class="Bound">R</a> <a id="1106" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a><a id="1107" class="Symbol">)</a>
 <a id="1110" href="Codata.Colist.Bisimilarity.html#1061" class="Function">reflexive</a> <a id="1120" href="Codata.Colist.Bisimilarity.html#1120" class="Bound">refl^R</a> <a id="1127" class="Symbol">{</a><a id="1128" href="Codata.Colist.html#1336" class="InductiveConstructor">[]</a><a id="1130" class="Symbol">}</a>     <a id="1136" class="Symbol">=</a> <a id="1138" href="Codata.Colist.Bisimilarity.html#911" class="InductiveConstructor">[]</a>
 <a id="1142" href="Codata.Colist.Bisimilarity.html#1061" class="Function">reflexive</a> <a id="1152" href="Codata.Colist.Bisimilarity.html#1152" class="Bound">refl^R</a> <a id="1159" class="Symbol">{</a><a id="1160" href="Codata.Colist.Bisimilarity.html#1160" class="Bound">r</a> <a id="1162" href="Codata.Colist.html#1355" class="InductiveConstructor Operator">∷</a> <a id="1164" href="Codata.Colist.Bisimilarity.html#1164" class="Bound">rs</a><a id="1166" class="Symbol">}</a> <a id="1168" class="Symbol">=</a> <a id="1170" href="Codata.Colist.Bisimilarity.html#1152" class="Bound">refl^R</a> <a id="1177" href="Codata.Colist.Bisimilarity.html#935" class="InductiveConstructor Operator">∷</a> <a id="1179" class="Symbol">λ</a> <a id="1181" class="Keyword">where</a> <a id="1187" class="Symbol">.</a><a id="1188" href="Codata.Thunk.html#550" class="Field">force</a> <a id="1194" class="Symbol">→</a> <a id="1196" href="Codata.Colist.Bisimilarity.html#1061" class="Function">reflexive</a> <a id="1206" href="Codata.Colist.Bisimilarity.html#1152" class="Bound">refl^R</a>

<a id="1214" class="Keyword">module</a> <a id="1221" href="Codata.Colist.Bisimilarity.html#1221" class="Module">_</a> <a id="1223" class="Symbol">{</a><a id="1224" href="Codata.Colist.Bisimilarity.html#1224" class="Bound">P</a> <a id="1226" class="Symbol">:</a> <a id="1228" href="Relation.Binary.Core.html#775" class="Function">REL</a> <a id="1232" href="Codata.Colist.Bisimilarity.html#734" class="Generalizable">A</a> <a id="1234" href="Codata.Colist.Bisimilarity.html#748" class="Generalizable">B</a> <a id="1236" href="Codata.Colist.Bisimilarity.html#716" class="Generalizable">p</a><a id="1237" class="Symbol">}</a> <a id="1239" class="Symbol">{</a><a id="1240" href="Codata.Colist.Bisimilarity.html#1240" class="Bound">Q</a> <a id="1242" class="Symbol">:</a> <a id="1244" href="Relation.Binary.Core.html#775" class="Function">REL</a> <a id="1248" href="Codata.Colist.Bisimilarity.html#748" class="Generalizable">B</a> <a id="1250" href="Codata.Colist.Bisimilarity.html#734" class="Generalizable">A</a> <a id="1252" href="Codata.Colist.Bisimilarity.html#718" class="Generalizable">q</a><a id="1253" class="Symbol">}</a> <a id="1255" class="Keyword">where</a>

 <a id="1263" href="Codata.Colist.Bisimilarity.html#1263" class="Function">symmetric</a> <a id="1273" class="Symbol">:</a> <a id="1275" href="Relation.Binary.Definitions.html#1433" class="Function">Sym</a> <a id="1279" href="Codata.Colist.Bisimilarity.html#1224" class="Bound">P</a> <a id="1281" href="Codata.Colist.Bisimilarity.html#1240" class="Bound">Q</a> <a id="1283" class="Symbol">→</a> <a id="1285" href="Relation.Binary.Definitions.html#1433" class="Function">Sym</a> <a id="1289" class="Symbol">(</a><a id="1290" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="1296" href="Codata.Colist.Bisimilarity.html#1224" class="Bound">P</a> <a id="1298" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a><a id="1299" class="Symbol">)</a> <a id="1301" class="Symbol">(</a><a id="1302" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="1308" href="Codata.Colist.Bisimilarity.html#1240" class="Bound">Q</a> <a id="1310" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a><a id="1311" class="Symbol">)</a>
 <a id="1314" href="Codata.Colist.Bisimilarity.html#1263" class="Function">symmetric</a> <a id="1324" href="Codata.Colist.Bisimilarity.html#1324" class="Bound">sym^PQ</a> <a id="1331" href="Codata.Colist.Bisimilarity.html#911" class="InductiveConstructor">[]</a>       <a id="1340" class="Symbol">=</a> <a id="1342" href="Codata.Colist.Bisimilarity.html#911" class="InductiveConstructor">[]</a>
 <a id="1346" href="Codata.Colist.Bisimilarity.html#1263" class="Function">symmetric</a> <a id="1356" href="Codata.Colist.Bisimilarity.html#1356" class="Bound">sym^PQ</a> <a id="1363" class="Symbol">(</a><a id="1364" href="Codata.Colist.Bisimilarity.html#1364" class="Bound">p</a> <a id="1366" href="Codata.Colist.Bisimilarity.html#935" class="InductiveConstructor Operator">∷</a> <a id="1368" href="Codata.Colist.Bisimilarity.html#1368" class="Bound">ps</a><a id="1370" class="Symbol">)</a> <a id="1372" class="Symbol">=</a> <a id="1374" href="Codata.Colist.Bisimilarity.html#1356" class="Bound">sym^PQ</a> <a id="1381" href="Codata.Colist.Bisimilarity.html#1364" class="Bound">p</a> <a id="1383" href="Codata.Colist.Bisimilarity.html#935" class="InductiveConstructor Operator">∷</a> <a id="1385" class="Symbol">λ</a> <a id="1387" class="Keyword">where</a> <a id="1393" class="Symbol">.</a><a id="1394" href="Codata.Thunk.html#550" class="Field">force</a> <a id="1400" class="Symbol">→</a> <a id="1402" href="Codata.Colist.Bisimilarity.html#1263" class="Function">symmetric</a> <a id="1412" href="Codata.Colist.Bisimilarity.html#1356" class="Bound">sym^PQ</a> <a id="1419" class="Symbol">(</a><a id="1420" href="Codata.Colist.Bisimilarity.html#1368" class="Bound">ps</a> <a id="1423" class="Symbol">.</a><a id="1424" href="Codata.Thunk.html#550" class="Field">force</a><a id="1429" class="Symbol">)</a>

<a id="1432" class="Keyword">module</a> <a id="1439" href="Codata.Colist.Bisimilarity.html#1439" class="Module">_</a> <a id="1441" class="Symbol">{</a><a id="1442" href="Codata.Colist.Bisimilarity.html#1442" class="Bound">P</a> <a id="1444" class="Symbol">:</a> <a id="1446" href="Relation.Binary.Core.html#775" class="Function">REL</a> <a id="1450" href="Codata.Colist.Bisimilarity.html#734" class="Generalizable">A</a> <a id="1452" href="Codata.Colist.Bisimilarity.html#748" class="Generalizable">B</a> <a id="1454" href="Codata.Colist.Bisimilarity.html#716" class="Generalizable">p</a><a id="1455" class="Symbol">}</a> <a id="1457" class="Symbol">{</a><a id="1458" href="Codata.Colist.Bisimilarity.html#1458" class="Bound">Q</a> <a id="1460" class="Symbol">:</a> <a id="1462" href="Relation.Binary.Core.html#775" class="Function">REL</a> <a id="1466" href="Codata.Colist.Bisimilarity.html#748" class="Generalizable">B</a> <a id="1468" href="Codata.Colist.Bisimilarity.html#762" class="Generalizable">C</a> <a id="1470" href="Codata.Colist.Bisimilarity.html#718" class="Generalizable">q</a><a id="1471" class="Symbol">}</a> <a id="1473" class="Symbol">{</a><a id="1474" href="Codata.Colist.Bisimilarity.html#1474" class="Bound">R</a> <a id="1476" class="Symbol">:</a> <a id="1478" href="Relation.Binary.Core.html#775" class="Function">REL</a> <a id="1482" href="Codata.Colist.Bisimilarity.html#734" class="Generalizable">A</a> <a id="1484" href="Codata.Colist.Bisimilarity.html#762" class="Generalizable">C</a> <a id="1486" href="Codata.Colist.Bisimilarity.html#720" class="Generalizable">r</a><a id="1487" class="Symbol">}</a> <a id="1489" class="Keyword">where</a>

 <a id="1497" href="Codata.Colist.Bisimilarity.html#1497" class="Function">transitive</a> <a id="1508" class="Symbol">:</a> <a id="1510" href="Relation.Binary.Definitions.html#1594" class="Function">Trans</a> <a id="1516" href="Codata.Colist.Bisimilarity.html#1442" class="Bound">P</a> <a id="1518" href="Codata.Colist.Bisimilarity.html#1458" class="Bound">Q</a> <a id="1520" href="Codata.Colist.Bisimilarity.html#1474" class="Bound">R</a> <a id="1522" class="Symbol">→</a> <a id="1524" href="Relation.Binary.Definitions.html#1594" class="Function">Trans</a> <a id="1530" class="Symbol">(</a><a id="1531" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="1537" href="Codata.Colist.Bisimilarity.html#1442" class="Bound">P</a> <a id="1539" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a><a id="1540" class="Symbol">)</a> <a id="1542" class="Symbol">(</a><a id="1543" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="1549" href="Codata.Colist.Bisimilarity.html#1458" class="Bound">Q</a> <a id="1551" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a><a id="1552" class="Symbol">)</a> <a id="1554" class="Symbol">(</a><a id="1555" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="1561" href="Codata.Colist.Bisimilarity.html#1474" class="Bound">R</a> <a id="1563" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a><a id="1564" class="Symbol">)</a>
 <a id="1567" href="Codata.Colist.Bisimilarity.html#1497" class="Function">transitive</a> <a id="1578" href="Codata.Colist.Bisimilarity.html#1578" class="Bound">trans^PQR</a> <a id="1588" href="Codata.Colist.Bisimilarity.html#911" class="InductiveConstructor">[]</a>       <a id="1597" href="Codata.Colist.Bisimilarity.html#911" class="InductiveConstructor">[]</a>       <a id="1606" class="Symbol">=</a> <a id="1608" href="Codata.Colist.Bisimilarity.html#911" class="InductiveConstructor">[]</a>
 <a id="1612" href="Codata.Colist.Bisimilarity.html#1497" class="Function">transitive</a> <a id="1623" href="Codata.Colist.Bisimilarity.html#1623" class="Bound">trans^PQR</a> <a id="1633" class="Symbol">(</a><a id="1634" href="Codata.Colist.Bisimilarity.html#1634" class="Bound">p</a> <a id="1636" href="Codata.Colist.Bisimilarity.html#935" class="InductiveConstructor Operator">∷</a> <a id="1638" href="Codata.Colist.Bisimilarity.html#1638" class="Bound">ps</a><a id="1640" class="Symbol">)</a> <a id="1642" class="Symbol">(</a><a id="1643" href="Codata.Colist.Bisimilarity.html#1643" class="Bound">q</a> <a id="1645" href="Codata.Colist.Bisimilarity.html#935" class="InductiveConstructor Operator">∷</a> <a id="1647" href="Codata.Colist.Bisimilarity.html#1647" class="Bound">qs</a><a id="1649" class="Symbol">)</a> <a id="1651" class="Symbol">=</a>
   <a id="1656" href="Codata.Colist.Bisimilarity.html#1623" class="Bound">trans^PQR</a> <a id="1666" href="Codata.Colist.Bisimilarity.html#1634" class="Bound">p</a> <a id="1668" href="Codata.Colist.Bisimilarity.html#1643" class="Bound">q</a> <a id="1670" href="Codata.Colist.Bisimilarity.html#935" class="InductiveConstructor Operator">∷</a> <a id="1672" class="Symbol">λ</a> <a id="1674" class="Keyword">where</a> <a id="1680" class="Symbol">.</a><a id="1681" href="Codata.Thunk.html#550" class="Field">force</a> <a id="1687" class="Symbol">→</a> <a id="1689" href="Codata.Colist.Bisimilarity.html#1497" class="Function">transitive</a> <a id="1700" href="Codata.Colist.Bisimilarity.html#1623" class="Bound">trans^PQR</a> <a id="1710" class="Symbol">(</a><a id="1711" href="Codata.Colist.Bisimilarity.html#1638" class="Bound">ps</a> <a id="1714" class="Symbol">.</a><a id="1715" href="Codata.Thunk.html#550" class="Field">force</a><a id="1720" class="Symbol">)</a> <a id="1722" class="Symbol">(</a><a id="1723" href="Codata.Colist.Bisimilarity.html#1647" class="Bound">qs</a> <a id="1726" class="Symbol">.</a><a id="1727" href="Codata.Thunk.html#550" class="Field">force</a><a id="1732" class="Symbol">)</a>

<a id="1735" class="Comment">------------------------------------------------------------------------</a>
<a id="1808" class="Comment">-- Congruence rules</a>

<a id="1829" class="Keyword">module</a> <a id="1836" href="Codata.Colist.Bisimilarity.html#1836" class="Module">_</a> <a id="1838" class="Symbol">{</a><a id="1839" href="Codata.Colist.Bisimilarity.html#1839" class="Bound">R</a> <a id="1841" class="Symbol">:</a> <a id="1843" href="Relation.Binary.Core.html#775" class="Function">REL</a> <a id="1847" href="Codata.Colist.Bisimilarity.html#734" class="Generalizable">A</a> <a id="1849" href="Codata.Colist.Bisimilarity.html#748" class="Generalizable">B</a> <a id="1851" href="Codata.Colist.Bisimilarity.html#720" class="Generalizable">r</a><a id="1852" class="Symbol">}</a> <a id="1854" class="Keyword">where</a>

  <a id="1863" href="Codata.Colist.Bisimilarity.html#1863" class="Function">++⁺</a> <a id="1867" class="Symbol">:</a> <a id="1869" class="Symbol">∀</a> <a id="1871" class="Symbol">{</a><a id="1872" href="Codata.Colist.Bisimilarity.html#1872" class="Bound">as</a> <a id="1875" href="Codata.Colist.Bisimilarity.html#1875" class="Bound">bs</a> <a id="1878" href="Codata.Colist.Bisimilarity.html#1878" class="Bound">xs</a> <a id="1881" href="Codata.Colist.Bisimilarity.html#1881" class="Bound">ys</a><a id="1883" class="Symbol">}</a> <a id="1885" class="Symbol">→</a> <a id="1887" href="Data.List.Relation.Binary.Pointwise.Base.html#784" class="Datatype">Pointwise</a> <a id="1897" href="Codata.Colist.Bisimilarity.html#1839" class="Bound">R</a> <a id="1899" href="Codata.Colist.Bisimilarity.html#1872" class="Bound">as</a> <a id="1902" href="Codata.Colist.Bisimilarity.html#1875" class="Bound">bs</a> <a id="1905" class="Symbol">→</a>
        <a id="1915" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="1921" href="Codata.Colist.Bisimilarity.html#1839" class="Bound">R</a> <a id="1923" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a> <a id="1925" href="Codata.Colist.Bisimilarity.html#1878" class="Bound">xs</a> <a id="1928" href="Codata.Colist.Bisimilarity.html#1881" class="Bound">ys</a> <a id="1931" class="Symbol">→</a> <a id="1933" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="1939" href="Codata.Colist.Bisimilarity.html#1839" class="Bound">R</a> <a id="1941" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a> <a id="1943" class="Symbol">(</a><a id="1944" href="Codata.Colist.html#3116" class="Function">fromList</a> <a id="1953" href="Codata.Colist.Bisimilarity.html#1872" class="Bound">as</a> <a id="1956" href="Codata.Colist.html#2190" class="Function Operator">++</a> <a id="1959" href="Codata.Colist.Bisimilarity.html#1878" class="Bound">xs</a><a id="1961" class="Symbol">)</a> <a id="1963" class="Symbol">(</a><a id="1964" href="Codata.Colist.html#3116" class="Function">fromList</a> <a id="1973" href="Codata.Colist.Bisimilarity.html#1875" class="Bound">bs</a> <a id="1976" href="Codata.Colist.html#2190" class="Function Operator">++</a> <a id="1979" href="Codata.Colist.Bisimilarity.html#1881" class="Bound">ys</a><a id="1981" class="Symbol">)</a>
  <a id="1985" href="Codata.Colist.Bisimilarity.html#1863" class="Function">++⁺</a> <a id="1989" href="Data.List.Relation.Binary.Pointwise.Base.html#893" class="InductiveConstructor">[]</a>       <a id="1998" href="Codata.Colist.Bisimilarity.html#1998" class="Bound">rs</a> <a id="2001" class="Symbol">=</a> <a id="2003" href="Codata.Colist.Bisimilarity.html#1998" class="Bound">rs</a>
  <a id="2008" href="Codata.Colist.Bisimilarity.html#1863" class="Function">++⁺</a> <a id="2012" class="Symbol">(</a><a id="2013" href="Codata.Colist.Bisimilarity.html#2013" class="Bound">r</a> <a id="2015" href="Data.List.Relation.Binary.Pointwise.Base.html#919" class="InductiveConstructor Operator">∷</a> <a id="2017" href="Codata.Colist.Bisimilarity.html#2017" class="Bound">pw</a><a id="2019" class="Symbol">)</a> <a id="2021" href="Codata.Colist.Bisimilarity.html#2021" class="Bound">rs</a> <a id="2024" class="Symbol">=</a> <a id="2026" href="Codata.Colist.Bisimilarity.html#2013" class="Bound">r</a> <a id="2028" href="Codata.Colist.Bisimilarity.html#935" class="InductiveConstructor Operator">∷</a> <a id="2030" class="Symbol">λ</a> <a id="2032" class="Keyword">where</a> <a id="2038" class="Symbol">.</a><a id="2039" href="Codata.Thunk.html#550" class="Field">force</a> <a id="2045" class="Symbol">→</a> <a id="2047" href="Codata.Colist.Bisimilarity.html#1863" class="Function">++⁺</a> <a id="2051" href="Codata.Colist.Bisimilarity.html#2017" class="Bound">pw</a> <a id="2054" href="Codata.Colist.Bisimilarity.html#2021" class="Bound">rs</a>

  <a id="2060" href="Codata.Colist.Bisimilarity.html#2060" class="Function">⁺++⁺</a> <a id="2065" class="Symbol">:</a> <a id="2067" class="Symbol">∀</a> <a id="2069" class="Symbol">{</a><a id="2070" href="Codata.Colist.Bisimilarity.html#2070" class="Bound">as</a> <a id="2073" href="Codata.Colist.Bisimilarity.html#2073" class="Bound">bs</a> <a id="2076" href="Codata.Colist.Bisimilarity.html#2076" class="Bound">xs</a> <a id="2079" href="Codata.Colist.Bisimilarity.html#2079" class="Bound">ys</a><a id="2081" class="Symbol">}</a> <a id="2083" class="Symbol">→</a> <a id="2085" href="Data.List.Relation.Binary.Pointwise.Base.html#784" class="Datatype">Pointwise</a> <a id="2095" href="Codata.Colist.Bisimilarity.html#1839" class="Bound">R</a> <a id="2097" class="Symbol">(</a><a id="2098" href="Data.List.NonEmpty.Base.html#1530" class="Function">List⁺.toList</a> <a id="2111" href="Codata.Colist.Bisimilarity.html#2070" class="Bound">as</a><a id="2113" class="Symbol">)</a> <a id="2115" class="Symbol">(</a><a id="2116" href="Data.List.NonEmpty.Base.html#1530" class="Function">List⁺.toList</a> <a id="2129" href="Codata.Colist.Bisimilarity.html#2073" class="Bound">bs</a><a id="2131" class="Symbol">)</a> <a id="2133" class="Symbol">→</a>
         <a id="2144" href="Codata.Thunk.html#753" class="Function">Thunk^R</a> <a id="2152" class="Symbol">(</a><a id="2153" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="2159" href="Codata.Colist.Bisimilarity.html#1839" class="Bound">R</a><a id="2160" class="Symbol">)</a> <a id="2162" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a> <a id="2164" href="Codata.Colist.Bisimilarity.html#2076" class="Bound">xs</a> <a id="2167" href="Codata.Colist.Bisimilarity.html#2079" class="Bound">ys</a> <a id="2170" class="Symbol">→</a> <a id="2172" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="2178" href="Codata.Colist.Bisimilarity.html#1839" class="Bound">R</a> <a id="2180" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a> <a id="2182" class="Symbol">(</a><a id="2183" href="Codata.Colist.Bisimilarity.html#2070" class="Bound">as</a> <a id="2186" href="Codata.Colist.html#3295" class="Function Operator">⁺++</a> <a id="2190" href="Codata.Colist.Bisimilarity.html#2076" class="Bound">xs</a><a id="2192" class="Symbol">)</a> <a id="2194" class="Symbol">(</a><a id="2195" href="Codata.Colist.Bisimilarity.html#2073" class="Bound">bs</a> <a id="2198" href="Codata.Colist.html#3295" class="Function Operator">⁺++</a> <a id="2202" href="Codata.Colist.Bisimilarity.html#2079" class="Bound">ys</a><a id="2204" class="Symbol">)</a>
  <a id="2208" href="Codata.Colist.Bisimilarity.html#2060" class="Function">⁺++⁺</a> <a id="2213" class="Symbol">(</a><a id="2214" href="Codata.Colist.Bisimilarity.html#2214" class="Bound">r</a> <a id="2216" href="Data.List.Relation.Binary.Pointwise.Base.html#919" class="InductiveConstructor Operator">∷</a> <a id="2218" href="Codata.Colist.Bisimilarity.html#2218" class="Bound">pw</a><a id="2220" class="Symbol">)</a> <a id="2222" href="Codata.Colist.Bisimilarity.html#2222" class="Bound">rs</a> <a id="2225" class="Symbol">=</a> <a id="2227" href="Codata.Colist.Bisimilarity.html#2214" class="Bound">r</a> <a id="2229" href="Codata.Colist.Bisimilarity.html#935" class="InductiveConstructor Operator">∷</a> <a id="2231" class="Symbol">λ</a> <a id="2233" class="Keyword">where</a> <a id="2239" class="Symbol">.</a><a id="2240" href="Codata.Thunk.html#550" class="Field">force</a> <a id="2246" class="Symbol">→</a> <a id="2248" href="Codata.Colist.Bisimilarity.html#1863" class="Function">++⁺</a> <a id="2252" href="Codata.Colist.Bisimilarity.html#2218" class="Bound">pw</a> <a id="2255" class="Symbol">(</a><a id="2256" href="Codata.Colist.Bisimilarity.html#2222" class="Bound">rs</a> <a id="2259" class="Symbol">.</a><a id="2260" href="Codata.Thunk.html#550" class="Field">force</a><a id="2265" class="Symbol">)</a>

<a id="2268" class="Comment">------------------------------------------------------------------------</a>
<a id="2341" class="Comment">-- Pointwise Equality as a Bisimilarity</a>

<a id="2382" class="Keyword">module</a> <a id="2389" href="Codata.Colist.Bisimilarity.html#2389" class="Module">_</a> <a id="2391" class="Symbol">{</a><a id="2392" href="Codata.Colist.Bisimilarity.html#2392" class="Bound">A</a> <a id="2394" class="Symbol">:</a> <a id="2396" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2400" href="Codata.Colist.Bisimilarity.html#710" class="Generalizable">a</a><a id="2401" class="Symbol">}</a> <a id="2403" class="Keyword">where</a>

  <a id="2412" class="Keyword">infix</a> <a id="2418" class="Number">1</a> <a id="2420" href="Codata.Colist.Bisimilarity.html#2428" class="Function Operator">_⊢_≈_</a>
  <a id="2428" href="Codata.Colist.Bisimilarity.html#2428" class="Function Operator">_⊢_≈_</a> <a id="2434" class="Symbol">:</a> <a id="2436" class="Symbol">∀</a> <a id="2438" href="Codata.Colist.Bisimilarity.html#2438" class="Bound">i</a> <a id="2440" class="Symbol">→</a> <a id="2442" href="Codata.Colist.html#1290" class="Datatype">Colist</a> <a id="2449" href="Codata.Colist.Bisimilarity.html#2392" class="Bound">A</a> <a id="2451" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a> <a id="2453" class="Symbol">→</a> <a id="2455" href="Codata.Colist.html#1290" class="Datatype">Colist</a> <a id="2462" href="Codata.Colist.Bisimilarity.html#2392" class="Bound">A</a> <a id="2464" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a> <a id="2466" class="Symbol">→</a> <a id="2468" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2472" href="Codata.Colist.Bisimilarity.html#2400" class="Bound">a</a>
  <a id="2476" href="Codata.Colist.Bisimilarity.html#2428" class="Function Operator">_⊢_≈_</a> <a id="2482" class="Symbol">=</a> <a id="2484" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="2490" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">_≡_</a>

  <a id="2497" href="Codata.Colist.Bisimilarity.html#2497" class="Function">refl</a> <a id="2502" class="Symbol">:</a> <a id="2504" href="Relation.Binary.Definitions.html#1348" class="Function">Reflexive</a> <a id="2514" class="Symbol">(</a><a id="2515" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a> <a id="2517" href="Codata.Colist.Bisimilarity.html#2428" class="Function Operator">⊢_≈_</a><a id="2521" class="Symbol">)</a>
  <a id="2525" href="Codata.Colist.Bisimilarity.html#2497" class="Function">refl</a> <a id="2530" class="Symbol">=</a> <a id="2532" href="Codata.Colist.Bisimilarity.html#1061" class="Function">reflexive</a> <a id="2542" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">Eq.refl</a>

  <a id="2553" href="Codata.Colist.Bisimilarity.html#2553" class="Function">fromEq</a> <a id="2560" class="Symbol">:</a> <a id="2562" class="Symbol">∀</a> <a id="2564" class="Symbol">{</a><a id="2565" href="Codata.Colist.Bisimilarity.html#2565" class="Bound">as</a> <a id="2568" href="Codata.Colist.Bisimilarity.html#2568" class="Bound">bs</a><a id="2570" class="Symbol">}</a> <a id="2572" class="Symbol">→</a> <a id="2574" href="Codata.Colist.Bisimilarity.html#2565" class="Bound">as</a> <a id="2577" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="2579" href="Codata.Colist.Bisimilarity.html#2568" class="Bound">bs</a> <a id="2582" class="Symbol">→</a> <a id="2584" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a> <a id="2586" href="Codata.Colist.Bisimilarity.html#2428" class="Function Operator">⊢</a> <a id="2588" href="Codata.Colist.Bisimilarity.html#2565" class="Bound">as</a> <a id="2591" href="Codata.Colist.Bisimilarity.html#2428" class="Function Operator">≈</a> <a id="2593" href="Codata.Colist.Bisimilarity.html#2568" class="Bound">bs</a>
  <a id="2598" href="Codata.Colist.Bisimilarity.html#2553" class="Function">fromEq</a> <a id="2605" href="Agda.Builtin.Equality.html#207" class="InductiveConstructor">Eq.refl</a> <a id="2613" class="Symbol">=</a> <a id="2615" href="Codata.Colist.Bisimilarity.html#2497" class="Function">refl</a>

  <a id="2623" href="Codata.Colist.Bisimilarity.html#2623" class="Function">sym</a> <a id="2627" class="Symbol">:</a> <a id="2629" href="Relation.Binary.Definitions.html#1507" class="Function">Symmetric</a> <a id="2639" class="Symbol">(</a><a id="2640" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a> <a id="2642" href="Codata.Colist.Bisimilarity.html#2428" class="Function Operator">⊢_≈_</a><a id="2646" class="Symbol">)</a>
  <a id="2650" href="Codata.Colist.Bisimilarity.html#2623" class="Function">sym</a> <a id="2654" class="Symbol">=</a> <a id="2656" href="Codata.Colist.Bisimilarity.html#1263" class="Function">symmetric</a> <a id="2666" href="Relation.Binary.PropositionalEquality.Core.html#1693" class="Function">Eq.sym</a>

  <a id="2676" href="Codata.Colist.Bisimilarity.html#2676" class="Function">trans</a> <a id="2682" class="Symbol">:</a> <a id="2684" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="2695" class="Symbol">(</a><a id="2696" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a> <a id="2698" href="Codata.Colist.Bisimilarity.html#2428" class="Function Operator">⊢_≈_</a><a id="2702" class="Symbol">)</a>
  <a id="2706" href="Codata.Colist.Bisimilarity.html#2676" class="Function">trans</a> <a id="2712" class="Symbol">=</a> <a id="2714" href="Codata.Colist.Bisimilarity.html#1497" class="Function">transitive</a> <a id="2725" href="Relation.Binary.PropositionalEquality.Core.html#1738" class="Function">Eq.trans</a>

<a id="isEquivalence"></a><a id="2735" href="Codata.Colist.Bisimilarity.html#2735" class="Function">isEquivalence</a> <a id="2749" class="Symbol">:</a> <a id="2751" class="Symbol">{</a><a id="2752" href="Codata.Colist.Bisimilarity.html#2752" class="Bound">R</a> <a id="2754" class="Symbol">:</a> <a id="2756" href="Relation.Binary.Core.html#891" class="Function">Rel</a> <a id="2760" href="Codata.Colist.Bisimilarity.html#734" class="Generalizable">A</a> <a id="2762" href="Codata.Colist.Bisimilarity.html#720" class="Generalizable">r</a><a id="2763" class="Symbol">}</a> <a id="2765" class="Symbol">→</a> <a id="2767" href="Relation.Binary.Structures.html#1531" class="Record">IsEquivalence</a> <a id="2781" href="Codata.Colist.Bisimilarity.html#2752" class="Bound">R</a> <a id="2783" class="Symbol">→</a> <a id="2785" href="Relation.Binary.Structures.html#1531" class="Record">IsEquivalence</a> <a id="2799" class="Symbol">(</a><a id="2800" href="Codata.Colist.Bisimilarity.html#791" class="Datatype">Bisim</a> <a id="2806" href="Codata.Colist.Bisimilarity.html#2752" class="Bound">R</a> <a id="2808" href="Codata.Colist.Bisimilarity.html#776" class="Generalizable">i</a><a id="2809" class="Symbol">)</a>
<a id="2811" href="Codata.Colist.Bisimilarity.html#2735" class="Function">isEquivalence</a> <a id="2825" href="Codata.Colist.Bisimilarity.html#2825" class="Bound">equiv^R</a> <a id="2833" class="Symbol">=</a> <a id="2835" class="Keyword">record</a>
  <a id="2844" class="Symbol">{</a> <a id="2846" href="Relation.Binary.Structures.html#1577" class="Field">refl</a>  <a id="2852" class="Symbol">=</a> <a id="2854" href="Codata.Colist.Bisimilarity.html#1061" class="Function">reflexive</a> <a id="2864" href="Relation.Binary.Structures.html#1577" class="Field">equiv^R.refl</a>
  <a id="2879" class="Symbol">;</a> <a id="2881" href="Relation.Binary.Structures.html#1603" class="Field">sym</a>   <a id="2887" class="Symbol">=</a> <a id="2889" href="Codata.Colist.Bisimilarity.html#1263" class="Function">symmetric</a> <a id="2899" href="Relation.Binary.Structures.html#1603" class="Field">equiv^R.sym</a>
  <a id="2913" class="Symbol">;</a> <a id="2915" href="Relation.Binary.Structures.html#1629" class="Field">trans</a> <a id="2921" class="Symbol">=</a> <a id="2923" href="Codata.Colist.Bisimilarity.html#1497" class="Function">transitive</a> <a id="2934" href="Relation.Binary.Structures.html#1629" class="Field">equiv^R.trans</a>
  <a id="2950" class="Symbol">}</a> <a id="2952" class="Keyword">where</a> <a id="2958" class="Keyword">module</a> <a id="2965" href="Codata.Colist.Bisimilarity.html#2965" class="Module">equiv^R</a> <a id="2973" class="Symbol">=</a> <a id="2975" href="Relation.Binary.Structures.html#1531" class="Module">IsEquivalence</a> <a id="2989" href="Codata.Colist.Bisimilarity.html#2825" class="Bound">equiv^R</a>

<a id="setoid"></a><a id="2998" href="Codata.Colist.Bisimilarity.html#2998" class="Function">setoid</a> <a id="3005" class="Symbol">:</a> <a id="3007" href="Relation.Binary.Bundles.html#1018" class="Record">Setoid</a> <a id="3014" href="Codata.Colist.Bisimilarity.html#710" class="Generalizable">a</a> <a id="3016" href="Codata.Colist.Bisimilarity.html#720" class="Generalizable">r</a> <a id="3018" class="Symbol">→</a> <a id="3020" href="Agda.Builtin.Size.html#213" class="Postulate">Size</a> <a id="3025" class="Symbol">→</a> <a id="3027" href="Relation.Binary.Bundles.html#1018" class="Record">Setoid</a> <a id="3034" href="Codata.Colist.Bisimilarity.html#710" class="Generalizable">a</a> <a id="3036" class="Symbol">(</a><a id="3037" href="Codata.Colist.Bisimilarity.html#710" class="Generalizable">a</a> <a id="3039" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="3041" href="Codata.Colist.Bisimilarity.html#720" class="Generalizable">r</a><a id="3042" class="Symbol">)</a>
<a id="3044" href="Codata.Colist.Bisimilarity.html#2998" class="Function">setoid</a> <a id="3051" href="Codata.Colist.Bisimilarity.html#3051" class="Bound">S</a> <a id="3053" href="Codata.Colist.Bisimilarity.html#3053" class="Bound">i</a> <a id="3055" class="Symbol">=</a> <a id="3057" class="Keyword">record</a>
  <a id="3066" class="Symbol">{</a> <a id="3068" href="Relation.Binary.Bundles.html#1141" class="Field">isEquivalence</a> <a id="3082" class="Symbol">=</a> <a id="3084" href="Codata.Colist.Bisimilarity.html#2735" class="Function">isEquivalence</a> <a id="3098" class="Symbol">{</a><a id="3099" class="Argument">i</a> <a id="3101" class="Symbol">=</a> <a id="3103" href="Codata.Colist.Bisimilarity.html#3053" class="Bound">i</a><a id="3104" class="Symbol">}</a> <a id="3106" class="Symbol">(</a><a id="3107" href="Relation.Binary.Bundles.html#1141" class="Field">Setoid.isEquivalence</a> <a id="3128" href="Codata.Colist.Bisimilarity.html#3051" class="Bound">S</a><a id="3129" class="Symbol">)</a>
  <a id="3133" class="Symbol">}</a>

<a id="3136" class="Keyword">module</a> <a id="≈-Reasoning"></a><a id="3143" href="Codata.Colist.Bisimilarity.html#3143" class="Module">≈-Reasoning</a> <a id="3155" class="Symbol">{</a><a id="3156" href="Codata.Colist.Bisimilarity.html#3156" class="Bound">a</a><a id="3157" class="Symbol">}</a> <a id="3159" class="Symbol">{</a><a id="3160" href="Codata.Colist.Bisimilarity.html#3160" class="Bound">A</a> <a id="3162" class="Symbol">:</a> <a id="3164" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="3168" href="Codata.Colist.Bisimilarity.html#3156" class="Bound">a</a><a id="3169" class="Symbol">}</a> <a id="3171" class="Symbol">{</a><a id="3172" href="Codata.Colist.Bisimilarity.html#3172" class="Bound">i</a><a id="3173" class="Symbol">}</a> <a id="3175" class="Keyword">where</a>

  <a id="3184" class="Keyword">open</a> <a id="3189" class="Keyword">import</a> <a id="3196" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="3229" class="Symbol">(</a><a id="3230" href="Codata.Colist.Bisimilarity.html#2998" class="Function">setoid</a> <a id="3237" class="Symbol">(</a><a id="3238" href="Relation.Binary.PropositionalEquality.Properties.html#3981" class="Function">Eq.setoid</a> <a id="3248" href="Codata.Colist.Bisimilarity.html#3160" class="Bound">A</a><a id="3249" class="Symbol">)</a> <a id="3251" href="Codata.Colist.Bisimilarity.html#3172" class="Bound">i</a><a id="3252" class="Symbol">)</a> <a id="3254" class="Keyword">public</a>
</pre></body></html>
<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Unary.Closure.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Closures of a unary relation with respect to a binary one.</a>
<a id="168" class="Comment">------------------------------------------------------------------------</a>

<a id="242" class="Symbol">{-#</a> <a id="246" class="Keyword">OPTIONS</a> <a id="254" class="Pragma">--cubical-compatible</a> <a id="275" class="Pragma">--safe</a> <a id="282" class="Symbol">#-}</a>

<a id="287" class="Keyword">open</a> <a id="292" class="Keyword">import</a> <a id="299" href="Relation.Binary.html" class="Module">Relation.Binary</a>

<a id="316" class="Keyword">module</a> <a id="323" href="Relation.Unary.Closure.Base.html" class="Module">Relation.Unary.Closure.Base</a> <a id="351" class="Symbol">{</a><a id="352" href="Relation.Unary.Closure.Base.html#352" class="Bound">a</a> <a id="354" href="Relation.Unary.Closure.Base.html#354" class="Bound">b</a><a id="355" class="Symbol">}</a> <a id="357" class="Symbol">{</a><a id="358" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="360" class="Symbol">:</a> <a id="362" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="366" href="Relation.Unary.Closure.Base.html#352" class="Bound">a</a><a id="367" class="Symbol">}</a> <a id="369" class="Symbol">(</a><a id="370" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="372" class="Symbol">:</a> <a id="374" href="Relation.Binary.Core.html#891" class="Function">Rel</a> <a id="378" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="380" href="Relation.Unary.Closure.Base.html#354" class="Bound">b</a><a id="381" class="Symbol">)</a> <a id="383" class="Keyword">where</a>

<a id="390" class="Keyword">open</a> <a id="395" class="Keyword">import</a> <a id="402" href="Level.html" class="Module">Level</a>
<a id="408" class="Keyword">open</a> <a id="413" class="Keyword">import</a> <a id="420" href="Data.Product.html" class="Module">Data.Product</a> <a id="433" class="Keyword">using</a> <a id="439" class="Symbol">(</a><a id="440" href="Data.Product.html#925" class="Function">Σ-syntax</a><a id="448" class="Symbol">;</a> <a id="450" href="Data.Product.html#1176" class="Function Operator">_×_</a><a id="453" class="Symbol">;</a> <a id="455" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="458" class="Symbol">;</a> <a id="460" href="Data.Product.html#2256" class="Function Operator">-,_</a><a id="463" class="Symbol">)</a>
<a id="465" class="Keyword">open</a> <a id="470" class="Keyword">import</a> <a id="477" href="Function.Base.html" class="Module">Function.Base</a> <a id="491" class="Keyword">using</a> <a id="497" class="Symbol">(</a><a id="498" href="Function.Base.html#1563" class="Function">flip</a><a id="502" class="Symbol">)</a>
<a id="504" class="Keyword">open</a> <a id="509" class="Keyword">import</a> <a id="516" href="Relation.Unary.html" class="Module">Relation.Unary</a> <a id="531" class="Keyword">using</a> <a id="537" class="Symbol">(</a><a id="538" href="Relation.Unary.html#1110" class="Function">Pred</a><a id="542" class="Symbol">)</a>

<a id="545" class="Comment">------------------------------------------------------------------------</a>
<a id="618" class="Comment">-- Definitions</a>

<a id="634" class="Comment">-- Box</a>

<a id="642" class="Comment">-- We start with the definition of □ (&quot;box&quot;) which is named after the box</a>
<a id="716" class="Comment">-- modality in modal logic. `□ T x` states that all the elements one step</a>
<a id="790" class="Comment">-- away from `x` with respect to the relation R satisfy `T`.</a>

<a id="□"></a><a id="852" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="854" class="Symbol">:</a> <a id="856" class="Symbol">∀</a> <a id="858" class="Symbol">{</a><a id="859" href="Relation.Unary.Closure.Base.html#859" class="Bound">t</a><a id="860" class="Symbol">}</a> <a id="862" class="Symbol">→</a> <a id="864" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="869" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="871" href="Relation.Unary.Closure.Base.html#859" class="Bound">t</a> <a id="873" class="Symbol">→</a> <a id="875" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="880" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="882" class="Symbol">(</a><a id="883" href="Relation.Unary.Closure.Base.html#352" class="Bound">a</a> <a id="885" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="887" href="Relation.Unary.Closure.Base.html#354" class="Bound">b</a> <a id="889" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="891" href="Relation.Unary.Closure.Base.html#859" class="Bound">t</a><a id="892" class="Symbol">)</a>
<a id="894" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="896" href="Relation.Unary.Closure.Base.html#896" class="Bound">T</a> <a id="898" href="Relation.Unary.Closure.Base.html#898" class="Bound">x</a> <a id="900" class="Symbol">=</a> <a id="902" class="Symbol">∀</a> <a id="904" class="Symbol">{</a><a id="905" href="Relation.Unary.Closure.Base.html#905" class="Bound">y</a><a id="906" class="Symbol">}</a> <a id="908" class="Symbol">→</a> <a id="910" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="912" href="Relation.Unary.Closure.Base.html#898" class="Bound">x</a> <a id="914" href="Relation.Unary.Closure.Base.html#905" class="Bound">y</a> <a id="916" class="Symbol">→</a> <a id="918" href="Relation.Unary.Closure.Base.html#896" class="Bound">T</a> <a id="920" href="Relation.Unary.Closure.Base.html#905" class="Bound">y</a>

<a id="923" class="Comment">-- Use cases of □ include:</a>
<a id="950" class="Comment">-- * The definition of the accessibility predicate corresponding to R:</a>
<a id="1021" class="Comment">--   data Acc (x : A) : Set (a ⊔ b) where</a>
<a id="1063" class="Comment">--     step : □ Acc x → Acc x</a>

<a id="1094" class="Comment">-- * The characterization of stability under weakening: picking R to be</a>
<a id="1166" class="Comment">--   `Data.List.Relation.Sublist.Inductive`, `∀ {Γ} → Tm Γ → □ T Γ`</a>
<a id="1234" class="Comment">--   corresponds to the fact that we have a notion of weakening for `Tm`.</a>

<a id="1309" class="Comment">-- Diamond</a>

<a id="1321" class="Comment">-- We then have the definition of ◇ (&quot;diamond&quot;) which is named after the</a>
<a id="1394" class="Comment">-- diamond modality in modal logic. In modal logic, `◇ T x` states that</a>
<a id="1466" class="Comment">-- there exists an element one step away from x with respect to the</a>
<a id="1534" class="Comment">-- relation R that satisfies T. It is worth noting that the modal logic</a>
<a id="1606" class="Comment">-- metaphor breaks down here: this only is a closure operator if the step</a>
<a id="1680" class="Comment">-- we take is *backwards* with respect to R.</a>

<a id="◇"></a><a id="1726" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="1728" class="Symbol">:</a> <a id="1730" class="Symbol">∀</a> <a id="1732" class="Symbol">{</a><a id="1733" href="Relation.Unary.Closure.Base.html#1733" class="Bound">t</a><a id="1734" class="Symbol">}</a> <a id="1736" class="Symbol">→</a> <a id="1738" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1743" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="1745" href="Relation.Unary.Closure.Base.html#1733" class="Bound">t</a> <a id="1747" class="Symbol">→</a> <a id="1749" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1754" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="1756" class="Symbol">(</a><a id="1757" href="Relation.Unary.Closure.Base.html#352" class="Bound">a</a> <a id="1759" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1761" href="Relation.Unary.Closure.Base.html#354" class="Bound">b</a> <a id="1763" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1765" href="Relation.Unary.Closure.Base.html#1733" class="Bound">t</a><a id="1766" class="Symbol">)</a>
<a id="1768" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="1770" href="Relation.Unary.Closure.Base.html#1770" class="Bound">T</a> <a id="1772" href="Relation.Unary.Closure.Base.html#1772" class="Bound">x</a> <a id="1774" class="Symbol">=</a> <a id="1776" href="Data.Product.html#925" class="Function">Σ[</a> <a id="1779" href="Relation.Unary.Closure.Base.html#1779" class="Bound">support</a> <a id="1787" href="Data.Product.html#925" class="Function">∈</a> <a id="1789" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="1791" href="Data.Product.html#925" class="Function">]</a> <a id="1793" class="Symbol">(</a><a id="1794" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="1796" href="Relation.Unary.Closure.Base.html#1779" class="Bound">support</a> <a id="1804" href="Relation.Unary.Closure.Base.html#1772" class="Bound">x</a> <a id="1806" href="Data.Product.html#1176" class="Function Operator">×</a> <a id="1808" href="Relation.Unary.Closure.Base.html#1770" class="Bound">T</a> <a id="1810" href="Relation.Unary.Closure.Base.html#1779" class="Bound">support</a><a id="1817" class="Symbol">)</a>

<a id="1820" class="Comment">-- Use cases of ◇ include:</a>
<a id="1847" class="Comment">-- * The characterization of strengthening: picking R to be</a>
<a id="1907" class="Comment">--   `Data.List.Relation.Sublist.Inductive`, `∀ {Γ} → Tm Γ → ◇ Tm Γ`</a>
<a id="1976" class="Comment">--   is the type of a function strengthening a term to its support:</a>
<a id="2044" class="Comment">--   all the unused variables are discarded early on by the `related`</a>
<a id="2114" class="Comment">--   proof.</a>
<a id="2126" class="Comment">-- Cf. Conor McBride&#39;s &quot;Everybody&#39;s got to be somewhere&quot; for a more</a>
<a id="2194" class="Comment">-- detailed treatment of such an example.</a>

<a id="2237" class="Comment">-- Closed</a>

<a id="2248" class="Comment">-- Whenever we have a value in one context, we can get one in any</a>
<a id="2314" class="Comment">-- related context.</a>

<a id="2335" class="Keyword">record</a> <a id="Closed"></a><a id="2342" href="Relation.Unary.Closure.Base.html#2342" class="Record">Closed</a> <a id="2349" class="Symbol">{</a><a id="2350" href="Relation.Unary.Closure.Base.html#2350" class="Bound">t</a><a id="2351" class="Symbol">}</a> <a id="2353" class="Symbol">(</a><a id="2354" href="Relation.Unary.Closure.Base.html#2354" class="Bound">T</a> <a id="2356" class="Symbol">:</a> <a id="2358" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2363" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="2365" href="Relation.Unary.Closure.Base.html#2350" class="Bound">t</a><a id="2366" class="Symbol">)</a> <a id="2368" class="Symbol">:</a> <a id="2370" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2374" class="Symbol">(</a><a id="2375" href="Relation.Unary.Closure.Base.html#352" class="Bound">a</a> <a id="2377" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2379" href="Relation.Unary.Closure.Base.html#354" class="Bound">b</a> <a id="2381" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2383" href="Relation.Unary.Closure.Base.html#2350" class="Bound">t</a><a id="2384" class="Symbol">)</a> <a id="2386" class="Keyword">where</a>
  <a id="2394" class="Keyword">field</a> <a id="Closed.next"></a><a id="2400" href="Relation.Unary.Closure.Base.html#2400" class="Field">next</a> <a id="2405" class="Symbol">:</a> <a id="2407" class="Symbol">∀</a> <a id="2409" class="Symbol">{</a><a id="2410" href="Relation.Unary.Closure.Base.html#2410" class="Bound">x</a><a id="2411" class="Symbol">}</a> <a id="2413" class="Symbol">→</a> <a id="2415" href="Relation.Unary.Closure.Base.html#2354" class="Bound">T</a> <a id="2417" href="Relation.Unary.Closure.Base.html#2410" class="Bound">x</a> <a id="2419" class="Symbol">→</a> <a id="2421" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="2423" href="Relation.Unary.Closure.Base.html#2354" class="Bound">T</a> <a id="2425" href="Relation.Unary.Closure.Base.html#2410" class="Bound">x</a>


<a id="2429" class="Comment">------------------------------------------------------------------------</a>
<a id="2502" class="Comment">-- Basic functions relating □ and ◇</a>

<a id="2539" class="Keyword">module</a> <a id="2546" href="Relation.Unary.Closure.Base.html#2546" class="Module">_</a> <a id="2548" class="Symbol">{</a><a id="2549" href="Relation.Unary.Closure.Base.html#2549" class="Bound">t</a> <a id="2551" href="Relation.Unary.Closure.Base.html#2551" class="Bound">p</a><a id="2552" class="Symbol">}</a> <a id="2554" class="Symbol">{</a><a id="2555" href="Relation.Unary.Closure.Base.html#2555" class="Bound">T</a> <a id="2557" class="Symbol">:</a> <a id="2559" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2564" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="2566" href="Relation.Unary.Closure.Base.html#2549" class="Bound">t</a><a id="2567" class="Symbol">}</a> <a id="2569" class="Symbol">{</a><a id="2570" href="Relation.Unary.Closure.Base.html#2570" class="Bound">P</a> <a id="2572" class="Symbol">:</a> <a id="2574" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2579" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="2581" href="Relation.Unary.Closure.Base.html#2551" class="Bound">p</a><a id="2582" class="Symbol">}</a> <a id="2584" class="Keyword">where</a>

  <a id="2593" href="Relation.Unary.Closure.Base.html#2593" class="Function">curry</a> <a id="2599" class="Symbol">:</a> <a id="2601" class="Symbol">(∀</a> <a id="2604" class="Symbol">{</a><a id="2605" href="Relation.Unary.Closure.Base.html#2605" class="Bound">x</a><a id="2606" class="Symbol">}</a> <a id="2608" class="Symbol">→</a> <a id="2610" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="2612" href="Relation.Unary.Closure.Base.html#2555" class="Bound">T</a> <a id="2614" href="Relation.Unary.Closure.Base.html#2605" class="Bound">x</a> <a id="2616" class="Symbol">→</a> <a id="2618" href="Relation.Unary.Closure.Base.html#2570" class="Bound">P</a> <a id="2620" href="Relation.Unary.Closure.Base.html#2605" class="Bound">x</a><a id="2621" class="Symbol">)</a> <a id="2623" class="Symbol">→</a> <a id="2625" class="Symbol">(∀</a> <a id="2628" class="Symbol">{</a><a id="2629" href="Relation.Unary.Closure.Base.html#2629" class="Bound">x</a><a id="2630" class="Symbol">}</a> <a id="2632" class="Symbol">→</a> <a id="2634" href="Relation.Unary.Closure.Base.html#2555" class="Bound">T</a> <a id="2636" href="Relation.Unary.Closure.Base.html#2629" class="Bound">x</a> <a id="2638" class="Symbol">→</a> <a id="2640" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="2642" href="Relation.Unary.Closure.Base.html#2570" class="Bound">P</a> <a id="2644" href="Relation.Unary.Closure.Base.html#2629" class="Bound">x</a><a id="2645" class="Symbol">)</a>
  <a id="2649" href="Relation.Unary.Closure.Base.html#2593" class="Function">curry</a> <a id="2655" href="Relation.Unary.Closure.Base.html#2655" class="Bound">f</a> <a id="2657" href="Relation.Unary.Closure.Base.html#2657" class="Bound">tx</a> <a id="2660" href="Relation.Unary.Closure.Base.html#2660" class="Bound">x∼y</a> <a id="2664" class="Symbol">=</a> <a id="2666" href="Relation.Unary.Closure.Base.html#2655" class="Bound">f</a> <a id="2668" class="Symbol">(</a><a id="2669" href="Data.Product.html#2256" class="Function Operator">-,</a> <a id="2672" href="Relation.Unary.Closure.Base.html#2660" class="Bound">x∼y</a> <a id="2676" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2678" href="Relation.Unary.Closure.Base.html#2657" class="Bound">tx</a><a id="2680" class="Symbol">)</a>

  <a id="2685" href="Relation.Unary.Closure.Base.html#2685" class="Function">uncurry</a> <a id="2693" class="Symbol">:</a> <a id="2695" class="Symbol">(∀</a> <a id="2698" class="Symbol">{</a><a id="2699" href="Relation.Unary.Closure.Base.html#2699" class="Bound">x</a><a id="2700" class="Symbol">}</a> <a id="2702" class="Symbol">→</a> <a id="2704" href="Relation.Unary.Closure.Base.html#2555" class="Bound">T</a> <a id="2706" href="Relation.Unary.Closure.Base.html#2699" class="Bound">x</a> <a id="2708" class="Symbol">→</a> <a id="2710" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="2712" href="Relation.Unary.Closure.Base.html#2570" class="Bound">P</a> <a id="2714" href="Relation.Unary.Closure.Base.html#2699" class="Bound">x</a><a id="2715" class="Symbol">)</a> <a id="2717" class="Symbol">→</a> <a id="2719" class="Symbol">(∀</a> <a id="2722" class="Symbol">{</a><a id="2723" href="Relation.Unary.Closure.Base.html#2723" class="Bound">x</a><a id="2724" class="Symbol">}</a> <a id="2726" class="Symbol">→</a> <a id="2728" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="2730" href="Relation.Unary.Closure.Base.html#2555" class="Bound">T</a> <a id="2732" href="Relation.Unary.Closure.Base.html#2723" class="Bound">x</a> <a id="2734" class="Symbol">→</a> <a id="2736" href="Relation.Unary.Closure.Base.html#2570" class="Bound">P</a> <a id="2738" href="Relation.Unary.Closure.Base.html#2723" class="Bound">x</a><a id="2739" class="Symbol">)</a>
  <a id="2743" href="Relation.Unary.Closure.Base.html#2685" class="Function">uncurry</a> <a id="2751" href="Relation.Unary.Closure.Base.html#2751" class="Bound">f</a> <a id="2753" class="Symbol">(_</a> <a id="2756" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2758" href="Relation.Unary.Closure.Base.html#2758" class="Bound">y∼x</a> <a id="2762" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2764" href="Relation.Unary.Closure.Base.html#2764" class="Bound">ty</a><a id="2766" class="Symbol">)</a> <a id="2768" class="Symbol">=</a> <a id="2770" href="Relation.Unary.Closure.Base.html#2751" class="Bound">f</a> <a id="2772" href="Relation.Unary.Closure.Base.html#2764" class="Bound">ty</a> <a id="2775" href="Relation.Unary.Closure.Base.html#2758" class="Bound">y∼x</a>

<a id="2780" class="Comment">------------------------------------------------------------------------</a>
<a id="2853" class="Comment">-- Properties</a>

<a id="2868" class="Keyword">module</a> <a id="□"></a><a id="2875" href="Relation.Unary.Closure.Base.html#2875" class="Module">□</a> <a id="2877" class="Symbol">{</a><a id="2878" href="Relation.Unary.Closure.Base.html#2878" class="Bound">t</a><a id="2879" class="Symbol">}</a> <a id="2881" class="Symbol">{</a><a id="2882" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a> <a id="2884" class="Symbol">:</a> <a id="2886" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2891" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="2893" href="Relation.Unary.Closure.Base.html#2878" class="Bound">t</a><a id="2894" class="Symbol">}</a> <a id="2896" class="Keyword">where</a>

  <a id="□.reindex"></a><a id="2905" href="Relation.Unary.Closure.Base.html#2905" class="Function">reindex</a> <a id="2913" class="Symbol">:</a> <a id="2915" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="2926" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="2928" class="Symbol">→</a> <a id="2930" class="Symbol">∀</a> <a id="2932" class="Symbol">{</a><a id="2933" href="Relation.Unary.Closure.Base.html#2933" class="Bound">x</a> <a id="2935" href="Relation.Unary.Closure.Base.html#2935" class="Bound">y</a><a id="2936" class="Symbol">}</a> <a id="2938" class="Symbol">→</a> <a id="2940" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="2942" href="Relation.Unary.Closure.Base.html#2933" class="Bound">x</a> <a id="2944" href="Relation.Unary.Closure.Base.html#2935" class="Bound">y</a> <a id="2946" class="Symbol">→</a> <a id="2948" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="2950" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a> <a id="2952" href="Relation.Unary.Closure.Base.html#2933" class="Bound">x</a> <a id="2954" class="Symbol">→</a> <a id="2956" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="2958" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a> <a id="2960" href="Relation.Unary.Closure.Base.html#2935" class="Bound">y</a>
  <a id="2964" href="Relation.Unary.Closure.Base.html#2905" class="Function">reindex</a> <a id="2972" href="Relation.Unary.Closure.Base.html#2972" class="Bound">trans</a> <a id="2978" href="Relation.Unary.Closure.Base.html#2978" class="Bound">x∼y</a> <a id="2982" href="Relation.Unary.Closure.Base.html#2982" class="Bound">□Tx</a> <a id="2986" href="Relation.Unary.Closure.Base.html#2986" class="Bound">y∼z</a> <a id="2990" class="Symbol">=</a> <a id="2992" href="Relation.Unary.Closure.Base.html#2982" class="Bound">□Tx</a> <a id="2996" class="Symbol">(</a><a id="2997" href="Relation.Unary.Closure.Base.html#2972" class="Bound">trans</a> <a id="3003" href="Relation.Unary.Closure.Base.html#2978" class="Bound">x∼y</a> <a id="3007" href="Relation.Unary.Closure.Base.html#2986" class="Bound">y∼z</a><a id="3010" class="Symbol">)</a>

  <a id="3015" class="Comment">-- Provided that R is reflexive and Transitive, □ is a comonad</a>
  <a id="□.map"></a><a id="3080" href="Relation.Unary.Closure.Base.html#3080" class="Function">map</a> <a id="3084" class="Symbol">:</a> <a id="3086" class="Symbol">∀</a> <a id="3088" class="Symbol">{</a><a id="3089" href="Relation.Unary.Closure.Base.html#3089" class="Bound">u</a><a id="3090" class="Symbol">}</a> <a id="3092" class="Symbol">{</a><a id="3093" href="Relation.Unary.Closure.Base.html#3093" class="Bound">U</a> <a id="3095" class="Symbol">:</a> <a id="3097" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="3102" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="3104" href="Relation.Unary.Closure.Base.html#3089" class="Bound">u</a><a id="3105" class="Symbol">}</a> <a id="3107" class="Symbol">{</a><a id="3108" href="Relation.Unary.Closure.Base.html#3108" class="Bound">x</a><a id="3109" class="Symbol">}</a> <a id="3111" class="Symbol">→</a> <a id="3113" class="Symbol">(∀</a> <a id="3116" class="Symbol">{</a><a id="3117" href="Relation.Unary.Closure.Base.html#3117" class="Bound">x</a><a id="3118" class="Symbol">}</a> <a id="3120" class="Symbol">→</a> <a id="3122" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a> <a id="3124" href="Relation.Unary.Closure.Base.html#3117" class="Bound">x</a> <a id="3126" class="Symbol">→</a> <a id="3128" href="Relation.Unary.Closure.Base.html#3093" class="Bound">U</a> <a id="3130" href="Relation.Unary.Closure.Base.html#3117" class="Bound">x</a><a id="3131" class="Symbol">)</a> <a id="3133" class="Symbol">→</a> <a id="3135" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="3137" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a> <a id="3139" href="Relation.Unary.Closure.Base.html#3108" class="Bound">x</a> <a id="3141" class="Symbol">→</a> <a id="3143" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="3145" href="Relation.Unary.Closure.Base.html#3093" class="Bound">U</a> <a id="3147" href="Relation.Unary.Closure.Base.html#3108" class="Bound">x</a>
  <a id="3151" href="Relation.Unary.Closure.Base.html#3080" class="Function">map</a> <a id="3155" href="Relation.Unary.Closure.Base.html#3155" class="Bound">f</a> <a id="3157" href="Relation.Unary.Closure.Base.html#3157" class="Bound">□Tx</a> <a id="3161" href="Relation.Unary.Closure.Base.html#3161" class="Bound">x~y</a> <a id="3165" class="Symbol">=</a> <a id="3167" href="Relation.Unary.Closure.Base.html#3155" class="Bound">f</a> <a id="3169" class="Symbol">(</a><a id="3170" href="Relation.Unary.Closure.Base.html#3157" class="Bound">□Tx</a> <a id="3174" href="Relation.Unary.Closure.Base.html#3161" class="Bound">x~y</a><a id="3177" class="Symbol">)</a>

  <a id="□.extract"></a><a id="3182" href="Relation.Unary.Closure.Base.html#3182" class="Function">extract</a> <a id="3190" class="Symbol">:</a> <a id="3192" href="Relation.Binary.Definitions.html#1348" class="Function">Reflexive</a> <a id="3202" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="3204" class="Symbol">→</a> <a id="3206" class="Symbol">∀</a> <a id="3208" class="Symbol">{</a><a id="3209" href="Relation.Unary.Closure.Base.html#3209" class="Bound">x</a><a id="3210" class="Symbol">}</a> <a id="3212" class="Symbol">→</a> <a id="3214" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="3216" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a> <a id="3218" href="Relation.Unary.Closure.Base.html#3209" class="Bound">x</a> <a id="3220" class="Symbol">→</a> <a id="3222" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a> <a id="3224" href="Relation.Unary.Closure.Base.html#3209" class="Bound">x</a>
  <a id="3228" href="Relation.Unary.Closure.Base.html#3182" class="Function">extract</a> <a id="3236" href="Relation.Unary.Closure.Base.html#3236" class="Bound">refl</a> <a id="3241" href="Relation.Unary.Closure.Base.html#3241" class="Bound">□Tx</a> <a id="3245" class="Symbol">=</a> <a id="3247" href="Relation.Unary.Closure.Base.html#3241" class="Bound">□Tx</a> <a id="3251" href="Relation.Unary.Closure.Base.html#3236" class="Bound">refl</a>

  <a id="□.duplicate"></a><a id="3259" href="Relation.Unary.Closure.Base.html#3259" class="Function">duplicate</a> <a id="3269" class="Symbol">:</a> <a id="3271" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="3282" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="3284" class="Symbol">→</a> <a id="3286" class="Symbol">∀</a> <a id="3288" class="Symbol">{</a><a id="3289" href="Relation.Unary.Closure.Base.html#3289" class="Bound">x</a><a id="3290" class="Symbol">}</a> <a id="3292" class="Symbol">→</a> <a id="3294" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="3296" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a> <a id="3298" href="Relation.Unary.Closure.Base.html#3289" class="Bound">x</a> <a id="3300" class="Symbol">→</a> <a id="3302" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="3304" class="Symbol">(</a><a id="3305" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="3307" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a><a id="3308" class="Symbol">)</a> <a id="3310" href="Relation.Unary.Closure.Base.html#3289" class="Bound">x</a>
  <a id="3314" href="Relation.Unary.Closure.Base.html#3259" class="Function">duplicate</a> <a id="3324" href="Relation.Unary.Closure.Base.html#3324" class="Bound">trans</a> <a id="3330" href="Relation.Unary.Closure.Base.html#3330" class="Bound">□Tx</a> <a id="3334" href="Relation.Unary.Closure.Base.html#3334" class="Bound">x∼y</a> <a id="3338" href="Relation.Unary.Closure.Base.html#3338" class="Bound">y∼z</a> <a id="3342" class="Symbol">=</a> <a id="3344" href="Relation.Unary.Closure.Base.html#3330" class="Bound">□Tx</a> <a id="3348" class="Symbol">(</a><a id="3349" href="Relation.Unary.Closure.Base.html#3324" class="Bound">trans</a> <a id="3355" href="Relation.Unary.Closure.Base.html#3334" class="Bound">x∼y</a> <a id="3359" href="Relation.Unary.Closure.Base.html#3338" class="Bound">y∼z</a><a id="3362" class="Symbol">)</a>

  <a id="3367" class="Comment">-- Provided that R is transitive, □ is a closure operator</a>
  <a id="3427" class="Comment">-- i.e. for any `T`, `□ T` is closed.</a>
  <a id="□.closed"></a><a id="3467" href="Relation.Unary.Closure.Base.html#3467" class="Function">closed</a> <a id="3474" class="Symbol">:</a> <a id="3476" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="3487" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="3489" class="Symbol">→</a> <a id="3491" href="Relation.Unary.Closure.Base.html#2342" class="Record">Closed</a> <a id="3498" class="Symbol">(</a><a id="3499" href="Relation.Unary.Closure.Base.html#852" class="Function">□</a> <a id="3501" href="Relation.Unary.Closure.Base.html#2882" class="Bound">T</a><a id="3502" class="Symbol">)</a>
  <a id="3506" href="Relation.Unary.Closure.Base.html#3467" class="Function">closed</a> <a id="3513" href="Relation.Unary.Closure.Base.html#3513" class="Bound">trans</a> <a id="3519" class="Symbol">=</a> <a id="3521" class="Keyword">record</a> <a id="3528" class="Symbol">{</a> <a id="3530" href="Relation.Unary.Closure.Base.html#2400" class="Field">next</a> <a id="3535" class="Symbol">=</a> <a id="3537" href="Relation.Unary.Closure.Base.html#3259" class="Function">duplicate</a> <a id="3547" href="Relation.Unary.Closure.Base.html#3513" class="Bound">trans</a> <a id="3553" class="Symbol">}</a>

<a id="3556" class="Keyword">module</a> <a id="◇"></a><a id="3563" href="Relation.Unary.Closure.Base.html#3563" class="Module">◇</a> <a id="3565" class="Symbol">{</a><a id="3566" href="Relation.Unary.Closure.Base.html#3566" class="Bound">t</a><a id="3567" class="Symbol">}</a> <a id="3569" class="Symbol">{</a><a id="3570" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="3572" class="Symbol">:</a> <a id="3574" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="3579" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="3581" href="Relation.Unary.Closure.Base.html#3566" class="Bound">t</a><a id="3582" class="Symbol">}</a> <a id="3584" class="Keyword">where</a>

  <a id="◇.reindex"></a><a id="3593" href="Relation.Unary.Closure.Base.html#3593" class="Function">reindex</a> <a id="3601" class="Symbol">:</a> <a id="3603" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="3614" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="3616" class="Symbol">→</a> <a id="3618" class="Symbol">∀</a> <a id="3620" class="Symbol">{</a><a id="3621" href="Relation.Unary.Closure.Base.html#3621" class="Bound">x</a> <a id="3623" href="Relation.Unary.Closure.Base.html#3623" class="Bound">y</a><a id="3624" class="Symbol">}</a> <a id="3626" class="Symbol">→</a> <a id="3628" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="3630" href="Relation.Unary.Closure.Base.html#3621" class="Bound">x</a> <a id="3632" href="Relation.Unary.Closure.Base.html#3623" class="Bound">y</a> <a id="3634" class="Symbol">→</a> <a id="3636" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="3638" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="3640" href="Relation.Unary.Closure.Base.html#3621" class="Bound">x</a> <a id="3642" class="Symbol">→</a> <a id="3644" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="3646" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="3648" href="Relation.Unary.Closure.Base.html#3623" class="Bound">y</a>
  <a id="3652" href="Relation.Unary.Closure.Base.html#3593" class="Function">reindex</a> <a id="3660" href="Relation.Unary.Closure.Base.html#3660" class="Bound">trans</a> <a id="3666" href="Relation.Unary.Closure.Base.html#3666" class="Bound">x∼y</a> <a id="3670" class="Symbol">(</a><a id="3671" href="Relation.Unary.Closure.Base.html#3671" class="Bound">z</a> <a id="3673" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3675" href="Relation.Unary.Closure.Base.html#3675" class="Bound">z∼x</a> <a id="3679" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3681" href="Relation.Unary.Closure.Base.html#3681" class="Bound">tz</a><a id="3683" class="Symbol">)</a> <a id="3685" class="Symbol">=</a> <a id="3687" href="Relation.Unary.Closure.Base.html#3671" class="Bound">z</a> <a id="3689" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3691" href="Relation.Unary.Closure.Base.html#3660" class="Bound">trans</a> <a id="3697" href="Relation.Unary.Closure.Base.html#3675" class="Bound">z∼x</a> <a id="3701" href="Relation.Unary.Closure.Base.html#3666" class="Bound">x∼y</a> <a id="3705" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3707" href="Relation.Unary.Closure.Base.html#3681" class="Bound">tz</a>

  <a id="3713" class="Comment">-- Provided that R is reflexive and Transitive, ◇ is a monad</a>
  <a id="◇.map"></a><a id="3776" href="Relation.Unary.Closure.Base.html#3776" class="Function">map</a> <a id="3780" class="Symbol">:</a> <a id="3782" class="Symbol">∀</a> <a id="3784" class="Symbol">{</a><a id="3785" href="Relation.Unary.Closure.Base.html#3785" class="Bound">u</a><a id="3786" class="Symbol">}</a> <a id="3788" class="Symbol">{</a><a id="3789" href="Relation.Unary.Closure.Base.html#3789" class="Bound">U</a> <a id="3791" class="Symbol">:</a> <a id="3793" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="3798" href="Relation.Unary.Closure.Base.html#358" class="Bound">A</a> <a id="3800" href="Relation.Unary.Closure.Base.html#3785" class="Bound">u</a><a id="3801" class="Symbol">}</a> <a id="3803" class="Symbol">{</a><a id="3804" href="Relation.Unary.Closure.Base.html#3804" class="Bound">x</a><a id="3805" class="Symbol">}</a> <a id="3807" class="Symbol">→</a> <a id="3809" class="Symbol">(∀</a> <a id="3812" class="Symbol">{</a><a id="3813" href="Relation.Unary.Closure.Base.html#3813" class="Bound">x</a><a id="3814" class="Symbol">}</a> <a id="3816" class="Symbol">→</a> <a id="3818" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="3820" href="Relation.Unary.Closure.Base.html#3813" class="Bound">x</a> <a id="3822" class="Symbol">→</a> <a id="3824" href="Relation.Unary.Closure.Base.html#3789" class="Bound">U</a> <a id="3826" href="Relation.Unary.Closure.Base.html#3813" class="Bound">x</a><a id="3827" class="Symbol">)</a> <a id="3829" class="Symbol">→</a> <a id="3831" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="3833" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="3835" href="Relation.Unary.Closure.Base.html#3804" class="Bound">x</a> <a id="3837" class="Symbol">→</a> <a id="3839" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="3841" href="Relation.Unary.Closure.Base.html#3789" class="Bound">U</a> <a id="3843" href="Relation.Unary.Closure.Base.html#3804" class="Bound">x</a>
  <a id="3847" href="Relation.Unary.Closure.Base.html#3776" class="Function">map</a> <a id="3851" href="Relation.Unary.Closure.Base.html#3851" class="Bound">f</a> <a id="3853" class="Symbol">(</a><a id="3854" href="Relation.Unary.Closure.Base.html#3854" class="Bound">y</a> <a id="3856" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3858" href="Relation.Unary.Closure.Base.html#3858" class="Bound">y∼x</a> <a id="3862" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3864" href="Relation.Unary.Closure.Base.html#3864" class="Bound">ty</a><a id="3866" class="Symbol">)</a> <a id="3868" class="Symbol">=</a> <a id="3870" href="Relation.Unary.Closure.Base.html#3854" class="Bound">y</a> <a id="3872" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3874" href="Relation.Unary.Closure.Base.html#3858" class="Bound">y∼x</a> <a id="3878" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3880" href="Relation.Unary.Closure.Base.html#3851" class="Bound">f</a> <a id="3882" href="Relation.Unary.Closure.Base.html#3864" class="Bound">ty</a>

  <a id="◇.pure"></a><a id="3888" href="Relation.Unary.Closure.Base.html#3888" class="Function">pure</a> <a id="3893" class="Symbol">:</a> <a id="3895" href="Relation.Binary.Definitions.html#1348" class="Function">Reflexive</a> <a id="3905" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="3907" class="Symbol">→</a> <a id="3909" class="Symbol">∀</a> <a id="3911" class="Symbol">{</a><a id="3912" href="Relation.Unary.Closure.Base.html#3912" class="Bound">x</a><a id="3913" class="Symbol">}</a> <a id="3915" class="Symbol">→</a> <a id="3917" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="3919" href="Relation.Unary.Closure.Base.html#3912" class="Bound">x</a> <a id="3921" class="Symbol">→</a> <a id="3923" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="3925" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="3927" href="Relation.Unary.Closure.Base.html#3912" class="Bound">x</a>
  <a id="3931" href="Relation.Unary.Closure.Base.html#3888" class="Function">pure</a> <a id="3936" href="Relation.Unary.Closure.Base.html#3936" class="Bound">refl</a> <a id="3941" href="Relation.Unary.Closure.Base.html#3941" class="Bound">tx</a> <a id="3944" class="Symbol">=</a> <a id="3946" href="Data.Product.html#2256" class="Function Operator">-,</a> <a id="3949" href="Relation.Unary.Closure.Base.html#3936" class="Bound">refl</a> <a id="3954" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3956" href="Relation.Unary.Closure.Base.html#3941" class="Bound">tx</a>

  <a id="◇.join"></a><a id="3962" href="Relation.Unary.Closure.Base.html#3962" class="Function">join</a> <a id="3967" class="Symbol">:</a> <a id="3969" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="3980" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="3982" class="Symbol">→</a> <a id="3984" class="Symbol">∀</a> <a id="3986" class="Symbol">{</a><a id="3987" href="Relation.Unary.Closure.Base.html#3987" class="Bound">x</a><a id="3988" class="Symbol">}</a> <a id="3990" class="Symbol">→</a> <a id="3992" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="3994" class="Symbol">(</a><a id="3995" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="3997" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a><a id="3998" class="Symbol">)</a> <a id="4000" href="Relation.Unary.Closure.Base.html#3987" class="Bound">x</a> <a id="4002" class="Symbol">→</a> <a id="4004" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="4006" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="4008" href="Relation.Unary.Closure.Base.html#3987" class="Bound">x</a>
  <a id="4012" href="Relation.Unary.Closure.Base.html#3962" class="Function">join</a> <a id="4017" href="Relation.Unary.Closure.Base.html#4017" class="Bound">trans</a> <a id="4023" class="Symbol">(_</a> <a id="4026" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4028" href="Relation.Unary.Closure.Base.html#4028" class="Bound">y∼x</a> <a id="4032" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4034" class="Symbol">_</a> <a id="4036" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4038" href="Relation.Unary.Closure.Base.html#4038" class="Bound">z∼y</a> <a id="4042" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4044" href="Relation.Unary.Closure.Base.html#4044" class="Bound">tz</a><a id="4046" class="Symbol">)</a> <a id="4048" class="Symbol">=</a> <a id="4050" class="Symbol">_</a> <a id="4052" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4054" href="Relation.Unary.Closure.Base.html#4017" class="Bound">trans</a> <a id="4060" href="Relation.Unary.Closure.Base.html#4038" class="Bound">z∼y</a> <a id="4064" href="Relation.Unary.Closure.Base.html#4028" class="Bound">y∼x</a> <a id="4068" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4070" href="Relation.Unary.Closure.Base.html#4044" class="Bound">tz</a>

  <a id="4076" class="Comment">-- Provided that R is transitive, ◇ is a closure operator</a>
  <a id="4136" class="Comment">-- i.e. for any `T`, `◇ T` is closed.</a>
  <a id="◇.closed"></a><a id="4176" href="Relation.Unary.Closure.Base.html#4176" class="Function">closed</a> <a id="4183" class="Symbol">:</a> <a id="4185" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="4196" href="Relation.Unary.Closure.Base.html#370" class="Bound">R</a> <a id="4198" class="Symbol">→</a> <a id="4200" href="Relation.Unary.Closure.Base.html#2342" class="Record">Closed</a> <a id="4207" class="Symbol">(</a><a id="4208" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="4210" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a><a id="4211" class="Symbol">)</a>
  <a id="4215" href="Relation.Unary.Closure.Base.html#4176" class="Function">closed</a> <a id="4222" href="Relation.Unary.Closure.Base.html#4222" class="Bound">trans</a> <a id="4228" class="Symbol">=</a> <a id="4230" class="Keyword">record</a> <a id="4237" class="Symbol">{</a> <a id="4239" href="Relation.Unary.Closure.Base.html#2400" class="Field">next</a> <a id="4244" class="Symbol">=</a> <a id="4246" class="Symbol">λ</a> <a id="4248" href="Relation.Unary.Closure.Base.html#4248" class="Bound">◇Tx</a> <a id="4252" href="Relation.Unary.Closure.Base.html#4252" class="Bound">x∼y</a> <a id="4256" class="Symbol">→</a> <a id="4258" href="Relation.Unary.Closure.Base.html#3593" class="Function">reindex</a> <a id="4266" href="Relation.Unary.Closure.Base.html#4222" class="Bound">trans</a> <a id="4272" href="Relation.Unary.Closure.Base.html#4252" class="Bound">x∼y</a> <a id="4276" href="Relation.Unary.Closure.Base.html#4248" class="Bound">◇Tx</a> <a id="4280" class="Symbol">}</a>

  <a id="◇.run"></a><a id="4285" href="Relation.Unary.Closure.Base.html#4285" class="Function">run</a> <a id="4289" class="Symbol">:</a> <a id="4291" href="Relation.Unary.Closure.Base.html#2342" class="Record">Closed</a> <a id="4298" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="4300" class="Symbol">→</a> <a id="4302" class="Symbol">∀</a> <a id="4304" class="Symbol">{</a><a id="4305" href="Relation.Unary.Closure.Base.html#4305" class="Bound">x</a><a id="4306" class="Symbol">}</a> <a id="4308" class="Symbol">→</a> <a id="4310" href="Relation.Unary.Closure.Base.html#1726" class="Function">◇</a> <a id="4312" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="4314" href="Relation.Unary.Closure.Base.html#4305" class="Bound">x</a> <a id="4316" class="Symbol">→</a> <a id="4318" href="Relation.Unary.Closure.Base.html#3570" class="Bound">T</a> <a id="4320" href="Relation.Unary.Closure.Base.html#4305" class="Bound">x</a>
  <a id="4324" href="Relation.Unary.Closure.Base.html#4285" class="Function">run</a> <a id="4328" href="Relation.Unary.Closure.Base.html#4328" class="Bound">closed</a> <a id="4335" class="Symbol">(_</a> <a id="4338" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4340" href="Relation.Unary.Closure.Base.html#4340" class="Bound">y∼x</a> <a id="4344" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4346" href="Relation.Unary.Closure.Base.html#4346" class="Bound">ty</a><a id="4348" class="Symbol">)</a> <a id="4350" class="Symbol">=</a> <a id="4352" href="Relation.Unary.Closure.Base.html#2400" class="Field">Closed.next</a> <a id="4364" href="Relation.Unary.Closure.Base.html#4328" class="Bound">closed</a> <a id="4371" href="Relation.Unary.Closure.Base.html#4346" class="Bound">ty</a> <a id="4374" href="Relation.Unary.Closure.Base.html#4340" class="Bound">y∼x</a>
</pre></body></html>
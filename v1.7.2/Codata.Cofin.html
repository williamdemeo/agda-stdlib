<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Cofin</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- &quot;Finite&quot; sets indexed on coinductive &quot;natural&quot; numbers</a>
<a id="164" class="Comment">------------------------------------------------------------------------</a>

<a id="238" class="Symbol">{-#</a> <a id="242" class="Keyword">OPTIONS</a> <a id="250" class="Pragma">--cubical-compatible</a> <a id="271" class="Pragma">--sized-types</a> <a id="285" class="Symbol">#-}</a>

<a id="290" class="Keyword">module</a> <a id="297" href="Codata.Cofin.html" class="Module">Codata.Cofin</a> <a id="310" class="Keyword">where</a>

<a id="317" class="Keyword">open</a> <a id="322" class="Keyword">import</a> <a id="329" href="Size.html" class="Module">Size</a>
<a id="334" class="Keyword">open</a> <a id="339" class="Keyword">import</a> <a id="346" href="Codata.Thunk.html" class="Module">Codata.Thunk</a>
<a id="359" class="Keyword">open</a> <a id="364" class="Keyword">import</a> <a id="371" href="Codata.Conat.html" class="Module">Codata.Conat</a> <a id="384" class="Symbol">as</a> <a id="387" class="Module">Conat</a>
  <a id="395" class="Keyword">using</a> <a id="401" class="Symbol">(</a><a id="402" href="Codata.Conat.html#529" class="Datatype">Conat</a><a id="407" class="Symbol">;</a> <a id="409" href="Codata.Conat.html#560" class="InductiveConstructor">zero</a><a id="413" class="Symbol">;</a> <a id="415" href="Codata.Conat.html#577" class="InductiveConstructor">suc</a><a id="418" class="Symbol">;</a> <a id="420" href="Codata.Conat.html#608" class="Function">infinity</a><a id="428" class="Symbol">;</a> <a id="430" href="Codata.Conat.html#2372" class="Function Operator">_ℕ&lt;_</a><a id="434" class="Symbol">;</a> <a id="436" href="Codata.Conat.html#2323" class="InductiveConstructor">sℕ≤s</a><a id="440" class="Symbol">;</a> <a id="442" href="Codata.Conat.html#2418" class="Function Operator">_ℕ≤infinity</a><a id="453" class="Symbol">)</a>
<a id="455" class="Keyword">open</a> <a id="460" class="Keyword">import</a> <a id="467" href="Codata.Conat.Bisimilarity.html" class="Module">Codata.Conat.Bisimilarity</a> <a id="493" class="Symbol">as</a> <a id="496" class="Module">Bisim</a> <a id="502" class="Keyword">using</a> <a id="508" class="Symbol">(</a><a id="509" href="Codata.Conat.Bisimilarity.html#1282" class="Datatype Operator">_⊢_≲_</a> <a id="515" class="Symbol">;</a> <a id="517" href="Codata.Conat.Bisimilarity.html#1351" class="InductiveConstructor">s≲s</a><a id="520" class="Symbol">)</a>
<a id="522" class="Keyword">open</a> <a id="527" class="Keyword">import</a> <a id="534" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a>
<a id="548" class="Keyword">open</a> <a id="553" class="Keyword">import</a> <a id="560" href="Data.Fin.Base.html" class="Module">Data.Fin.Base</a> <a id="574" class="Symbol">as</a> <a id="577" class="Module">Fin</a> <a id="581" class="Keyword">hiding</a> <a id="588" class="Symbol">(</a><a id="589" href="Data.Fin.Base.html#1876" class="Function">fromℕ</a><a id="594" class="Symbol">;</a> <a id="596" href="Data.Fin.Base.html#8497" class="Function">fromℕ≤</a><a id="602" class="Symbol">;</a> <a id="604" href="Data.Fin.Base.html#1982" class="Function">fromℕ&lt;</a><a id="610" class="Symbol">;</a> <a id="612" href="Data.Fin.Base.html#1261" class="Function">toℕ</a><a id="615" class="Symbol">)</a>
<a id="617" class="Keyword">open</a> <a id="622" class="Keyword">import</a> <a id="629" href="Function.html" class="Module">Function</a>
<a id="638" class="Keyword">open</a> <a id="643" class="Keyword">import</a> <a id="650" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>

<a id="689" class="Comment">------------------------------------------------------------------------</a>
<a id="762" class="Comment">-- The type</a>

<a id="775" class="Comment">-- Note that `Cofin infnity` is /not/ finite. Note also that this is not a</a>
<a id="850" class="Comment">-- coinductive type, but it is indexed on a coinductive type.</a>

<a id="913" class="Keyword">data</a> <a id="Cofin"></a><a id="918" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="924" class="Symbol">:</a> <a id="926" href="Codata.Conat.html#529" class="Datatype">Conat</a> <a id="932" href="Agda.Builtin.Size.html#298" class="Postulate">∞</a> <a id="934" class="Symbol">→</a> <a id="936" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="940" class="Keyword">where</a>
  <a id="Cofin.zero"></a><a id="948" href="Codata.Cofin.html#948" class="InductiveConstructor">zero</a> <a id="953" class="Symbol">:</a> <a id="955" class="Symbol">∀</a> <a id="957" class="Symbol">{</a><a id="958" href="Codata.Cofin.html#958" class="Bound">n</a><a id="959" class="Symbol">}</a> <a id="961" class="Symbol">→</a> <a id="963" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="969" class="Symbol">(</a><a id="970" href="Codata.Conat.html#577" class="InductiveConstructor">suc</a> <a id="974" href="Codata.Cofin.html#958" class="Bound">n</a><a id="975" class="Symbol">)</a>
  <a id="Cofin.suc"></a><a id="979" href="Codata.Cofin.html#979" class="InductiveConstructor">suc</a>  <a id="984" class="Symbol">:</a> <a id="986" class="Symbol">∀</a> <a id="988" class="Symbol">{</a><a id="989" href="Codata.Cofin.html#989" class="Bound">n</a><a id="990" class="Symbol">}</a> <a id="992" class="Symbol">→</a> <a id="994" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="1000" class="Symbol">(</a><a id="1001" href="Codata.Cofin.html#989" class="Bound">n</a> <a id="1003" class="Symbol">.</a><a id="1004" href="Codata.Thunk.html#550" class="Field">force</a><a id="1009" class="Symbol">)</a> <a id="1011" class="Symbol">→</a> <a id="1013" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="1019" class="Symbol">(</a><a id="1020" href="Codata.Conat.html#577" class="InductiveConstructor">suc</a> <a id="1024" href="Codata.Cofin.html#989" class="Bound">n</a><a id="1025" class="Symbol">)</a>

<a id="suc-injective"></a><a id="1028" href="Codata.Cofin.html#1028" class="Function">suc-injective</a> <a id="1042" class="Symbol">:</a> <a id="1044" class="Symbol">∀</a> <a id="1046" class="Symbol">{</a><a id="1047" href="Codata.Cofin.html#1047" class="Bound">n</a><a id="1048" class="Symbol">}</a> <a id="1050" class="Symbol">{</a><a id="1051" href="Codata.Cofin.html#1051" class="Bound">p</a> <a id="1053" href="Codata.Cofin.html#1053" class="Bound">q</a> <a id="1055" class="Symbol">:</a> <a id="1057" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="1063" class="Symbol">(</a><a id="1064" href="Codata.Cofin.html#1047" class="Bound">n</a> <a id="1066" class="Symbol">.</a><a id="1067" href="Codata.Thunk.html#550" class="Field">force</a><a id="1072" class="Symbol">)}</a> <a id="1075" class="Symbol">→</a>
                <a id="1093" class="Symbol">(</a><a id="1094" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="1100" class="Symbol">(</a><a id="1101" class="InductiveConstructor">suc</a> <a id="1105" href="Codata.Cofin.html#1047" class="Bound">n</a><a id="1106" class="Symbol">)</a> <a id="1108" href="Function.Base.html#4571" class="Function Operator">∋</a> <a id="1110" class="InductiveConstructor">suc</a> <a id="1114" href="Codata.Cofin.html#1051" class="Bound">p</a><a id="1115" class="Symbol">)</a> <a id="1117" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1119" class="InductiveConstructor">suc</a> <a id="1123" href="Codata.Cofin.html#1053" class="Bound">q</a> <a id="1125" class="Symbol">→</a> <a id="1127" href="Codata.Cofin.html#1051" class="Bound">p</a> <a id="1129" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1131" href="Codata.Cofin.html#1053" class="Bound">q</a>
<a id="1133" href="Codata.Cofin.html#1028" class="Function">suc-injective</a> <a id="1147" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a> <a id="1152" class="Symbol">=</a> <a id="1154" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

<a id="1160" class="Comment">------------------------------------------------------------------------</a>
<a id="1233" class="Comment">-- Some operations</a>

<a id="fromℕ&lt;"></a><a id="1253" href="Codata.Cofin.html#1253" class="Function">fromℕ&lt;</a> <a id="1260" class="Symbol">:</a> <a id="1262" class="Symbol">∀</a> <a id="1264" class="Symbol">{</a><a id="1265" href="Codata.Cofin.html#1265" class="Bound">n</a> <a id="1267" href="Codata.Cofin.html#1267" class="Bound">k</a><a id="1268" class="Symbol">}</a> <a id="1270" class="Symbol">→</a> <a id="1272" href="Codata.Cofin.html#1267" class="Bound">k</a> <a id="1274" href="Codata.Conat.html#2372" class="Function Operator">ℕ&lt;</a> <a id="1277" href="Codata.Cofin.html#1265" class="Bound">n</a> <a id="1279" class="Symbol">→</a> <a id="1281" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="1287" href="Codata.Cofin.html#1265" class="Bound">n</a>
<a id="1289" href="Codata.Cofin.html#1253" class="Function">fromℕ&lt;</a> <a id="1296" class="Symbol">{</a><a id="1297" href="Codata.Conat.html#560" class="InductiveConstructor">zero</a><a id="1301" class="Symbol">}</a>  <a id="1304" class="Symbol">()</a>
<a id="1307" href="Codata.Cofin.html#1253" class="Function">fromℕ&lt;</a> <a id="1314" class="Symbol">{</a><a id="1315" href="Codata.Conat.html#577" class="InductiveConstructor">suc</a> <a id="1319" href="Codata.Cofin.html#1319" class="Bound">n</a><a id="1320" class="Symbol">}</a> <a id="1322" class="Symbol">{</a><a id="1323" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a><a id="1327" class="Symbol">}</a>  <a id="1330" class="Symbol">(</a><a id="1331" href="Codata.Conat.html#2323" class="InductiveConstructor">sℕ≤s</a> <a id="1336" href="Codata.Cofin.html#1336" class="Bound">p</a><a id="1337" class="Symbol">)</a> <a id="1339" class="Symbol">=</a> <a id="1341" href="Codata.Cofin.html#948" class="InductiveConstructor">zero</a>
<a id="1346" href="Codata.Cofin.html#1253" class="Function">fromℕ&lt;</a> <a id="1353" class="Symbol">{</a><a id="1354" href="Codata.Conat.html#577" class="InductiveConstructor">suc</a> <a id="1358" href="Codata.Cofin.html#1358" class="Bound">n</a><a id="1359" class="Symbol">}</a> <a id="1361" class="Symbol">{</a><a id="1362" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1366" href="Codata.Cofin.html#1366" class="Bound">k</a><a id="1367" class="Symbol">}</a> <a id="1369" class="Symbol">(</a><a id="1370" href="Codata.Conat.html#2323" class="InductiveConstructor">sℕ≤s</a> <a id="1375" href="Codata.Cofin.html#1375" class="Bound">p</a><a id="1376" class="Symbol">)</a> <a id="1378" class="Symbol">=</a> <a id="1380" href="Codata.Cofin.html#979" class="InductiveConstructor">suc</a> <a id="1384" class="Symbol">(</a><a id="1385" href="Codata.Cofin.html#1253" class="Function">fromℕ&lt;</a> <a id="1392" href="Codata.Cofin.html#1375" class="Bound">p</a><a id="1393" class="Symbol">)</a>

<a id="fromℕ"></a><a id="1396" href="Codata.Cofin.html#1396" class="Function">fromℕ</a> <a id="1402" class="Symbol">:</a> <a id="1404" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1406" class="Symbol">→</a> <a id="1408" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="1414" href="Codata.Conat.html#608" class="Function">infinity</a>
<a id="1423" href="Codata.Cofin.html#1396" class="Function">fromℕ</a> <a id="1429" href="Codata.Cofin.html#1429" class="Bound">k</a> <a id="1431" class="Symbol">=</a> <a id="1433" href="Codata.Cofin.html#1253" class="Function">fromℕ&lt;</a> <a id="1440" class="Symbol">(</a><a id="1441" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1445" href="Codata.Cofin.html#1429" class="Bound">k</a> <a id="1447" href="Codata.Conat.html#2418" class="Function Operator">ℕ≤infinity</a><a id="1457" class="Symbol">)</a>

<a id="toℕ"></a><a id="1460" href="Codata.Cofin.html#1460" class="Function">toℕ</a> <a id="1464" class="Symbol">:</a> <a id="1466" class="Symbol">∀</a> <a id="1468" class="Symbol">{</a><a id="1469" href="Codata.Cofin.html#1469" class="Bound">n</a><a id="1470" class="Symbol">}</a> <a id="1472" class="Symbol">→</a> <a id="1474" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="1480" href="Codata.Cofin.html#1469" class="Bound">n</a> <a id="1482" class="Symbol">→</a> <a id="1484" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a>
<a id="1486" href="Codata.Cofin.html#1460" class="Function">toℕ</a> <a id="1490" href="Codata.Cofin.html#948" class="InductiveConstructor">zero</a>    <a id="1498" class="Symbol">=</a> <a id="1500" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>
<a id="1505" href="Codata.Cofin.html#1460" class="Function">toℕ</a> <a id="1509" class="Symbol">(</a><a id="1510" href="Codata.Cofin.html#979" class="InductiveConstructor">suc</a> <a id="1514" href="Codata.Cofin.html#1514" class="Bound">i</a><a id="1515" class="Symbol">)</a> <a id="1517" class="Symbol">=</a> <a id="1519" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1523" class="Symbol">(</a><a id="1524" href="Codata.Cofin.html#1460" class="Function">toℕ</a> <a id="1528" href="Codata.Cofin.html#1514" class="Bound">i</a><a id="1529" class="Symbol">)</a>

<a id="fromFin"></a><a id="1532" href="Codata.Cofin.html#1532" class="Function">fromFin</a> <a id="1540" class="Symbol">:</a> <a id="1542" class="Symbol">∀</a> <a id="1544" class="Symbol">{</a><a id="1545" href="Codata.Cofin.html#1545" class="Bound">n</a><a id="1546" class="Symbol">}</a> <a id="1548" class="Symbol">→</a> <a id="1550" href="Data.Fin.Base.html#1135" class="Datatype">Fin</a> <a id="1554" href="Codata.Cofin.html#1545" class="Bound">n</a> <a id="1556" class="Symbol">→</a> <a id="1558" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="1564" class="Symbol">(</a><a id="1565" href="Codata.Conat.html#677" class="Function">Conat.fromℕ</a> <a id="1577" href="Codata.Cofin.html#1545" class="Bound">n</a><a id="1578" class="Symbol">)</a>
<a id="1580" href="Codata.Cofin.html#1532" class="Function">fromFin</a> <a id="1588" href="Data.Fin.Base.html#1157" class="InductiveConstructor">zero</a>    <a id="1596" class="Symbol">=</a> <a id="1598" href="Codata.Cofin.html#948" class="InductiveConstructor">zero</a>
<a id="1603" href="Codata.Cofin.html#1532" class="Function">fromFin</a> <a id="1611" class="Symbol">(</a><a id="1612" href="Data.Fin.Base.html#1188" class="InductiveConstructor">suc</a> <a id="1616" href="Codata.Cofin.html#1616" class="Bound">i</a><a id="1617" class="Symbol">)</a> <a id="1619" class="Symbol">=</a> <a id="1621" href="Codata.Cofin.html#979" class="InductiveConstructor">suc</a> <a id="1625" class="Symbol">(</a><a id="1626" href="Codata.Cofin.html#1532" class="Function">fromFin</a> <a id="1634" href="Codata.Cofin.html#1616" class="Bound">i</a><a id="1635" class="Symbol">)</a>

<a id="toFin"></a><a id="1638" href="Codata.Cofin.html#1638" class="Function">toFin</a> <a id="1644" class="Symbol">:</a> <a id="1646" class="Symbol">∀</a> <a id="1648" href="Codata.Cofin.html#1648" class="Bound">n</a> <a id="1650" class="Symbol">→</a> <a id="1652" href="Codata.Cofin.html#918" class="Datatype">Cofin</a> <a id="1658" class="Symbol">(</a><a id="1659" href="Codata.Conat.html#677" class="Function">Conat.fromℕ</a> <a id="1671" href="Codata.Cofin.html#1648" class="Bound">n</a><a id="1672" class="Symbol">)</a> <a id="1674" class="Symbol">→</a> <a id="1676" href="Data.Fin.Base.html#1135" class="Datatype">Fin</a> <a id="1680" href="Codata.Cofin.html#1648" class="Bound">n</a>
<a id="1682" href="Codata.Cofin.html#1638" class="Function">toFin</a> <a id="1688" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>    <a id="1696" class="Symbol">()</a>
<a id="1699" href="Codata.Cofin.html#1638" class="Function">toFin</a> <a id="1705" class="Symbol">(</a><a id="1706" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1710" href="Codata.Cofin.html#1710" class="Bound">n</a><a id="1711" class="Symbol">)</a> <a id="1713" href="Codata.Cofin.html#948" class="InductiveConstructor">zero</a>    <a id="1721" class="Symbol">=</a> <a id="1723" href="Data.Fin.Base.html#1157" class="InductiveConstructor">zero</a>
<a id="1728" href="Codata.Cofin.html#1638" class="Function">toFin</a> <a id="1734" class="Symbol">(</a><a id="1735" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1739" href="Codata.Cofin.html#1739" class="Bound">n</a><a id="1740" class="Symbol">)</a> <a id="1742" class="Symbol">(</a><a id="1743" href="Codata.Cofin.html#979" class="InductiveConstructor">suc</a> <a id="1747" href="Codata.Cofin.html#1747" class="Bound">i</a><a id="1748" class="Symbol">)</a> <a id="1750" class="Symbol">=</a> <a id="1752" href="Data.Fin.Base.html#1188" class="InductiveConstructor">suc</a> <a id="1756" class="Symbol">(</a><a id="1757" href="Codata.Cofin.html#1638" class="Function">toFin</a> <a id="1763" href="Codata.Cofin.html#1739" class="Bound">n</a> <a id="1765" href="Codata.Cofin.html#1747" class="Bound">i</a><a id="1766" class="Symbol">)</a>
</pre></body></html>
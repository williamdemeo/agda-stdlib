<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Trie</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Trie, basic type and operations</a>
<a id="141" class="Comment">------------------------------------------------------------------------</a>

<a id="215" class="Comment">-- See README.Data.Trie.NonDependent for an example of using a trie to</a>
<a id="286" class="Comment">-- build a lexer.</a>

<a id="305" class="Symbol">{-#</a> <a id="309" class="Keyword">OPTIONS</a> <a id="317" class="Pragma">--without-K</a> <a id="329" class="Pragma">--sized-types</a> <a id="343" class="Symbol">#-}</a>

<a id="348" class="Keyword">open</a> <a id="353" class="Keyword">import</a> <a id="360" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="376" class="Keyword">using</a> <a id="382" class="Symbol">(</a><a id="383" href="Relation.Binary.Core.html#882" class="Function">Rel</a><a id="386" class="Symbol">;</a> <a id="388" href="Relation.Binary.Bundles.html#6937" class="Record">StrictTotalOrder</a><a id="404" class="Symbol">)</a>

<a id="407" class="Keyword">module</a> <a id="414" href="Data.Trie.html" class="Module">Data.Trie</a> <a id="424" class="Symbol">{</a><a id="425" href="Data.Trie.html#425" class="Bound">k</a> <a id="427" href="Data.Trie.html#427" class="Bound">e</a> <a id="429" href="Data.Trie.html#429" class="Bound">r</a><a id="430" class="Symbol">}</a> <a id="432" class="Symbol">(</a><a id="433" href="Data.Trie.html#433" class="Bound">S</a> <a id="435" class="Symbol">:</a> <a id="437" href="Relation.Binary.Bundles.html#6937" class="Record">StrictTotalOrder</a> <a id="454" href="Data.Trie.html#425" class="Bound">k</a> <a id="456" href="Data.Trie.html#427" class="Bound">e</a> <a id="458" href="Data.Trie.html#429" class="Bound">r</a><a id="459" class="Symbol">)</a> <a id="461" class="Keyword">where</a>

<a id="468" class="Keyword">open</a> <a id="473" class="Keyword">import</a> <a id="480" href="Level.html" class="Module">Level</a>
<a id="486" class="Keyword">open</a> <a id="491" class="Keyword">import</a> <a id="498" href="Size.html" class="Module">Size</a>
<a id="503" class="Keyword">open</a> <a id="508" class="Keyword">import</a> <a id="515" href="Data.List.Base.html" class="Module">Data.List.Base</a> <a id="530" class="Keyword">using</a> <a id="536" class="Symbol">(</a><a id="537" href="Agda.Builtin.List.html#148" class="Datatype">List</a><a id="541" class="Symbol">;</a> <a id="543" href="Data.List.Base.html#7361" class="InductiveConstructor">[]</a><a id="545" class="Symbol">;</a> <a id="547" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">_∷_</a><a id="550" class="Symbol">;</a> <a id="552" href="Data.List.Base.html#1929" class="Function Operator">_++_</a><a id="556" class="Symbol">)</a>
<a id="558" class="Keyword">import</a> <a id="565" href="Data.List.NonEmpty.html" class="Module">Data.List.NonEmpty</a> <a id="584" class="Symbol">as</a> <a id="587" class="Module">List⁺</a>
<a id="593" class="Keyword">open</a> <a id="598" class="Keyword">import</a> <a id="605" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a> <a id="621" class="Symbol">as</a> <a id="624" class="Module">Maybe</a> <a id="630" class="Keyword">using</a> <a id="636" class="Symbol">(</a><a id="637" href="Agda.Builtin.Maybe.html#136" class="Datatype">Maybe</a><a id="642" class="Symbol">;</a> <a id="644" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor">just</a><a id="648" class="Symbol">;</a> <a id="650" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">nothing</a><a id="657" class="Symbol">;</a> <a id="659" href="Data.Maybe.Base.html#1569" class="Function">maybe′</a><a id="665" class="Symbol">)</a>
<a id="667" class="Keyword">open</a> <a id="672" class="Keyword">import</a> <a id="679" href="Data.Product.html" class="Module">Data.Product</a> <a id="692" class="Symbol">as</a> <a id="695" class="Module">Prod</a> <a id="700" class="Keyword">using</a> <a id="706" class="Symbol">(</a><a id="707" href="Data.Product.html#648" class="Function">∃</a><a id="708" class="Symbol">)</a>
<a id="710" class="Keyword">open</a> <a id="715" class="Keyword">import</a> <a id="722" href="Data.These.Base.html" class="Module">Data.These.Base</a> <a id="738" class="Symbol">as</a> <a id="741" class="Module">These</a> <a id="747" class="Keyword">using</a> <a id="753" class="Symbol">(</a><a id="754" href="Data.These.Base.html#528" class="Datatype">These</a><a id="759" class="Symbol">)</a>
<a id="761" class="Keyword">open</a> <a id="766" class="Keyword">import</a> <a id="773" href="Function.html" class="Module">Function</a>
<a id="782" class="Keyword">open</a> <a id="787" class="Keyword">import</a> <a id="794" href="Relation.Unary.html" class="Module">Relation.Unary</a> <a id="809" class="Keyword">using</a> <a id="815" class="Symbol">(</a><a id="816" href="Relation.Unary.html#3508" class="Function">IUniversal</a><a id="826" class="Symbol">;</a> <a id="828" href="Relation.Unary.html#4500" class="Function Operator">_⇒_</a><a id="831" class="Symbol">)</a>

<a id="834" class="Keyword">open</a> <a id="839" href="Relation.Binary.Bundles.html#6937" class="Module">StrictTotalOrder</a> <a id="856" href="Data.Trie.html#433" class="Bound">S</a>
  <a id="860" class="Keyword">using</a> <a id="866" class="Symbol">(</a><a id="867" class="Keyword">module</a> <a id="874" href="Relation.Binary.Bundles.html#4465" class="Module">Eq</a><a id="876" class="Symbol">)</a>
  <a id="880" class="Keyword">renaming</a> <a id="889" class="Symbol">(</a><a id="890" href="Relation.Binary.Bundles.html#7024" class="Field">Carrier</a> <a id="898" class="Symbol">to</a> <a id="901" class="Field">Key</a><a id="904" class="Symbol">)</a>

<a id="907" class="Keyword">open</a> <a id="912" class="Keyword">import</a> <a id="919" href="Data.List.Relation.Binary.Equality.Setoid.html" class="Module">Data.List.Relation.Binary.Equality.Setoid</a> <a id="961" href="Relation.Binary.Bundles.html#4478" class="Function">Eq.setoid</a>
<a id="971" class="Keyword">open</a> <a id="976" class="Keyword">import</a> <a id="983" href="Data.Tree.AVL.Value.html" class="Module">Data.Tree.AVL.Value</a> <a id="1003" href="Data.List.Relation.Binary.Equality.Setoid.html#1729" class="Function">≋-setoid</a> <a id="1012" class="Keyword">using</a> <a id="1018" class="Symbol">(</a><a id="1019" href="Data.Tree.AVL.Value.html#638" class="Record">Value</a><a id="1024" class="Symbol">)</a>

<a id="1027" class="Comment">------------------------------------------------------------------------</a>
<a id="1100" class="Comment">-- Definition</a>

<a id="1115" class="Comment">-- Trie is defined in terms of Trie⁺, the type of non-empty trie. This</a>
<a id="1186" class="Comment">-- guarantees that the trie is minimal: each path in the tree leads to</a>
<a id="1257" class="Comment">-- either a value or a number of non-empty sub-tries.</a>

<a id="1312" class="Keyword">open</a> <a id="1317" class="Keyword">import</a> <a id="1324" href="Data.Trie.NonEmpty.html" class="Module">Data.Trie.NonEmpty</a> <a id="1343" href="Data.Trie.html#433" class="Bound">S</a> as <a id="Trie⁺"></a><a id="1348" href="Data.Trie.html#1348" class="Module">Trie⁺</a> <a id="1354" class="Keyword">public</a>
  <a id="1363" class="Keyword">using</a> <a id="1369" class="Symbol">(</a><a id="1370" href="Data.Trie.NonEmpty.html#1768" class="Datatype">Trie⁺</a><a id="1375" class="Symbol">;</a> <a id="1377" href="Data.Trie.NonEmpty.html#1821" class="Function">Tries⁺</a><a id="1383" class="Symbol">;</a> <a id="1385" href="Data.Trie.NonEmpty.html#1601" class="Function">Word</a><a id="1389" class="Symbol">;</a> <a id="1391" href="Data.Trie.NonEmpty.html#1631" class="Function">eat</a><a id="1394" class="Symbol">)</a>

<a id="Trie"></a><a id="1397" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="1402" class="Symbol">:</a> <a id="1404" class="Symbol">∀</a> <a id="1406" class="Symbol">{</a><a id="1407" href="Data.Trie.html#1407" class="Bound">v</a><a id="1408" class="Symbol">}</a> <a id="1410" class="Symbol">(</a><a id="1411" href="Data.Trie.html#1411" class="Bound">V</a> <a id="1413" class="Symbol">:</a> <a id="1415" href="Data.Tree.AVL.Value.html#638" class="Record">Value</a> <a id="1421" href="Data.Trie.html#1407" class="Bound">v</a><a id="1422" class="Symbol">)</a> <a id="1424" class="Symbol">→</a> <a id="1426" href="Agda.Builtin.Size.html#202" class="Postulate">Size</a> <a id="1431" class="Symbol">→</a> <a id="1433" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1437" class="Symbol">(</a><a id="1438" href="Data.Trie.html#1407" class="Bound">v</a> <a id="1440" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1442" href="Data.Trie.html#425" class="Bound">k</a> <a id="1444" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1446" href="Data.Trie.html#427" class="Bound">e</a> <a id="1448" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="1450" href="Data.Trie.html#429" class="Bound">r</a><a id="1451" class="Symbol">)</a>
<a id="1453" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="1458" href="Data.Trie.html#1458" class="Bound">V</a> <a id="1460" href="Data.Trie.html#1460" class="Bound">i</a> <a id="1462" class="Symbol">=</a> <a id="1464" href="Agda.Builtin.Maybe.html#136" class="Datatype">Maybe</a> <a id="1470" class="Symbol">(</a><a id="1471" href="Data.Trie.NonEmpty.html#1768" class="Datatype">Trie⁺</a> <a id="1477" href="Data.Trie.html#1458" class="Bound">V</a> <a id="1479" href="Data.Trie.html#1460" class="Bound">i</a><a id="1480" class="Symbol">)</a>

<a id="1483" class="Comment">------------------------------------------------------------------------</a>
<a id="1556" class="Comment">-- Operations</a>

<a id="1571" class="Comment">-- Functions acting on Trie are wrappers for functions acting on Tries.</a>
<a id="1643" class="Comment">-- Sometimes the empty case is handled in a special way (e.g. insertWith</a>
<a id="1716" class="Comment">-- calls singleton when faced with an empty Trie).</a>

<a id="1768" class="Keyword">module</a> <a id="1775" href="Data.Trie.html#1775" class="Module">_</a> <a id="1777" class="Symbol">{</a><a id="1778" href="Data.Trie.html#1778" class="Bound">v</a><a id="1779" class="Symbol">}</a> <a id="1781" class="Symbol">{</a><a id="1782" href="Data.Trie.html#1782" class="Bound">V</a> <a id="1784" class="Symbol">:</a> <a id="1786" href="Data.Tree.AVL.Value.html#638" class="Record">Value</a> <a id="1792" href="Data.Trie.html#1778" class="Bound">v</a><a id="1793" class="Symbol">}</a> <a id="1795" class="Keyword">where</a>

  <a id="1804" class="Keyword">private</a> <a id="1812" href="Data.Trie.html#1812" class="Function">Val</a> <a id="1816" class="Symbol">=</a> <a id="1818" href="Data.Tree.AVL.Value.html#708" class="Field">Value.family</a> <a id="1831" href="Data.Trie.html#1782" class="Bound">V</a>

<a id="1834" class="Comment">------------------------------------------------------------------------</a>
<a id="1907" class="Comment">-- Lookup</a>

  <a id="1920" href="Data.Trie.html#1920" class="Function">lookup</a> <a id="1927" class="Symbol">:</a> <a id="1929" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="1934" href="Data.Trie.html#1782" class="Bound">V</a> <a id="1936" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="1938" class="Symbol">→</a> <a id="1940" class="Symbol">∀</a> <a id="1942" href="Data.Trie.html#1942" class="Bound">ks</a> <a id="1945" class="Symbol">→</a> <a id="1947" href="Agda.Builtin.Maybe.html#136" class="Datatype">Maybe</a> <a id="1953" class="Symbol">(</a><a id="1954" href="Data.These.Base.html#528" class="Datatype">These</a> <a id="1960" class="Symbol">(</a><a id="1961" href="Data.Trie.html#1812" class="Function">Val</a> <a id="1965" href="Data.Trie.html#1942" class="Bound">ks</a><a id="1967" class="Symbol">)</a> <a id="1969" class="Symbol">(</a><a id="1970" href="Data.Trie.NonEmpty.html#1821" class="Function">Tries⁺</a> <a id="1977" class="Symbol">(</a><a id="1978" href="Data.Trie.NonEmpty.html#1631" class="Function">eat</a> <a id="1982" href="Data.Trie.html#1782" class="Bound">V</a> <a id="1984" href="Data.Trie.html#1942" class="Bound">ks</a><a id="1986" class="Symbol">)</a> <a id="1988" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a><a id="1989" class="Symbol">))</a>
  <a id="1994" href="Data.Trie.html#1920" class="Function">lookup</a> <a id="2001" href="Data.Trie.html#2001" class="Bound">t</a> <a id="2003" href="Data.Trie.html#2003" class="Bound">ks</a> <a id="2006" class="Symbol">=</a> <a id="2008" href="Data.Trie.html#2001" class="Bound">t</a> <a id="2010" href="Data.Maybe.Base.html#2242" class="Function Operator">Maybe.&gt;&gt;=</a> <a id="2020" class="Symbol">λ</a> <a id="2022" href="Data.Trie.html#2022" class="Bound">ts</a> <a id="2025" class="Symbol">→</a> <a id="2027" href="Data.Trie.NonEmpty.html#2356" class="Function">Trie⁺.lookup</a> <a id="2040" href="Data.Trie.html#2022" class="Bound">ts</a> <a id="2043" href="Data.Trie.html#2003" class="Bound">ks</a>

  <a id="2049" href="Data.Trie.html#2049" class="Function">lookupValue</a> <a id="2061" class="Symbol">:</a> <a id="2063" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2068" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2070" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="2072" class="Symbol">→</a> <a id="2074" class="Symbol">∀</a> <a id="2076" href="Data.Trie.html#2076" class="Bound">ks</a> <a id="2079" class="Symbol">→</a> <a id="2081" href="Agda.Builtin.Maybe.html#136" class="Datatype">Maybe</a> <a id="2087" class="Symbol">(</a><a id="2088" href="Data.Trie.html#1812" class="Function">Val</a> <a id="2092" href="Data.Trie.html#2076" class="Bound">ks</a><a id="2094" class="Symbol">)</a>
  <a id="2098" href="Data.Trie.html#2049" class="Function">lookupValue</a> <a id="2110" href="Data.Trie.html#2110" class="Bound">t</a> <a id="2112" href="Data.Trie.html#2112" class="Bound">ks</a> <a id="2115" class="Symbol">=</a> <a id="2117" href="Data.Trie.html#2110" class="Bound">t</a> <a id="2119" href="Data.Maybe.Base.html#2242" class="Function Operator">Maybe.&gt;&gt;=</a> <a id="2129" class="Symbol">λ</a> <a id="2131" href="Data.Trie.html#2131" class="Bound">ts</a> <a id="2134" class="Symbol">→</a> <a id="2136" href="Data.Trie.NonEmpty.html#2679" class="Function">Trie⁺.lookupValue</a> <a id="2154" href="Data.Trie.html#2131" class="Bound">ts</a> <a id="2157" href="Data.Trie.html#2112" class="Bound">ks</a>

  <a id="2163" href="Data.Trie.html#2163" class="Function">lookupTries⁺</a> <a id="2176" class="Symbol">:</a> <a id="2178" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2183" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2185" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="2187" class="Symbol">→</a> <a id="2189" class="Symbol">∀</a> <a id="2191" href="Data.Trie.html#2191" class="Bound">ks</a> <a id="2194" class="Symbol">→</a> <a id="2196" href="Agda.Builtin.Maybe.html#136" class="Datatype">Maybe</a> <a id="2202" class="Symbol">(</a><a id="2203" href="Data.Trie.NonEmpty.html#1821" class="Function">Tries⁺</a> <a id="2210" class="Symbol">(</a><a id="2211" href="Data.Trie.NonEmpty.html#1631" class="Function">eat</a> <a id="2215" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2217" href="Data.Trie.html#2191" class="Bound">ks</a><a id="2219" class="Symbol">)</a> <a id="2221" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a><a id="2222" class="Symbol">)</a>
  <a id="2226" href="Data.Trie.html#2163" class="Function">lookupTries⁺</a> <a id="2239" href="Data.Trie.html#2239" class="Bound">t</a> <a id="2241" href="Data.Trie.html#2241" class="Bound">ks</a> <a id="2244" class="Symbol">=</a> <a id="2246" href="Data.Trie.html#2239" class="Bound">t</a> <a id="2248" href="Data.Maybe.Base.html#2242" class="Function Operator">Maybe.&gt;&gt;=</a> <a id="2258" class="Symbol">λ</a> <a id="2260" href="Data.Trie.html#2260" class="Bound">ts</a> <a id="2263" class="Symbol">→</a> <a id="2265" href="Data.Trie.NonEmpty.html#2800" class="Function">Trie⁺.lookupTries⁺</a> <a id="2284" href="Data.Trie.html#2260" class="Bound">ts</a> <a id="2287" href="Data.Trie.html#2241" class="Bound">ks</a>

  <a id="2293" href="Data.Trie.html#2293" class="Function">lookupTrie</a> <a id="2304" class="Symbol">:</a> <a id="2306" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2311" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2313" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="2315" class="Symbol">→</a> <a id="2317" class="Symbol">∀</a> <a id="2319" href="Data.Trie.html#2319" class="Bound">k</a> <a id="2321" class="Symbol">→</a> <a id="2323" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2328" class="Symbol">(</a><a id="2329" href="Data.Trie.NonEmpty.html#1631" class="Function">eat</a> <a id="2333" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2335" class="Symbol">(</a><a id="2336" href="Data.Trie.html#2319" class="Bound">k</a> <a id="2338" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="2340" href="Agda.Builtin.List.html#185" class="InductiveConstructor">[]</a><a id="2342" class="Symbol">))</a> <a id="2345" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a>
  <a id="2349" href="Data.Trie.html#2293" class="Function">lookupTrie</a> <a id="2360" href="Data.Trie.html#2360" class="Bound">t</a> <a id="2362" href="Data.Trie.html#2362" class="Bound">k</a> <a id="2364" class="Symbol">=</a> <a id="2366" href="Data.Trie.html#2360" class="Bound">t</a> <a id="2368" href="Data.Maybe.Base.html#2242" class="Function Operator">Maybe.&gt;&gt;=</a> <a id="2378" class="Symbol">λ</a> <a id="2380" href="Data.Trie.html#2380" class="Bound">ts</a> <a id="2383" class="Symbol">→</a> <a id="2385" href="Data.Trie.NonEmpty.html#2924" class="Function">Trie⁺.lookupTrie⁺</a> <a id="2403" href="Data.Trie.html#2380" class="Bound">ts</a> <a id="2406" href="Data.Trie.html#2362" class="Bound">k</a>

<a id="2409" class="Comment">------------------------------------------------------------------------</a>
<a id="2482" class="Comment">-- Construction</a>

  <a id="2501" href="Data.Trie.html#2501" class="Function">empty</a> <a id="2507" class="Symbol">:</a> <a id="2509" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2514" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2516" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a>
  <a id="2520" href="Data.Trie.html#2501" class="Function">empty</a> <a id="2526" class="Symbol">=</a> <a id="2528" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">nothing</a>

  <a id="2539" href="Data.Trie.html#2539" class="Function">singleton</a> <a id="2549" class="Symbol">:</a> <a id="2551" class="Symbol">∀</a> <a id="2553" href="Data.Trie.html#2553" class="Bound">ks</a> <a id="2556" class="Symbol">→</a> <a id="2558" href="Data.Trie.html#1812" class="Function">Val</a> <a id="2562" href="Data.Trie.html#2553" class="Bound">ks</a> <a id="2565" class="Symbol">→</a> <a id="2567" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2572" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2574" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a>
  <a id="2578" href="Data.Trie.html#2539" class="Function">singleton</a> <a id="2588" href="Data.Trie.html#2588" class="Bound">ks</a> <a id="2591" href="Data.Trie.html#2591" class="Bound">v</a> <a id="2593" class="Symbol">=</a> <a id="2595" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor">just</a> <a id="2600" class="Symbol">(</a><a id="2601" href="Data.Trie.NonEmpty.html#3153" class="Function">Trie⁺.singleton</a> <a id="2617" href="Data.Trie.html#2588" class="Bound">ks</a> <a id="2620" href="Data.Trie.html#2591" class="Bound">v</a><a id="2621" class="Symbol">)</a>

  <a id="2626" href="Data.Trie.html#2626" class="Function">insertWith</a> <a id="2637" class="Symbol">:</a> <a id="2639" class="Symbol">∀</a> <a id="2641" href="Data.Trie.html#2641" class="Bound">ks</a> <a id="2644" class="Symbol">→</a> <a id="2646" class="Symbol">(</a><a id="2647" href="Agda.Builtin.Maybe.html#136" class="Datatype">Maybe</a> <a id="2653" class="Symbol">(</a><a id="2654" href="Data.Trie.html#1812" class="Function">Val</a> <a id="2658" href="Data.Trie.html#2641" class="Bound">ks</a><a id="2660" class="Symbol">)</a> <a id="2662" class="Symbol">→</a> <a id="2664" href="Data.Trie.html#1812" class="Function">Val</a> <a id="2668" href="Data.Trie.html#2641" class="Bound">ks</a><a id="2670" class="Symbol">)</a> <a id="2672" class="Symbol">→</a> <a id="2674" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2679" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2681" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="2683" class="Symbol">→</a> <a id="2685" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2690" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2692" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a>
  <a id="2696" href="Data.Trie.html#2626" class="Function">insertWith</a> <a id="2707" href="Data.Trie.html#2707" class="Bound">ks</a> <a id="2710" href="Data.Trie.html#2710" class="Bound">f</a> <a id="2712" class="Symbol">(</a><a id="2713" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor">just</a> <a id="2718" href="Data.Trie.html#2718" class="Bound">t</a><a id="2719" class="Symbol">)</a> <a id="2721" class="Symbol">=</a> <a id="2723" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor">just</a> <a id="2728" class="Symbol">(</a><a id="2729" href="Data.Trie.NonEmpty.html#3324" class="Function">Trie⁺.insertWith</a> <a id="2746" href="Data.Trie.html#2707" class="Bound">ks</a> <a id="2749" href="Data.Trie.html#2710" class="Bound">f</a> <a id="2751" href="Data.Trie.html#2718" class="Bound">t</a><a id="2752" class="Symbol">)</a>
  <a id="2756" href="Data.Trie.html#2626" class="Function">insertWith</a> <a id="2767" href="Data.Trie.html#2767" class="Bound">ks</a> <a id="2770" href="Data.Trie.html#2770" class="Bound">f</a> <a id="2772" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">nothing</a>  <a id="2781" class="Symbol">=</a> <a id="2783" href="Data.Trie.html#2539" class="Function">singleton</a> <a id="2793" href="Data.Trie.html#2767" class="Bound">ks</a> <a id="2796" class="Symbol">(</a><a id="2797" href="Data.Trie.html#2770" class="Bound">f</a> <a id="2799" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">nothing</a><a id="2806" class="Symbol">)</a>

  <a id="2811" href="Data.Trie.html#2811" class="Function">insert</a> <a id="2818" class="Symbol">:</a> <a id="2820" class="Symbol">∀</a> <a id="2822" href="Data.Trie.html#2822" class="Bound">ks</a> <a id="2825" class="Symbol">→</a> <a id="2827" href="Data.Trie.html#1812" class="Function">Val</a> <a id="2831" href="Data.Trie.html#2822" class="Bound">ks</a> <a id="2834" class="Symbol">→</a> <a id="2836" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2841" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2843" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="2845" class="Symbol">→</a> <a id="2847" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2852" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2854" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a>
  <a id="2858" href="Data.Trie.html#2811" class="Function">insert</a> <a id="2865" href="Data.Trie.html#2865" class="Bound">ks</a> <a id="2868" class="Symbol">=</a> <a id="2870" href="Data.Trie.html#2626" class="Function">insertWith</a> <a id="2881" href="Data.Trie.html#2865" class="Bound">ks</a> <a id="2884" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="2887" href="Function.Base.html#716" class="Function">const</a>

  <a id="2896" href="Data.Trie.html#2896" class="Function">fromList</a> <a id="2905" class="Symbol">:</a> <a id="2907" href="Agda.Builtin.List.html#148" class="Datatype">List</a> <a id="2912" class="Symbol">(</a><a id="2913" href="Data.Product.html#648" class="Function">∃</a> <a id="2915" href="Data.Trie.html#1812" class="Function">Val</a><a id="2918" class="Symbol">)</a> <a id="2920" class="Symbol">→</a> <a id="2922" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="2927" href="Data.Trie.html#1782" class="Bound">V</a> <a id="2929" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a>
  <a id="2933" href="Data.Trie.html#2896" class="Function">fromList</a> <a id="2942" class="Symbol">=</a> <a id="2944" href="Data.Maybe.Base.html#2035" class="Function">Maybe.map</a> <a id="2954" href="Data.Trie.NonEmpty.html#4052" class="Function">Trie⁺.fromList⁺</a> <a id="2970" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="2973" href="Data.List.NonEmpty.Base.html#1834" class="Function">List⁺.fromList</a>

  <a id="2991" href="Data.Trie.html#2991" class="Function">toList</a> <a id="2998" class="Symbol">:</a> <a id="3000" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="3005" href="Data.Trie.html#1782" class="Bound">V</a> <a id="3007" href="Agda.Builtin.Size.html#304" class="Postulate">∞</a> <a id="3009" class="Symbol">→</a> <a id="3011" href="Agda.Builtin.List.html#148" class="Datatype">List</a> <a id="3016" class="Symbol">(</a><a id="3017" href="Data.Product.html#648" class="Function">∃</a> <a id="3019" href="Data.Trie.html#1812" class="Function">Val</a><a id="3022" class="Symbol">)</a>
  <a id="3026" href="Data.Trie.html#2991" class="Function">toList</a> <a id="3033" class="Symbol">(</a><a id="3034" href="Agda.Builtin.Maybe.html#174" class="InductiveConstructor">just</a> <a id="3039" href="Data.Trie.html#3039" class="Bound">t</a><a id="3040" class="Symbol">)</a> <a id="3042" class="Symbol">=</a> <a id="3044" href="Data.List.NonEmpty.Base.html#1782" class="Function">List⁺.toList</a> <a id="3057" class="Symbol">(</a><a id="3058" href="Data.Trie.NonEmpty.html#4154" class="Function">Trie⁺.toList⁺</a> <a id="3072" href="Data.Trie.html#3039" class="Bound">t</a><a id="3073" class="Symbol">)</a>
  <a id="3077" href="Data.Trie.html#2991" class="Function">toList</a> <a id="3084" href="Agda.Builtin.Maybe.html#195" class="InductiveConstructor">nothing</a>  <a id="3093" class="Symbol">=</a> <a id="3095" href="Agda.Builtin.List.html#185" class="InductiveConstructor">[]</a>

<a id="3099" class="Comment">------------------------------------------------------------------------</a>
<a id="3172" class="Comment">-- Modification</a>

<a id="3189" class="Keyword">module</a> <a id="3196" href="Data.Trie.html#3196" class="Module">_</a> <a id="3198" class="Symbol">{</a><a id="3199" href="Data.Trie.html#3199" class="Bound">v</a> <a id="3201" href="Data.Trie.html#3201" class="Bound">w</a><a id="3202" class="Symbol">}</a> <a id="3204" class="Symbol">{</a><a id="3205" href="Data.Trie.html#3205" class="Bound">V</a> <a id="3207" class="Symbol">:</a> <a id="3209" href="Data.Tree.AVL.Value.html#638" class="Record">Value</a> <a id="3215" href="Data.Trie.html#3199" class="Bound">v</a><a id="3216" class="Symbol">}</a> <a id="3218" class="Symbol">{</a><a id="3219" href="Data.Trie.html#3219" class="Bound">W</a> <a id="3221" class="Symbol">:</a> <a id="3223" href="Data.Tree.AVL.Value.html#638" class="Record">Value</a> <a id="3229" href="Data.Trie.html#3201" class="Bound">w</a><a id="3230" class="Symbol">}</a> <a id="3232" class="Keyword">where</a>

  <a id="3241" class="Keyword">private</a>
    <a id="3253" href="Data.Trie.html#3253" class="Function">Val</a> <a id="3257" class="Symbol">=</a> <a id="3259" href="Data.Tree.AVL.Value.html#708" class="Field">Value.family</a> <a id="3272" href="Data.Trie.html#3205" class="Bound">V</a>
    <a id="3278" href="Data.Trie.html#3278" class="Function">Wal</a> <a id="3282" class="Symbol">=</a> <a id="3284" href="Data.Tree.AVL.Value.html#708" class="Field">Value.family</a> <a id="3297" href="Data.Trie.html#3219" class="Bound">W</a>

  <a id="3302" href="Data.Trie.html#3302" class="Function">map</a> <a id="3306" class="Symbol">:</a> <a id="3308" class="Symbol">∀</a> <a id="3310" class="Symbol">{</a><a id="3311" href="Data.Trie.html#3311" class="Bound">i</a><a id="3312" class="Symbol">}</a> <a id="3314" class="Symbol">→</a> <a id="3316" href="Relation.Unary.html#3508" class="Function">∀[</a> <a id="3319" href="Data.Trie.html#3253" class="Function">Val</a> <a id="3323" href="Relation.Unary.html#4500" class="Function Operator">⇒</a> <a id="3325" href="Data.Trie.html#3278" class="Function">Wal</a> <a id="3329" href="Relation.Unary.html#3508" class="Function">]</a> <a id="3331" class="Symbol">→</a> <a id="3333" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="3338" href="Data.Trie.html#3205" class="Bound">V</a> <a id="3340" href="Data.Trie.html#3311" class="Bound">i</a> <a id="3342" class="Symbol">→</a> <a id="3344" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="3349" href="Data.Trie.html#3219" class="Bound">W</a> <a id="3351" href="Data.Trie.html#3311" class="Bound">i</a>
  <a id="3355" href="Data.Trie.html#3302" class="Function">map</a> <a id="3359" class="Symbol">=</a> <a id="3361" href="Data.Maybe.Base.html#2035" class="Function">Maybe.map</a> <a id="3371" href="Function.Base.html#3637" class="Function Operator">∘′</a> <a id="3374" href="Data.Trie.NonEmpty.html#1884" class="Function">Trie⁺.map</a> <a id="3384" href="Data.Trie.html#3205" class="Bound">V</a> <a id="3386" href="Data.Trie.html#3219" class="Bound">W</a>

<a id="3389" class="Comment">-- Deletion</a>

<a id="3402" class="Keyword">module</a> <a id="3409" href="Data.Trie.html#3409" class="Module">_</a> <a id="3411" class="Symbol">{</a><a id="3412" href="Data.Trie.html#3412" class="Bound">v</a><a id="3413" class="Symbol">}</a> <a id="3415" class="Symbol">{</a><a id="3416" href="Data.Trie.html#3416" class="Bound">V</a> <a id="3418" class="Symbol">:</a> <a id="3420" href="Data.Tree.AVL.Value.html#638" class="Record">Value</a> <a id="3426" href="Data.Trie.html#3412" class="Bound">v</a><a id="3427" class="Symbol">}</a> <a id="3429" class="Keyword">where</a>

  <a id="3438" class="Comment">-- Use a function to decide how to modify the sub-Trie⁺ whose root is</a>
  <a id="3510" class="Comment">-- at the end of path ks.</a>
  <a id="3538" href="Data.Trie.html#3538" class="Function">deleteWith</a> <a id="3549" class="Symbol">:</a> <a id="3551" class="Symbol">∀</a> <a id="3553" class="Symbol">{</a><a id="3554" href="Data.Trie.html#3554" class="Bound">i</a><a id="3555" class="Symbol">}</a> <a id="3557" class="Symbol">(</a><a id="3558" href="Data.Trie.html#3558" class="Bound">ks</a> <a id="3561" class="Symbol">:</a> <a id="3563" href="Data.Trie.NonEmpty.html#1601" class="Function">Word</a><a id="3567" class="Symbol">)</a> <a id="3569" class="Symbol">→</a>
     <a id="3576" class="Symbol">(∀</a> <a id="3579" class="Symbol">{</a><a id="3580" href="Data.Trie.html#3580" class="Bound">i</a><a id="3581" class="Symbol">}</a> <a id="3583" class="Symbol">→</a> <a id="3585" href="Data.Trie.NonEmpty.html#1768" class="Datatype">Trie⁺</a> <a id="3591" class="Symbol">(</a><a id="3592" href="Data.Trie.NonEmpty.html#1631" class="Function">eat</a> <a id="3596" href="Data.Trie.html#3416" class="Bound">V</a> <a id="3598" href="Data.Trie.html#3558" class="Bound">ks</a><a id="3600" class="Symbol">)</a> <a id="3602" href="Data.Trie.html#3580" class="Bound">i</a> <a id="3604" class="Symbol">→</a> <a id="3606" href="Agda.Builtin.Maybe.html#136" class="Datatype">Maybe</a> <a id="3612" class="Symbol">(</a><a id="3613" href="Data.Trie.NonEmpty.html#1768" class="Datatype">Trie⁺</a> <a id="3619" class="Symbol">(</a><a id="3620" href="Data.Trie.NonEmpty.html#1631" class="Function">eat</a> <a id="3624" href="Data.Trie.html#3416" class="Bound">V</a> <a id="3626" href="Data.Trie.html#3558" class="Bound">ks</a><a id="3628" class="Symbol">)</a> <a id="3630" href="Data.Trie.html#3580" class="Bound">i</a><a id="3631" class="Symbol">))</a> <a id="3634" class="Symbol">→</a>
     <a id="3641" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="3646" href="Data.Trie.html#3416" class="Bound">V</a> <a id="3648" href="Data.Trie.html#3554" class="Bound">i</a> <a id="3650" class="Symbol">→</a> <a id="3652" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="3657" href="Data.Trie.html#3416" class="Bound">V</a> <a id="3659" href="Data.Trie.html#3554" class="Bound">i</a>
  <a id="3663" href="Data.Trie.html#3538" class="Function">deleteWith</a> <a id="3674" href="Data.Trie.html#3674" class="Bound">ks</a> <a id="3677" href="Data.Trie.html#3677" class="Bound">f</a> <a id="3679" href="Data.Trie.html#3679" class="Bound">t</a> <a id="3681" class="Symbol">=</a> <a id="3683" href="Data.Trie.html#3679" class="Bound">t</a> <a id="3685" href="Data.Maybe.Base.html#2242" class="Function Operator">Maybe.&gt;&gt;=</a> <a id="3695" href="Data.Trie.NonEmpty.html#4619" class="Function">Trie⁺.deleteWith</a> <a id="3712" href="Data.Trie.html#3674" class="Bound">ks</a> <a id="3715" href="Data.Trie.html#3677" class="Bound">f</a>

  <a id="3720" class="Comment">-- Remove the whole node</a>
  <a id="3747" href="Data.Trie.html#3747" class="Function">deleteTrie⁺</a> <a id="3759" class="Symbol">:</a> <a id="3761" class="Symbol">∀</a> <a id="3763" class="Symbol">{</a><a id="3764" href="Data.Trie.html#3764" class="Bound">i</a><a id="3765" class="Symbol">}</a> <a id="3767" class="Symbol">(</a><a id="3768" href="Data.Trie.html#3768" class="Bound">ks</a> <a id="3771" class="Symbol">:</a> <a id="3773" href="Data.Trie.NonEmpty.html#1601" class="Function">Word</a><a id="3777" class="Symbol">)</a> <a id="3779" class="Symbol">→</a> <a id="3781" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="3786" href="Data.Trie.html#3416" class="Bound">V</a> <a id="3788" href="Data.Trie.html#3764" class="Bound">i</a> <a id="3790" class="Symbol">→</a> <a id="3792" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="3797" href="Data.Trie.html#3416" class="Bound">V</a> <a id="3799" href="Data.Trie.html#3764" class="Bound">i</a>
  <a id="3803" href="Data.Trie.html#3747" class="Function">deleteTrie⁺</a> <a id="3815" href="Data.Trie.html#3815" class="Bound">ks</a> <a id="3818" href="Data.Trie.html#3818" class="Bound">t</a> <a id="3820" class="Symbol">=</a> <a id="3822" href="Data.Trie.html#3818" class="Bound">t</a> <a id="3824" href="Data.Maybe.Base.html#2242" class="Function Operator">Maybe.&gt;&gt;=</a> <a id="3834" href="Data.Trie.NonEmpty.html#5458" class="Function">Trie⁺.deleteTrie⁺</a> <a id="3852" href="Data.Trie.html#3815" class="Bound">ks</a>

  <a id="3858" class="Comment">-- Remove the value and keep the sub-Tries (if any)</a>
  <a id="3912" href="Data.Trie.html#3912" class="Function">deleteValue</a> <a id="3924" class="Symbol">:</a> <a id="3926" class="Symbol">∀</a> <a id="3928" class="Symbol">{</a><a id="3929" href="Data.Trie.html#3929" class="Bound">i</a><a id="3930" class="Symbol">}</a> <a id="3932" class="Symbol">(</a><a id="3933" href="Data.Trie.html#3933" class="Bound">ks</a> <a id="3936" class="Symbol">:</a> <a id="3938" href="Data.Trie.NonEmpty.html#1601" class="Function">Word</a><a id="3942" class="Symbol">)</a> <a id="3944" class="Symbol">→</a> <a id="3946" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="3951" href="Data.Trie.html#3416" class="Bound">V</a> <a id="3953" href="Data.Trie.html#3929" class="Bound">i</a> <a id="3955" class="Symbol">→</a> <a id="3957" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="3962" href="Data.Trie.html#3416" class="Bound">V</a> <a id="3964" href="Data.Trie.html#3929" class="Bound">i</a>
  <a id="3968" href="Data.Trie.html#3912" class="Function">deleteValue</a> <a id="3980" href="Data.Trie.html#3980" class="Bound">ks</a> <a id="3983" href="Data.Trie.html#3983" class="Bound">t</a> <a id="3985" class="Symbol">=</a> <a id="3987" href="Data.Trie.html#3983" class="Bound">t</a> <a id="3989" href="Data.Maybe.Base.html#2242" class="Function Operator">Maybe.&gt;&gt;=</a> <a id="3999" href="Data.Trie.NonEmpty.html#5576" class="Function">Trie⁺.deleteValue</a> <a id="4017" href="Data.Trie.html#3980" class="Bound">ks</a>

  <a id="4023" class="Comment">-- Remove the sub-Tries and keep the value (if any)</a>
  <a id="4077" href="Data.Trie.html#4077" class="Function">deleteTries⁺</a> <a id="4090" class="Symbol">:</a> <a id="4092" class="Symbol">∀</a> <a id="4094" class="Symbol">{</a><a id="4095" href="Data.Trie.html#4095" class="Bound">i</a><a id="4096" class="Symbol">}</a> <a id="4098" class="Symbol">(</a><a id="4099" href="Data.Trie.html#4099" class="Bound">ks</a> <a id="4102" class="Symbol">:</a> <a id="4104" href="Data.Trie.NonEmpty.html#1601" class="Function">Word</a><a id="4108" class="Symbol">)</a> <a id="4110" class="Symbol">→</a> <a id="4112" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="4117" href="Data.Trie.html#3416" class="Bound">V</a> <a id="4119" href="Data.Trie.html#4095" class="Bound">i</a> <a id="4121" class="Symbol">→</a> <a id="4123" href="Data.Trie.html#1397" class="Function">Trie</a> <a id="4128" href="Data.Trie.html#3416" class="Bound">V</a> <a id="4130" href="Data.Trie.html#4095" class="Bound">i</a>
  <a id="4134" href="Data.Trie.html#4077" class="Function">deleteTries⁺</a> <a id="4147" href="Data.Trie.html#4147" class="Bound">ks</a> <a id="4150" href="Data.Trie.html#4150" class="Bound">t</a> <a id="4152" class="Symbol">=</a> <a id="4154" href="Data.Trie.html#4150" class="Bound">t</a> <a id="4156" href="Data.Maybe.Base.html#2242" class="Function Operator">Maybe.&gt;&gt;=</a> <a id="4166" href="Data.Trie.NonEmpty.html#5740" class="Function">Trie⁺.deleteTries⁺</a> <a id="4185" href="Data.Trie.html#4147" class="Bound">ks</a>
</pre></body></html>
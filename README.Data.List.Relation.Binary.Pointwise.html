<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>README.Data.List.Relation.Binary.Pointwise</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Documentation for pointwise lifting of relations over `List`s</a>
<a id="171" class="Comment">------------------------------------------------------------------------</a>

<a id="245" class="Keyword">module</a> <a id="252" href="README.Data.List.Relation.Binary.Pointwise.html" class="Module">README.Data.List.Relation.Binary.Pointwise</a> <a id="295" class="Keyword">where</a>

<a id="302" class="Keyword">open</a> <a id="307" class="Keyword">import</a> <a id="314" href="Data.Nat.html" class="Module">Data.Nat</a> <a id="323" class="Keyword">using</a> <a id="329" class="Symbol">(</a><a id="330" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a><a id="331" class="Symbol">;</a> <a id="333" href="Data.Nat.Base.html#1645" class="Function Operator">_&lt;_</a><a id="336" class="Symbol">;</a> <a id="338" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a><a id="341" class="Symbol">;</a> <a id="343" href="Data.Nat.Base.html#1558" class="InductiveConstructor">z≤n</a><a id="346" class="Symbol">)</a>
<a id="348" class="Keyword">open</a> <a id="353" class="Keyword">import</a> <a id="360" href="Data.List.html" class="Module">Data.List</a> <a id="370" class="Keyword">using</a> <a id="376" class="Symbol">(</a><a id="377" href="Agda.Builtin.List.html#148" class="Datatype">List</a><a id="381" class="Symbol">;</a> <a id="383" href="Data.List.Base.html#7361" class="InductiveConstructor">[]</a><a id="385" class="Symbol">;</a> <a id="387" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">_∷_</a><a id="390" class="Symbol">;</a> <a id="392" href="Data.List.Base.html#5058" class="Function">length</a><a id="398" class="Symbol">)</a>
<a id="400" class="Keyword">open</a> <a id="405" class="Keyword">import</a> <a id="412" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>
  <a id="452" class="Keyword">using</a> <a id="458" class="Symbol">(</a><a id="459" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">_≡_</a><a id="462" class="Symbol">;</a> <a id="464" href="Agda.Builtin.Equality.html#208" class="InductiveConstructor">refl</a><a id="468" class="Symbol">;</a> <a id="470" href="Relation.Binary.PropositionalEquality.Core.html#1698" class="Function">sym</a><a id="473" class="Symbol">;</a> <a id="475" href="Relation.Binary.PropositionalEquality.Core.html#1144" class="Function">cong</a><a id="479" class="Symbol">;</a> <a id="481" href="Relation.Binary.PropositionalEquality.Properties.html#5390" class="Function">setoid</a><a id="487" class="Symbol">)</a>
<a id="489" class="Keyword">open</a> <a id="494" class="Keyword">import</a> <a id="501" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a> <a id="527" class="Keyword">using</a> <a id="533" class="Symbol">(</a><a id="534" href="Relation.Nullary.Negation.Core.html#815" class="Function Operator">¬_</a><a id="536" class="Symbol">)</a>

<a id="539" class="Comment">------------------------------------------------------------------------</a>
<a id="612" class="Comment">-- Pointwise</a>

<a id="626" class="Comment">-- One of the most basic ways to form a binary relation between two</a>
<a id="694" class="Comment">-- lists of type `List A`, given a binary relation over `A`, is to say</a>
<a id="765" class="Comment">-- that two lists are related if they are the same length and:</a>
<a id="828" class="Comment">--   i) the first elements in the lists are related</a>
<a id="880" class="Comment">--   ii) the second elements in the lists are related</a>
<a id="934" class="Comment">--   iii) the third elements in the lists are related etc.</a>
<a id="993" class="Comment">--   etc.</a>
<a id="1003" class="Comment">--</a>
<a id="1006" class="Comment">-- A formalisation of this &quot;pointwise&quot; lifting of a relation to lists</a>
<a id="1076" class="Comment">-- is found in:</a>

<a id="1093" class="Keyword">open</a> <a id="1098" class="Keyword">import</a> <a id="1105" href="Data.List.Relation.Binary.Pointwise.html" class="Module">Data.List.Relation.Binary.Pointwise</a>

<a id="1142" class="Comment">-- The same syntax to construct a list (`[]` &amp; `_∷_`) is used to</a>
<a id="1207" class="Comment">-- construct proofs for the `Pointwise` relation. For example if you</a>
<a id="1276" class="Comment">-- want to prove that one list is strictly less than another list:</a>

<a id="lem₁"></a><a id="1344" href="README.Data.List.Relation.Binary.Pointwise.html#1344" class="Function">lem₁</a> <a id="1349" class="Symbol">:</a> <a id="1351" href="Data.List.Relation.Binary.Pointwise.Base.html#775" class="Datatype">Pointwise</a> <a id="1361" href="Data.Nat.Base.html#1645" class="Function Operator">_&lt;_</a> <a id="1365" class="Symbol">(</a><a id="1366" class="Number">0</a> <a id="1368" class="InductiveConstructor Operator">∷</a> <a id="1370" class="Number">2</a> <a id="1372" class="InductiveConstructor Operator">∷</a> <a id="1374" class="Number">1</a> <a id="1376" class="InductiveConstructor Operator">∷</a> <a id="1378" class="InductiveConstructor">[]</a><a id="1380" class="Symbol">)</a> <a id="1382" class="Symbol">(</a><a id="1383" class="Number">1</a> <a id="1385" class="InductiveConstructor Operator">∷</a> <a id="1387" class="Number">4</a> <a id="1389" class="InductiveConstructor Operator">∷</a> <a id="1391" class="Number">2</a> <a id="1393" class="InductiveConstructor Operator">∷</a> <a id="1395" class="InductiveConstructor">[]</a><a id="1397" class="Symbol">)</a>
<a id="1399" href="README.Data.List.Relation.Binary.Pointwise.html#1344" class="Function">lem₁</a> <a id="1404" class="Symbol">=</a> <a id="1406" href="README.Data.List.Relation.Binary.Pointwise.html#1437" class="Function">0&lt;1</a> <a id="1410" href="Data.List.Relation.Binary.Pointwise.Base.html#910" class="InductiveConstructor Operator">∷</a> <a id="1412" href="README.Data.List.Relation.Binary.Pointwise.html#1453" class="Function">2&lt;4</a> <a id="1416" href="Data.List.Relation.Binary.Pointwise.Base.html#910" class="InductiveConstructor Operator">∷</a> <a id="1418" href="README.Data.List.Relation.Binary.Pointwise.html#1481" class="Function">1&lt;2</a> <a id="1422" href="Data.List.Relation.Binary.Pointwise.Base.html#910" class="InductiveConstructor Operator">∷</a> <a id="1424" href="Data.List.Relation.Binary.Pointwise.Base.html#884" class="InductiveConstructor">[]</a>
  <a id="1429" class="Keyword">where</a>
  <a id="1437" href="README.Data.List.Relation.Binary.Pointwise.html#1437" class="Function">0&lt;1</a> <a id="1441" class="Symbol">=</a> <a id="1443" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="1447" href="Data.Nat.Base.html#1558" class="InductiveConstructor">z≤n</a>
  <a id="1453" href="README.Data.List.Relation.Binary.Pointwise.html#1453" class="Function">2&lt;4</a> <a id="1457" class="Symbol">=</a> <a id="1459" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="1463" class="Symbol">(</a><a id="1464" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="1468" class="Symbol">(</a><a id="1469" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="1473" href="Data.Nat.Base.html#1558" class="InductiveConstructor">z≤n</a><a id="1476" class="Symbol">))</a>
  <a id="1481" href="README.Data.List.Relation.Binary.Pointwise.html#1481" class="Function">1&lt;2</a> <a id="1485" class="Symbol">=</a> <a id="1487" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="1491" href="README.Data.List.Relation.Binary.Pointwise.html#1437" class="Function">0&lt;1</a>

<a id="1496" class="Comment">-- Lists that are related by `Pointwise` must be of the same length.</a>
<a id="1565" class="Comment">-- For example:</a>

<a id="lem₂"></a><a id="1582" href="README.Data.List.Relation.Binary.Pointwise.html#1582" class="Function">lem₂</a> <a id="1587" class="Symbol">:</a> <a id="1589" href="Relation.Nullary.Negation.Core.html#815" class="Function Operator">¬</a> <a id="1591" href="Data.List.Relation.Binary.Pointwise.Base.html#775" class="Datatype">Pointwise</a> <a id="1601" href="Data.Nat.Base.html#1645" class="Function Operator">_&lt;_</a> <a id="1605" class="Symbol">(</a><a id="1606" class="Number">0</a> <a id="1608" class="InductiveConstructor Operator">∷</a> <a id="1610" class="Number">2</a> <a id="1612" class="InductiveConstructor Operator">∷</a> <a id="1614" class="InductiveConstructor">[]</a><a id="1616" class="Symbol">)</a> <a id="1618" class="Symbol">(</a><a id="1619" class="Number">1</a> <a id="1621" class="InductiveConstructor Operator">∷</a> <a id="1623" class="InductiveConstructor">[]</a><a id="1625" class="Symbol">)</a>
<a id="1627" href="README.Data.List.Relation.Binary.Pointwise.html#1582" class="Function">lem₂</a> <a id="1632" class="Symbol">(</a><a id="1633" href="README.Data.List.Relation.Binary.Pointwise.html#1633" class="Bound">0&lt;1</a> <a id="1637" href="Data.List.Relation.Binary.Pointwise.Base.html#910" class="InductiveConstructor Operator">∷</a> <a id="1639" class="Symbol">())</a>

<a id="1644" class="Comment">-- Proofs about pointwise, including that of the above fact are</a>
<a id="1708" class="Comment">-- also included in the module:</a>

<a id="lem₃"></a><a id="1741" href="README.Data.List.Relation.Binary.Pointwise.html#1741" class="Function">lem₃</a> <a id="1746" class="Symbol">:</a> <a id="1748" class="Symbol">∀</a> <a id="1750" class="Symbol">{</a><a id="1751" href="README.Data.List.Relation.Binary.Pointwise.html#1751" class="Bound">xs</a> <a id="1754" href="README.Data.List.Relation.Binary.Pointwise.html#1754" class="Bound">ys</a><a id="1756" class="Symbol">}</a> <a id="1758" class="Symbol">→</a> <a id="1760" href="Data.List.Relation.Binary.Pointwise.Base.html#775" class="Datatype">Pointwise</a> <a id="1770" href="Data.Nat.Base.html#1645" class="Function Operator">_&lt;_</a> <a id="1774" href="README.Data.List.Relation.Binary.Pointwise.html#1751" class="Bound">xs</a> <a id="1777" href="README.Data.List.Relation.Binary.Pointwise.html#1754" class="Bound">ys</a> <a id="1780" class="Symbol">→</a> <a id="1782" href="Data.List.Base.html#5058" class="Function">length</a> <a id="1789" href="README.Data.List.Relation.Binary.Pointwise.html#1751" class="Bound">xs</a> <a id="1792" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">≡</a> <a id="1794" href="Data.List.Base.html#5058" class="Function">length</a> <a id="1801" href="README.Data.List.Relation.Binary.Pointwise.html#1754" class="Bound">ys</a>
<a id="1804" href="README.Data.List.Relation.Binary.Pointwise.html#1741" class="Function">lem₃</a> <a id="1809" class="Symbol">=</a> <a id="1811" href="Data.List.Relation.Binary.Pointwise.html#4659" class="Function">Pointwise-length</a>
</pre></body></html>